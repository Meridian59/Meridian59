% Meridian 59, Copyright 1994-2012 Andrew Kirmse and Chris Kirmse.
% All rights reserved.
%
% This software is distributed under a license that is described in
% the LICENSE file that accompanies it.
%
% Meridian is a registered trademark.


%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
Ant is Monster

constants:

   include blakston.khd

resources:

   include ant.lkod

   ant_koc_name_rsc = "yokinich"
   ant_name_rsc = "ant"
   ant_icon_rsc = ant.bgf
   ant_desc_rsc = \
      "Snapping pincers, bloody from the ant's last meal, lash "
      "out at you.  Although slow and nearly blind, ants have a "
      "keen sense of smell and a taste for human flesh."

   ant_dead_icon_rsc = antX.bgf
   ant_dead_name_rsc = "dead ant"

   ant_sound_hit = ant_atkh.wav
   ant_sound_miss = ant_atkm.wav
   ant_sound_death = ant_dth.wav
   ant_sound_aware = ant_awr.wav

classvars:

   vrName = ant_name_rsc
   vrIcon = ant_icon_rsc
   vrDesc = ant_desc_rsc

   viIndefinite = ARTICLE_AN
   vrKocName = ant_koc_name_rsc

   vrDead_icon = ant_dead_icon_rsc
   vrDead_name = ant_dead_name_rsc

   viSpeed = SPEED_SLOW
   viAttack_type = ATCK_WEAP_BITE
   viLevel = 40
   viDifficulty = 4
   viVisionDistance = 14
   viKarma = -10
   viCashmin = 100
   viCashmax = 300
   viDefault_behavior = AI_FIGHT_AGGRESSIVE | AI_FIGHT_WIZARD_KILLER
   viAttributes = MOB_SPASM

   viTreasure_type = TID_WIMPY_MEDIUM

   vrSound_hit = ant_sound_hit
   vrSound_miss = ant_sound_miss
   vrSound_aware = ant_sound_aware
   vrSound_death = ant_sound_death

properties:

   piAnimation = ANIM_NONE

messages:

   Constructed()
   {
      plResistances = [ [-ATCK_WEAP_ALL, -20] ];

      propagate;
   }

   DoSpasm()
   {
      local roll;

      roll = Random(1,10);

      if roll = 1
      {
         Send(self,@DoLegSpasm);

         propagate;
      }

      if roll <= 5
      {
         Send(poOwner,@SomethingTurned,#what=self,
               #new_angle=Random(0,MAX_ANGLE-1));
      }

      propagate;
   }

   MonsterAttack(what = $)
   {
      piAnimation = ANIM_ATTACK;
      Send(poOwner,@SomethingChanged,#what=self);
      piAnimation = ANIM_NONE;

      return;
   }

   DoLegSpasm()
   {
      piAnimation = ANIM_ATTACK;
      Send(poOwner,@SomethingChanged,#what=self);
      piAnimation = ANIM_NONE;

      return;
   }

   SendMoveAnimation()
   {
      AddPacket(1,ANIMATE_CYCLE, 4,150, 2,1, 2,3);

      return;
   }

   SendLookAnimation()
   {
      AddPacket(1,ANIMATE_CYCLE);
      AddPacket(4,150, 2,1, 2,3);

      return;
   }

   SendAnimation()
   {
      if piAnimation = ANIM_ATTACK
      {
         AddPacket(1,ANIMATE_ONCE, 4,150, 2,1, 2,6, 2,1);

         return;
      }

      % if no body animation
      AddPacket(1,ANIMATE_NONE,2,1);

      return;
   }

end
%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
