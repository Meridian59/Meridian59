% Meridian 59, Copyright 1994-2012 Andrew Kirmse and Chris Kirmse.
% All rights reserved.
%
% This software is distributed under a license that is described in
% the LICENSE file that accompanies it.
%
% Meridian is a registered trademark.


%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
DragonFlyPet is Monster

constants:

   include blakston.khd

   ANIM_SPEED = 100
   WING_ANIM_SPEED = 100
   ANIM_HOVER = 2
   HOVER_DELAY = ANIM_SPEED * 5
   LAND_DELAY = HOVER_DELAY * 5
   % any attack or spasm animation?

   FRAME_STAND = 1
   FRAME_LAND = 1
   FRAME_HOVER_START = 2
   FRAME_HOVER_END = 3
   FRAME_FLY_START = 4
   FRAME_FLY_END = 5
   FRAME_ATTACK_START = 6
   FRAME_ATTACK_END = 9

   HS_WINGS = 1

resources:

   include dflypet.lkod

   DragonFlyPet_koc_name_rsc = "vo'huzotz"
   DragonFlyPet_name_rsc = "pet dragonfly"
   DragonFlyPet_icon_rsc = dflypet.bgf
   DragonFlyPet_desc_rsc = \
      "These small lizards look exceptionally friendly.  Their gossamer "
      "wings allow them to jump into the air and take flight for brief "
      "periods of time.  When backed into a corner, dragonflies are known "
      "to have a nasty spitting attack."

   DragonFlyPet_wings_rsc = dflypetw.bgf

   DragonFlyPet_dead_icon_rsc = dflypetX.bgf
   DragonFlyPet_dead_name_rsc = "dead pet dragonfly"

   DragonFlyPet_sound_hit = df_atkm.wav
   DragonFlyPet_sound_miss = df_atkm.wav
   DragonFlyPet_sound_death = df_dth.wav
   DragonFlyPet_sound_aware = df_awr.wav

   DragonFlyPet_spit = loogy.bgf

classvars:

   viIndefinite = ARTICLE_A
   vrName = DragonFlyPet_name_rsc
   vrIcon = DragonFlyPet_icon_rsc
   vrDesc = DragonFlyPet_desc_rsc
   vrDead_icon = DragonFlyPet_dead_icon_rsc
   vrDead_name = DragonFlyPet_dead_name_rsc

   viTreasure_type = TID_NONE

   viSpeed = SPEED_AVERAGE
   viAttack_type = ATCK_WEAP_PIERCE
   viLevel = 120
   viDifficulty = 6
   % Attack range of 192, or 3 row/col.
   viAttackRange = 192
   viVisionDistance = 18
   viKarma = 40
   viDefault_behavior = AI_FIGHT_AGGRESSIVE | AI_FIGHT_NEWBIESAFE
   viCashmin = 0
   viCashmax = 0
   viWimpy = 5

   vrSound_hit = DragonFlyPet_sound_hit
   vrSound_miss = DragonFlyPet_sound_miss
   vrSound_aware = DragonFlyPet_sound_aware
   vrSound_death = DragonFlyPet_sound_death

properties:

   piAnimation = ANIM_NONE
   ptHover = $
   ptLand = $

   piColor = PT_GRAY_TO_DGREEN

messages:

   Constructed()
   {
      plResistances = [ [-ATCK_SPELL_COLD, 25 ],
                        [-ATCK_SPELL_SHOCK, 40 ],
                        [-ATCK_SPELL_FIRE, -20 ],
                        [ATCK_WEAP_THRUST, 40 ],
                        [ATCK_WEAP_NERUDITE, 25 ],
                        [ATCK_WEAP_MAGIC, -10]
                      ];

      propagate;
   }

   MonsterAttack(what = $)
   {
      Send(self,@DoSpit);

      return;
   }

   DoSpit()
   {
      piAnimation = ANIM_ATTACK;
      piDrawEffectFlag = piDrawEffectFlag | OF_BOUNCING;
      Send(poOwner,@SomethingChanged,#what=self);
      Send(poOwner,@SomethingShot,#who=self,#target=poTarget,
           #projectile = self);

     piAnimation = ANIM_HOVER;
      if ptHover = $
      {
         ptHover = CreateTimer(self,@HoverTimer,HOVER_DELAY);
      }

      return;
   }

   GetProjectileSpeed()
   {
      return 8;
   }

   SendProjectileAnimation()
   {
      AddPacket(1, ANIMATE_CYCLE, 4, 40, 2, 1, 2, 5);   % 40ms between animations

      return;
   }

   GetProjectileIcon()
   {
      return DragonFlyPet_spit;
   }

   HoverTimer()
   {
      ptHover = $;

      Send(poOwner,@SomethingChanged,#what=self);
      if ptLand <> $
      {
         DeleteTimer(ptLand);
      }

      ptLand = CreateTimer(self,@LandTimer,LAND_DELAY);

      return;
   }

   LandTimer()
   {
      ptLand = $;

      if (poTarget = $) AND (piAnimation = ANIM_HOVER)
      {
         piAnimation = ANIM_NONE;
         piDrawEffectFlag = (piDrawEffectFlag & (~OF_BOUNCING));
         Send(poOwner,@SomethingChanged,#what=self);
      }

      return;
   }

   SendLookAnimation()
   {
      AddPacket(1,ANIMATE_TRANSLATION,1,piColor);
      AddPacket(1,ANIMATE_NONE);
      AddPacket(2,FRAME_LAND);

      return;
   }

   SendLookOverlays()
   {
      AddPacket(1,0);

      return;
   }

   SendMoveAnimation()
   {
      AddPacket(1,ANIMATE_TRANSLATION,1,piColor);
      AddPacket(1,ANIMATE_CYCLE,4,ANIM_SPEED,2,FRAME_FLY_START,2,FRAME_FLY_END);
      %if piState & STATE_MOVE { piAnimation = ANIM_NONE; }

      return;
   }

   SendMoveOverlays()
   {
      AddPacket(1,1);
      AddPacket(4,DragonFlyPet_wings_rsc,1,HS_WINGS);
      AddPacket(1,ANIMATE_EFFECT,1,DRAWFX_TRANSLUCENT_25-DRAWFX_TRANSLUCENT_25+1);
      AddPacket(1,ANIMATE_CYCLE,4,WING_ANIM_SPEED,
            2,(FRAME_FLY_START - 1),2,(FRAME_FLY_END - 1) );

      return;
   }

   SendOverlays()
   {
      if piAnimation = ANIM_HOVER
      {
         AddPacket(1,1);
         AddPacket(4,DragonFlyPet_wings_rsc,1,HS_WINGS);
         AddPacket(1,ANIMATE_EFFECT,1,DRAWFX_TRANSLUCENT_25-DRAWFX_TRANSLUCENT_25+1);
         AddPacket(1,ANIMATE_CYCLE,4,WING_ANIM_SPEED,
               2,(FRAME_HOVER_START-1),2,(FRAME_HOVER_END-1));

         return;
      }

      if piAnimation = ANIM_ATTACK
      {
         % Uses normal body speed for attack
         AddPacket(1,1);
         AddPacket(4,DragonFlyPet_wings_rsc,1,HS_WINGS);
         AddPacket(1,ANIMATE_EFFECT,1,DRAWFX_TRANSLUCENT_25-DRAWFX_TRANSLUCENT_25+1);
         AddPacket(1,ANIMATE_ONCE,4,ANIM_SPEED,2,(FRAME_ATTACK_START - 1),
               2,(FRAME_ATTACK_END - 1),2,(FRAME_HOVER_END - 1) );

         return;
      }

     propagate;
   }

   SendAnimation()
   {
      AddPacket(1,ANIMATE_TRANSLATION,1,piColor);

      if piAnimation = ANIM_HOVER
      {
         AddPacket(1,ANIMATE_CYCLE,4,ANIM_SPEED,
               2,FRAME_HOVER_START,2,FRAME_HOVER_END);

         return;
      }

      if piAnimation = ANIM_ATTACK
      {
         AddPacket(1,ANIMATE_ONCE,4,ANIM_SPEED,2,FRAME_ATTACK_START,
               2,FRAME_ATTACK_END,2,FRAME_HOVER_END);

         return;
      }

      % No body animation.
      propagate;
   }

   Delete()
   {
      if ptHover <> $
      {
         DeleteTimer(ptHover);
         ptHover = $;
      }

      if ptLand <> $
      {
         DeleteTimer(ptLand);
         ptLand = $;
      }

      propagate;
   }

   CanMorphTo()
   {
      return FALSE;
   }

   GetObjectFlags()
   {
      if piAnimation = ANIM_NONE
      {
         return OF_ATTACKABLE;
      }

      return OF_ATTACKABLE | OF_BOUNCING;
   }

   SetColor(iColor=PT_GRAY_TO_DGREEN)
   {
      piColor = iColor;
      if poOwner <> $
      {
         Send(poOwner,@SomethingChanged,#what=self);
      }

      return;
   }

end
%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
