% Meridian 59, Copyright 1994-2012 Andrew Kirmse and Chris Kirmse.
% All rights reserved.
%
% This software is distributed under a license that is described in
% the LICENSE file that accompanies it.
%
% Meridian is a registered trademark.


%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
DuskRat is Monster

constants:

   include blakston.khd

   POISON_CHANCE = 6
   POISON_DURATION = 30000   %%% in milliseconds
   POISON_LOSSRATE = 10000    %%% in health points * 10^-4 / second

resources:

   include duskrat.lkod

   duskrat_koc_name_rsc = "teotnapyijoa"
   duskrat_name_rsc = "dusk rat"
   duskrat_icon_rsc = duskrat.bgf

   duskrat_desc_rsc = \
      "The air around the rat is dark and thick with evil.  The smell of "
      "death and disease linger on its fetid breath as it darts towards "
      "its target.  The origins of this abomination are unknown.  The only "
      "definite knowledge known is of the deadly poison it carries "
      "in its bite."

   duskrat_poisoned_add_rsc = \
      "As the poison travels to your head, you begin "
      "to feel a little light-headed.  The room spins around you."
   duskrat_illusion_poisoned = \
      "%s%s reels as poison from your attack courses through %s veins."

   duskrat_dead_icon_rsc = ratX.bgf
   duskrat_dead_name_rsc = "dead dusk rat"

   duskrat_sound_hit = rat_atkh.wav
   duskrat_sound_miss = rat_atkm.wav
   duskrat_sound_death = rat_dth.wav
   duskrat_sound_aware = rat_awr.wav

classvars:

   vrKocName = duskrat_koc_name_rsc
   vrName = duskrat_name_rsc
   vrIcon = duskrat_icon_rsc
   vrDesc = duskrat_desc_rsc
   vrDead_icon = duskrat_dead_icon_rsc
   vrDead_name = duskrat_dead_name_rsc
   viDead_drawfx = DRAWFX_BLACK

   viTreasure_type = TID_RAT

   viSpeed = SPEED_VERY_FAST
   viAttack_type = ATCK_WEAP_CLAW
   viAttributes = 0
   viLevel = 90
   viDifficulty = 6
   % Attack range of 192, or 3 row/col.
   viAttackRange = 192
   viVisionDistance = 11
   viKarma = -40
   viDefault_behavior = \
      AI_FIGHT_AGGRESSIVE | AI_FIGHT_SWITCHALOT | AI_MOVE_REGROUP
   viLight_Penalty = 30
   viCashmin = 250
   viCashmax = 750

   vrSound_hit = duskrat_sound_hit
   vrSound_miss = duskrat_sound_miss
   vrSound_aware = duskrat_sound_aware
   vrSound_death = duskrat_sound_death

properties:

   piAnimation = ANIM_NONE
   piDrawfx = DRAWFX_BLACK

messages:

   Constructed()
   {
      plResistances = [ [-ATCK_SPELL_FIRE, -30 ],
                        [ATCK_WEAP_BLUDGEON, 10 ],
                        [ATCK_WEAP_SLASH, 20 ]
                      ];

      propagate;
   }

   MonsterAttack(what=$)
   {
      Send(self,@DoSlash);

      return;
   }

   DoSlash()
   {
      piAnimation = ANIM_ATTACK;
      Send(poOwner,@SomethingChanged,#what=self);
      piAnimation = ANIM_NONE;

      return;
   }

   SendLookAnimation()
   {
      AddPacket(1,ANIMATE_CYCLE,4,125,2,2,2,6);

      return;
   }

   SendMoveAnimation()
   {
      AddPacket(1,ANIMATE_CYCLE,4,75,2,2,2,6);

      return;
   }

   SendAnimation()
   {
      if piAnimation = ANIM_ATTACK
      {
         AddPacket(1,ANIMATE_ONCE,4,100,2,7,2,11,2,1);

         return;
      }

      propagate;
   }

   NewOwner(what=$)
   "Make room darker when present, about 2X darker than a ShadowBeast"
   {
      %New room
      if what <> $
      {
         Send(what,@AddBaseLight,#amount=-viLight_Penalty);
      }

      %Old room
      if poOwner <> $
      {
         Send(poOwner,@AddBaseLight,#amount=viLight_Penalty);
      }

      propagate;
   }

   HitSideEffect(what=$,who=$)
   {
      local oSpell;

      if Random(1,POISON_CHANCE) = 1
      {
         oSpell = Send(SYS,@FindSpellByNum,#num=SID_POISON);
         Send(oSpell,@MakePoisoned,#who=what,
               #lossrate=POISON_LOSSRATE,#duration=POISON_DURATION);

         if IsClass(what,&Player)
         {
            Send(what,@EffectSendUserDuration,#effect=EFFECT_WAVER,
                  #duration=10000);
            Send(what,@MsgSendUser,#message_rsc=duskrat_poisoned_add_rsc);
         }
         
         if who <> $
         {
            Send(who,@MsgSendUser,#message_rsc=duskrat_illusion_poisoned,
                  #parm1=Send(what,@GetDef),#parm2=Send(what,@GetName),
                  #parm3=Send(what,@GetHisHer));
         }
      }

      return;
   }

   IsAlly(target=$)
   {
      % Qor Characters?
      return FALSE;
   }

   Killed()
   {
      % old room
      if poOwner <> $
      {
         Send(poOwner,@AddBaseLight,#amount=viLight_Penalty);
      }

      propagate;
   }

   Delete()
   {
      if poOwner <> $
      {
         Send(poOwner,@AddBaseLight,#amount=viLight_Penalty);
      }

      propagate;
   }

end
%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
