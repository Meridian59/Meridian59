% Meridian 59, Copyright 1994-2012 Andrew Kirmse and Chris Kirmse.
% All rights reserved.
%
% This software is distributed under a license that is described in
% the LICENSE file that accompanies it.
%
% Meridian is a registered trademark.


%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
GiantRat is Monster

constants:

   include blakston.khd

resources:

   giantrat_koc_name_rsc = "napyijoa"
   giantrat_name_rsc = "giant rat"
   giantrat_icon_rsc = rat.bgf
   giantrat_desc_rsc = \
   "The giant rat bares its yellow teeth in defiance.  A "
   "smell of rot emanates from its filthy coat."

   giantrat_dead_icon_rsc = ratX.bgf
   giantrat_dead_name_rsc = "dead giant rat"

   giantrat_sound_hit = rat_atkh.wav
   giantrat_sound_miss = rat_atkm.wav
   giantrat_sound_death = rat_dth.wav
   giantrat_sound_aware = rat_awr.wav

classvars:

   vrKocName = giantrat_koc_name_rsc
   vrName = giantrat_name_rsc
   vrIcon = giantrat_icon_rsc
   vrDesc = giantrat_desc_rsc
   vrDead_icon = giantrat_dead_icon_rsc
   vrDead_name = giantrat_dead_name_rsc

   viTreasure_type = TID_RAT

   viSpeed = SPEED_FAST
   viAttack_type = ATCK_WEAP_CLAW
   viAttributes = 0
   viLevel = 30
   viDifficulty = 1
   viKarma = -20
   viDefault_behavior = AI_FIGHT_AGGRESSIVE | AI_FIGHT_SWITCHALOT


   vrSound_hit = giantrat_sound_hit
   vrSound_miss = giantrat_sound_miss
   vrSound_aware = giantrat_sound_aware
   vrSound_death = giantrat_sound_death

properties:

   piAnimation = ANIM_NONE

messages:

   Constructed()
   {
      plResistances = [ [-20, -ATCK_SPELL_FIRE ], 
			[-10, ATCK_WEAP_BLUDGEON ] ,
			[ 20, ATCK_WEAP_SLASH ] ];
      propagate;
   }

   MonsterAttack(what = $)
   {
      % add in any attack animation calls
      Send(self,@DoSlash);
      return;
   }

   DoSlash()
   {
      piAnimation = ANIM_ATTACK;
      Send(poOwner,@SomethingChanged,#what=self);
      piAnimation = ANIM_NONE;

      return;
   }

   SendLookAnimation()
   {
      AddPacket(1,ANIMATE_CYCLE,4,125,2,2,2,6);

      return;
   }

   SendMoveAnimation()
   {
      AddPacket(1,ANIMATE_CYCLE,4,75,2,2,2,6);

      return;
   }

   SendAnimation()
   {
      if piAnimation = ANIM_ATTACK
      {
	 AddPacket(1,ANIMATE_ONCE,4,100,2,7,2,11,2,1);
	 return;
      }

      % if no body animation
      
      propagate;
   }

   IsAlly(target = $)
   "Newbie monster has no allies."
   {
      return FALSE;
   }

end
%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
