% Meridian 59, Copyright 1994-2012 Andrew Kirmse and Chris Kirmse.
% All rights reserved.
%
% This software is distributed under a license that is described in
% the LICENSE file that accompanies it.
%
% Meridian is a registered trademark.


%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
LupoggKing is Monster

constants:

   include blakston.khd
   

   ANIM_BREATH = 1
   ANIM_BITE = 2
   ANIM_SPIT = 3

   SPIT_COLOR = 82

resources:

   include lupking.lkod

   lupoggking_name_rsc = "lupogg king"
   lupoggking_icon_rsc = lupking.bgf
   lupoggking_desc_rsc = \
      "The ancient King of the Lupoggs lords over these dark caves, knowing "
      "no creature to be its equal.  The rock walls rumble and shake whenever "
      "this monstrosity wails.  You feel as though you have already outlived "
      "most people who encounter the Lupogg King, but that thought does not "
      "comfort you any."


   lupoggking_dead_icon_rsc = lupkingx.bgf
   lupoggking_dead_name_rsc = "dead lupogg king"

   lupoggking_sound_hit = lup_atkm.wav
   lupoggking_sound_miss = lup_atkm.wav
   lupoggking_sound_death = lup_dth.wav
   lupoggking_sound_aware = lup_awr.wav

   lupoggking_cast_spell = \
      "The lupogg king covers you in a foul paralyzing slime!"
   lupoggking_cast_sound = bsp_dth.wav

   lupoggking_death_blow = \
      "Your last attack ends the lupogg king's reign!"
   lupoggking_single_killer = \
      "You have personally slain the mighty lupogg king all by your lonesome!"
   lupoggking_double_killer = \
      "The lupogg king is dead, thanks to you and your comrade!"
   lupoggking_helper_killer = \
      "The lupogg king is dead, thanks to you and your comrades!"
   lupoggking_no_truce = \
      "The evil in this place eliminates the possibility of any truce."
   lupoggking_no_illusion = \
      "Your illusions have no effect on the mighty lupogg king!"
   lupoggking_no_firewall = \
      "You cannot place a wall of fire here."
   lupoggking_no_lwall = \
      "You cannot place a wall of lightning here."
   lupoggking_spit = gob.bgf
   
   angered_by_wall_element_msg = \
      "Enraged by %s%s, the lupogg king stomps down hard!"

classvars:
   vrName = lupoggking_name_rsc
   vrIcon = lupoggking_icon_rsc
   vrDesc = lupoggking_desc_rsc
   vrDead_icon = lupoggking_dead_icon_rsc
   vrDead_name = lupoggking_dead_name_rsc

   viTreasure_type = TID_LUPOGG_KING
   viSpeed = SPEED_SLOW
   viAttack_type = ATCK_WEAP_CLAW
   viLevel = 200
   viDifficulty = 9
   viVisionDistance = 40
   % Attack range of 256, or 4 row/col.
   viAttackRange = 256
   viKarma = 100
   viDefault_behavior = \
      AI_FIGHT_AGGRESSIVE |  AI_FIGHT_WIZARD_KILLER | AI_FIGHT_HYPERAGGRESSIVE
   viAttributes = MOB_SPASM
   viGender = GENDER_MALE
   viCashmin = 20000
   viCashmax = 50000
   vrSound_hit = lupoggking_sound_hit
   vrSound_miss = lupoggking_sound_miss
   vrSound_aware = lupoggking_sound_aware
   vrSound_death = lupoggking_sound_death

   viHoldDurationMax = 25000  % Milliseconds.
   viHoldDurationMin = 15000  % Milliseconds.

properties:

   piAnimation = ANIM_NONE
   plAttackers = $  % [player,player....]

   piHoldAttackChance = 12 % Chance he'll attack with a hold
   piHoldCounterChance = 5 % Chance he'll counter a spell or arrow with a hold
   piHoldDurationMax = 25000  % Milliseconds.
   piHoldDurationMin = 15000  % Milliseconds.

messages:

   Constructed()
   {
      % Give lupogg king more HP.
      piMax_Hit_Points = piMax_Hit_Points * 3;
      % piHit_Points uses high-precision numbers, so multiply by 100.
      piHit_Points = piMax_Hit_Points * 100;

      % Set hold duration from class variables. Allows modifying
      % classvars for ALL lupogg kings, or modifying the property
      % for one specific lupogg king.
      piHoldDurationMin = viHoldDurationMin;
      piHoldDurationMax = viHoldDurationMax;

      plResistances = [ [ATCK_WEAP_ALL, 40 ],
                        [ATCK_WEAP_PIERCE, 90 ],
                        [-ATCK_SPELL_COLD, 90 ],
                        [-ATCK_SPELL_SHOCK, 90],
                        [-ATCK_SPELL_ACID, -20 ],
                        [-ATCK_SPELL_FIRE, -20 ],
                        [-ATCK_SPELL_QUAKE, 100 ],
                        [ATCK_WEAP_NERUDITE, -40 ]
                      ];

      propagate;
   }

   Delete()
   {
      plAttackers = $;

      propagate;
   }

   AssessDamage(what = $,damage = $,atype = 0, aspell = 0,bonus = 0)
   "This is called when something causes damage to us"
   {
      local i;

      foreach i in plAttackers
      {
         if i = what
         {
            propagate;
         }
      }

      if what <> $
         AND IsClass(what,&Battler)
      {
         plAttackers = Cons(what,plAttackers);
      }

      propagate;
   }

   DoSpasm()
   {
      Send(self,@DoBreath);

      propagate;
   }

   MonsterAttack(what = $)
   {
      piAnimation = ANIM_BITE;
      Send(poOwner,@SomethingChanged,#what=self);
      piAnimation = ANIM_NONE;

      return;
   }

   SomethingMoved(what=$)
   {
      local oTrance, oActive, lActive, i;

      if what <> self OR poOwner = $
      {
         propagate;
      }

      % Shake the ground when I take a step.
      % Small chance each person might lose any active trance.

      Send(poOwner,@Rumble,#duration=250,#disruption=50);
      
      propagate;
   }

   DoCast()
   {
      piAnimation = ANIM_SPIT;
      Send(poOwner,@SomethingChanged,#what=self);
      Send(poOwner,@SomethingShot,#who=self,#target=poTarget,
            #projectile=self);
      piAnimation = ANIM_NONE;

      return;
   }

   GetProjectileIcon()
   {
      return lupoggking_spit;
   }

   GetProjectileSpeed()
   {
      return 8;
   }
   
   SendProjectileAnimation()
   {
      AddPacket(1, ANIMATE_CYCLE, 4, 40, 2, 1, 2, 5);   % 40ms between animations

      return;
   }

   GetProjectileLightFlags()
   {
      % No lighting information.
      return LIGHT_FLAG_NONE;
   }

   DoBreath()
   {
      piAnimation = ANIM_BREATH;
      Send(poOwner,@SomethingChanged,#what=self);
      piAnimation = ANIM_NONE;

      return;
   }

   SendMoveAnimation()
   {
      AddPacket(1,ANIMATE_CYCLE, 4,200, 2,7, 2,11);

      return;
   }

   SendLookAnimation()
   {
      AddPacket(1,ANIMATE_CYCLE, 4,200, 2,2, 2,21);

      return;
   }

   SendAnimation()
   {
      if piAnimation = ANIM_BITE
      {
         AddPacket(1,ANIMATE_ONCE, 4,200, 2,12, 2,16, 2,1);

         return;
      }

      if piAnimation = ANIM_SPIT
      {
         AddPacket(1,ANIMATE_ONCE, 4,200, 2,17, 2,21, 2,1);

         return;
      }

      if piAnimation = ANIM_BREATH
      {
         AddPacket(1,ANIMATE_ONCE, 4,200, 2,2, 2,6, 2,1);

         return;
      }

      propagate;
   }

   ReqSpellCast(who = $, oSpell = $, lItems = $)
   {
      if oSpell = Send(SYS,@FindSpellByNum,#num=SID_TRUCE)
      {
         Send(who,@MsgSendUser,#message_rsc=lupoggking_no_truce);

         return FALSE;
      }

      if Random(1,piHoldCounterChance) = 1
      {
         Send(self,@MonsterCastAttackSpell,#what=who);

         return FALSE;
      }

      propagate;
   }

   ReqSomethingAttack(what = $,victim = $,use_weapon = $)
   {
      if use_weapon <> $
         AND IsClass(use_weapon,&RangedWeapon)
         AND victim = self
         AND Random(1,piHoldCounterChance) = 1
      {
         Send(self,@MonsterCastAttackSpell,#What=what);

         return FALSE;
      }

      propagate;
   }

   MonsterCastAttackSpell(what=$)
   {
      local oSpell, iRandom, oTarget;

      if what = $
      {
         oTarget = poTarget;
      }
      else
      {
         oTarget = what;
      }

      % Vision distance check.
      if NOT Send(self,@CanSee,#what=poTarget)
      {
         return;
      }

      % Req attack and line of sight check.
      if NOT Send(poOwner,@ReqSomethingAttack,#what=self,
                  #victim=poTarget,#use_weapon=self)
         OR NOT Send(poOwner,@LineOfSight,#obj1=self,#obj2=oTarget)
      {
         return;
      }

      oSpell = Send(SYS,@FindSpellByNum,#num=SID_HOLD);
      iRandom = Random(piHoldDurationMin,piHoldDurationMax);

      Send(poOwner,@SomethingWaveRoom,#what=self,
            #wave_rsc=lupoggking_cast_sound);
      Send(self,@DoCast);
      Send(oSpell,@DoSpell,#what=self,#oTarget=oTarget,#iDuration=iRandom);
      if IsClass(oTarget,&User)
      {
         Send(oTarget,@MsgSendUser,#message_rsc=lupoggking_cast_spell);
         Send(oTarget,@EffectSendUserDurationAndXlat,#effect=EFFECT_FLASHXLAT,
               #duration=iRandom*1000,#xlat=SPIT_COLOR);
      }

      % Do damage?

      return;
   }

   AttackTimer(timer = $)
   "We were bashing on something before, and now its time to hit it again."
   {
      local oSpell, iRandom;

      iRandom = Random(1,piHoldAttackChance);
      oSpell = Send(SYS,@FindSpellByNum,#num=SID_HOLD);
      Send(self,@ClearBehavior,#timer=timer);

      % Sanity checks.

      if poOwner = $
      {
         Debug("Unreachable. Null Owner"); 
         Send(self,@EnterStateLimbo);

         return;
      }

      if poTarget=$
      {
         Debug("Unreachable. Null poTarget","monster",self);
         Send(self,@EnterStateWait);

         return;
      }

      Send(self,@FindNewTarget);
      if NOT Send(self,@CanReach,#what=poTarget)
         AND Send(poTarget,@IsEnchanted,#what=oSpell)
      {
         Send(self,@EnterStateChase,#Target=poTarget);

         return;
      }
      else
      {
         if NOT Send(self,@CanReach,#what=poTarget) 
         {
            Send(self,@MonsterCastAttackSpell);
            Send(self,@EnterStateChase,#Target=poTarget);
            %Send(poBrain,@PostAttackTimer,#mob=self,#state=piState);

            return;
         }
      }

      if NOT Send(poTarget,@IsEnchanted,#what=oSpell)
         AND iRandom = 1
      {
         Send(self,@MonsterCastAttackSpell);
      }
      else
      {
         Send(self,@TryAttack,#what=poTarget);
      }

      if poBrain <> $
      {
         Send(poBrain,@PostAttackTimer,#mob=self,#state=piState);
      }

      return;
   }

   ReqMonsterMove(new_row = $,new_col = $)
   "Called by monster moving code to check if we really want to go"
   "to <new_row>,<new_col>."
   {
      if poOwner <> $
         AND Send(poOwner,@GetRoomNum) = RID_SEWER_KING
         AND (new_row > 17 OR new_col > 43 OR new_row < 2 OR new_col < 12)
      {
         return FALSE;
      }

      propagate;
   }

   CanSee(what=$)
   {
      local iRow, iCol;

      if poOwner <> $
         AND Send(poOwner,@GetRoomNum) = RID_SEWER_KING
         AND what <> $
      {
         iRow = Send(what,@GetRow);
         iCol = Send(what,@GetCol);

         if iRow > 18 OR iCol > 43 OR iCol < 24
         {
            return FALSE;
         }
      }

      propagate;
   }

   FindNewTarget()
   {
      local oHold, lActive, iRandom, iDistance, iBestDistance, i, each_obj;

      % Only one in three people are considered eligible
      % targets.  The closest is targeted.  Held people
      % aren't considered - he deals with his current
      % target first.  Only people in view are considered.

      oHold = Send(SYS,@FindSpellByNum,#num=SID_HOLD);
      lActive = Send(poOwner,@GetHolderActive);
      iRandom = Random(1,30);
      iBestDistance = 10000;

      if Send(poTarget,@IsEnchanted,#what=oHold)
      {
         foreach i in lActive
         {
            each_obj = Send(poOwner,@HolderExtractObject,#data=i);
            iDistance = Send(self,@SquaredDistanceTo,#what=each_obj);

            if IsClass(each_obj,&User)
               AND Send(self,@CanSee,#what=each_obj)
               AND NOT Send(each_obj,@IsEnchanted,#what=oHold)
               AND (iRandom < 10 OR iDistance < iBestDistance)
            {
               iBestDistance = iDistance;
               Send(self,@TargetSwitch,#what=each_obj,#iHatred=100);
               if iRandom < 5
               {
                  return;
               }
            }
         }
      }

      return;
   }

   Killed(what=$)
   {
      local oSpell, oRoom, NumAttackers, i, lFinalTargets;

      NumAttackers = length(plAttackers);

      foreach i in plAttackers
      {
         if i = what
         {
            Send(i,@MsgSendUser,#message_rsc=lupoggking_death_blow );
         }

         if NumAttackers = 1
         {
            Send(i,@MsgSendUser,#message_rsc=lupoggking_single_killer );
         }

         if NumAttackers = 2
         {
            Send(i,@MsgSendUser,#message_rsc=lupoggking_double_killer );
         }

         if NumAttackers > 2
         {
            Send(i,@MsgSendUser,#message_rsc=lupoggking_helper_killer );
         }

      }

      oSpell = Send(SYS,@FindSpellByNum,#num=SID_EARTHQUAKE);
      lFinalTargets = Send(oSpell,@GetTargets,#who=poOwner);
      Post(oSpell,@CastSpell,#who=poOwner,#iSpellPower=40,
            #lTargets=lFinalTargets);

      propagate;
   }

   CreateTreasure(who=$,corpse=$)
   {
      local oTreasure, iNumber;

      oTreasure = Create(&BlueDragonScale,#corpse=corpse);
      iNumber = Random(3,8);
      Send(oTreasure,@AddNumber,#number=iNumber);

      if Send(poOwner,@ReqNewHold,#what=oTreasure,#new_row=piRow,#new_col=piCol)
      {
         Send(poOwner,@NewHold,#what=oTreasure,
               #new_row=piRow,#new_col=piCol,
               #fine_row=piFine_row,#fine_col=piFine_col);
      }
      else
      {
         Send(oTreasure,@Delete);
      }

      propagate;
   }

   CanMorphTo()
   {
      return FALSE;
   }

   DMCanArmageddon()
   {
      % Can't destroy the lupogg king in his home.
      if poOwner <> $
         AND Send(poOwner,@GetRoomNum) = RID_SEWER_KING
      {
         return FALSE;
      }

      propagate;
   }

   CanEvilTwin()
   {
      return FALSE;
   }
   
   AngeredByWallElement(what=$)
   {
      local i, oSpell, lFinalTargets;
      
      foreach i in plAttackers
      {
         Send(i,@MsgSendUser,#message_rsc=angered_by_wall_element_msg,
                             #parm1=Send(what,@GetDef),
                             #parm2=Send(what,@GetName));
      }
      
      Send(poOwner,@DeleteWallsAroundBattler,#who=self);
      
      oSpell = Send(SYS,@FindSpellByNum,#num=SID_EARTHQUAKE);
      lFinalTargets = Send(oSpell,@GetTargets,#who=poOwner);
      Post(oSpell,@CastSpell,#who=poOwner,#iSpellPower=40,
            #lTargets=lFinalTargets);

      return;
   }

end
%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
