% Meridian 59, Copyright 1994-2012 Andrew Kirmse and Chris Kirmse.
% All rights reserved.
%
% This software is distributed under a license that is described in
% the LICENSE file that accompanies it.
%
% Meridian is a registered trademark.


%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
Reflection is Monster

% TODO BUG FIX: Somehow reflections are ending up with a nil owner.  How?

constants:

   include blakston.khd

   MANA_DRAIN_DELAY = 8000
   MANA_DRAIN_AMOUNT = 1

resources:

   include reflectn.lkod

   reflection_name_rsc = "reflection"

   reflection_male_icon_rsc = btg.bgf
   reflection_female_icon_rsc = bth.bgf

   reflection_dead_icon_rsc = amoebaX.bgf
   reflection_dead_name_rsc = "dead reflection"
   reflection_weapon_rsc = scimov.bgf

   reflection_sound_hit = orc_atkh.wav
   reflection_sound_miss = orc_atkm.wav
   reflection_sound_death = orc_dth.wav
   reflection_sound_aware = orc_awr.wav

   reflection_delete =  "%s%s disappears into a puff of smoke."

   reflection_trouble = \
      "You sense that your reflection has gotten you into some trouble..."

classvars:

   viIndefinite = ARTICLE_NONE
   viDefinite = ARTICLE_NONE

   vrDead_icon = reflection_dead_icon_rsc
   vrDead_name = reflection_dead_name_rsc

   viTreasure_type = TID_NONE

   viSpeed = SPEED_VERY_FAST
   viAttack_type = ATCK_WEAP_SLASH
   viAttributes = 0
   viLevel = 45
   viDifficulty = 4
   viDamagePercent = 50
   viVisionDistance = 60

   viKarma = 0
   viCashmin = 0
   viCashmax = 0
   vrSound_hit = reflection_sound_hit
   vrSound_miss = reflection_sound_miss
   vrSound_aware = reflection_sound_aware
   vrSound_death = reflection_sound_death
   vrMinion_trouble = reflection_trouble

   viDefault_behavior = AI_FIGHT_MONSTERS | AI_FIGHT_NEWBIESAFE \
                        | AI_FIGHT_AGGRESSIVE

properties:

   ptManaDrain
   vrName = reflection_name_rsc
   piDrawfx = 4
   vrIcon = reflection_male_icon_rsc
   piAnimation = PANM_NONE
   % Who the reflection is of.
   poOriginal = $
   piGender = GENDER_MALE
   pbIllusion = TRUE

   piSpellpower = 1
   poArrow = $
   piDamagePercent
   ptSomethingChanged = $

messages:

   Constructor(iSpellPower=1,oMaster=$)
   {
      if oMaster <> $
      {
         poMaster = oMaster;
      }

      piSpellpower = iSpellpower;
      ptManaDrain = CreateTimer(self,@ManaDrainTimer,MANA_DRAIN_DELAY);
      poArrow = Create(&Arrow);
      piDamagePercent = viDamagePercent;
      propagate;
   }

   GetApparentName()
   {
      return Send(poOriginal,@GetApparentName);
   }

   GetName(cap=FALSE)
   {
      return Send(poOriginal,@GetName,#cap=cap);
   }

   GetDef(cap=FALSE)
   {
      return Send(poOriginal,@GetDef,#cap=cap);
   }

   GetIndef(cap=FALSE)
   {
      return Send(poOriginal,@GetIndef,#cap=cap);
   }

   GetHisHer(cap=FALSE)
   {
      return Send(poOriginal,@GetHisHer,#cap=cap);
   }

   GetHeShe(cap=FALSE)
   {
      return Send(poOriginal,@GetHeShe,#cap=cap);
   }

   GetLordLady(cap=FALSE)
   {
      return Send(poOriginal,@GetLordLady,#cap=cap);
   }

   GetHisHers(cap=FALSE)
   {
      return Send(poOriginal,@GetHisHers,#cap=cap);
   }

   GetHimHer(cap=FALSE)
   {
      return Send(poOriginal,@GetHimHer,#cap=cap);
   }

   ReqSomethingEntered(what=$)
   {
      if poOriginal = $
      {
         return TRUE;
      }
      else
      {
         if IsClass(poOriginal,&Player)
            AND NOT Send(poOriginal,@IsLoggedOn)
         {
            Post(self,@Delete);

            return TRUE;
         }
      }

      propagate;
   }

   SomethingLeft(what=$)
   {
      if poOriginal = $
      {
         return;
      }

      if IsClass(poOriginal,&Player)
         AND NOT Send(poOriginal,@IsLoggedOn)
      {
         % This happens when the original leaves (i.e. logs off), and the
         % reflection is deleted when it is removed from the system list.
         % We don't need to delete it again here.
         return;
      }

      propagate;
   }

   SomethingKilled(what=$,victim=$,use_weapon=$)
   "Someone just died, perhaps we need to clear our target."
   {
      if victim = poOriginal
      {
         Post(self,@Delete);

         return;
      }

      propagate;
   }

   SetMaster(oMaster=$)
   {
      if oMaster = $
      {
         if poMaster <> $
         {
            % Reflections return to owner's control, attacking the player
            % who stole them.

            if poMaster = Send(self,@GetOriginal)
            {
               % Don't need to do anything.
               return;
            }

            Send(poMaster,@RemoveControlledMinion,#what=self);
            Send(self,@TargetSwitch,#what=poMaster,#iHatred=150);
            poMaster = Send(self,@GetOriginal);
            % Add ourselves back to master's control list.
            Send(poMaster,@NewControlledMinion,#minion=self);
            Send(self,@EnterStateChase);

         }
      }
      else
      {
         poMaster = oMaster;

         % Break target.
         Send(self,@EnterStateWait);

         % Uncomment this if post-capture attack behavior changes.
         %if NOT Send(SYS,@IsPKAllowed)
         %   AND (poTarget <> $
         %      AND IsClass(poTarget,&Player))
         %{
         %   % Forget our target
         %   Send(self,@EnterStateWait);
         %}
      }

      if poOwner <> $
      {
         Post(poOwner,@SomethingChanged,#what=self);
      }

      return;
   }

   ManaDrainTimer()
   {
      ptManaDrain = $;

      if poOriginal = $
      {
         return;
      }

      % Ignore mana drain for non-players.
      if IsClass(poOriginal,&Monster)
      {
         return;
      }

      if NOT Send(poOriginal,@IsLoggedOn)
      {
         Send(self,@Delete);

         return;
      }

      if Send(poOriginal,@GetMana) < MANA_DRAIN_AMOUNT
      {
         Send(self,@Delete);

         return;
      }

      Send(poOriginal,@LoseMana,#amount=MANA_DRAIN_AMOUNT);
      Send(self,@GetOriginalInfo);
      Send(poOwner,@SomethingChanged,#what=self);
      ptManaDrain = CreateTimer(self,@ManaDrainTimer,MANA_DRAIN_DELAY);

      return;
   }

   Delete(bMonsterReport=TRUE)
   {
      Send(SYS,@RemoveReflection,#oReflection=self);

      if poOwner <> $
         AND (IsClass(poOriginal,&Monster)
            OR Send(poOriginal,@IsLoggedOn))
      {
         Send(poOwner,@SomeoneSaid,#type=SAY_MESSAGE,#string=reflection_delete,
              #what=self,#parm1=Send(self,@GetDef,#cap=TRUE),
              #parm2=Send(self,@GetName,#cap=FALSE));
      }

      if ptManaDrain <> $
      {
         DeleteTimer(ptManaDrain);
         ptManaDrain = $;
      }

      if ptSomethingChanged <> $
      {
         DeleteTimer(ptSomethingChanged);
         ptSomethingChanged = $;
      }

      if poArrow <> $
      {
         Send(poArrow,@Delete);
      }
      poArrow = $;
      Post(self,@RemoveOriginal);

      propagate;
   }

   RemoveOriginal()
   {
      poOriginal = $;

      return;
   }

   GetOriginal()
   {
      if poOriginal <> $
      {
         return poOriginal;
      }

      return;
   }

   GetOriginalInfo()
   {
      if poOriginal = $
      {
         return;
      }

      if IsClass(poOriginal,&Player)
      {
         vrIcon = Send(poOriginal,@GetPlayerIcon);
         piDrawfx = Send(poOriginal,@GetDrawingEffects);
         vrName = Send(poOriginal,@GetTrueName);
      }
      else
      {
         vrIcon = Send(poOriginal,@GetIcon);
         piDrawfx = Send(poOriginal,@GetDrawingEffects);
         vrName = Send(poOriginal,@GetName);
      }

      piGender = Send(poOriginal,@GetGender);

      return;
   }

   GetObjectFlags()
   {
      if poOriginal = $
      {
         return 0;
      }

      return Send(poOriginal,@GetObjectFlags);
   }

   GetDrawingEffects()
   {
      if poOriginal = $
      {
         return 0;
      }

      return Send(poOriginal,@GetDrawingEffects);
   }

   GetPlayerNameColor()
   {
      if poOriginal = $
      {
         return 0;
      }

      return Send(poOriginal,@GetPlayerNameColor);
   }

   GetClientObjectType()
   {
      if poOriginal = $
      {
         return 0;
      }

      return Send(poOriginal,@GetClientObjectType);
   }

   GetOptimalRange()
   {
      return Send(self,@GetAttackRange);
   }

   GetAttackRange()
   {
      local iWeapon;

      if poOriginal = $
      {
         return 0;
      }

      if IsClass(poOriginal,&Player)
      {
         iWeapon = Send(poOriginal,@GetWeapon);

         if iWeapon <> $
         {
            return Send(iWeapon,@GetRange);
         }
      }
      else
      {
         return Send(poOriginal,@GetAttackRange);
      }

      return 64;
   }

   GetOffense(what=$,stroke_obj=$)
   {
      return Send(poOriginal,@GetOffense,#what=what,#stroke_obj=stroke_obj);
   }

   GetDefense(what = $, stroke_obj=$)
   {
      return Send(poOriginal,@GetDefense,#what=what,#stroke_obj=stroke_obj);
   }

   GetDamage(what = $, stroke_obj=$)
   {
      local oStroke, iDamage;

      if poOriginal = $
      {
         return 0;
      }

      oStroke = stroke_obj;
      if IsClass(poOriginal,&Player)
      {
         oStroke = $;
      }
      iDamage = Send(poOriginal,@GetDamage,#what=what,#stroke_obj=oStroke);

      return iDamage * piDamagePercent / 100;
   }

   GetDamageType(what=$,use_weapon=$)
   {
      if poOriginal = $
      {
         return viAttack_type;
      }

      return Send(poOriginal,@GetDamageType,#what=what,#use_weapon=use_weapon);
   }

   GetSpellType(what=$,use_weapon=$)
   {
      if poOriginal = $
      {
         return viAttack_spell;
      }

      return Send(poOriginal,@GetSpellType,#what=what,#use_weapon=use_weapon);
   }

   SomethingChanged(what=$)
   {
      if poOriginal = $
      {
         return;
      }

      % Our source object changed; tell everyone that we've changed, too.
      if (what = poOriginal)
         AND IsClass(poOriginal,&Player)
      {
         piDrawfx = Send(poOriginal,@GetDrawingEffects);

         % Players report that if a character casts a lot of
         % reflections and then changes state, e.g. by putting on lots
         % of clothing, other clients in the room may crash.  Our
         % suspicion is that a server buffer gets filled, and then
         % clients go out of sync.  We increased server buffer sizes
         % and this appeared to fix it, but players report there may
         % still be a problem.  Thus we jitter reflection updates to
         % try to avoid lots of messages being sent out at once.
         if ptSomethingChanged <> $
         {
            ptSomethingChanged = CreateTimer(self,@SomethingChangedTimer,750);
         }
      }

      propagate;
   }

   SomethingChangedTimer()
   "Tell room that we've changed.  This is on a timer to avoid tons "
   "of changes sent all at once."
   {
      ptSomethingChanged = $;
      if poOriginal <> $
         AND poOwner <> $
      {
         Post(poOwner,@SomethingChanged,#what=self);
      }

      return;
   }

   SetOriginal(who=$)
   {
      if who = $
      {
         Debug("Reflection called with invalid $ SetOriginal");
      }

      poOriginal = who;
      piMax_Hit_Points = piSpellpower / 3;
      piMax_hit_points = Bound(piMax_hit_points,15,40);
      piHit_Points = piMax_Hit_Points*100;

      Send(self,@GetOriginalInfo);

      return;
   }

   GetIcon()
   {
      return Send(poOriginal,@GetIcon);
   }

   MonsterAttack(what=$)
   {
      % Do the animation!
      Send(self,@DoAttack);

      return;
   }

   % The next three messages deal with the three defense skills.
   % These messages return the relative values of the three skills.
   %  Used in player for defense, used in battler for defense messages.
   GetParryAbility(stroke_obj=$)
   {
      return Send(poOriginal,@GetParryAbility,#stroke_obj=stroke_obj);
   }

   GetBlockAbility(stroke_obj=$)
   {
      return Send(poOriginal,@GetBlockAbility,#stroke_obj=stroke_obj);
   }

   GetDodgeAbility(stroke_obj=$)
   {
      return Send(poOriginal,@GetDodgeAbility,#stroke_obj=stroke_obj);
   }

   DoAttack()
   {
      local Weapon, ammotype, i;

      Send(self,@GetOriginalInfo);

      if IsClass(poOriginal,&Player)
      {
         Weapon = Send(poOriginal,@GetWeapon);
         ammotype = poArrow;

         if Weapon <> $
         {
            if IsClass(Weapon,&Bow)
            {
               piAnimation = PANM_BOW_FIRE;

               foreach i in Send(poOriginal,@GetPlayerUsing)
               {
                  if Send(i,@GetItemUseType) = ITEM_USE_QUIVER
                  {
                     AmmoType = i;
                  }
               }

               Send(poOwner,@SomethingShot,#who=self,#target=poTarget,
                    #projectile=AmmoType);
            }
            else
            {
               if IsClass(Weapon,&TouchAttackSpell)
               {
                  piAnimation = PANM_CAST;
               }
               else
               {
                  piAnimation = PANM_WEAPON_ATTACK;
               }
            }
         }
         else
         {
            piAnimation = PANM_FIST_ATTACK;
         }
      }
      else
      {
         % Kludgish:
         %  Currently special-cased for the dark angels.  Should standardize
         %   monsters to make this smoother.
         piAnimation = ANIM_ATTACK;
         if Send(poOriginal,@GetProjectileIcon) <> $
         {
            Send(poOwner,@SomethingShot,#who=self,#target=poTarget,
                 #projectile=poOriginal);
         }
      }

      Send(poOwner,@SomethingChanged,#what=self);

      if IsClass(poOriginal,&Player)
      {
         piAnimation = PANM_NONE;
      }
      else
      {
         piAnimation = ANIM_NONE;
      }

      return;
   }

   TryLook(what=$)
   {
      if what = $
      {
         return;
      }

      if poOriginal = $
      {
         return;
      }

      if IsClass(poOriginal,&Player)
      {
         if Send(poOriginal,@CheckPlayerFlag,#flag=PFLAG_INVISIBLE)
            AND (what <> poOriginal)
         {
            return;
         }

         Send(what,@SendLookPlayer,#what=poOriginal);
      }
      else
      {
         Send(poOriginal,@TryLook,#what=what);
      }

      return;
   }

   ShowDesc()
   {
      return Send(poOriginal,@ShowDesc);
   }

   ShowExtraInfo()
   {
      return Send(poOriginal,@ShowExtraInfo);
   }

   GetURL()
   {
      return Send(poOriginal,@GetURL);
   }

   SendMoveAnimation()
   {
      return Send(poOriginal,@SendMoveAnimation);
   }

   SendMoveOverlays()
   {
      return Send(poOriginal,@SendMoveOverlays);
   }

   SendAnimation()
   {
       return Send(poOriginal,@SendAnimation,#iAnimation=piAnimation);
   }

   SendOverlays()
   {
      return Send(poOriginal,@SendOverlays,#iAnimation=piAnimation);
   }

   GetIllusionForm()
   {
      return poOriginal;
   }  

   SendLightingInformation()
   {
      return Send(poOriginal,@SendLightingInformation);
   }

   SpecialHatredBehavior(what=$)
   {
      % Trying to reduce the tendency of reflections to turn on themselves
      %  or their master.
      if (IsClass(what,&Reflection)
          AND Send(what,@GetIllusionForm) = poOriginal)
         OR what = poOriginal
         OR (IsClass(poOriginal,&Monster)
             AND IsClass(what,GetClass(poOriginal)))
         OR IsClass(what,&Revenant)
         OR IsClass(what,&EvilTwin)
      {
         return -1000;
      }

      return 0;
   }

   GetCaster()
   {
      return poOriginal;
   }

   CanEvilTwin()
   {
      return FALSE;
   }

end
%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
