% Meridian 59, Copyright 1994-2012 Andrew Kirmse and Chris Kirmse.
% All rights reserved.
%
% This software is distributed under a license that is described in
% the LICENSE file that accompanies it.
%
% Meridian is a registered trademark.


%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
SpiderQueen is Monster

constants:

   include blakston.khd

   POISON_CHANCE = 15
   % 30 seconds duration
   POISON_DURATION = 30000
   % in health points * 10^-4 / second
   POISON_LOSSRATE = 2500    

resources:


   spiderqueen_name_rsc = "queen spider"
   spiderqueen_koc_name_rsc = "kokinich"
   spiderqueen_icon_rsc = spdrqu.bgf
   spiderqueen_desc_rsc = \
      "The huge egg sac of the queen spider makes her slow and heavy, but "
      "dark intelligence shines in her many red eyes."

   spiderqueen_dead_icon_rsc = spdrquX.bgf
   spiderqueen_dead_name_rsc = "dead queen spider"

   spiderqueen_sound_hit = qn_atkh.wav
   spiderqueen_sound_miss = qn_atkm.wav
   spiderqueen_sound_death = qn_dth.wav
   spiderqueen_sound_aware = qn_awr.wav

classvars:

   vrKocName = spiderqueen_koc_name_rsc
   vrName = spiderqueen_name_rsc
   vrIcon = spiderqueen_icon_rsc
   vrDesc = spiderqueen_desc_rsc
   vrDead_icon = spiderqueen_dead_icon_rsc
   vrDead_name = spiderqueen_dead_name_rsc

   viTreasure_type = TID_SPIDER_QUEEN
   viSpeed = SPEED_NONE
   viAttack_type = ATCK_WEAP_BITE
   viAttributes = MOB_NOMOVE
   viLevel = 165
   viDifficulty = 9
   viKarma = -90
   viDefault_behavior = AI_NOMOVE | AI_FIGHT_AGGRESSIVE | AI_FIGHT_SWITCHALOT
   viGender = GENDER_FEMALE   

   vrSound_hit = spiderqueen_sound_hit
   vrSound_miss = spiderqueen_sound_miss
   vrSound_aware = spiderqueen_sound_aware
   vrSound_death = spiderqueen_sound_death

properties:

   piAnimation = ANIM_NONE

messages:

   Constructed()
   {
      plResistances = [ [ 85, -ATCK_SPELL_ALL],
                        [ 85, ATCK_WEAP_MAGIC],
                        [ 85, ATCK_WEAP_BLUDGEON],
                        [-50, -ATCK_SPELL_ACID],
                        [-20, ATCK_WEAP_THRUST]
                      ];

      propagate;
   }

   MonsterAttack(what = $)
   {
      piAnimation = ANIM_ATTACK;
      Send(poOwner,@SomethingChanged,#what=self);
      piAnimation = ANIM_NONE;
      
      return;
   }

   % Increasing how far the spider queen can reach, since she's immobile.
   GetAttackRange()
   {
      return 3;
   }

   SendLookAnimation()
   {
      AddPacket(1,ANIMATE_CYCLE);
      AddPacket(4,200, 2,1, 2,1);

      return;
   }

   SendAnimation()
   {
      if piAnimation = ANIM_ATTACK
      {
         AddPacket(1,ANIMATE_ONCE, 4,200, 2,1, 2,3, 2,1);

         return;
      }

      propagate;
   }

   DestroyDisposable()
   {
      return;
   }

   HitSideEffect( what = $ )
   {
      local oSpell;

      if Random(1,POISON_CHANCE) = 1
      {
         oSpell = Send(SYS,@FindSpellByNum,#num=SID_POISON);
         Send(oSpell,@MakePoisoned,#who=what,#lossrate=POISON_LOSSRATE,
              #duration=POISON_DURATION);
      }

      return;
   }

   IsAlly(target = $)
   {
      if target = $
      {
         return FALSE;
      }

      if IsClass(target,&Spider)          
         OR IsClass(target,&SpiderBaby)
         OR IsClass(target,&SpiderQueen)
      {
         return TRUE;
      }

      propagate;
   }


end
%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
