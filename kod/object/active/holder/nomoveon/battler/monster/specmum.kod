% Meridian 59, Copyright 1994-2012 Andrew Kirmse and Chris Kirmse.
% All rights reserved.
%
% This software is distributed under a license that is described in
% the LICENSE file that accompanies it.
%
% Meridian is a registered trademark.


%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
SpectralMummy is Monster

constants:

   include blakston.khd

resources:

   include specmum.lkod

   SpectralMummy_koc_name_rsc = "kosnapleoc"
   SpectralMummy_name_rsc = "spectral mummy"
   SpectralMummy_icon_rsc = mummy.bgf
   SpectralMummy_desc_rsc = \
      "This poor creature was once a person who was mummified in an ancient "
      "ritual and put to rest in the crypt.  Ages have passed and only the "
      "spirit of the original person remains.  It has been dead and mummified "
      "for so long it has forgotten what it originally looked like and now "
      "wanders this plane as a spectral being, appearing as it did when it "
      "was originally put to rest."

   SpectralMummy_dead_icon_rsc = mummyX.bgf
   SpectralMummy_dead_name_rsc = "dead spectral mummy"

   SpectralMummy_sound_hit = mum_atkh.wav
   SpectralMummy_sound_miss = mum_atkm.wav
   SpectralMummy_sound_death = mum_die.wav
   SpectralMummy_sound_aware = mum_awr2.wav

classvars:

   vrKocName = SpectralMummy_koc_name_rsc
   vrName = SpectralMummy_name_rsc
   vrIcon = SpectralMummy_icon_rsc
   vrDesc = SpectralMummy_desc_rsc

   vrDead_icon = SpectralMummy_dead_icon_rsc
   vrDead_name = SpectralMummy_dead_name_rsc
   viDead_drawfx = DRAWFX_TRANSLUCENT_50

   viTreasure_type = TID_MUMMY
   viSpeed = SPEED_SLOW
   viLevel = 40
   viDifficulty = 5
   viVisionDistance = 8
   viKarma = 0
   vbIsUndead = TRUE
   viDefault_behavior = AI_FIGHT_AGGRESSIVE | AI_MOVE_FLEE_FRIGHTENERS
   viCashmin = 120
   viCashmax = 320
   vrSound_hit = SpectralMummy_sound_hit
   vrSound_miss = SpectralMummy_sound_miss
   vrSound_aware = SpectralMummy_sound_aware
   vrSound_death = SpectralMummy_sound_death

properties:

   piDrawfx = DRAWFX_TRANSLUCENT_50
   piDrawEffectFlag = OF_FLICKERING

   piReputationGroup = REP_UNDEAD

messages:

   Constructed()
   {
      plResistances = [ [-ATCK_SPELL_UNHOLY, 20 ],
                        [-ATCK_SPELL_HOLY, -10 ],
                        [-ATCK_SPELL_FIRE, -10 ],
                        [-ATCK_SPELL_SHOCK, -15 ],
                        [ATCK_WEAP_BLUDGEON, -15 ]
                      ];

      plReputationEnemies = [REP_FAROL];

      propagate;
   }

   MonsterAttack(what = $)
   {
      piAnimation = ANIM_ATTACK;
      Send(poOwner,@SomethingChanged,#what=self);
      piAnimation = ANIM_NONE;

      return;
   }

   SendMoveAnimation()
   {
      AddPacket(1,ANIMATE_CYCLE,4,200,2,2,2,5);

      return;
   }

   SendLookAnimation()
   {
      AddPacket(1,ANIMATE_CYCLE,4,200,2,2,2,5);

      return;
   }

   SendAnimation()
   {
      if piAnimation = ANIM_ATTACK
      {
         AddPacket(1,ANIMATE_ONCE,4,200,2,6,2,9,2,1);

         return;
      }

      propagate;
   }

   SendLightingInformation()
   {
      AddPacket(2,(LIGHT_FLAG_ON | LIGHT_FLAG_DYNAMIC));
      % 5 out of 255 intensity of light
      AddPacket(1,5);
      % Dark Greenish eerie glow.
      AddPacket(2,LIGHT_SPECTRAL);

      return;
   }

end
%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
