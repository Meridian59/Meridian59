% Meridian 59, Copyright 1994-2012 Andrew Kirmse and Chris Kirmse.
% All rights reserved.
%
% This software is distributed under a license that is described in
% the LICENSE file that accompanies it.
%
% Meridian is a registered trademark.


%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
StoneTroll is Monster

constants:

   include blakston.khd

resources:

   include stntroll.lkod

   StoneTroll_name_rsc = "Guardian of Zjiria"
   stonetroll_realname_rsc = "stone troll"
   StoneTroll_icon_rsc = Troll.bgf
   StoneTroll_desc_rsc = \
      "Given the ominous task of guarding the Heart of Zjiria, this stone "
      "troll glares darkly at all those passing by.  You have a feeling you "
      "better start running."

   StoneTroll_desc2_rsc = \
      "This stone troll is the grey-skinned brother of his more common "
      "brother.  Stone trolls were driven into the mountains ages ago, "
      "and were thought extinct.  However, more recently, they have emerged "
      "from the hills in small raiding parties."

   StoneTroll_koc_name_rsc = "ha'humoch"

   StoneTroll_dead_icon_rsc = TrollX.bgf
   StoneTroll_dead_name_rsc = "dead stonetroll"

   StoneTroll_sound_hit = trl_atkh.wav
   StoneTroll_sound_miss = trl_atkm.wav
   StoneTroll_sound_death = trl_dth.wav
   StoneTroll_sound_aware = trl_awr.wav

classvars:

   vrKocName = StoneTroll_koc_name_rsc
   vrIcon = StoneTroll_icon_rsc
   vrDead_icon = StoneTroll_dead_icon_rsc
   vrDead_name = StoneTroll_dead_name_rsc

   viTreasure_type = TID_MEDIUM_HUMAN
   viSpeed = SPEED_FAST
   viAttributes = 0
   viLevel = 120
   viDifficulty = 8
   viVisionDistance = 20
   % Attack range of 192, or 3 row/col.
   viAttackRange = 192
   viKarma = -100
   viDefault_behavior = AI_FIGHT_AGGRESSIVE
   viCashmin = 500
   viCashmax = 1000
   vrSound_hit = StoneTroll_sound_hit
   vrSound_miss = StoneTroll_sound_miss
   vrSound_aware = StoneTroll_sound_aware
   vrSound_death = StoneTroll_sound_death

   viDead_drawfx = DRAWFX_SECONDTRANS

properties:

   vrName = StoneTroll_realname_rsc
   vrDesc = StoneTroll_desc_rsc

   piAnimation = ANIM_NONE
   piDrawFX = DRAWFX_SECONDTRANS

   viReputationGroup = REP_TROLL

messages:

   Constructed()
   {
      plResistances = [ [-ATCK_SPELL_HOLY, 80 ],
                        [-ATCK_SPELL_ACID, 80 ],
                        [ATCK_WEAP_NERUDITE, -80 ],
                        [ATCK_WEAP_NONMAGIC, 80 ]
                      ];

      vlReputationEnemies = [REP_ORC];

      propagate;
   }

   MonsterAttack(what = $)
   {
      piAnimation = ANIM_ATTACK;
      Send(poOwner,@SomethingChanged,#what=self);
      piAnimation = ANIM_NONE;

      return;
   }

   SendLookAnimation()
   {
      AddPacket(1,ANIMATE_ONCE, 4,400, 2,4, 2,7, 2,1);

      return;
   }

   SendMoveAnimation()
   {
      AddPacket(1,ANIMATE_CYCLE, 4,200, 2,2, 2,3);

      return;
   }

   SendAnimation()
   {
      if piAnimation = ANIM_ATTACK
      {
         AddPacket(1,ANIMATE_ONCE, 4,200, 2,4, 2,7, 2,1);

         return;
      }

      % if no body animation
     propagate;
   }

   NewOwner(what=$)
   {
      if what <> $ AND Send(what,@GetRoomNum) = RID_I9
      {
         vrName = stonetroll_name_rsc;
         vrDesc = StoneTroll_desc_rsc;
      }
      else
      {
         vrName = StoneTroll_realname_rsc;
         vrDesc = StoneTroll_desc2_rsc;
      }

      propagate;
   }

   CanMorphTo()
   {
      % Too many abuses were caused by morphing into a stone troll. :(
      return FALSE;
   }

end
%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
