% Meridian 59, Copyright 1994-2012 Andrew Kirmse and Chris Kirmse.
% All rights reserved.
%
% This software is distributed under a license that is described in
% the LICENSE file that accompanies it.
%
% Meridian is a registered trademark.


%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
RiijaMonk is Temples

constants:

   include blakston.khd

resources:

   include kcrjmonk.lkod

   RiijaMonk_name_rsc = "Monk of Riija"
   RiijaMonk_icon_rsc = kcrjmonk.bgf
   RiijaMonk_desc_rsc = \
      "Floating several feet above the ground, this enigma of a being watches "
      "you.  When it speaks, the monk's voice crackles like century old "
      "parchment. You have a very uneasy feeling that all is not as it seems. "

   RiijaMonk_name_rsc2 = "spooky little girl"
   RiijaMonk_icon_rsc2 = kcrjgirl.bgf
   RiijaMonk_desc_rsc2 = \
      "With a sickly sweet child's voice and an unwholesome look of innocence, "
      "the priestess of Riija welcomes you to her inner sanctum of unreality."

   riijamonk_teach = \
      "Through ~I~rRiija's madness, ~byou can learn %s%s%s%s%s%s%s%s%s%s%s%s "
      "at that level."
   riijamonk_notworthy = \
      "~II see ~gno evidence that ~byou~g are ready to ~rlearn."
   riijamonk_worthy = "~IAre you ~rprepared for ~I~B~fenlightenment?"
   riijamonk_nobait = \
      "~lPerhaps if ~I`myou were ~Imore adept at ~cattracting "
      "~fmonsters `I`tyou would be ~y~I~Bable ~Bto learn ~p~ISeduce."

   riijamonk_dispel = "~B~rNever do that again!"
   riijamonk_respel = "Yrxlfle'man ~I~gGrn'Solagh ~rYippie ~bYup-Yup..."
   riijamonk_unwanted_give = \
      "~k%s%s tells you, \"~BFool,~I I do not need anything "
      "from ~n~r~Byou~n~k\""

classvars:

   vrName = RiijaMonk_name_rsc
   vrIcon = RiijaMonk_icon_rsc
   vrDesc = RiijaMonk_desc_rsc
   vrName2 = RiijaMonk_name_rsc2
   vrIcon2 = RiijaMonk_icon_rsc2
   vrDesc2 = RiijaMonk_desc_rsc2
   viMerchant_markup = MERCHANT_EXPENSIVE
   viKarma = 0
   viDefinite = ARTICLE_THE
   viIndefinite = ARTICLE_THE

   viAttributes = MOB_NOFIGHT | MOB_LISTEN | MOB_RANDOM | MOB_RECEIVE | MOB_TEACHER | MOB_PERM_QUESTER | MOB_SELLER
   viOccupation = MOB_ROLE_PRIESTESS

   viGender = GENDER_NEUTER
   vrTeach_message = riijamonk_teach
   vrUnwanted_give = riijamonk_unwanted_give

   viQuestID = 0

properties:

   pbIllusioned = TRUE
   piDrawEffectFlag = OF_BOUNCING

   ptGoIllusioned = $
   ptWorthyTimer = $
   piSpeechDelay = 8000

messages:

   Constructed()
   {
      piDrawEffectFlag = OF_BOUNCING;

      propagate;
   }

   Delete()
   {
      if ptGoIllusioned <> $
      {
         DeleteTimer(ptGoIllusioned);
         ptGoIllusioned = $;
      }

      propagate;
   }

   GetIcon()
   {
      if pbIllusioned
      {
         return vrIcon;
      }

      return vrIcon2;
   }

   GetName()
   {
      if pbIllusioned
      {
         return vrName;
      }

      return vrName2;
   }

   GetApparentName()
   {
      if pbIllusioned
      {
         return vrName;
      }

      return vrName2;
   }

   ShowDesc()
   {
      if pbIllusioned
      {
         AddPacket(4,vrDesc);

         return;
      }

      AddPacket(4,vrDesc2);

      return;
   }

   DispelIllusions()
   {
      if NOT pbIllusioned
      {
         return;
      }

      Post(poOwner,@SomeoneSaid,#what=self,#string=riijamonk_dispel,
            #type=SAY_RESOURCE);

      pbIllusioned = FALSE;
      piDrawfx = DRAWFX_NONE;
      piDrawEffectFlag = piDrawEffectFlag & ~OF_BOUNCING;
      Send(poOwner,@SomethingChanged,#what=self);

      if ptGoIllusioned <> $
      {
         DeleteTimer(ptGoIllusioned);
         ptGoIllusioned = $;
      }

      ptGoIllusioned=CreateTimer(self,@ReplaceIllusions,18000);

      return;
   }

   ReplaceIllusions()
   {
      ptGoIllusioned = $;

      if pbIllusioned
      {
         return;
      }

      Post(poOwner,@SomeoneSaid,#what=self,#string=riijamonk_respel,
            #type=SAY_RESOURCE);

      pbIllusioned = TRUE;
      piDrawEffectFlag = piDrawEffectFlag | OF_BOUNCING;
      Send(poOwner,@SomethingChanged,#what=self);

      return;
   }

   SetForSale()
   {
      plFor_Sale = [
         $,$,
         [
            SID_ANONYMITY,
            SID_APPARITION,
            SID_ARTIFICE,
            SID_SEDUCE,
            SID_FORGET,
            SID_MORPH,
            SID_REFLECTION,
            SID_ILLUSIONARY_FORM,
            SID_FORESIGHT,
            SID_VERTIGO,
            SID_DENIAL,
            SID_ILLUSIONARY_WOUNDS,
            SID_FLASH,
            SID_SHADOW_FORM,
            SID_ILLUSIONARY_FIREWALL,
            SID_BAIT,
            SID_FEIGN_DEATH,
            SID_ELUSION,
            SID_EVIL_TWIN 
         ],
      $];
      return;
   }

   CanAddSpell(who=$,num=$,report=TRUE)
   {
      local bWorthy, i, iAbility;

      bWorthy = FALSE;

      foreach i in Send(who,@GetPlayerUsing)
      {
         if IsClass(i,&RiijaSword) AND Send(i,@GetQuester) = who
         {
            bWorthy = TRUE;
         }
      }

      if report
         AND ptWorthyTimer = $
      {
         if not bWorthy
         {
            Post(who,@SomeoneSaid,#what=self,#string=riijamonk_notworthy,
                  #type=SAY_RESOURCE);
         }
         else
         {
            Post(who,@SomeoneSaid,#what=self,#string=riijamonk_worthy,
                  #type=SAY_RESOURCE);
         }

         % This is to stop the monk being so spammy.
         ptWorthyTimer = CreateTimer(self,@EndWorthyTimer,piSpeechDelay);
      }

      if NOT bWorthy
      {
         return FALSE;
      }

      if num = SID_SEDUCE
      {
         iAbility = Send(who,@GetSpellAbility,#spell_num=SID_BAIT);
         if iAbility < 89
         {
            Post(who,@SomeoneSaid,#what=self,#string=riijamonk_nobait,
                  #type=SAY_RESOURCE);

            return FALSE;
         }
      }

      propagate;
   }

   EndWorthyTimer(timer=$)
   {
      if ptWorthyTimer <> $
      {
         ptWorthyTimer = $;
      }

      return;
   }

   CanDoTeach(who=$,sid=0)
   {
      local iAbility;

      if sid = SID_SEDUCE
      {
         iAbility = Send(who,@GetSpellAbility,#spell_num=SID_BAIT);
         if iAbility < 89
         {
            Post(poOwner,@SomeoneSaid,#what=self,#string=riijamonk_nobait,
                  #type=SAY_RESOURCE);

            return FALSE;
         }
      }

      propagate;
   }

   GetFaction()
   {
      return FACTION_NEUTRAL;
   }

end
%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
