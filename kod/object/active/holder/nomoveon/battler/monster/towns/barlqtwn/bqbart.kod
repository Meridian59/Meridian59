% Meridian 59, Copyright 1994-2012 Andrew Kirmse and Chris Kirmse.
% All rights reserved.
%
% This software is distributed under a license that is described in
% the LICENSE file that accompanies it.
%
% Meridian is a registered trademark.


%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
BarloqueBartender is BarloqueTown

constants:

   include blakston.khd

   ANIM_WAVE = 1

resources:

   BarloqueBartender_name_rsc = "Meidei"
   BarloqueBartender_icon_rsc = bqbart.bgf
   BarloqueBartender_desc_rsc = "At one time Meidei only served the rich or those who tipped well, until "
      "recently when he \"found\" Shal'ille.  Now a hard core believer, he almost drives his own customers "
      "away from his ale."

   BaBar_entry_welcome = "Hail %s%s, and welcome to my humble tavern!  Praise Shal'ille!"
   BaBar_unwanted_keep = "~k%s%s tells you, \"I thank thee for thy offering.  I shall distribute it to the poor and needy.~n~k\""


classvars:

   vrName = BarloqueBartender_name_rsc
   vrIcon = BarloqueBartender_icon_rsc
   vrDesc = BarloqueBartender_desc_rsc
   viMerchant_markup = MERCHANT_EXPENSIVE
   viAttributes = MOB_NOFIGHT | MOB_RANDOM | MOB_NOMOVE | MOB_LISTEN | MOB_RECEIVE | MOB_SELLER | MOB_LAWFUL
   viOccupation = MOB_ROLE_BARTENDER

   vrUnwanted_keep = BaBar_unwanted_keep

properties:
   
messages:

   AffectMood(why = $,what = $)
   {
      if why = MOODMOD_FACTION_CHANGE
      {
         if what = FACTION_DUKE 
         {
            send( self, @SetMood, #new_mood = piMood - 1 );
            
            return;
         }
         
         if what = FACTION_PRINCESS 
         {
            send( self, @SetMood, #new_mood = piMood + 1 );
            
            return;
         }
      }
      
      if why = MOODMOD_ACCEPT_ITEM
      {
         if IsClass(what, &torch)
         {
            send( self, @SetMood, #new_mood = piMood + 1 );
         }
         
         return;
      }
      
      if why = MOODMOD_SELL_ITEM 
      {
         if IsClass(what,&Food) 
         {
            if send(what,@IsAlcohol) 
            {
               send(self,@SetMood,#new_mood=piMood-1);
            }
	      }
	      
	      return;
      }

      return;
   }

   UserEntered(who = $)
   {
      local iFlags;
      
      if send(who,@GetHomeRoom) = RID_MAR_INN 
      {
         send(self,@SetMood,#new_mood=piMood+1);
      }
      
      iFlags = send(who,@GetObjectFlags);
      if iFlags & PLAYER_PK
      {
         send(self,@SetMood,#new_mood=piMood-2);

         % don't say welcome to these guys
         propagate;
      }

      if iFlags & PLAYER_OUTLAW
      {
         send(self,@SetMood,#new_mood=piMood-1);
         
         % don't say welcome to these guys
         propagate;
      }
      
      Post(poOwner,@SomeoneSaid,#what = self, #string=BaBar_entry_welcome,
           #type = SAY_RESOURCE, 
           #parm1=Send(who,@GetDef),#parm2=Send(who,@GetName));
           
      propagate;
   }

   SetForSale()
   {
      plFor_sale=[[  Create(&WineGoblet),
                     Create(&Mug,#number=4),
                     Create(&Apple,#number=4),
                     Create(&Waterskin,#number=4),
                     Create(&Chess),
                     Create(&Bread,#number=4),
                     Create(&MeatPie,#number=4),
                     Create(&Cheese,#number=2)],$,$];
                  
      return;
   }

end

%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%

