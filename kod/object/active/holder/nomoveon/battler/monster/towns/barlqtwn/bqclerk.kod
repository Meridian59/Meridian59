% Meridian 59, Copyright 1994-2012 Andrew Kirmse and Chris Kirmse.
% All rights reserved.
%
% This software is distributed under a license that is described in
% the LICENSE file that accompanies it.
%
% Meridian is a registered trademark.


%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
BarloqueClerk is BarloqueTown

constants:

   include blakston.khd
   include protocol.khd

   % Number of pardons allowed in one term
   %  (unlike "pardon" spell, a justicar pardon takes you down one notch,
   %   from murderer to outlaw, or from outlaw to lawful)
   PARDON_LIMIT = 8

   BOOTH_WEST = 6
   BOOTH_EAST = 7
   BOOTH_NORTH = 12
   BOOTH_SOUTH = 14

   COMMAND_NONE = 0
   COMMAND_PARDON = 1
   COMMAND_REMAND = 2

   STATE_ACCEPTING_VOTES = 1
   STATE_TERM_IN_OFFICE  = 2
   STATE_NO_ELECTIONS = 3

resources:

   barloque_clerk_name_rsc = "Caramo"
   barloque_clerk_icon_rsc = bqclerk.bgf
   barloque_clerk_desc_rsc = \
      "Not your typical Royal bureaucrat.  "
      "She's attractive, charming, and by all accounts, honorable.  "
      "Caramo has spent years counting the Duke's money, "
      "filing the former Queen's taxation scrolls, and more recently, "
      "serving as clerk for the Office of the Justicar."

   % Accountability

   barloque_clerk_pardon = "pardon"
   barloque_clerk_pardons = "pardons"
   barloque_clerk_anoutlaw = "an outlaw"
   barloque_clerk_outlaws = "outlaws"
   barloque_clerk_amurderer = "a murderer"
   barloque_clerk_murderers = "murderers"
   barloque_clerk_alawful = "a lawful citizen"
   barloque_clerk_lawfuls = "lawful citizens"
   barloque_clerk_aperson = "a person"
   barloque_clerk_person = "person"
   barloque_clerk_people = "people"
   barloque_clerk_pardons_served1 = \
      "In this term, Justicar %q hath served %i %s."
   barloque_clerk_pardons_served2 = \
      "Of those, %i %s to %s, and %i to %s."

   % Problems

   barloque_clerk_no_justicar = \
      "In faith, there is no one serving as Justicar currently."
      
   % Intelligence

   barloque_clerk_summary_lawful = \
      "According to records, "
      "%q is a lawful citizen in keeping with the Royal Establishment."
   barloque_clerk_summary_outlaw = \
      "According to records, "
      "%q is an outlaw for having caused injury upon lawful citizens, "
      "or for breaking other laws of the land."
   barloque_clerk_summary_murderer1 = \
      "According to records, "
      "%q hath killed %i %s."
   barloque_clerk_summary_murderer2 = \
      "Of those, %i being %s, and %i %s justifiable by law."
   barloque_clerk_was = "was"
   barloque_clerk_were = "were"
   
   indulgence_not_enough_money = "You think the law will forgive you for partial measures? Pay the full amount or get out of my sight."
   indulgence_success = "Thank you for paying your debt to society. You will no longer be considered a murderer in the eyes of the law."
   indulgence_not_a_murderer = "Only murderers may buy indulgences."
   barloque_clerk_indulgence_1 = "Let it be known that "
   barloque_clerk_indulgence_2 = " has been granted an indulgence and pardoned.  So it has been spoken, so it shall be done.\n\n-- Caramo"
   barloque_clerk_indulgence_3 = " granted indulgence"
   
   no_more_ballots = "According to the Royal Paperwork Reduction Act, ballots are no longer used in this office."

classvars:

   vrName = barloque_clerk_name_rsc
   vrIcon = barloque_clerk_icon_rsc
   vrDesc = barloque_clerk_desc_rsc

   viAttributes = MOB_NOFIGHT | MOB_LISTEN | MOB_SELLER | MOB_NOMOVE | MOB_LAWFUL | MOB_FULL_TALK | MOB_RECEIVE
   viGender = GENDER_FEMALE

properties:

   piAnimation = ANIM_NONE

   piJusticar_State = STATE_NO_ELECTIONS

   piCommand = COMMAND_NONE
   
messages:

   Constructor()
   {
      plWantedItems = [ &BallotItem, &Money ];
      Send(SYS,@SetCaramo,#oCaramo=self);
      
      piJusticar_State = STATE_NO_ELECTIONS;

      propagate;
   }


   GotWantedItem(obj = $, who = $)
   {
      if isClass(obj, &BallotItem)
      {
         Post(poOwner,@SomeoneSaid,#what=self,#string=no_more_ballots,#type=SAY_RESOURCE);
         return;
      }

      if send(self,@CheckForIndulgence,#who=who,#obj=obj)
      {
         return;
      }
      
      propagate;
   }

   CheckForIndulgence(who=$, obj = 0)
   {
      local oMoney, oBank, amount;

      if not isClass(obj,&Money)
      {
         return false;
      }
      
      amount = send(obj,@GetNumber);
      if amount <= 0
      {
         return false;
      }

      % Check for hacked offers
      oMoney = Send(who,@GetMoneyObject);
      if oMoney = $ 
      {
	     return false;
      }
      if send(oMoney,@GetValue) < amount
      {
	     return false;
      }
      
      if NOT Send(who,@CheckPlayerFlag,#flag=PFLAG_MURDERER)
      {
         Post(poOwner,@SomeoneSaid,#what=self,#string=indulgence_not_a_murderer,#type=SAY_RESOURCE);
         return true;
      }

      if amount < Send(Send(SYS,@GetSettings),@GetPardonIndulgenceCost)
      {
         Post(poOwner,@SomeoneSaid,#what=self,#string=indulgence_not_enough_money,
	                  #type=SAY_RESOURCE);
       
	     return true;      
      }
      send(oMoney,@SubtractNumber,#number=amount);
      send(self,@PerformIndulgencePardonMurderer,#who=who);
      post(poOwner,@SomeoneSaid,#what=self,#string=indulgence_success,#type=SAY_RESOURCE);
      return true;
   }

   PerformIndulgencePardonMurderer(who=$)
   {
      Send(self,@RecordIndulgencePardonBookOfJala,#who=who);

      Send(who,@SetPlayerFlag,#flag=PFLAG_MURDERER,#value=FALSE);
      Send(who,@SetPlayerFlag,#flag=PFLAG_OUTLAW,#value=TRUE);

      return;
   }

   RecordIndulgencePardonBookOfJala(who=$, parm1=$, parm2=$)
   {
      local sString, sString2, oBook;

      ClearTempString();
      AppendTempString(barloque_clerk_indulgence_1);
      AppendTempString(Send(who,@GetTrueName));
      AppendTempString(barloque_clerk_indulgence_2);
      sString = CreateString();
      setString(sString,GetTempString());

      ClearTempString();
      AppendTempString(Send(who,@GetTrueName));
      AppendTempString(barloque_clerk_indulgence_3);
      sString2 = CreateString();
      setString(sString2,GetTempString());
      
      oBook = Send(poOwner,@GetBook);
      Send(oBook,@PostNews,#what=self,#title=sString2,#body=sString);

      return;
   }

   %%% Handling citizen commands.

   SomeoneSaid(what = $,type = $,string = $)
   {
      local iRow, iCol, oTarget;

      if what = $ OR NOT IsClass(what,&Player)
      {
         propagate;
      }

      % Public Commands
      if isClass(what,&User)
      {
         oTarget = Send(SYS,@FindUserByString,#string=string);      
         if oTarget <> $
         {
            Send(self,@CitizenRequestsPlayerRecord,#citizen=what,#who=oTarget);
            return;
         }
      }

      propagate;
   }

   CitizenRequestsPlayerRecord(citizen=$,who=$)
   {
      local iTotal, iJust, iUnjust, bMurderer, bOutlaw;

      if who = $
         OR citizen = $
      {
         return;
      }
      
     iJust = Send(who,@GetJustifiedKills);
     iUnjust = Send(who,@GetUnjustifiedKills);
     iTotal = iJust + iUnjust;
                           
     Send(self,@SayToOne,#target=citizen,
          #message_rsc=barloque_clerk_summary_murderer1,
          #parm1=Send(who,@GetTrueName),#type1=STRING_RESOURCE,
          #parm2=iTotal,
          #parm3=Send(self,@SingPlur,#num=iTotal,#sing=barloque_clerk_person,
                      #plur=barloque_clerk_people));

     Send(self,@SayToOne,#target=citizen,
          #message_rsc=barloque_clerk_summary_murderer2,
          #parm1=iUnjust,
          #parm2=Send(self,@SingPlur,#num=iUnjust,#sing=barloque_clerk_alawful,
                      #plur=barloque_clerk_lawfuls),
          #parm3=iJust,
          #parm4=Send(self,@SingPlur,#num=iJust,#sing=barloque_clerk_was,
                      #plur=barloque_clerk_were));

      return;
   }

   SingPlur(num=$,sing=$,plur=$)
   {
      if num = 1
      {
         return sing;
      }
      
      return plur;
   }

   ResetBrain()
   {
      poBrain = Send(SYS,@FindBrainByNum,#num=viBrain_type);
      
      return;
   }

   Delete()
   {
      Send(SYS,@DeleteCaramo);
      
      propagate;
   }


end
%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
