% Meridian 59, Copyright 1994-2012 Andrew Kirmse and Chris Kirmse.
% All rights reserved.
%
% This software is distributed under a license that is described in
% the LICENSE file that accompanies it.
%
% Meridian is a registered trademark.


%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
BarloqueInnkeeper is BarloqueTown

constants:

   include blakston.khd
   include protocol.khd

resources:

   include bqinnk.lkod

   Barloque_Innkeep_name_rsc = "Pritchett"
   Barloque_Innkeep_icon_rsc = bqinnk.bgf
   Barloque_Innkeep_desc_rsc = \
      "Of everyone in Barloque, the former sailor turned innkeeper Pritchett "
      "is the least haughty.  Perhaps it is because he was not originally from "
      "the Royal City and moved to North Barloque to buy a title."
   BqInn_entry_welcome = "I am gladeth to see you, mate."

   BqInn_mace_sale = \
      "Aye, ye might needeth one of them if ye plan on leavingeth our "
      "cultured city. I got some ye can buyeth."
   BqInn_unwanted_give = \
      "~k%s%s tells you, \"Whyfore dost you offer me that?~n~k\""

   BarloqueInnkeeper_room_rented = \
      "Here is the key to thine room.  I trust you will findest "
      "our rooms to be most well-appointed.  Enjoyest thy stay!"

   BarloqueInnkeeper_cost = \
      "The rooms here are the finest.  I needeth %i shillings when thou "
      "~Irent~neth a ~Iroom~n for for 85 days.  After that, I requireth %i "
      "per day up front.  Key copies costeth %i shillings if thou wishest to "
      "share thine room with a special someone."

   BarloqueInnkeeper_days_left = \
      "I thanketh thee.  Thou hast %q days left at my luxurious inn."
   BarloqueInnkeeper_too_long = \
      "It pleaseth me that ye liketh thine room, but thou hast paid enough "
      "in rent already!  I canst not accept more from you right now."

   BarloqueInnkeeper_destroy_key_copy = \
      "~kPritchett tells you, \"I will discardeth the key for thou.\""
   
   BqInn_room_command = "room"
   BqInn_rent_command = "rent"
   BqInn_cost_command = "cost"
   BqInn_copy_key_command = "copy key"

classvars:

   vrName = Barloque_Innkeep_name_rsc
   vrIcon = Barloque_Innkeep_icon_rsc
   vrDesc = Barloque_Innkeep_desc_rsc

   viAttributes = \
      MOB_NOFIGHT | MOB_RANDOM | MOB_LISTEN | MOB_NOMOVE \
         | MOB_RECEIVE | MOB_COND_SELLER | MOB_LAWFUL
   viOccupation = MOB_ROLE_INNKEEPER

   vrUnwanted_give = BqInn_unwanted_give

properties:

   piInitialRoomCost = 5000
   piPerDayCost = 75
   piKeyCopyCosts = 500

messages:

   UserEntered(who = $)
   {
      if Send(who,@GetHomeRoom) = RID_BAR_INN
      {
         Send(self,@SetMood,#new_mood=piMood + 2);
      }

      if Send(who,@GetAction) = UA_HAPPY
      {
         Send(self,@SetMood,#new_mood=piMood + 1);
      }

      if Send(who,@GetFaction) = FACTION_DUKE
      {
         Send(self,@SetMood,#new_mood=piMood - 2);
         % Don't welcome these guys

         propagate;
      }

      Post(who,@SomeoneSaid,#what=self,#string=BqInn_entry_welcome,
            #type=SAY_RESOURCE);

      propagate;
   }

   AffectMood(why=$,what=$)
   {
      if why = MOODMOD_ACCEPT_ITEM
      {
         Send(self,@SetMood,#new_mood=piMood+1);

         return;
      }

      if why = MOODMOD_FACTION_CHANGE
      {
         if what = FACTION_DUKE
         {
            Send(self,@SetMood,#new_mood=piMood-2);

            return;
         }

         if what = FACTION_PRINCESS
         {
            Send(self,@SetMood,#new_mood=piMood+2);

            return;
         }
      }
      
      if why = MOODMOD_DAWN
      {
         Send(self,@SetMood,#new_mood=piMood+1);

         return;
      }

      if why = MOODMOD_DUSK
      {
         Send(self,@SetMood,#new_mood=piMood-1);

         return;
      }

      if why = MOODMOD_SELL_ITEM
      {
         Send(self,@SetMood,#new_mood=piMood+2);

         return;
      }

      return;
   }

   SetForSale()
   {
      plFor_sale = [
         [ 
            Create(&LoadoutScroll,#labelled=TRUE),
            Create(&SetScroll,#labelled=TRUE),
            Create(&PantsC,#translation=PT_GRAY_TO_KGRAY),
            Create(&PantsC,#translation=PT_GRAY_TO_BLACK),
            Create(&TurkeyLeg)
         ],
      $,$,$];

      return;
   }

   InitCondSale()
   {
      local oObj;

      oObj=Create(&Mace);
      Send(Send(SYS,@GetLibrary),@AddToMobLib,#mob=self,
            #triggers=[Send(oObj,@GetName)],
            #action=[LIBACT_CONDITIONAL,oObj,100,BqInn_mace_sale]);

      return;
   }

   SomeoneSaid(what=$,type=$,string=$)
   {
      local oRoomMaint, rSayRsc;

      if IsClass(what,&User)
      {
         oRoomMaint = Send(SYS,@GetRentableRoomMaintenance);

         if (StringContain(string,BqInn_rent_command)
               AND StringContain(string,BqInn_room_command))
            OR (StringContain(string,"rent")
               AND StringContain(string,"room"))
         {
            rSayRsc = Send(oRoomMaint,@RentRoom,#who=what,
                           #iLocation=RID_BAR_INN,#iCost=piInitialRoomCost);

            % rSayRsc is $ on success.  Use the innkeeper's saying.
            if rSayRsc = $
            {
               rSayRsc = BarloqueInnkeeper_room_rented;
            }

            Post(poOwner,@SomeoneSaid,#type=SAY_RESOURCE,
                  #what=self,#string=rSayRsc);

            return;
         }

         if StringContain(string,BqInn_copy_key_command)
            OR StringContain(string,"copy key")
         {
            rSayRsc = Send(oRoomMaint,@CopyKey,#who=what,
                           #iLocation=RID_BAR_INN,#iCost=piKeyCopyCosts);

            % Tell the player.
            Post(poOwner,@SomeoneSaid,#type=SAY_RESOURCE,
                  #what=self,#string=rSayRsc);

            return;
         }

         if (StringContain(string,BqInn_room_command)
               AND StringContain(string,BqInn_cost_command))
            OR (StringContain(string,"room")
               AND StringContain(string,"cost"))
         {
            Post(poOwner,@SomeoneSaid,#what=self,#type=SAY_RESOURCE,
                  #string=BarloqueInnkeeper_cost,#type1=0,
                  #parm1=piInitialRoomCost,#parm2=piPerDayCost,
                  #parm3=piKeyCopyCosts);

            return;
         }
      }

      propagate;
   }

   ReqOffer(what=$,item_list=$)
   {
      local i, iMoney, oRoomMaint, iNewDays;

      % Check for if in the same room.
      if Send(what,@GetOwner) <> poOwner
      {
         Debug("ALERT! ",Send(what,@GetTrueName),what," offered items to NPC ",
               Send(self,@GetTrueName)," from distant RID ",
               Send(Send(what,@GetOwner),@GetRoomNum));

         return FALSE;
      }

      foreach i in item_list
      {
         if NOT IsClass(i,&Money)
         {
            if GetClass(i) = &RoomKeyCopy
            {
               % Say something and delete the key.
               Send(what,@MsgSendUser,
                     #message_rsc=BarloqueInnkeeper_destroy_key_copy);
               Send(i,@Delete);

               return FALSE;
            }

            propagate;
         }
      }

      iMoney = 0;
      foreach i in item_list
      {
         iMoney = iMoney + Send(i,@GetValue);
      }

      oRoomMaint = Send(SYS,@GetRentableRoomMaintenance);

      iNewDays = Send(oRoomMaint,@GotRent,#who=what,#iAmount=iMoney,
                        #iCost=piPerDayCost,#iLocation=RID_BAR_INN);

      if iNewDays <> $
      {
         % iNewDays = 0 means that we've trying to pay for than max days.
         if iNewDays = 0
         {
            Post(poOwner,@SomeoneSaid,#type=SAY_RESOURCE,#what=self,
                  #string=BarloqueInnkeeper_too_long);
         }
         else
         {
            Post(poOwner,@SomeoneSaid,#type=SAY_RESOURCE,#what=self,
                  #string=BarloqueInnkeeper_days_left,
                  #parm1=Send(SYS,@IntToString,#num=iNewDays),
                  #type1=STRING_RESOURCE);
         }

         return FALSE;
      }

      propagate;
   }

end
%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
