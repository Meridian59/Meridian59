% Meridian 59, Copyright 1994-2012 Andrew Kirmse and Chris Kirmse.
% All rights reserved.
%
% This software is distributed under a license that is described in
% the LICENSE file that accompanies it.
%
% Meridian is a registered trademark.


%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
BarloqueInnkeeper is BarloqueTown

constants:

   include blakston.khd
   include protocol.khd

resources:

   Barloque_Innkeep_name_rsc = "Pritchett"
   Barloque_Innkeep_icon_rsc = bqinnk.bgf
   Barloque_Innkeep_desc_rsc = "Of everyone in Barloque, the former sailor turned innkeeper Pritchett is "
      "the least haughty.  Perhaps it is because he was not originally from the Royal City and moved to "
      "North Barloque to buy a title."

   % Note: Pritchett's manner of speaking is intentionally bad/incorrect due to him being 
   % a non-native citizen of Barloque

   BqInn_entry_welcome = "I am gladeth to see you, mate."

   BqInn_mace_sale = "Aye, ye might needeth one of them if ye plan on leavingeth our cultured city. I got some ye can buyeth."
   BqInn_unwanted_give = "~k%s%s tells you, \"Whyfore dost you offer me that?~n~k\""

   BarloqueInnkeeper_room_rented = "Here is the key to thine room.  I trust you will findest "
      "our rooms to be most well-appointed.  Enjoyest thine stay!"

   BarloqueInnkeeper_cost = \
      "The rooms here are the finest.  I needeth %i shillings when thou ~Irent~neth a "
      "~Iroom~n for for 85 days.  After that, I requireth %i per day up front.  Key copies "
      "costeth %i shillings if thou wishest to share thine room with a special someone."

   BarloqueInnkeeper_days_left = "I thanketh thee.  Thou hast %i days left at my luxurious inn."
   BarloqueInnkeeper_too_long = "It pleaseth me that ye liketh thine room, but thou hast paid enough in rent already!  I canst not accept more from you right now."

   BarloqueInnkeeper_destroy_key_copy = "I will discardeth the key for thou."
   BarloqueInnkeeper_change_lock = \
      "Oh, hast ye misplaced one of thine key copies?  I shalt changeth the room lock at once "
      "for thou, denying any unwashed miscreants access therein!"
   
classvars:

   vrName = Barloque_Innkeep_name_rsc
   vrIcon = Barloque_Innkeep_icon_rsc
   vrDesc = Barloque_Innkeep_desc_rsc

   viAttributes = MOB_NOFIGHT | MOB_RANDOM | MOB_LISTEN | MOB_NOMOVE | MOB_RECEIVE | MOB_COND_SELLER | MOB_LAWFUL
   viOccupation = MOB_ROLE_INNKEEPER

   vrUnwanted_give = BqInn_unwanted_give

properties:
   
   piInitialRoomCost = 5000
   piPerDayCost = 75
   piKeyCopyCosts = 500

messages:

   UserEntered(who = $)
   {
      if send(who,@GetHomeRoom) = RID_BAR_INN
      {
         send(self,@SetMood,#new_mood=piMood + 2);
      }
      
      if Send(who,@GetAction) = UA_HAPPY
      {
         send(self,@SetMood,#new_mood=piMood + 1);
      }
      
      if Send(who,@GetFaction) = FACTION_DUKE
      {
         send(self,@SetMood,#new_mood=piMood - 2);
         % don't welcome these guys
         propagate;
      }
      
      Send(poOwner,@SomeoneSaid,#what=self,#string=BqInn_entry_welcome,#type=SAY_RESOURCE);
      
      propagate;
   }

   AffectMood(why = $, what = $)
   {
      if why = MOODMOD_ACCEPT_ITEM
      {
         send(self,@SetMood,#new_mood=piMood + 1);
         return;
      }
      
      if why = MOODMOD_FACTION_CHANGE 
      {
         if what = FACTION_DUKE 
         {
            send(self,@SetMood,#new_mood=piMood - 2);
            return;
         }
         if what = FACTION_PRINCESS
         {
            send(self,@SetMood,#new_mood=piMood + 2);
            return;
         }
      }
      
      if why = MOODMOD_DAWN 
      {
         send(self,@SetMood,#new_mood=piMood + 1);
         return;
      }
      
      if why = MOODMOD_DUSK 
      {
         send(self,@SetMood,#new_mood=piMood - 1);
         return;
      }
      
      if why = MOODMOD_SELL_ITEM 
      {
         send(self,@SetMood,#new_mood=piMood + 2);
         return;
      }

      return;
   }

   SetForSale()
   {
      plFor_sale=[ [ Create(&PantsC,#translation=PT_GRAY_TO_KGRAY),
                     Create(&PantsC,#translation=PT_GRAY_TO_BLACK),
                     Create(&TurkeyLeg)
                   ],
                   $,$,$];
      return;
   }

   InitCondSale()
   {
      local oObj;
      
      oObj=Create(&Mace);
      Send(Send(SYS,@GetLibrary),@AddToMobLib,#mob=self,
           #triggers=[Send(oObj,@GetName)],
           #action=[LIBACT_CONDITIONAL,oObj,100,BqInn_mace_sale]);

      return;
   }

   SomeoneSaid(what = $,type = $,string = $)
   {
      Send(Send(SYS, @GetRentableRoomMaintenance), @ParseInnkeeperCommands,
         #who=self, #what=what, #innRid=RID_BAR_INN, #speech=string,
         #initCost=piInitialRoomCost, #perDayCost=piPerDayCost, #keyCopyCost=piKeyCopyCosts,
         #rscRoomRented=BarloqueInnkeeper_room_rented, #rscRoomCost=BarloqueInnkeeper_cost,
         #rscChangeLock=BarloqueInnkeeper_change_lock);
      
      propagate;
   }

   ReqOffer(what = $,item_list = $)
   {  
      % Check for if in the same room.
      if send(what,@GetOwner) <> poOwner
      {
         debug("ALERT! ",send(what,@GetTrueName),what," offered items to NPC ",send(self,@GetTrueName)," from distant RID ",send(send(what,@GetOwner),@GetRoomNum));
         
         return FALSE;
      }     

      if Send(Send(SYS, @GetRentableRoomMaintenance), @ReqOfferedRoomItems, #who=self, #what=what,
         #itemList=item_list, #innRid=RID_BAR_INN, #perDayCost=piPerDayCost,
         #rscDaysLeft=BarloqueInnkeeper_days_left, #rscTooLong=BarloqueInnkeeper_too_long,
         #rscDestroyKeyCopy=BarloqueInnkeeper_destroy_key_copy)
      {
         return FALSE;
      }

      propagate;
   }


end

%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
