% Meridian 59, Copyright 1994-2012 Andrew Kirmse and Chris Kirmse.
% All rights reserved.
%
% This software is distributed under a license that is described in
% the LICENSE file that accompanies it.
%
% Meridian is a registered trademark.


%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
BarloqueMerchant is BarloqueTown

constants:

   include blakston.khd

resources:

   include bqmerch.lkod

   barloque_merch_name_rsc = "Herbutte"
   barloque_merch_icon_rsc = bqmerch.bgf
   barloque_merch_desc_rsc = \
      "Fancying himself a connoisseur of the finer things, Herbutte is little "
      "more than a old school aristocratic snob.  Within him is a dark streak "
      "of contempt for nearly all save royalty.  He will never run out of bad "
      "things to say about the other towns and the low life transients that "
      "wander between them."

   Bqmerch_unwanted_give = \
      "~k%s%s tells you, \"Wherefore dost thou offer to me such common "
      "baubles?!  I have servants who go to market to buy such "
      "things for me.~n~k\""

classvars:
   vrName = barloque_merch_name_rsc
   vrIcon = barloque_merch_icon_rsc
   vrDesc = barloque_merch_desc_rsc

   viMerchant_markup = MERCHANT_DISCOUNT

   viAttributes = \
      MOB_NOFIGHT | MOB_RANDOM | MOB_LISTEN | \
      MOB_BUYER | MOB_SELLER | MOB_NOMOVE | MOB_LAWFUL
   viOccupation = MOB_ROLE_MERCHANT

   vrUnwanted_give = Bqmerch_unwanted_give

properties:

messages:

   UserEntered(who=$)
   {
      if Send(who,@CheckPlayerFlag,#flag=PFLAG_MURDERER)
      {
         Send(self,@SetMood,#new_mood=piMood - 3 );
      }

      if Send(who,@CheckPlayerFlag,#flag=PFLAG_OUTLAW)
      {
         Send(self,@SetMood,#new_mood=piMood-2);
      }

      if Send(who,@GetHomeRoom) = RID_BAR_INN
      {
         Send(self,@SetMood,#new_mood=piMood+1);
      }

      if Send(who,@GetHomeRoom) = RID_TOS_INN
      {
         Send(self,@SetMood,#new_mood=piMood-1);
      }

      propagate;
   }

   AffectMood(why=$,what=$)
   {
      if why = MOODMOD_ACCEPT_ITEM
      {
         if Send(self,@IsObjectGem,#what=what)
         {
            Send(self,@SetMood,#new_mood=piMood+1);
         }

         return;
      }

      if why = MOODMOD_FACTION_CHANGE
      {
         if what = FACTION_DUKE
         {
            Send(self,@SetMood,#new_mood=piMood-1);

            return;
         }

         if what = FACTION_PRINCESS
         {
            Send(self,@SetMood,#new_mood=piMood+1);

            return;
         }
      }

      if why = MOODMOD_DAWN
      {
         Send(self,@SetMood,#new_mood=piMood+1);

         return;
      }

      if why = MOODMOD_DUSK
      {
         Send(self,@SetMood,#new_mood=piMood-1);

         return;
      }

      if why = MOODMOD_SELL_ITEM
      {
         if Send(self,@IsObjectGem,#what=what)
         {
            Send(self,@SetMood,#new_mood=piMood+2);
         }

         return;
      }

      return;
   }

   ObjectDesired(what=$)
   {
      if Send(self,@IsObjectGem,#what=what)
         OR Send(self,@IsObjectSundry,#what=what)
      {
         return TRUE;
      }

      return FALSE;
   }

   SetForSale()
   {
      plFor_sale = [
         [ 
            %Create(&GemRemove),
            Create(&Diamond,#number=1),
            Create(&Torch),
            Create(&Sapphire,#number=1),
            Create(&Emerald,#number=1)
         ],
      $,$,$];

      return;
   }

end
%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
