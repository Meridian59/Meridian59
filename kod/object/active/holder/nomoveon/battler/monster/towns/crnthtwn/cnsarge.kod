% Meridian 59, Copyright 1994-2012 Andrew Kirmse and Chris Kirmse.
% All rights reserved.
%
% This software is distributed under a license that is described in
% the LICENSE file that accompanies it.
%
% Meridian is a registered trademark.


%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
CorNothSergeant is CorNothTown

constants:

   include blakston.khd

resources:

   cornothsergeant_name_rsc = "Rook"
   cornothsergeant_icon_rsc = cnsarge.bgf
   cornothsergeant_desc_rsc = "A nobleman and former military officer, Rook carries himself with a stiff dignity.  "
      "He is a weaponsmaster, and makes a living passing his skills on to students of the "
      "warriors' path.  His feats with the sword are famous and unparalleled as is his love for a good drink."

   CnSer_forget_potion_sale = "Good, no self-respecting warrior shares company with those mana-crazed buffoons.  "
      "I will most gladly sell %s%s that strips all dependency on the so-called Warrior god from thy memory."
   
   CnSer_Alcohol_taken = "Ohhh, thank you! Here, have some of my old training swords! They won't last much longer, "
      "but maybe you can fix them up?"

classvars:

   vrName = cornothsergeant_name_rsc
   vrIcon = cornothsergeant_icon_rsc
   vrDesc = cornothsergeant_desc_rsc

   viMerchant_markup = MERCHANT_RIPOFF

   viAttributes = MOB_NOFIGHT | MOB_RANDOM | MOB_LISTEN | MOB_TEACHER | MOB_RECEIVE | MOB_NOMOVE | MOB_SELLER | MOB_COND_SELLER
   viOccupation = MOB_ROLE_SERGEANT

properties:
   
messages:

   Constructed()
   {
      plWantedItems = [ &Mug, &StoutGoblet, &wineGoblet, &KocatanMug, &Goblet ];
      propagate;
   }

   UserEntered(who = $)
   {
      if send(who,@GetFaction) = FACTION_DUKE
      {
         send(self,@SetMood,#new_mood=piMood - 3 );
      }
      
      if send(who,@GetFaction) = FACTION_PRINCESS
      {
         send(self,@SetMood,#new_mood=piMood + 3 );
      }
      
      return;
   }

   SetForSale()
   {
      plFor_sale = [
       [ Create(&LongSword),
         Create(&RingDefense),
         Create(&ShortSword),	
         Create(&Mace),
         Create(&LeatherArmor)],
       [ SKID_DODGE, SKID_PUNCH, SKID_SLASH, SKID_PROFICIENCY_AXE,
         SKID_PROFICIENCY_HAMMER, SKID_BRAWLING, SKID_PROFICIENCY_SHORTSWORD,
         SKID_PROFICIENCY_MACE, SKID_PROFICIENCY_SWORD, SKID_PARRY ],
         $,$];
      return;
   }
   
   InitCondSale()
   {
      local oObj;
      oObj=Create(&ForgetPotionKraanan);
      Send(Send(SYS,@GetLibrary),@AddToMobLib,#mob=self,
           #triggers=[Send(oObj,@GetName)],
           #action=[LIBACT_CONDITIONAL,oObj,2500,CnSer_forget_potion_sale]);
      return;
   }

   GotWantedItem(obj=$,who=$)
   {
      local oWeapon, iAmount, iHits;

      iAmount = send(obj,@GetNumber);

      if not send(obj,@IsBeverage)
      {
         propagate;
      }

      send(self,@SayToOne,#target=who,#message_rsc=CnSer_Alcohol_taken);

      Send(who,@RemoveNumberItemFromPossession,#ToBeRemoved=obj);
             
      while (iAmount > 0)
      {
         iHits = random(1,10);
         oWeapon = Create(&LongSword);
         Send(oWeapon,@SetHits,#number=iHits);
         
         if send(who,@ReqNewHold,#what=oWeapon)
         {
            Send(who,@NewHold,#what=oWeapon);
         }

         Send(obj,@Delete);

         iAmount = iAmount - 1;
      }
      
      return;
   }

end
%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
