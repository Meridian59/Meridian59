% Meridian 59, Copyright 1994-2012 Andrew Kirmse and Chris Kirmse.
% All rights reserved.
%
% This software is distributed under a license that is described in
% the LICENSE file that accompanies it.
%
% Meridian is a registered trademark.


%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
JasperBanker is JasperTown

constants:

   include blakston.khd

resources:

   include jsbanker.lkod

   JasperBanker_name_rsc = "Yevitan"
   JasperBanker_icon_rsc = jsbanker.bgf
   JasperBanker_desc_rsc = \
      "Yevitan studies you with thoughtful, intelligent eyes.  "
      "He is surprisingly spry for his age."

   JB_need_balance = \
      "Perhaps, If you utilized my bank more... I would offer you a "
      "special favor."
   JB_favored_status = \
      "To devoted disciples of Qor's magic, I teach how to blind your foes."

   JasperBanker_entry_welcome = "Hello %s%s, and welcome."

classvars:

   vrName = JasperBanker_name_rsc
   vrDesc = JasperBanker_desc_rsc
   vrIcon = JasperBanker_icon_rsc
   viAttributes = \
      MOB_NOFIGHT | MOB_BANKER | MOB_NOMOVE | MOB_LISTEN \
      | MOB_RANDOM | MOB_RECEIVE | MOB_TEACHER
   viOccupation = MOB_ROLE_BANKER
   viBank = BID_TOS

properties:

messages:

   Constructor()
   {
      plWantedItems=[&Money];

      propagate;
   }

   SetForSale()
   {
      plFor_sale = [ 
         $,$,
       [
            SID_BLIND
         ],
      $];

      return;
   }

   CanAddSpell(who=$,num=$)
   {
      if num=SID_BLIND
      {
         if 4999 > Send(Send(SYS,@FindBankByNum,#num=viBank),
                        @GetAccount,#what=who)
         {
            Post(poOwner,@SomeoneSaid,#what=self,#string=JB_need_balance,
                  #type=SAY_RESOURCE);

            return FALSE;
         }
         else
         {
            Post(poOwner,@SomeoneSaid,#what=self,#string=JB_favored_status,
                  #type=SAY_RESOURCE);
         }
      }

      propagate;
   }

   GetBankNum()
   {
      return viBank;
   }

   UserEntered(who=$)
   {
      if Send(who,@CheckPlayerFlag,#flag=PFLAG_OUTLAW)
      {
         Send(self,@SetMood,#new_mood=piMood+3);
      }
      else
      {
         if Send(who,@CheckPlayerFlag,#flag=PFLAG_MURDERER)
         {
            Send(self,@SetMood,#new_mood=piMood+3);
         }
         else
         {
            Send(self,@SetMood,#new_mood=piMood-3);
         }
      }


      if Send(who,@GetHomeRoom) = RID_JAS_INN
      {
         Send(self,@SetMood,#new_mood=piMood+2);
      }

      if Send(who,@GetFaction) = FACTION_NEUTRAL
      {
         Send(self,@SetMood,#new_mood=piMood+3);
      }

      Post(who,@SomeoneSaid,#what=self,#string=JasperBanker_entry_welcome,
            #type=SAY_RESOURCE,
            #parm1=Send(who,@GetDef),#parm2=Send(who,@GetName));

      return;
   }

end
%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
