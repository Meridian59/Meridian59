% Meridian 59, Copyright 1994-2012 Andrew Kirmse and Chris Kirmse.
% All rights reserved.
%
% This software is distributed under a license that is described in
% the LICENSE file that accompanies it.
%
% Meridian is a registered trademark.


%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
JasperMerchant is JasperTown

constants:

   include blakston.khd

resources:

   include jsmerch.lkod

   jaspermerchant_name_rsc = "Zhieu B'hob"
   jaspermerchant_icon_rsc = jsmerch.bgf
   jaspermerchant_desc_rsc = \
      "Zhieu B'hob works very hard to make his business thrive in this "
      "depressed community.  He is eager, and busy with the appearances."

   JsMer_entry_welcome = "Might I interest you in some fine merchandise?"

   JsMer_ruby_sale = \
      "Yes, I do have %s%s.  They are the pride of my small store."

classvars:

   vrName = jaspermerchant_name_rsc
   vrIcon = jaspermerchant_icon_rsc
   vrDesc = jaspermerchant_desc_rsc
   viAttributes = \
      MOB_NOFIGHT | MOB_SELLER | MOB_BUYER | MOB_NOMOVE \
      | MOB_RANDOM | MOB_LISTEN | MOB_COND_SELLER
   viOccupation = MOB_ROLE_MERCHANT
   viMerchant_markup = MERCHANT_RIPOFF

properties:

messages:

   AffectMood(what=$,why=$)
   {
      if why = MOODMOD_ACCEPT_ITEM
      {
         Send(self,@SetMood,#new_mood=piMood-2);
      }

      return;
   }

   SetForSale()
   {
      plFor_sale = [
         [  
            Create(&PantsC,#translation=PT_GRAY_TO_RED),
            Create(&PantsC,#translation=PT_GRAY_TO_LORANGE),
            Create(&PantsC,#translation=PT_GRAY_TO_LGREEN),
            Create(&PantsC,#translation=PT_GRAY_TO_LBGREEN),
            Create(&PantsC,#translation=PT_GRAY_TO_LSKY),
            Create(&PantsC,#translation=PT_GRAY_TO_LBLUE),
            Create(&PantsC,#translation=PT_GRAY_TO_LPURPLE),

            Create(&Shirt,#color=XLAT_TO_RED),
            Create(&Shirt,#color=XLAT_TO_ORANGE),
            Create(&Shirt,#color=XLAT_TO_GREEN),
            Create(&Shirt,#color=XLAT_TO_SKY),
            Create(&Shirt,#color=XLAT_TO_PURPLE),

            Create(&Torch),
            Create(&Waterskin),

            Create(&Sapphire,#number=1),
            Create(&Emerald,#number=2),
            Create(&PurpleMushroom,#number=2),
            Create(&BlueMushroom,#number=4),
            Create(&Mushroom,#number=4),
            Create(&Elderberry,#number=4),
            Create(&Herbs,#number=4)
         ],
         $,$,$];

      return;
   }

   ObjectDesired(what=$)
   {
      if Send(what,@CanBeGiventoNPC)
         AND Send(self,@IsObjectReagent,#what=what)
      {
         return TRUE;
      }

      return FALSE;
   }

   InitCondSale()
   {
      local oObj;

      oObj = Create(&Ruby,#number=4);

      Send(Send(SYS,@GetLibrary),@AddToMobLib,#mob=self,
            #triggers=[Send(oObj,@GetName)],
            #action=[LIBACT_CONDITIONAL,oObj,250,JsMer_ruby_sale]);

      return;
   }

   UserEntered(who=$)
   {
      if Send(who,@CheckPlayerFlag,#flag=PFLAG_OUTLAW)
      {
         Send(self,@SetMood,#new_mood=piMood-2);
      }

      if Send(who,@GetHomeRoom) = RID_JAS_INN
      {
         Send(self,@SetMood,#new_mood=piMood+2);
      }

      Post(who,@SomeoneSaid,#what=self,#string=JsMer_entry_welcome,
            #type=SAY_RESOURCE);

      propagate;
   }

end
%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%

