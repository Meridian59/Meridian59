% Meridian 59, Copyright 1994-2012 Andrew Kirmse and Chris Kirmse.
% All rights reserved.
%
% This software is distributed under a license that is described in
% the LICENSE file that accompanies it.
%
% Meridian is a registered trademark.


%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
KocatanTown is Towns

constants:

   include blakston.khd
   
resources:

   include kocatwn.lkod

   kocatan_may_pass = "You may pass."
   kocatan_no_alcohol = "No smuggling alcohol to the natives!"
   kocatan_no_nerudite = \
      "The avars must not be permitted to get their hands on raw nerudite!"
   kocatan_no_weapons = \
      "It is forbidden to carry into the jungle any weapons other than those "
      "that are necessary to ensure the safety of the traveller."
   kocatan_no_armor = "You may only carry two suits of armor into the jungle!"
   kocatan_no_helm = "You may only take two helmets with you out there."
   kocatan_no_gauntlets = "Only two sets of gauntlets permitted."
   kocatan_no_shields = \
      "You may only take two shields plus a soldier shield with "
      "you into the outland."

messages:

   CanPass(what=$)
   {
      local i, iArmor, bNerudite, iWeapon, iGauntlet,
            iHelmet, iShield, bAlcohol;

      iWeapon = 0;
      iArmor = 0;
      iShield = 0;
      iHelmet = 0;
      iGauntlet = 0;
      bAlcohol = FALSE;
      bNerudite = FALSE;

      foreach i in Send(what,@GetHolderPassive)
      {
         % Don't include the assassin's weapon as a real weapon.
         if IsClass(i,&Weapon) AND NOT IsClass(i,&BlackDagger)
         {
            iWeapon = iWeapon + 1;
         }

         if IsClass(i,&Gauntlet)
         {
            iGauntlet = iGauntlet + 1;
         }

         if IsClass(i,&Shield)
            AND NOT IsClass(i,&Torch)
            AND NOT IsClass(i,&SoldierShield)
         {
            iShield = iShield + 1;
         }

         if IsClass(i,&Armor)
            AND NOT (IsClass(i,&Robe)
               OR IsClass(i,&LightRobe))
         {
            iArmor = iArmor + 1;
         }

         if IsClass(i,&Helmet)
            AND NOT (IsClass(i,&Circlet)
               OR IsClass(i,&IvyCirclet)
               OR IsClass(i,&FaceMask))
         {
            iHelmet = iHelmet + 1;
         }

         if IsClass(i,&Food) AND Send(i,@IsAlcohol)
         {
            bAlcohol = TRUE;
         }

         if IsClass(i,&NeruditeOreChunk)
         {
            bNerudite = TRUE;
         }
      }

      if (iWeapon < 5) AND (iArmor < 3)
         AND (iShield < 3) AND (iHelmet < 3) AND (iGauntlet < 3)
         AND (NOT bAlcohol) AND (NOT bNerudite)
      {
         Send(self,@say,#message_rsc = kocatan_may_pass);

         return TRUE;
      }

      % Okay, at this point, we know they are illegal.  If we are the evil
      % guy, we may allow them in anyway.
      if Send(self,@AllowContrabandToPass,#what=what)
      {
         return TRUE;
      }

      if bAlcohol
      {
         Send(self,@Say,#message_rsc=kocatan_no_alcohol);
         
         return FALSE;
      }
      
      if bNerudite
      {
         Send(self,@Say,#message_rsc=kocatan_no_nerudite);

         return FALSE;
      }

      if iWeapon > 4
      {
         Send(self,@Say,#message_rsc=kocatan_no_weapons);

         return FALSE;
      }

      if iArmor > 2
      {
         Send(self,@Say,#message_rsc=kocatan_no_armor);

         return FALSE;
      }

      if iHelmet > 2
      {
         Send(self,@Say,#message_rsc=kocatan_no_helm);

         return FALSE;
      }

      if iGauntlet > 2
      {
         Send(self,@Say,#message_rsc=kocatan_no_gauntlets);

         return FALSE;
      }

      if iShield > 2
      {
         Send(self,@Say,#message_rsc=kocatan_no_shields);

         return FALSE;
      }

      return FALSE;
   }

   AllowContrabandToPass()
   {
      return FALSE;
   }

end
%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
