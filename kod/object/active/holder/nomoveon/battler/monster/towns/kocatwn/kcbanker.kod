% Meridian 59, Copyright 1994-2012 Andrew Kirmse and Chris Kirmse.
% All rights reserved.
%
% This software is distributed under a license that is described in
% the LICENSE file that accompanies it.
%
% Meridian is a registered trademark.


%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
KocatanBanker is KocatanTown

constants:

   include blakston.khd

resources:

   include kcbanker.lkod

   Kocatanbanker_name_rsc = "Huital ko'Nosak"
   kocatanbanker_icon_rsc = kcbanker.bgf
   kocatanbanker_desc_rsc =  \
      "Well respected as the master of all monetary affairs on the island, "
      "Huital pays close attention to the economy and how it relates to "
      "every business.  Before engaging in any new transaction of coins, "
      "he quickly washes his hands in the pedestal of clear water. "

   kocatanbanker_entry_welcome = \
      "Greetings.  We hope to serve you well, your valuables will be "
      "safe with us."
   kocatanbanker_unwanted_give = \
      "~k%s%s tells you, \"You mistake me for my brother.  He will "
      "store your items.~n~k\""

classvars:

   vrName = Kocatanbanker_name_rsc
   vrIcon = Kocatanbanker_icon_rsc
   vrDesc = Kocatanbanker_desc_rsc
   viMerchant_markup = MERCHANT_EXPENSIVE

   viAttributes = \
      MOB_NOFIGHT | MOB_SELLER | MOB_RANDOM | MOB_LISTEN \
      | MOB_NOMOVE | MOB_RECEIVE | MOB_BANKER
   viOccupation = MOB_ROLE_BANKER

   viBank = BID_KOCATAN

   vrUnwanted_give = kocatanbanker_unwanted_give

properties:

messages:

   Constructor()
   {
      plWantedItems = [&Money];

      propagate;
   }

   AffectMood(why=$,what=$)
   {
      if why = MOODMOD_DAWN
      {
         Send(self,@SetMood,#new_mood=piMood+5);

         return;
      }

      if why = MOODMOD_DUSK
      {
         Send(self,@SetMood,#new_mood=piMood-5);

         return;
      }

      if why = MOODMOD_BANK_WITHDRAWAL
      {
         Send(self,@SetMood,#new_mood=piMood-2);

         return;
      }

      if why = MOODMOD_BANK_DEPOSIT
      {
         Send(self,@SetMood,#new_mood=piMood+2);

         return;
      }

      return;
   }

   UserEntered(who=$)
   {
      if Send(who,@CheckPlayerFlag,#flag=PFLAG_MURDERER)
      {
         Send(self,@SetMood,#new_mood=piMood-1);

         % Don't welcome these guys.
         propagate;
      }

      if Send(who,@CheckPlayerFlag,#flag=PFLAG_OUTLAW)
      {
         Send(self,@SetMood,#new_mood=piMood-1);

         % Don't welcome these guys.
         propagate;
      }

      Post(who,@SomeoneSaid,#what=self,#string=Kocatanbanker_entry_welcome,
             #type = SAY_RESOURCE);

      propagate;
   }

   GetBankNum()
   {
      return viBank;
   }

end
%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
