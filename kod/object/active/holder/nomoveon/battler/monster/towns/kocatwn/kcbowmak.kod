% Meridian 59, Copyright 1994-2012 Andrew Kirmse and Chris Kirmse.
% All rights reserved.
%
% This software is distributed under a license that is described in
% the LICENSE file that accompanies it.
%
% Meridian is a registered trademark.


%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
KocatanBowMaker is KocatanTown

constants:

   include blakston.khd

resources:

   KocatanBowMaker_name_rsc = "Hanla zax'Ta"
   KocatanBowMaker_icon_rsc = kcbowmak.bgf
   KocatanBowMaker_desc_rsc = "This becoming, young woman appears as cold and curt to new faces.  "
      "Despite that, it is obvious to all that her true passion lies in the crafting of fine bows."

   KocatanBowMaker_KriipaClaw_sale = \
         "I have claws from several Kriipa I've killed in the jungle.  The Kriipa is a truly"
         "fearsome beast, as I'm sure you know, and a hunter who wishes to obtain their claws must pay for them"
         "in blood spilt in battle.  You will understand, then, that my price for them is high."
   
   KocatanBowMaker_NeruditeArrow_sale = \
         "You're after a bit more bite in your shots, hmm? Then the nerudite arrow is what you seek. "
         "I carve the green stone arrowheads by hand - delicate work, but it lends them extra force. "
         "If that stone shatters on impact, it can be downright fatal for whatever poor beast - "
         "or unlucky soul - stands in your way. Of course, true craftsmanship doesn't come cheap. "
         "Still interested? Then let's see how many your coinpurse can manage."

   KocatanBowMaker_FireArrow_sale = \
         "Looking to scorch your prey, are you? These fire arrows are fitted with searing-hot, enchanted heads -"
         "perfect for setting flammable foes alight. Don't mistake that glow for mere show; "
         "they can burn straight through flesh and wood alike, if you aim true. "
         "Quality like this doesn't come cheap, of course. Ready to part with your coin?"

   KocatanBowMaker_SilverArrow_sale = \
         "If you plan to make short work of the undead, you'll want these. The clean edge "
         "tears flesh and sinew alike, leaving wounds few creatures can shrug off - especially those "
         "that crawl from the grave. Quality doesn't come cheap, though. Care to buy?"


   KocatanBowMaker_unwanted_give = "~k%s%s tells you, \"I have enough of those type of supplies to hold us through the summer.~n~k\""

   
classvars:

   vrName = KocatanBowMaker_name_rsc
   vrIcon = KocatanBowMaker_icon_rsc
   vrDesc = KocatanBowMaker_desc_rsc
   viMerchant_markup = MERCHANT_EXPENSIVE

   viAttributes = MOB_NOFIGHT | MOB_SELLER | MOB_RANDOM | MOB_LISTEN | MOB_NOMOVE | MOB_RECEIVE
   viOccupation = MOB_ROLE_BLACKSMITH

   viGender = GENDER_FEMALE

   vrUnwanted_give = KocatanBowMaker_unwanted_give


properties:
   
messages:

   AffectMood(why = $, what = $)
   {
      if why = MOODMOD_DAWN
      {
         send(self,@SetMood,#new_mood=piMood + 2);

         return;
      }

      if why = MOODMOD_DUSK
      {
         send(self,@SetMood,#new_mood=piMood - 2);

         return;
      }

      if why = MOODMOD_SELL_ITEM
      {
         if IsClass(what,&LongBow)
         {
            send(self,@SetMood,#new_mood=piMood - 2);
         }

         return;
      }

      return;
   }

   UserEntered(who = $)
   {
      if Send(who,@GetGender) = GENDER_MALE
      {
         send(self,@SetMood,#new_mood=piMood - 1);
      }

      if Send(who,@GetGender) = GENDER_FEMALE
      {
         send(self,@SetMood,#new_mood=piMood + 2);
      }

      return;
   }

   SetForSale()
   {
      plFor_sale = [ [ Create(&LongBow),
                       Create(&Arrow)
                     ],
                     $,$,$
                   ];
      return;
   }

   InitCondSale()
   {
      local oObj,oObj2,oObj3,oObj4;

      oObj=Create(&KriipaClaw,#number=4);
      Send(Send(SYS,@GetLibrary),@AddToMobLib,#mob=self,
           #triggers=[Send(oObj,@GetName)],
           #action=[LIBACT_CONDITIONAL,oObj,250,KocatanBowMaker_KriipaClaw_sale]);

      oObj2=Create(&NeruditeArrow,#number=50);
      Send(Send(SYS,@GetLibrary),@AddToMobLib,#mob=self,
           #triggers=[Send(oObj2,@GetName)],
           #action=[LIBACT_CONDITIONAL,oObj2,50,KocatanBowMaker_NeruditeArrow_sale]);

      oObj3=Create(&SilverArrow,#number=50);
      Send(Send(SYS,@GetLibrary),@AddToMobLib,#mob=self,
           #triggers=[Send(oObj3,@GetName)],
           #action=[LIBACT_CONDITIONAL,oObj3,60,KocatanBowMaker_SilverArrow_sale]);

      oObj4=Create(&FireArrow,#number=50);
      Send(Send(SYS,@GetLibrary),@AddToMobLib,#mob=self,
           #triggers=[Send(oObj4,@GetName)],
           #action=[LIBACT_CONDITIONAL,oObj4,70,KocatanBowMaker_FireArrow_sale]);

      return;
   }


end
%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%


