% Meridian 59, Copyright 1994-2012 Andrew Kirmse and Chris Kirmse.
% All rights reserved.
%
% This software is distributed under a license that is described in
% the LICENSE file that accompanies it.
%
% Meridian is a registered trademark.


%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
KocatanInnkeeper is KocatanTown

constants:

   include blakston.khd
   include protocol.khd

resources:

   Kocataninnkeeper_name_rsc = "Moxal ix'Hanak"
   kocataninnkeeper_icon_rsc = kcinnk.bgf
   kocataninnkeeper_desc_rsc =  \
     "Entrepreneurial by nature, Moxal ix'Hanak is always on the look out "
     "for a new high risk investment.  He greets you with a confident smile "
     "and a clandestine gleam in his eye."

   kocataninnkeeper_entry_welcome = "Welcome, good patron.  I hope you find your stay to be a pleasant one."
   kocinn_winknudge = "Thanks!  I'll be sure to let you know how it all turns out!"
   kocataninnkeeper_unwanted_keep = "~k%s%s tells you, \"Thank you.  You won't regret letting me borrow that.~n~k\""

   KocatanInkeeper_purplemushroom_sale = "Ah!  You would like %s%s.  Yes, I imported these wondrous beauties directly from the mainland.  Guaranteed to make you stronger if you eat 3000 of them, I've heard!"

   % Rentable room stuff.
   KocatanInnkeeper_room_rented = \
      "My friend, you definitely know a good deal when you see one!  Please "
      "enjoy your stay here, and come see me right away if you have any questions, "
      "concerns, or if you want in on a rather lucrative prospect that's come to "
      "my attention recently!"

   KocatanInnkeeper_cost = \
      "You'll not find a better deal on the island!  It costs %i shillings to "
      "~Irent~n a ~Iroom~n at my fine establishment for 85 days.  If you "
      "want to stay longer, that'll be %i shillings per day, paid in advance.  "
      "For a very modest fee of %i shillings, I can even provide you "
      "with a copy of your room key.  Act now, because this offer won't last "
      "long!  Space is very limited and my rooms are first-come, first-serve!"

   KocatanInnkeeper_days_left = \
      "Thank you, my friend!  I'll have triple this amount for you after "
      "an associate of mine can...  Oh right, you're just paying rent.  Your "
      "room is paid for up to %i days now, so make yourself right at home!"
   KocatanInnkeeper_too_long = \
      "Oh boy, I know of some amazing investment opportunities that could "
      "make us both rich beyond our wildest dreams with this!  Alas, I'd hate "
      "to lose my favorite patron and business partner should things not "
      "quite pan out, so maybe you should hang onto that money for a while "
      "longer.  Your rent isn't due right now either, but come see me again "
      "when it is."

   KocatanInnkeeper_destroy_key_copy = \
      "Oh, you don't want this key copy anymore?  "
      "Just this once, I'll take it off your hands for free as a courtesy."
   KocatanInnkeeper_change_lock = \
      "Alright, your existing key copies will be invalidated right away.  "
      "If you want new copies, I'll of course be more than happy to sell you "
      "some at a bargain price."

classvars:

   vrName = Kocataninnkeeper_name_rsc
   vrIcon = Kocataninnkeeper_icon_rsc
   vrDesc = Kocataninnkeeper_desc_rsc

   viMerchant_markup = MERCHANT_EXPENSIVE

   viAttributes = MOB_NOFIGHT | MOB_SELLER | MOB_RANDOM | MOB_LISTEN | MOB_NOMOVE | MOB_RECEIVE | MOB_COND_SELLER
   viOccupation = MOB_ROLE_INNKEEPER

   vrUnwanted_keep = kocataninnkeeper_unwanted_keep

properties:   

   piInitialRoomCost = 5000
   piPerDayCost = 75
   piKeyCopyCosts = 500

messages:

   AffectMood(why = $, what = $)
   {
      if why = MOODMOD_SELL_ITEM
      {
         send(self,@SetMood,#new_mood=piMood + 1);
      }
      if why = MOODMOD_DAWN
      {
         send(self,@SetMood,#new_mood=piMood + 3);
         return;
      }
      if why = MOODMOD_DUSK
      {
         send(self,@SetMood,#new_mood=piMood - 3);
         return;
      }
      return;
   }

   Constructor()
   {
       plWantedItems = [&Money];
       propagate;
   }

   GotWantedItem(obj = $, who = $)
   {
      if send(self,@CheckForScheme,#obj=obj,#who=who)
      {
         return;
      }
      
      propagate;     
   }

   CheckForScheme(who=$, obj = 0)
   {
      local oMoney, amount;

      if not isClass(obj,&Money)
      {
         return FALSE;
      }
      
      amount = send(obj,@GetNumber);

      if amount <= 0
      {
         return FALSE;
      }

      oMoney = Send(who,@GetMoneyObject);
      
      if oMoney = $ 
      {
         return FALSE;
      }
      
      if send(oMoney,@GetValue) < amount
      {
         return FALSE;
      }
      
      Send(oMoney,@SubtractNumber,#number=amount);            
      post(self,@SayToOne,#target=who,#message_rsc=kocinn_winknudge);

      return TRUE;
   }

   SetForSale()
   {
      plFor_sale = [ [                       
                       Create(&Meatpie,#number=4),
                       Create(&Bread,#number=4),
                       Create(&Cheese,#number=6)
	               ],
                      $, $, $ ];
      return;
   }

   UserEntered(who = $)
   {
      if Send(who,@GetAction) = UA_HAPPY
      {
         send(self,@SetMood,#new_mood=piMood + 2);
      }
      if Send(who,@GetAction) = UA_SAD
      {
         send(self,@SetMood,#new_mood=piMood + 1);
      }
      if Send(who,@GetAction) = UA_WRY
      {
         send(self,@SetMood,#new_mood=piMood - 2);
      }
      
      Send(poOwner,@SomeoneSaid,#what=self,#string=Kocataninnkeeper_entry_welcome,
             #type = SAY_RESOURCE);
             
      propagate;
   }

   InitCondSale()
   {
      local oObj;
      
      oObj = Create(&PurpleMushroom,#number=2);
      Send(Send(SYS,@GetLibrary),@AddToMobLib,#mob=self,
           #triggers=[Send(oObj,@GetName)],
           #action=[LIBACT_CONDITIONAL,oObj,1250,KocatanInkeeper_purplemushroom_sale]);
           
      return;
   }

   SomeoneSaid(what=$,string=$)
   {
      Send(Send(SYS, @GetRentableRoomMaintenance), @ParseInnkeeperCommands,
         #who=self, #what=what, #innRid=RID_KOC_INN, #speech=string,
         #initCost=piInitialRoomCost, #perDayCost=piPerDayCost, #keyCopyCost=piKeyCopyCosts,
         #rscRoomRented=KocatanInnkeeper_room_rented, #rscRoomCost=KocatanInnkeeper_cost,
         #rscChangeLock=KocatanInnkeeper_change_lock);

      propagate;
   }

   ReqOffer(what = $,item_list = $)
   {
      % Check for if in the same room.
      if Send(what,@GetOwner) <> poOwner
      {
         Debug("ALERT! ",Send(what,@GetTrueName),what," offered items to NPC ",
               Send(self,@GetTrueName)," from distant RID ",
               Send(Send(what,@GetOwner),@GetRoomNum));

         return FALSE;
      }

      if Send(Send(SYS, @GetRentableRoomMaintenance), @ReqOfferedRoomItems, #who=self, #what=what,
         #itemList=item_list, #innRid=RID_KOC_INN, #perDayCost=piPerDayCost,
         #rscDaysLeft=KocatanInnkeeper_days_left, #rscTooLong=KocatanInnkeeper_too_long,
         #rscDestroyKeyCopy=KocatanInnkeeper_destroy_key_copy)
      {
         return FALSE;
      }

      propagate;
   }
   
end
%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
