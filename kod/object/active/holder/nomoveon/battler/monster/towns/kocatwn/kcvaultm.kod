% Meridian 59, Copyright 1994-2012 Andrew Kirmse and Chris Kirmse.
% All rights reserved.
%
% This software is distributed under a license that is described in
% the LICENSE file that accompanies it.
%
% Meridian is a registered trademark.


%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
KocatanVaultman is KocatanTown

constants:

   include blakston.khd

resources:

   include kcvaultm.lkod

   Kocatanvaultman_name_rsc = "Itzal cha'Nosak"
   kocatanvaultman_icon_rsc = kcvault.bgf
   kocatanvaultman_desc_rsc =  \
      "Itzal's often rude attitude and awkward bodily noises make him far less"
      "popular than his fraternal twin, Huital.  With a labored sigh and a "
      "piggish grunt, he begrudgingly accommodates the needs of those who "
      "wish to store items in the vault."

   kocatanvaultman_entry_welcome = \
      "Oh, I've had it up to HERE with your patronizing attitude."

classvars:

   vrName = Kocatanvaultman_name_rsc
   vrIcon = Kocatanvaultman_icon_rsc
   vrDesc = Kocatanvaultman_desc_rsc
   viMerchant_markup = MERCHANT_EXPENSIVE

   viAttributes = \
      MOB_NOFIGHT | MOB_SELLER | MOB_RANDOM | MOB_LISTEN \
      | MOB_NOMOVE | MOB_RECEIVE | MOB_VAULTMAN
   viOccupation = MOB_ROLE_VAULTMAN

   viVault = VID_KOCATAN

properties:
   
messages:

   AffectMood(why=$,what=$)
   {
      if why = MOODMOD_DAWN
      {
         Send(self,@SetMood,#new_mood=piMood+3);

         return;
      }

      if why = MOODMOD_DUSK
      {
         Send(self,@SetMood,#new_mood=piMood-6);

         return;
      }

      if why = MOODMOD_VAULT_WITHDRAWAL
      {
         Send(self,@SetMood,#new_mood=piMood-2);

         return;
      }

      if why = MOODMOD_VAULT_DEPOSIT
      {
         Send(self,@SetMood,#new_mood=piMood+2 );

         return;
      }

      return;
   }

   UserEntered(who=$)
   {
      if Random(1,10) = 1
      {
         Post(who,@SomeoneSaid,#what=self,#string=Kocatanvaultman_entry_welcome,
               #type = SAY_RESOURCE);
      }

      if Send(who,@CheckPlayerFlag,#flag=PFLAG_MURDERER)
      {
         Send(self,@SetMood,#new_mood=piMood-1);
      }

      propagate;
   }

   GetVaultNum()
   {
      return viVault;
   }

   GetVaultDepositFee(what=$)
   "Default cost is 1 gp per stone."
   {
      return 2*Send(what,@GetBulk);
   }

   GetVaultRetrievalFee(what=$)
   "Default cost no cost.  However, expensive places can charge more."
   {
      return 0;
   }

end
%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
