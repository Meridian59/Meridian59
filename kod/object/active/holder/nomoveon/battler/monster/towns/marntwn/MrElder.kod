% Meridian 59, Copyright 1994-2012 Andrew Kirmse and Chris Kirmse.
% All rights reserved.
%
% This software is distributed under a license that is described in
% the LICENSE file that accompanies it.
%
% Meridian is a registered trademark.


%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
MarionElder is MarionTown

constants:

   include blakston.khd

resources:

   MarionElder_name_rsc = "Ran er'Hoth"
   MarionElder_icon_rsc = mrelder.bgf
   MarionElder_desc_rsc = \
      "An ancient member of the Marion community, Ran er'Hoth has taken on a "
      "somewhat legendary status throughout Meridian, and is sought out for "
      "his opinions and knowledge by pilgrims and travelers.  Whether he is "
      "considered a wise man or the village idiot locally really depends on "
      "who you talk to."

   MrEl_entry_welcome = \
      "Another traveler through Marion. Perhaps %s%s will come tell me of "
      "their adventures."

   Ran_orctooth = \
      "I've pulled a few from their rotting skulls up in the mountains. You "
      "see, I know where their burial grounds are, and you could say that "
      "I’m a bit of a ~Icollector~I."

classvars:

   viDefinite = ARTICLE_NONE
   viIndefinite = ARTICLE_NONE
   vrName = MarionElder_name_rsc
   vrIcon = MarionElder_icon_rsc
   vrDesc = MarionElder_desc_rsc
   viAttributes = MOB_RANDOM | MOB_LISTEN | MOB_NOFIGHT | MOB_NOMOVE | MOB_TEACHER | MOB_RECEIVE | MOB_COND_SELLER
   viOccupation = MOB_ROLE_ELDER

properties:

messages:

   AffectMood(what=$,why=$)
   {
      if why = MOODMOD_ACCEPT_ITEM
      {
         if isClass(what,&Money)
         {
            send(self,@SetMood,#new_mood=piMood+1);
         }
         
         if isClass(what,&Food)
         {
            send(self,@SetMood,#new_mood=piMood+1);
         }
         
         return;
      }
      
      return;
   }

   SetForSale()
   {
      plFor_sale=[$,$,[SID_TRUCE],$];

      return;
   }

   InitCondSale()
   {
      local oObj;

      oObj = Create(&OrcTooth,#number=4);
      Send(Send(SYS,@GetLibrary),@AddToMobLib,#mob=self,
           #triggers=[Send(oObj,@GetName)],
           #action=[LIBACT_CONDITIONAL,oObj,350,ran_orctooth]);
           
      return;
   }
   
   UserEntered(who=$)
   {
      Post(poOwner,@someoneSaid,#what=self,#string=MrEl_entry_welcome,
          #type=SAY_RESOURCE,#parm1=send(who,@GetDef),
          #parm2=Send(who,@GetName));
          
      propagate;
   }


end
%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
