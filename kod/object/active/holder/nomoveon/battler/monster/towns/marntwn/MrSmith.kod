% Meridian 59, Copyright 1994-2012 Andrew Kirmse and Chris Kirmse.
% All rights reserved.
%
% This software is distributed under a license that is described in
% the LICENSE file that accompanies it.
%
% Meridian is a registered trademark.


%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
MarionBlacksmith is MarionTown

constants:

   include blakston.khd

resources:

   MarionBlacksmith_name_rsc = "Colhorr"
   MarionBlacksmith_icon_rsc = mrsmith.bgf
   MarionBlacksmith_desc_rsc = "A country blacksmith with years of pounding the anvil ringing in his ears, "
      "Colhorr is an established landmark in Marion.  Most of his work consists of fabricating and repairing "
      "farming implements and hardware for the agrarian locals."

   MrBS_entry_welcome = "Huh? Who is that?"

   MarionBlacksmith_unwanted_keep = "~k%s%s tells you, \"Hmmm, okay I guess.~n~k\""
   MarionBlacksmith_unwanted_pay = "~k%s%s tells you, \"Well it certainly looks valuable!~n~k\""
   MarionBlacksmith_unwanted_give = "~k%s%s tells you, \"Don't waste my time with that.~n~k\""

classvars:

   vrName = MarionBlacksmith_name_rsc
   vrIcon = MarionBlacksmith_icon_rsc
   vrDesc = MarionBlacksmith_desc_rsc
   viMerchant_markup = MERCHANT_NORMAL
   viAttributes = MOB_NOMOVE | MOB_NOFIGHT | MOB_TEACHER | MOB_RANDOM | MOB_LISTEN | MOB_BUYER | MOB_SELLER
   viOccupation = MOB_ROLE_BLACKSMITH

   vrUnwanted_keep = MarionBlacksmith_unwanted_keep
   vrUnwanted_pay = MarionBlacksmith_unwanted_pay
   vrUnwanted_give = MarionBlacksmith_unwanted_give

properties:   

messages:

   AffectMood(what=$,why=$)
   {
      if (why = MOODMOD_ACCEPT_ITEM)
      {
         if( isClass(what,&waterskin) )
         {
            send( self, @SetMood, #new_mood = piMood + 2 );
         }
         
         if( isClass(what,&food) and send(what,@IsAlcohol) )
         {
            send( self, @SetMood, #new_mood = piMood - 5 );
         }
         
         return;
      }
      
      if (why = MOODMOD_SELL_ITEM)
      {	  
         send( self, @SetMood, #new_mood = piMood + 1 );
      }
      
      return;
   }

   SetForSale()
   {
      plFor_sale=[ [Create(&LeatherArmor),
					Create(&MetalShield),
					Create(&WoodenShield),
					Create(&Axe),
					Create(&Shortsword),
					Create(&Mace)],
                    $,[SID_MEND] ];
      return;
   }

   ObjectDesired(what = $)
   {
      % Allow him to buy the weapons and shields from the nearby crypt.
      if Send(what,@CanBeGiventoNPC)
         AND (Send(Self,@IsObjectWeapon,#what=what)
              OR IsClass(what,&Shield))
      {
         return TRUE;
      }
      
      return FALSE;
   }

   UserEntered(who = $)
   {
      Post(poOwner,@SomeoneSaid,#what=self,#string=MrBs_entry_welcome,
           #type = SAY_RESOURCE);
              
      return;
   }

end

%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%

