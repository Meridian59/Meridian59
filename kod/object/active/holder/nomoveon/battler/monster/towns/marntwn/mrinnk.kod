% Meridian 59, Copyright 1994-2012 Andrew Kirmse and Chris Kirmse.
% All rights reserved.
%
% This software is distributed under a license that is described in
% the LICENSE file that accompanies it.
%
% Meridian is a registered trademark.


%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
MarionInnkeeper is MarionTown

constants:

   include blakston.khd
   include protocol.khd

resources:

   MarionInnkeep_name_rsc = "Morrigan"
   MarionInnkeep_icon_rsc = mrinnk.bgf
   MarionInnkeep_desc_rsc = "A silly little man, Morrigan maintains a modest business alternately "
      "buying and selling reagents and other various notions.  Although his heart doesn't seem to be in "
      "commerce, there can be little doubt Morrigan loves his job. "

   MrIk_entry_welcome = "~kMorrigan says, \"Pardon me for saying so, but you look like you could use a rest, %s%s.\""

   MarionInnkeeper_room_rented = \
      "Here ya go then.  Hope you enjoy your stay, if you have any problems Tova "
      "will be glad to help you."
   MarionInnkeeper_cost = \
      "Oh, good, you should rest.  Let me see....  I charge %i shillings to ~Irent~n "
      "a nice ~Iroom~n here for 85 days.  If you wish to stay longer, I charge %i "
      "per day if you pre-pay.  Key copies, uh, key copies....  Oh, they cost %i "
      "shillings to make.  I know you'll like your room."
   MarionInnkeeper_days_left = \
      "You like it here, I see.  There are %i days until you should pay me again."
   MarionInnkeeper_too_long = \
      "Oh, I can't be responsible for holding so much money!  Pay me again later "
      "when you don't have so many days left on your tab."
   MarionInnkeeper_destroy_key_copy = \
      "Oh, yes. I can get rid of this key for you.  No problem."
   MarionInnkeeper_change_lock = \
      "Ah yes, of course.  I'll ask Tova to change the lock to your room right away.  "
      "Any existing copies of your room key won't work anymore."

classvars:

   vrName = MarionInnkeep_name_rsc
   vrIcon = MarionInnkeep_icon_rsc
   vrDesc = MarionInnkeep_desc_rsc
   viMerchant_Markup = MERCHANT_RIPOFF
   viAttributes = MOB_NOFIGHT | MOB_NOMOVE | MOB_LISTEN | MOB_BUYER | MOB_SELLER | MOB_LISTEN_MOB | MOB_FULL_TALK
   viOccupation = MOB_ROLE_INNKEEPER

properties:

   piInitialRoomCost = 5000
   piPerDayCost = 75
   piKeyCopyCosts = 500

messages:

   % special handling to make him laugh at Tova's chatter
   SomeoneSaid(what=$,string=$)
   {
      % if it's Tova talking
      if IsClass(what, &MarionBartender)
      {
         % one out of three times
         if Random(1,3) = 1
         {
            % simulate randomtimer firing
            send(self,@RandomTimer,#test=TRUE);
            return;
         }
      }
      
      Send(Send(SYS, @GetRentableRoomMaintenance), @ParseInnkeeperCommands,
         #who=self, #what=what, #innRid=RID_MAR_INN, #speech=string,
         #initCost=piInitialRoomCost, #perDayCost=piPerDayCost, #keyCopyCost=piKeyCopyCosts,
         #rscRoomRented=MarionInnkeeper_room_rented, #rscRoomCost=MarionInnkeeper_cost,
         #rscChangeLock=MarionInnkeeper_change_lock);

      propagate;
   }

   ReqOffer(what = $,item_list = $)
   {
      % Check for if in the same room.
      if send(what,@GetOwner) <> poOwner
      {
         debug("ALERT! ",send(what,@GetTrueName),what," offered items to NPC ",send(self,@GetTrueName)," from distant RID ",send(send(what,@GetOwner),@GetRoomNum));
         
         return FALSE;
      }     

      if Send(Send(SYS, @GetRentableRoomMaintenance), @ReqOfferedRoomItems, #who=self, #what=what,
         #itemList=item_list, #innRid=RID_MAR_INN, #perDayCost=piPerDayCost,
         #rscDaysLeft=MarionInnkeeper_days_left, #rscTooLong=MarionInnkeeper_too_long,
         #rscDestroyKeyCopy=MarionInnkeeper_destroy_key_copy)
      {
         return FALSE;
      }

      propagate;
   }

   ObjectDesired(what=$)
   {
      if Send(self,@IsObjectSundry,#what=what)
         OR Send(self,@IsObjectReagent,#what=what)
         OR Send(self,@IsObjectWearable,#what=what)
      {
         return TRUE;
      }
      
      return FALSE;
   }

   SetForSale()
   {
      plFor_sale=[[           
		   Create(&PantsC,#translation=PT_GRAY_TO_LRED),
		   Create(&PantsC,#translation=PT_GRAY_TO_KORANGE),
		   Create(&Herbs,#number=5),
         Create(&Elderberry,#number=4),
		   Create(&Torch)
		  ],
		  $,$];
		  
      return;
   }

   UserEntered(who = $)
   {

      Post(poOwner,@SomeoneSaid,#what=self,#type=SAY_MESSAGE,
      #string=MrIk_entry_welcome,
      #parm1=Send(who,@GetDef),
      #parm2=Send(who,@GetName));
	 
      propagate;
   }
   

end
%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%

