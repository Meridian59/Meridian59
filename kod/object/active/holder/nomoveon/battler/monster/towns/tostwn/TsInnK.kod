% Meridian 59, Copyright 1994-2012 Andrew Kirmse and Chris Kirmse.
% All rights reserved.
%
% This software is distributed under a license that is described in
% the LICENSE file that accompanies it.
%
% Meridian is a registered trademark.


%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
TosInnKeeper is TosTown

constants:

   include blakston.khd

   PADDOCK_YELL_CHANCE = 30

resources:

   tosinnkeeper_name_rsc = "Paddock"
   tosinnkeeper_icon_rsc = tsinnk.bgf
   tosinnkeeper_desc_rsc = "As owner of perhaps the most popular bar in the world, Paddock goes out of his way to "
      "smile at every customer who comes through the door.  His down to earth demeanor and "
      "common man approach to his clientele makes him one of the most likable men in Tos."

   TsIK_entry_welcome = "Welcome to Familiars, the most popular bar in the land!"

   TsIK_purplemushroom_sale = "Yeah, I got %s%s here. Let's see what kind of deal I can make with you for these little rarities."
   TsIK_orcteeth_sale = "Another customer in the market for %s%s?  I should shut down the bar and just sell these things."
   TsIK_agedcheese_sale = "You want some aged cheese? Hah! That's about the only good thing coming from that town of clowns "
      "that calls itself Barloque!"
   TsIK_spidereyes_sale = "Did you know that spider eyes improve your sight? Usually, you can only get them at one "
      "of the Duke's fancy parties, but you're in luck! I got some for you right here!"

   TsIK_watch_head = "Hey, %s%s!  Watch your head!"
   
   TsIK_agedcheese_trigger = "aged cheese"
   

classvars:

   vrName = tosinnkeeper_name_rsc
   vrIcon = tosinnkeeper_icon_rsc
   vrDesc = tosinnkeeper_desc_rsc
   viAttributes = MOB_NOFIGHT | MOB_RECEIVE | MOB_RANDOM | MOB_LISTEN | MOB_NOMOVE | MOB_SELLER | MOB_COND_SELLER | MOB_LAWFUL
   viOccupation = MOB_ROLE_INNKEEPER
   viMerchant_markup = MERCHANT_DISCOUNT

properties:

messages:

   AffectMood(why = $, what = $)
   {
      if why = MOODMOD_FACTION_CHANGE
      {
         if what = FACTION_DUKE
         {
            send(self,@SetMood,#new_mood=piMood + 2);
            return;
         }
         
         if what = FACTION_PRINCESS
         {
            send(self,@SetMood,#new_mood=piMood - 4);
            return;
         }
      }
      
      if why = MOODMOD_SELL_ITEM
      {
         if IsClass(what, &Food)
         {
            if send(what, @IsAlcohol)
            {
               send(self,@SetMood,#new_mood=piMood + 1);
            }
         }
         
         return;
      }
      
      if why = MOODMOD_DAWN
      {
         send(self,@SetMood,#new_mood=piMood - 3);
         return;
      }
      
      if why = MOODMOD_DUSK
      {
         send(self,@SetMood,#new_mood=piMood + 3);
         return;
      }
      
      return;
   }

   SetForSale()
   {
      plFor_sale = [[Create(&Mug),
                     Create(&SetScroll,#labelled=TRUE),
                     Create(&LoadoutScroll,#labelled=TRUE),
                     Create(&Cheese,#number=3)],$,$,$];
      
      return;
   }

   InitCondSale()
   {
      local oObj,oObj2,oObj3,oObj4;
      
      oObj=Create(&PurpleMushroom,#number=4);
      Send(Send(SYS,@GetLibrary),@AddToMobLib,#mob=self,
           #triggers=[Send(oObj,@GetName)],
           #action=[LIBACT_CONDITIONAL,oObj,250,TsIK_purplemushroom_sale]);
           
      oObj2=Create(&OrcTooth,#number=2);
      Send(Send(SYS,@GetLibrary),@AddToMobLib,#mob=self,
           #triggers=[Send(oObj2,@GetName)],
           #action=[LIBACT_CONDITIONAL,oObj2,650,TsIK_orcteeth_sale]);

      oObj3=Create(&AgedCheese,#number=1);
      Send(Send(SYS,@GetLibrary),@AddToMobLib,#mob=self,
           #triggers=[TsIK_agedcheese_trigger],
           #action=[LIBACT_CONDITIONAL,oObj3,1000,TsIK_agedcheese_sale]);
           
      oObj4=Create(&Spideye,#number=4);
      Send(Send(SYS,@GetLibrary),@AddToMobLib,#mob=self,
           #triggers=[Send(oObj4,@GetName)],
           #action=[LIBACT_CONDITIONAL,oObj4,50,TsIK_spidereyes_sale]);
           
      return;
   }
   
   UserEntered(who = $)
   {
      if Send(who,@GetAction) = UA_HAPPY
      {
         send(self,@SetMood,#new_mood=piMood + 1);
      }
      
      if Send(who,@GetAction) = UA_SAD
      {
         send(self,@SetMood,#new_mood=piMood - 1);
      }
      
      Send(poOwner,@SomeoneSaid,#what=self,#string=TsIK_entry_welcome,#type=SAY_RESOURCE);
           
      propagate;
   }

   SomethingMoved(what = $, new_row = $, new_col = $, fine_row = FINENESS_HALF, fine_col = FINENESS_HALF)
   {
      % Give a warning for people going up the stairs to watch their head.  Something goofy that
      %   Rob suggested.  Blame him.
      if new_row = 2 AND (new_col = 8 OR (new_col = 7 AND fine_col > 20))
         AND random(0,100) < PADDOCK_YELL_CHANCE
      {
         Send(poOwner,@SomeoneSaid,#what=self,#string=TsIK_watch_head,#type=SAY_RESOURCE,
              #parm1=send(what,@GetIndef),#parm2=send(what,@GetName));
      }

      propagate;
   }

end


%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
