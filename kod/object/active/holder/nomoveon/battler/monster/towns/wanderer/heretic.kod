% Meridian 59, Copyright 1994-2012 Andrew Kirmse and Chris Kirmse.
% All rights reserved.
%
% This software is distributed under a license that is described in
% the LICENSE file that accompanies it.
%
% Meridian is a registered trademark.


%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
Heretic is Wanderer

constants:

   include blakston.khd
   ANIM_SPEED_FAST = 100
   ANIM_SPEED_SLOW = 150
   FRAME_DANCE_START = 1
   FRAME_DANCE_END = 8

resources:

   include heretic.lkod

   Heretic_name_rsc = "Miriana"
   Heretic_icon_rsc = wnhereti.bgf
   Heretic_possessed_icon_rsc = wnheret2.bgf
   Heretic_desc_rsc =  \
      "Outcast by the religious community due to her blasphemous views of "
      "the gods, Miriana dances by herself far from the rigid walls of "
      "civilization.  Some call her a heretic, others a child, but all seem "
      "to find something dangerous about the distant girl."

   heretic_inexperienced = "I will not teach these arts to one so weak."
   Heretic_already_have_spell = "Silly!  You already know the %s spell."

classvars:

   vrName = Heretic_name_rsc
   vrDesc = Heretic_desc_rsc

   viAttributes = \
      MOB_NOFIGHT | MOB_RANDOM | MOB_LISTEN \
      | MOB_NOMOVE | MOB_RECEIVE | MOB_TEACHER
   viOccupation = MOB_ROLE_PRIESTESS
   viGender = GENDER_FEMALE

properties:

   vrIcon = Heretic_icon_rsc
   piAnimation = ANIM_SPEED_FAST

messages:

   UserEntered(who=$)
   {
      local iAction;

      iAction = Send(who,@GetAction);

      if iAction = UA_SAD
      {
         Send(self,@SetMood,#new_mood=piMood+1);
      }

      if iAction = UA_HAPPY
      {
         Send(self,@SetMood,#new_mood=piMood-1);
      }

      if iAction = UA_WRY
      {
         Send(self,@SetMood,#new_mood=piMood+2);
      }

      propagate;
   }

   CreateDestinationList()
   {
      plDestinations = [ [RID_A1, 24, 46], [RID_F7, 4, 16], [RID_A6, 20, 32],
                         [RID_I7, 32, 7]  ];
      return;
   }
   
   SetForSale()
   {
      plFor_Sale = [
         $,$,
         [
            SID_FIREWALL,
            SID_MARTYRS_BATTLEGROUND,
            SID_BRAMBLEWALL,
            SID_BOND,
            SID_SILENCE,
            SID_RING_OF_FLAMES
         ],
      $];
      return;
   }

   SetPossessed(bValue=FALSE)
   {
      if bValue
      {
         vrIcon = Heretic_possessed_icon_rsc;
         piAnimation = ANIM_SPEED_SLOW;
      }
      else
      {
         vrIcon = Heretic_icon_rsc;
         piAnimation = ANIM_SPEED_FAST;
      }

      return;
   }

   SendLookAnimation()
   {
      AddPacket(1,ANIMATE_CYCLE);
      AddPacket(4,piAnimation,2,FRAME_DANCE_START,2,FRAME_DANCE_END);

      return;
   }
   
   SendAnimation()
   {
      AddPacket(1,ANIMATE_CYCLE);
      AddPacket(4,piAnimation,2,FRAME_DANCE_START,2,FRAME_DANCE_END);

      return;
   }

   CanDoTeach(who=$,sid=0)
   {
      if NOT Send(self,@SpecialTeachChecks,#report=TRUE,#who=who,#num=sid)
      {
         return FALSE;
      }

      propagate;
   }
   
   CanAddSpell(who=$,num=0,report=FALSE)
   {
      if NOT Send(self,@SpecialTeachChecks,#report=report,#who=who,#num=num)
      {
         return FALSE;
      }

      propagate;
   }
   
   SpecialTeachChecks(who=$,num=0,report=FALSE)
   {
      if who = $ or num = 0
      {
         Debug("Called CanAddSpell with bad parameters!");

         return FALSE;
      }

      if Send(who,@HasSpell,#num=num)
      {
         if report
         {
            Post(self,@SayToOne,#target=who,
                  #message_rsc=Heretic_already_have_spell,
                  #parm1=Send(Send(SYS,@FindSpellByNum,#num=num),@GetName));
         }

         return FALSE;
      }

      % Only players who are killable may learn.
      if (NOT Send(who,@CheckPlayerFlag,#flag=PFLAG_PKILL_ENABLE)
         AND (num = SID_BRAMBLEWALL OR num = SID_FIREWALL
               OR num = SID_RING_OF_FLAMES))
      {
         if report
         {
            Post(self,@SayToOne,#target=who,#message_rsc=heretic_inexperienced);
         }

         return FALSE;
      }

      return TRUE;
   }

end
%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
