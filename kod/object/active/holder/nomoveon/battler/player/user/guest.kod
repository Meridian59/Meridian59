% Meridian 59, Copyright 1994-2012 Andrew Kirmse and Chris Kirmse.
% All rights reserved.
%
% This software is distributed under a license that is described in
% the LICENSE file that accompanies it.
%
% Meridian is a registered trademark.


%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
guest is user

constants:

   include blakston.khd

resources:

   guest_cant_broadcast = "Only regular players may broadcast messages."

   guest_cant_send_non_guest = "Only regular players may send messages to the real world."
   
   guest_welcome = "~B~rYou are currently logged in as a guest. This character is only temporary, "
				   "dying or logging off will delete your character permanently. "
				   "You have been given a beta potion for your survival here on the island. Enjoy!"

classvars:


properties:

messages:

   UserLogonSetPosition()
   {
      Send(Send(SYS,@FindRoomByNum,#num=RID_JUNGLE_TRADING_POST),@Teleport,#what=self);

      return;
   }

   CheckPlayerFlag(flag=0)
   {
      if flag = PFLAG_SAFETY
      {  return True; }

      propagate;
   }

   UserGotoDeadRoom()
   {
	  % Guests have a perm death
      Post(self,@PerformSuicide);

      return;
   }

   TryBroadcast()
   "Called when the user wants to broadcast a string.  Guests can't! "
   {
      Send(self,@MsgSendUser,#message_rsc=guest_cant_broadcast);

      return False;
   }

   TrySayGroup(users = $)
   {
      local i,oRoom;

      for i in users
      {
		oRoom = Send(SYS,@UtilGetRoom,#what=i);
		if oRoom = $ { continue; }        %% this group member is not logged in - lagged out?

		if not Send(oRoom,@IsGuestRoom)
		{
			Send(self,@MsgSendUser,#message_rsc=guest_cant_send_non_guest);
			return False;
		}
      }
      propagate;
   }

   SendFirstTimeAppeal()
   {
	  Post(self,@MsgSendUser,#message_rsc=guest_welcome);
   
      return;
   }

   PlayerNewCharInfo()
   {
      %debug("player new char info");
      propagate;
   }

   UserLogon()
   {
      local i;

      %debug("userlogon");

      piHealth = piBase_max_health;
      Send(self,@NewHealth);
      Send(self,@setplayerflag,#flag=PFLAG_PKILL_ENABLE,#value=True);


      propagate;
   }

   LoadMailNews()
   {
      return;
   }
   UserMail()
   {
      return;
   }
   UserGetNewMail()
   {
      return;
   }
   ReceiveMail()
   {
      return;
   }
   ReceiveNestedMail()
   {
      return;
   }

   AddDefaultObjects()
   "Add the objects a player is given when creating a guest character"
   {
      Send(self,@NewHold,#what=Create(&Money,#number=2500));
      Send(self,@NewHold,#what=Create(&BetaPotion));

      return;
   }
  

   IsFirstTime()
   {
      %debug("isfirsttime");

      % called by server to tell client if they NEED to go through char creation or not
      return True;
   }
   
   SetInitialHomeroom()
   {
      piHomeroom = RID_JUNGLE_TRADING_POST;
	  
      return;
   }

end
%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
