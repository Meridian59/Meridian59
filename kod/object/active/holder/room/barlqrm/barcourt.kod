% Meridian 59, Copyright 1994-2012 Andrew Kirmse and Chris Kirmse.
% All rights reserved.
%
% This software is distributed under a license that is described in
% the LICENSE file that accompanies it.
%
% Meridian is a registered trademark.


%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
BarloqueCourt is BarloqueRoom

constants:

   include blakston.khd

resources:

   room_name_barloque_court = "Office of the Justicar"
   room_barloque_court = barcourt.roo
   
   news_justicar = "Book of Jala"
   news_justicar_desc = \
      "Chronicling the history of the office of the Justicar."

   BarloqueCourt_sign_name = "Justicar information"
   BarloqueCourt_sign_icon = newbsign.bgf

   BarloqueCourt_sign_desc = \
      "Etched into the sign is the following text:\n\n"
      "The office of Justicar is very important.  It is the duty of the "
      "Justicar to give pardons to those who no longer deserve the scorn "
      "of being an outlaw or a murderer.  The Justicar is elected by the "
      "people of the land."
      "\n\n"
      "To vote, buy a ballot from Caramo the royal clerk.  Write the name of "
      "the person that you wish to vote for on the ballot and hand it back to "
      "her.  Note that outlaws, murderers, and individuals still under "
      "Shal'ille's protection are not able to vote for or hold the office of "
      "Justicar."
      "\n\n"
      "If elected Justicar, Caramo will send you a mail with details about "
      "the abilities and responsibilities of the office."
      "\n\n"
      "Caramo announces when there is a new vote for Justicar.  If you wish "
      "to be told when there is a new vote, say 'advertise' to Caramo.  If "
      "you no longer wish to be notified, say 'no advertise' to her instead.  "
      "Outlaws and murderers will not be notified of the vote, since they "
      "cannot properly participate."

classvars:

   vrName = room_name_barloque_court

   viTeleport_row = 6
   viTeleport_col = 12

   viPermanent_flags = ROOM_NO_COMBAT
   viTerrain_Type = TERRAIN_CITY | TERRAIN_SHOP

properties:

   prRoom = room_barloque_court
   piRoom_num = RID_BAR_COURT

   piBaseLight = LIGHT_NICE
   piOutside_factor = 0

   poCaramo = $
   poBookOfJala = $

messages:

   % The gallery is separated from the official courtroom by a wall.
   % Only those who are in the courtroom area are subject to Justicar
   %  commands.  This allows others to watch a trial and not be abused.

   ReqInSpecialArea(row = $, col = $, obj = $)
   {
      local iRow, iCol;

      if obj <> $
      {
         iRow = Send(obj,@GetRow);
         iCol = Send(obj,@GetCol);
      }
      else
      {
         iRow = row;
         iCol = col;
      }

      if iRow >= 7
      {
         return TRUE;
      }

      return FALSE;
   }

   CreateStandardObjects()
   {
      local oSign;

      % The clerk     
      poCaramo = Send(SYS,@GetCaramo);
      if poCaramo = $
      {
         poCaramo = Create(&BarloqueClerk);
      }
      
      Send(self,@NewHold,#what=poCaramo,
           #new_row=12,#new_col=12,#fine_row=28,#fine_col=28,
           #new_angle=ANGLE_WEST);

      % A guard
      Send(self,@NewHold,#what=Create(&DukeGuard),
           #new_row=10,#new_col=1,#fine_row=60,#fine_col=28,
           #new_angle=ANGLE_EAST);

      % A book of jala
      poBookOfJala = Send(SYS,@FindNewsByNum,#num=NID_JUSTICAR);
      if poBookOfJala = $
      {
         poBookOfJala = Create(&NewsJusticar,#nid=NID_JUSTICAR,
                               #name=news_justicar,#desc=news_justicar_desc);
      }
      
      Send(self,@NewHold,#what=poBookOfJala,
           #new_row=2,#new_col=11,#fine_row=44,#fine_col=44,
           #new_angle=ANGLE_SOUTH_WEST);

      % A information sign
      oSign = Create(&Sign,#name=BarloqueCourt_sign_name,
                     #icon=BarloqueCourt_sign_icon,
                     #desc=BarloqueCourt_sign_desc);
      Send(self,@NewHold,#what=oSign,#new_row=11,#new_col=12,
           #fine_row=0,#fine_col=32);

      % Braziers
      Send(self,@NewHold,#what=Create(&Brazier),#new_row=1,#new_col=6,
           #fine_row=28,#fine_col=60);
      Send(self,@NewHold,#what=Create(&Brazier),#new_row=13,#new_col=1,
           #fine_row=39,#fine_col=44);

      % Table
      Send(self,@NewHold,#what=Create(&Table),#new_row=2,#new_col=9,
           #fine_row=28,#fine_col=28);

      % Candelabra
      Send(self,@NewHold,#what=Create(&Candelabra),#new_row=12,#new_col=11,
           #fine_row=60,#fine_col=52);

      propagate;
   }

   CreateStandardExits()
   {
      plExits = $;

      % TO TOWN
      plExits = Cons([ 2, 4, RID_BAR_NORTH, 33, 54, ROTATE_NONE ],plExits); %% north
      plExits = Cons([ 4, 2, RID_BAR_NORTH, 38, 51, ROTATE_NONE ],plExits); %% west

      % GALLERY GATE
      plExits = Cons([ 6, 11, RID_BAR_COURT, 8, 11, ROTATE_NONE ],plExits); %% to court
      plExits = Cons([ 7, 11, RID_BAR_COURT,  5, 11, ROTATE_NONE ],plExits); %% to gallery

      propagate;
   }

   RecalcLightAndWeather()
   {
      Send(poCaramo,@NewHour);
      
      propagate;
   }

   GetClerk()
   {
      return poCaramo;
   }

   GetBook()
   {
      return poBookOfJala;
   }
   
   Delete()
   {
      if poCaramo <> $
      {
         Send(poCaramo,@NewOwner,#what=$);
         poCaramo = $;
      }
      
      poBookofJala = $;

      propagate;
   }


end
%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
