% Meridian 59, Copyright 1994-2012 Andrew Kirmse and Chris Kirmse.
% All rights reserved.
%
% This software is distributed under a license that is described in
% the LICENSE file that accompanies it.
%
% Meridian is a registered trademark.


%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
GuildHall2 is GuildHall

constants:

   include blakston.khd

   BOOKS = 1
   CLOSET = 2
   MAINDOOR = 3
   EASTDOOR = 4
   OFFICEDOOR = 5
   SOUTHDOOR = 6
   WESTDOOR = 7
   RAIDDOOR = 8

   DOOR_DELAY = 10000

resources:

   include guildh2.lkod

   room_name_guildh2 = "The Hall of Explorers"
   room_guildh2 = guildh2.roo

   guildhall2_sound = down2.wav

classvars:

   vrName = room_name_guildh2

   viTeleport_row = 18
   viTeleport_col = 32

   viNews_row = 8
   viNews_col = 28
   viNews_finerow = 16
   viNews_finecol = 0

   viFoyer_north = 16
   viFoyer_south = 20
   viFoyer_east = 34
   viFoyer_west = 30

   viLever_row = 21
   viLever_col = 17
   viLever_fine_row = 56
   viLever_fine_col = 56

   viInner_teleport_row = 8
   viInner_teleport_col = 25

   viQuality = 5
   viTerrain_type = TERRAIN_CITY | TERRAIN_GUILDHALL
   viSeconds_til_close = 13

properties:

   prRoom = room_guildh2
   piRoom_num = RID_GUILDH2

   piBaseLight = LIGHT_NICE

   piPosition = 0

   ptSecretDoor = $
   ptEntrance = $
   ptCloset = $
   ptOfficeDoor = $
   ptEastDoor = $
   ptSouthDoor = $
   ptWestDoor = $

messages:

   Constructor()
   {
      plGuild_doors = [ [18,30] ];

      % This is a box containing the guild door, used
      % for removing players from it when it closes.
      plEntrance = [ 1800, 1900, 2978, 2996 ];

      propagate;
   }

   CreateStandardExits()
   {
      plExits = $;

      %% TO TOWN
      plExits = Cons([ 18, 34, RID_BAR_SOUTH, 16, 43, ROTATE_NONE ],plExits);
      plExits = Cons([ 18, 33, RID_BAR_SOUTH, 16, 43, ROTATE_NONE ],plExits);

      propagate;
   }

   OpenSecretDoor()
   {
      if ptSecretDoor = $
      {
         Send(self,@SomethingWaveRoom,#wave_rsc=guildhall2_sound);
         Send(self,@SetSector,#sector=BOOKS,#animation=ANIMATE_CEILING_LIFT,
               #height=184,#speed=16);
         ptSecretDoor = CreateTimer(self,@CloseSecretDoor,DOOR_DELAY);
         Send(self,@OpenSecretDoorSound);
      }

      return;
   }


   CreateStandardObjects()
   {
      Send(self,@NewHold,#what=Create(&Skull),
            #new_row=9,#new_col=24,#fine_row=16,#fine_col=0);

      Send(self,@NewHold,#what=Create(&Table),
            #new_row=20,#new_col=25,#fine_row=48,#fine_col=48,
            #new_angle=ANGLE_SOUTH);
      Send(self,@NewHold,#what=Create(&Table),
            #new_row=20,#new_col=28,#fine_row=48,#fine_col=16,
            #new_angle=ANGLE_SOUTH);

      Send(self,@NewHold,#what=Create(&Firepit),
          #new_row=18,#new_col=18,#fine_col=32,#fine_row=32);


      Send(self,@NewHold,#what=Create(&Brazier),
            #new_row=8,#new_col=21,#fine_row=16,#fine_col=32);
      Send(self,@NewHold,#what=Create(&Brazier),
            #new_row=8,#new_col=25,#fine_row=16,#fine_col=32);
      Send(self,@NewHold,#what=Create(&Brazier),
            #new_row=9,#new_col=26,#fine_row=48,#fine_col=48);
      Send(self,@NewHold,#what=Create(&Brazier),
            #new_row=9,#new_col=29,#fine_row=48,#fine_col=16);
      Send(self,@NewHold,#what=Create(&Brazier),
            #new_row=12,#new_col=19,#fine_row=32,#fine_col=16);
      Send(self,@NewHold,#what=Create(&Brazier),
            #new_row=12,#new_col=23,#fine_row=32,#fine_col=16);

      Send(self,@NewHold,#what=Create(&WoodenBox),#new_row=8,#new_col=19,
            #fine_row=16,#fine_col=48,#new_angle=ANGLE_SOUTH);

      propagate;
   }

   OpenEntranceDoor()
   {
      if ptEntrance = $
      {
         Send(self,@TimeStampDoor);
         Send(self,@SomethingWaveRoom,#wave_rsc=guildhall2_sound);
         Send(self,@SetSector,#sector=MAINDOOR,#animation=ANIMATE_CEILING_LIFT,
               #height=88,#speed=100);
         ptEntrance = CreateTimer(self,@CloseEntranceDoor,DOOR_DELAY);
         Send(self,@OpenEntranceDoorSound);

         return TRUE;
      }

      return;
   }

   SomethingTryGo(what=$,row=$,col=$)
   {
      if (row = 16 OR row = 17) AND (col = 31 OR col = 32) AND ptCloset = $
      {
         Send(self,@SomethingWaveRoom,#wave_rsc=guildhall2_sound);
         Send(self,@SetSector,#sector=CLOSET,#animation=ANIMATE_CEILING_LIFT,
               #height=96,#speed=100);
         ptCloset = CreateTimer(self,@CloseCloset,DOOR_DELAY);

         return TRUE;
      }

      if row = 18 AND col = 29
         AND ptEntrance = $
      {
         Send(self,@OpenEntranceDoor);
      }

      if (row = 11 OR row = 12) AND col = 28 AND ptEastDoor = $
      {
         Send(self,@SomethingWaveRoom,#wave_rsc=guildhall2_sound);
         Send(self,@SetSector,#sector=EASTDOOR,#animation=ANIMATE_CEILING_LIFT,
               #height=168,#speed=100);
         ptEastDoor = CreateTimer(self,@CloseEastDoor,DOOR_DELAY);

         return TRUE;
      }

      if (row = 11 OR row = 12) AND col = 25 AND ptOfficeDoor = $
      {
         Send(self,@SomethingWaveRoom,#wave_rsc=guildhall2_sound);
         Send(self,@SetSector,#sector=OFFICEDOOR,#animation=ANIMATE_CEILING_LIFT,
               #height=176,#speed=100);
         ptOfficeDoor = CreateTimer(self,@CloseOfficeDoor,DOOR_DELAY);

         return TRUE;
      }

      if (row = 14 OR row = 15) AND (col = 19 OR col = 20) AND ptSouthDoor = $
      {
         Send(self,@SomethingWaveRoom,#wave_rsc=guildhall2_sound);
         Send(self,@SetSector,#sector=SOUTHDOOR,#animation=ANIMATE_CEILING_LIFT,
               #height=96,#speed=100);
         ptSouthDoor = CreateTimer(self,@CloseSouthDoor,DOOR_DELAY);

         return TRUE;
      }

      if (row = 12 OR row = 13) AND (col > 16 AND col < 20) AND ptWestDoor = $
      {
         Send(self,@SomethingWaveRoom,#wave_rsc=guildhall2_sound);
         Send(self,@SetSector,#sector=WESTDOOR,#animation=ANIMATE_CEILING_LIFT,
               #height=184,#speed=100);
         ptWestDoor = CreateTimer(self,@CloseWestDoor,DOOR_DELAY);

         return TRUE;
      }

      propagate;
   }

   CloseSecretDoor()
   {
      ptSecretDoor = $;
      Send(self,@SomethingWaveRoom,#wave_rsc=guildhall2_sound);
      Send(self,@SetSector,#sector=BOOKS,#animation=ANIMATE_CEILING_LIFT,
            #height=104,#speed=16);
      Send(self,@CloseSecretDoorSound);

      return;
   }

   CloseEntranceDoor()
   {
      Send(self,@SomethingWaveRoom,#wave_rsc=guildhall2_sound);
      Send(self,@SetSector,#sector=MAINDOOR,#animation=ANIMATE_CEILING_LIFT,
            #height=24,#speed=50);
      Send(self,@CloseEntranceDoorSound);

      % We now propagate this to handle removing players from the door,
      % and so the door can still be entered while it is closing.
      propagate;
   }

   CloseCloset()
   {
      ptCloset = $;
      Send(self,@SomethingWaveRoom,#wave_rsc=guildhall2_sound);
      Send(self,@SetSector,#sector=CLOSET,#animation=ANIMATE_CEILING_LIFT,
            #height=24,#speed=100);

      return;
   }

   CloseEastDoor()
   {
      ptEastDoor = $;
      Send(self,@SomethingWaveRoom,#wave_rsc=guildhall2_sound);
      Send(self,@SetSector,#sector=EASTDOOR,#animation=ANIMATE_CEILING_LIFT,
            #height=96,#speed=100);

      return;
   }

   CloseOfficeDoor()
   {
      ptOfficeDoor = $;
      Send(self,@SomethingWaveRoom,#wave_rsc=guildhall2_sound);
      Send(self,@SetSector,#sector=OFFICEDOOR,#animation=ANIMATE_CEILING_LIFT,
            #height=96,#speed=100);

      return;
   }

   CloseSouthDoor()
   {
      ptSouthDoor = $;
      Send(self,@SomethingWaveRoom,#wave_rsc=guildhall2_sound);
      Send(self,@SetSector,#sector=SOUTHDOOR,#animation=ANIMATE_CEILING_LIFT,
            #height=24,#speed=100);

      return;
   }

   CloseWestDoor()
   {
      ptWestDoor = $;
      Send(self,@SomethingWaveRoom,#wave_rsc=guildhall2_sound);
      Send(self,@SetSector,#sector=WESTDOOR,#animation=ANIMATE_CEILING_LIFT,
            #height=24,#speed=100);

      return;
   }

   OpenRaidDoor()
   {
      Send(self,@SomethingWaveRoom,#wave_rsc=guildhall2_sound);
      Send(self,@SetSector,#sector=RAIDDOOR,#animation=ANIMATE_CEILING_LIFT,
            #height=184,#speed=30);

      return;
   }

   CloseRaidDoor()
   {
      Send(self,@SomethingWaveRoom,#wave_rsc=guildhall2_sound);
      Send(self,@SetSector,#sector=RAIDDOOR,#animation=ANIMATE_CEILING_LIFT,
            #height=104,#speed=30);

     return;
   }

   DefensesDown()
   {
      Send(self,@OpenRaidDoor);

      propagate;
   }

   DefensesUp()
   {
      Send(self,@CloseRaidDoor);

      propagate;
   }

   ClaimGuildHall()
   {
      Send(self,@CloseRaidDoor);

      propagate;
   }

   ResetGuildHall()
   {
      Send(self,@CloseRaidDoor);

      propagate;
   }

   ConquerTimer()
   {
      Send(self,@CloseRaidDoor);

      propagate;
   }

   Delete()
   {
      if ptSecretDoor <> $
      {
         DeleteTimer(ptSecretDoor);
         ptSecretDoor = $;
      }

      if ptEntrance <> $
      {
         DeleteTimer(ptEntrance);
         ptEntrance = $;
      }

      if ptCloset <> $
      {
         DeleteTimer(ptCloset);
         ptCloset = $;
      }

      if ptOfficeDoor <> $
      {
         DeleteTimer(ptOfficeDoor);
         ptOfficeDoor = $;
      }

      if ptEastDoor <> $
      {
         DeleteTimer(ptEastDoor);
         ptEastDoor = $;
      }

      if ptSouthDoor <> $
      {
         DeleteTimer(ptSouthDoor);
         ptSouthDoor = $;
      }

      if ptWestDoor <> $
      {
         DeleteTimer(ptWestDoor);
         ptWestDoor = $;
      }

      propagate;
   }

end
%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
