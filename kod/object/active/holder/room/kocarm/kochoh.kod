% Meridian 59, Copyright 1994-2012 Andrew Kirmse and Chris Kirmse.
% All rights reserved.
%
% This software is distributed under a license that is described in
% the LICENSE file that accompanies it.
%
% Meridian is a registered trademark.


%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
KocatanHall is KocatanRoom

constants:

   include blakston.khd

resources:

   include kochoh.lkod

   room_Kocatanhall = Kochoh.roo
   room_name_Kocatanhall = "The Hall of Heroes"

   Kocatanhall_music = extra01.mp3

   news_Kocatan_hall = "General News"
   news_Kocatan_hall_desc = "Find out what's happening around Meridian."

   hoh_best_adventurer = \
      "This is a statue of %q, greatest adventurer in the land."
   hoh_best_warrior = "This is a statue of %q, mightiest warrior in the land."
   hoh_best_wizard = \
      "This is a statue of %q, most powerful wizard in the land."

   hoh_best_faren = "This is a statue of %q, master of Faren."
   hoh_best_qor = "This is a statue of %q, master of Qor."
   hoh_best_shalille = "This is a statue of %q, master of Shalille."
   hoh_best_riija = "This is a statue of %q, master of Riija."
   hoh_best_kraanan = "This is a statue of %q, master of Kraanan."
   hoh_best_jala = "This is a statue of %q, master of Jala."

classvars:

   vrName = room_name_Kocatanhall

   viTeleport_row = 13
   viTeleport_col = 7

   viTerrain_type = TERRAIN_CITY | TERRAIN_SHOP

properties:

   viPermanent_flags = ROOM_NO_COMBAT | ROOM_SAFELOGOFF

   prRoom = room_Kocatanhall
   piRoom_num = RID_KOC_HALL_OF_HEROES

   piBaseLight = LIGHT_NICE
   piOutside_factor = 3

   prMusic = Kocatanhall_music
   plStatues = $
   pbEverEntered = FALSE

messages:

   ChooseStatues()
   {
      local oLore;

      if pbEverEntered
      {
         return Send(SYS,@GetFailureRsc);
      }

      pbEverEntered = TRUE;

      oLore = Send(SYS,@GetLore);
      if oLore = $
      {
         return Send(SYS,@GetFailureRsc);
      }

      Post(self,@CreateStatues);
      Post(oLore,@ChooseCurrentBests);

      return Send(SYS,@GetPleaseWaitRsc);
   }

   CreateStandardExits()
   {
      plExits = $;

      % TO TOWN
      plExits = Cons([ 14, 7, RID_KOCATAN, 23, 30, ROTATE_NONE ],plExits);
      
      % TO BOW MASTER
      plExits = Cons([ 3, 3, RID_KOC_HALL_OF_HEROES_A, 2, 4, ROTATE_NONE ],plExits);
      plExits = Cons([ 4, 3, RID_KOC_HALL_OF_HEROES_A, 2, 4, ROTATE_NONE ],plExits);
      
      % TO LANGUAGE MASTER
      plExits = Cons([ 3, 11, RID_KOC_HALL_OF_HEROES_B, 2, 4, ROTATE_NONE ],plExits);
      plExits = Cons([ 4, 11, RID_KOC_HALL_OF_HEROES_B, 2, 4, ROTATE_NONE ],plExits);

      propagate;
   }

   CreateStandardObjects()
   {
      local oNews, oStatue;

      oNews = Create(&NewsLink,#nid=NID_TOS_HALL,#name=news_Kocatan_hall,
                     #desc=news_Kocatan_hall_desc);
      Send(self,@NewHold,#what=oNews,#new_row=7,#new_col=7,
           #fine_row=16,#fine_col=32);      

      % Do not create statues here.  We don't know if they are legal.

      propagate;
   }

   CreateStatues()
   {
      local oStatue;

      if NOT pbEverEntered
      {
         return Send(SYS,@GetFailureRsc);
      }

      % The order of the statues is important.  The most important
      % statues are at the end, with the best player being last.
      % The first statues go down the side halls.

      plStatues = $;

      % Qor/Kraanan/Faren
      oStatue = Create(&Statue,#bDefault=TRUE);
      plStatues = Cons(oStatue,plStatues);
      Send(self,@NewHold,#what=oStatue,#new_angle=ANGLE_EAST,
            #new_row=5,#new_col=1,#fine_row=16,#fine_col=24);

      % Shalille/Kraanan/Faren
      oStatue = Create(&Statue,#bDefault=TRUE);
      plStatues = Cons(oStatue,plStatues);
      Send(self,@NewHold,#what=oStatue,#new_angle=ANGLE_WEST,
            #new_row=5,#new_col=13,#fine_row=16,#fine_col=40);

      % Qor/Kraanan
      oStatue = Create(&Statue,#bDefault=TRUE);
      plStatues = Cons(oStatue,plStatues);
      Send(self,@NewHold,#what=oStatue,#new_angle=ANGLE_EAST,
            #new_row=7,#new_col=1,#fine_row=48,#fine_col=24);

      % Shalille/Kraanan
      oStatue = Create(&Statue,#bDefault=TRUE);
      plStatues = Cons(oStatue,plStatues);
      Send(self,@NewHold,#what=oStatue,#new_angle=ANGLE_WEST,
            #new_row=7,#new_col=13,#fine_row=48,#fine_col=40);

      % Qor/Faren
      oStatue = Create(&Statue,#bDefault=TRUE);
      plStatues = Cons(oStatue,plStatues);
      Send(self,@NewHold,#what=oStatue,#new_angle=ANGLE_EAST,
            #new_row=10,#new_col=1,#fine_row=16,#fine_col=24);

      % Shalille/Faren
      oStatue = Create(&Statue,#bDefault=TRUE);
      plStatues = Cons(oStatue,plStatues);
      Send(self,@NewHold,#what=oStatue,#new_angle=ANGLE_WEST,
            #new_row=10,#new_col=13,#fine_row=16,#fine_col=40);

      % Jala
      oStatue = Create(&Statue,#bDefault=TRUE,#desc=hoh_best_jala);
      plStatues = Cons(oStatue,plStatues);
      Send(self,@NewHold,#what=oStatue,#new_angle=ANGLE_WEST,
            #new_row=9,#new_col=11,#fine_row=40,#fine_col=0);

      % Qor
      oStatue = Create(&Statue,#bDefault=TRUE,#desc=hoh_best_qor);
      plStatues = Cons(oStatue,plStatues);
      Send(self,@NewHold,#what=oStatue,#new_angle=ANGLE_EAST,
            #new_row=9,#new_col=4,#fine_row=40,#fine_col=0);

      % Shal'ille
      oStatue = Create(&Statue,#bDefault=TRUE,#desc=hoh_best_shalille);
      plStatues = Cons(oStatue,plStatues);
      Send(self,@NewHold,#what=oStatue,#new_angle=ANGLE_WEST,
            #new_row=7,#new_col=11,#fine_row=24,#fine_col=0);

      % Kraanan
      oStatue = Create(&Statue,#bDefault=TRUE,#desc=hoh_best_kraanan);
      plStatues = Cons(oStatue,plStatues);
      Send(self,@NewHold,#what=oStatue,#new_angle=ANGLE_EAST,
            #new_row=7,#new_col=4,#fine_row=24,#fine_col=0);

      % Faren
      oStatue = Create(&Statue,#bDefault=TRUE,#desc=hoh_best_faren);
      plStatues = Cons(oStatue,plStatues);
      Send(self,@NewHold,#what=oStatue,#new_angle=ANGLE_WEST,
            #new_row=5,#new_col=11,#fine_row=8,#fine_col=0);

      % Riija
      oStatue = Create(&Statue,#bDefault=TRUE,#desc=hoh_best_riija);
      plStatues = Cons(oStatue,plStatues);
      Send(self,@NewHold,#what=oStatue,#new_angle=ANGLE_EAST,
            #new_row=5,#new_col=4,#fine_row=8,#fine_col=0);

      % Wizard
      oStatue = Create(&Statue,#bDefault=TRUE,#desc=hoh_best_wizard);
      plStatues = Cons(oStatue,plStatues);
      Send(self,@NewHold,#what=oStatue,#new_angle=ANGLE_SOUTH_EAST,
            #new_row=2,#new_col=4,#fine_row=24,#fine_col=56);

      % Warrior (skills)
      oStatue = Create(&Statue,#bDefault=TRUE,#desc=hoh_best_warrior);
      plStatues = Cons(oStatue,plStatues);
      Send(self,@NewHold,#what=oStatue,#new_angle=ANGLE_SOUTH_WEST,
            #new_row=2,#new_col=10,#fine_row=24,#fine_col=8);

      % Adventurer (overall)
      oStatue = Create(&Statue,#bDefault=TRUE,#desc=hoh_best_adventurer);
      plStatues = Cons(oStatue,plStatues);
      Send(self,@NewHold,#what=oStatue,#new_angle=ANGLE_SOUTH,
            #new_row=1,#new_col=7,#fine_row=32,#fine_col=32);

      return Send(SYS,@GetSuccessRsc);
   }

   ResetStatues()
   "Admin supported. Resets all of the Hall of Heroes statues."
   {
      local i, oLore;

      oLore = Send(SYS,@GetLore);
      if oLore = $
      {
         return Send(SYS,@GetFailureRsc);
      }

      % Delete statues, clear out "best" lists in lore, then recreate statues.
      foreach i in plStatues
      {	 
         Send(i, @Delete);
      }
      plStatues = $;

      Send(oLore, @ResetLore);
      Send(self, @CreateStatues);

      return Send(SYS,@GetSuccessRsc);
   }

   UpdateStatues(lData = $)
   {
      local i, index;

      if lData = $
      {
         debug("bad data!");

         return;
      }

      if plStatues = $
      {
         % Statues haven't been created yet.  No one has entered yet.
         return;
      }

      index = 0;
      foreach i in plStatues
      {
         index = index + 1;
         if Nth(lData,index) <> $
            AND Send(i,@GetOriginal) <> Nth(lData,index)
         {
            Send(i,@SetStatue,#original=Nth(lData,index));
         }
      }

      return;
   }

   Delete()
   {
      plStatues = $;

      propagate;
   }

end
%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
