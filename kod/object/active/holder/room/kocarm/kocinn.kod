% Meridian 59, Copyright 1994-2012 Andrew Kirmse and Chris Kirmse.
% All rights reserved.
%
% This software is distributed under a license that is described in
% the LICENSE file that accompanies it.
%
% Meridian is a registered trademark.


%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
KocatanInn is KocatanRoom

constants:

   include blakston.khd

resources:
  
   room_kocinn = kocinn.roo
   room_name_kocinn = "The Aerie Guest House"

   kocinn_waz_locked = "Room waz is locked."
   kocinn_wazwaz_locked = "Room wazwaz is locked."
   kocinn_zik_locked = "Room zik is locked.  There is a small keyhole in the "
      "door, however.  Perhaps if you used a key in it, something interesting might happen."
   kocinn_zikzik_locked = "Room zikzik is locked.  There is a small keyhole in the "
      "door, however.  Perhaps if you used a key in it, something interesting might happen."
   kocinn_katalin_locked = "Room katalin is locked.  There is a small keyhole in the "
      "door, however.  Perhaps if you used a key in it, something interesting might happen."
   kocinn_katalinwaz_locked = "Room katalinwaz is locked.  There is a small keyhole "
      "in the door, however.  Perhaps if you used a key in it, something interesting might happen."

   kocinn_rentableroom_door = \
      "  There is a small keyhole in the door, however.  Perhaps if "
      "you used a key in it, something interesting might happen."

   kocnews_god_name = "Designers' News"
   kocnews_god_desc = "Check here for information from the game administrators."

   kocinn_music = song12.mid

classvars:

   vrName = room_name_kocinn

   viTeleport_row = 4
   viTeleport_col = 5

   viTerrain_Type = TERRAIN_CITY | TERRAIN_SHOP
   viPermanent_flags = ROOM_NO_COMBAT | ROOM_SANCTUARY | ROOM_SAFELOGOFF

properties:

   prRoom = room_kocinn
   piRoom_num = RID_KOC_INN

   piBaseLight = LIGHT_NICE
   piOutside_factor = 3

   prMusic = kocinn_music

messages:

   CreateStandardExits()
   {
      plExits = $;

      %% to kocatan
      plExits = Cons([ 2, 5, RID_KOCATAN, 28, 7, ROTATE_NONE ],plExits);
      
      %% to kocatan tavern
      plExits = Cons([ 12, 8, RID_KOC_TAVERN, 16, 17, ROTATE_NONE ],plExits);

      plExits = Cons([ 5, 2, ROOM_LOCKED_DOOR, kocinn_waz_locked],plExits);
      plExits = Cons([ 6, 2, ROOM_LOCKED_DOOR, kocinn_waz_locked],plExits);
      plExits = Cons([ 5, 3, ROOM_LOCKED_DOOR, kocinn_waz_locked],plExits);
      plExits = Cons([ 6, 3, ROOM_LOCKED_DOOR, kocinn_waz_locked],plExits);

      plExits = Cons([ 10, 3, ROOM_LOCKED_DOOR, kocinn_wazwaz_locked],plExits);
      plExits = Cons([ 10, 4, ROOM_LOCKED_DOOR, kocinn_wazwaz_locked],plExits);

      propagate;
   }

   SomethingTryGo(what=$, row=0, col=0, fine_row=0, fine_col=0)
   {
      if (row=10) and (col=7) and (fine_col < 32)
      {     
         send(what,@msgsenduser,#message_rsc=kocinn_zik_locked);	 
         return TRUE;
      }
      
      if (row=11) and (col=8) and (fine_row < 32)
      {
         send(what,@msgsenduser,#message_rsc=kocinn_katalinwaz_locked);
         return TRUE;
      }
      
      if (row=7) and (col=5) and (fine_col > 32)
      {
         send(what,@msgsenduser,#message_rsc=kocinn_zikzik_locked);          
         return TRUE;
      }
      
      if (row=9) and (col=6) and (fine_row < 32)
      {
         send(what,@msgsenduser,#message_rsc=kocinn_katalin_locked);
         return TRUE;
      }
      
      propagate;
   }

   CreateStandardObjects()
   {
      local lObjCoords1, oNews;

      oNews = Create(&NewsLink,#nid=NID_ANNOUNCEMENTS,
	                  #name=kocnews_god_name,#desc=kocnews_god_desc);
      Send(self,@NewHold,#what=oNews,#new_row=4,#new_col=6,#fine_row=8,#fine_col=56);

      lObjCoords1 = [ [02,09,08,16,OO_JUG],[12,04,48,08,OO_JUG],[11,09,40,16,OO_GREY_JUG] ];

      Send(self,@CreateOrnObjFromList,#lObjlist=lObjCoords1);
      
      send(self,@NewHold,#what=Create(&KocatanInnkeeper),
           #new_row=5,#new_col=6,#fine_row=48,#fine_col=56,
           #new_angle=ANGLE_EAST);

      propagate;
   }

   EnterRentableRoom(who=$)
   "Sees if we are in the proper location to enter a rentable room in this inn."
   {
      local iRow, iCol;

      iRow = Send(who,@GetRow);
      iCol = Send(who,@GetCol);
      
      % coords: [5,3],[7,5],[9,6],[10,4],[10,7],[11,8]
      if (iRow = 10
            AND (iCol = 3
               OR iCol = 4
               OR iCol = 6
               OR iCol = 7
               OR iCol = 8))
         OR ((iRow = 5
               OR iRow = 6)
            AND (iCol = 2
               OR iCol = 3))
         OR (iRow = 7
              AND (iCol = 5
                  OR iCol = 6))
         OR ((iRow = 8
               OR iRow = 9)
            AND (iCol = 6))
         OR (iRow = 11
              AND (iCol = 8))
      {
         return TRUE;
      }

      return FALSE;
   }

end
%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
