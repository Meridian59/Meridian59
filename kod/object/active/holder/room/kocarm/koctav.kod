% Meridian 59, Copyright 1994-2012 Andrew Kirmse and Chris Kirmse.
% All rights reserved.
%
% This software is distributed under a license that is described in
% the LICENSE file that accompanies it.
%
% Meridian is a registered trademark.


%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
KocatanTavern is KocatanRoom

constants:

   include blakston.khd

resources:

   include koctav.lkod
  
   room_name_koctavern = "The Barking Monster"
   room_koctavern = koctav.roo
   koctavern_music = song12.mp3

classvars:

   vrName = room_name_koctavern

   viTeleport_row = 13
   viTeleport_col = 21

   viTerrain_Type = TERRAIN_CITY | TERRAIN_SHOP 
   viPermanent_flags = ROOM_NO_COMBAT | ROOM_SANCTUARY | ROOM_SAFELOGOFF

properties:

   prRoom = room_koctavern
   piRoom_num = RID_KOC_TAVERN

   piBaseLight = LIGHT_NICE
   piOutside_factor = 3

   pbSnowGroundTexture = TRUE

   prMusic = koctavern_music

messages:

   CreateStandardExits()
   {
      plExits = $;

      plExits = Cons([ 11, 30, RID_KOCATAN, 36, 14, ROTATE_NONE ],plExits);
      plExits = Cons([ 12, 30, RID_KOCATAN, 36, 14, ROTATE_NONE ],plExits);
      plExits = Cons([ 11, 31, RID_KOCATAN, 36, 14, ROTATE_NONE ],plExits);
      plExits = Cons([ 12, 31, RID_KOCATAN, 36, 14, ROTATE_NONE ],plExits);

      plExits = Cons([ 14, 25, ROOM_LOCKED_DOOR],plExits);
      plExits = Cons([ 15, 25, ROOM_LOCKED_DOOR],plExits);

      plExits = Cons([ 15, 16, RID_KOC_INN, 12, 7, ROTATE_NONE ],plExits);
      plExits = Cons([ 15, 17, RID_KOC_INN, 12, 7, ROTATE_NONE ],plExits);
      plExits = Cons([ 14, 16, RID_KOC_INN, 12, 7, ROTATE_NONE ],plExits);
      plExits = Cons([ 14, 17, RID_KOC_INN, 12, 7, ROTATE_NONE ],plExits);

      propagate;
   }

   CreateStandardObjects()
   {
      local lObjCoords1;

      lObjCoords1 = [ [14,19,16,32,OO_DECORATIVE_POT], [21,15,32,56,OO_JUG],
                      [10,25,00,16,OO_JUG], [10,16,08,32,OO_URN],
                      [10,17,08,56,OO_URN], [10,17,08,16,OO_GREY_JUG],
                      % second pass
                      [02,13,48,48,OO_PITCHER1], [03,08,00,48,OO_PITCHER1],
                      [01,25,56,24,OO_PITCHER1], [08,20,08,00,OO_PITCHER1],
                      [11,07,48,24,OO_PITCHER3], [20,07,00,00,OO_PITCHER3],
                      [21,30,32,48,OO_BARREL], [21,13,32,48,OO_BARREL],
                      [21,30,40,08,OO_BARREL2], [21,12,24,40,OO_BARREL2],
                      [20,13,40,08,OO_BARREL2], [04,07,08,32,OO_TALLURN],
                      [02,18,00,08,OO_BIGURN], [02,16,00,08,OO_BLUEJUG],
                      [16,25,00,16,OO_BLUEJUG], [19,30,00,56,OO_RICE]
                    ];

      Send(self,@CreateOrnObjFromList,#lObjlist=lObjCoords1);

      Send(self,@NewHold,#what=Create(&KocatanLamp),#new_row=10,#new_col=14,
            #fine_row=32,#fine_col=32);
      Send(self,@NewHold,#what=Create(&KocatanLamp),#new_row=13,#new_col=14,
            #fine_row=40,#fine_col=32);
      Send(self,@NewHold,#what=Create(&KocatanLamp),#new_row=10,#new_col=18,
            #fine_row=48,#fine_col=32);
      Send(self,@NewHold,#what=Create(&KocatanLamp),#new_row=13,#new_col=18,
            #fine_row=16,#fine_col=32);
      Send(self,@NewHold,#what=Create(&KocatanLamp),#new_row=19,#new_col=26,
            #fine_row=08,#fine_col=56);
      Send(self,@NewHold,#what=Create(&KocatanLamp),#new_row=05,#new_col=26,
            #fine_row=08,#fine_col=56);
      Send(self,@NewHold,#what=Create(&KocatanLamp),#new_row=04,#new_col=15,
            #fine_row=56,#fine_col=16);

      Send(self,@NewHold,#what=Create(&Candle),#new_row=02,#new_col=11,
            #fine_row=48,#fine_col=16);

      Send(self,@NewHold,#what=Create(&KocatanBartender),
             #new_row=19,#new_col=19,#fine_row=0,#fine_col=0,
             #new_angle=ANGLE_EAST);

      propagate;
   }

   StartSnow(bOnGround=TRUE)
   {
      % Check if we can have weather effects here.
      if NOT pbWeatherEffects
      {
         return;
      }

      % If there's a sandstorm weather effect here, clear it.
      if Send(self,@CheckRoomFlag,#flag=ROOM_SANDSTORM)
      {
         Send(self,@EndSandstorm);
      }

      if bOnGround
      {
         if pbSnowGroundTexture
         {
            % We just want to show snow in the outside area.
            Send(self,@ChangeTexture,#id=1,#new_texture=61015,#flags=CTF_FLOOR);
         }
      }

      Send(self,@SetRoomFlag,#flag=ROOM_SNOWING,#value=TRUE);
      Send(self,@WeatherChanged);

      return;
   }

   EndSnow(override=FALSE)
   "Use override if rooms have manually placed textures."
   {
      if Send(self,@CheckRoomFlag,#flag=ROOM_SNOWING)
      {
         Send(self,@SetRoomFlag,#flag=ROOM_SNOWING,#value=FALSE);
         Send(self,@WeatherChanged);
      }

      if pbSnowGroundTexture
         OR override
      {
         Send(self,@RemoveTextureChange,#id=1);
      }

      return;
   }

end
%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
