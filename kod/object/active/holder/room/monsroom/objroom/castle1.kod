% Meridian 59, Copyright 1994-2012 Andrew Kirmse and Chris Kirmse.
% All rights reserved.
%
% This software is distributed under a license that is described in
% the LICENSE file that accompanies it.
%
% Meridian is a registered trademark.


%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
CASTLE1 is ObjectRoom

constants:

   include blakston.khd

resources:

   include castle1.lkod

   room_castle1 = castle1.roo
   room_name_castle1 = "Castle Victoria"

   castle1_music = castle2.mp3

classvars:

   vrName = room_name_castle1

   viTeleport_row = 13
   viTeleport_col = 18

   viTerrain_type = TERRAIN_CASTLE

   viPermanent_Flags = ROOM_GUILD_PK_ONLY

   viWeatherZone = WEATHER_ZONE_CV
   viWeatherMask = WEATHER_MASK_MOUNTAINS

properties:

   prMusic = castle1_music

   piBaseLight = LIGHT_DARK
   piOutside_factor = OUTDOORS_3 

   prRoom = room_castle1
   piRoom_num = RID_CASTLE1

   pbMobUseDoors = TRUE

messages:

   Constructed()
   {
      plMonsters = [ [&Skeleton, 60]];

      plGenerators = [ [5,18], [13, 3], [14, 7], [12, 12],
                       [13, 8], [14, 24], [12, 27] ,
                       [13, 30], [12, 37], [19, 40] ,
                       [15, 44], [19,36] ] ;
                       
      plPatrolPaths = [ [ [5,23], [ 10,22 ], [11,21], [5,23], [5,18] ],
                        [ [10,4], [15,6], [13,3] ],
                        [ [9,17], [14,23], [12,46], [14,7] ],
                        [ [16,18], [7,14], [12,12] ],
                        [ [15,2], [13,8] ],
                        [ [9,14], [14,24] ],
                        [ [10,42], [12,27] ],
                        [ [10,26], [12,19], [13,30] ],
                        [ $ ],
                        [ [13,45], [19,40] ],
                        [ [20,36], [15,44] ],
                        [ $ ]];

      propagate;
   }

   CreateObjectGeneration()
   {
      plObject_gen = $;

      plObject_gen = Cons([&PurpleMushroom, GEN_INF, 4,  4, 30  ], plObject_gen);
      plObject_gen = Cons([&PurpleMushroom, GEN_INF, 4, 7, 33 ], plObject_gen);

      propagate;
   }


   CreateStandardExits()
   {
      plExits = $;

      plExits = Cons([ 6, 17, RID_THRONE1, 22, 6, ROTATE_NONE ],plExits);
      plExits = Cons([ 6, 18, RID_THRONE1, 22, 6, ROTATE_NONE ],plExits);
      plExits = Cons([ 6, 19, RID_THRONE1, 22, 6, ROTATE_NONE ],plExits);

      plExits = Cons([ 1, 17, RID_CASTLE1B, 8, 23, ROTATE_NONE ],plExits);
      plExits = Cons([ 2, 17, RID_CASTLE1B, 8, 23, ROTATE_NONE ],plExits);
      plExits = Cons([ 1, 19, RID_CASTLE1B, 8, 28, ROTATE_NONE ],plExits);
      plExits = Cons([ 2, 19, RID_CASTLE1B, 8, 28, ROTATE_NONE ],plExits);

      plExits = Cons([ 16, 16, RID_CASTLE1C, 4, 43, ROTATE_90 ],plExits);
      plExits = Cons([ 16, 17, RID_CASTLE1C, 4, 43, ROTATE_90 ],plExits);
      plExits = Cons([ 16, 18, RID_CASTLE1C, 4, 43, ROTATE_90 ],plExits);
      plExits = Cons([ 16, 19, RID_CASTLE1C, 4, 43, ROTATE_90 ],plExits);

      plExits = Cons([ 9, 4, RID_CASTLE1,  7, 4, ROTATE_NONE ],plExits);
      plExits = Cons([ 8, 4, RID_CASTLE1, 10, 4, ROTATE_NONE ],plExits);

      plExits = Cons([ 9, 10, RID_CASTLE1, 7, 10, ROTATE_NONE ],plExits);
      plExits = Cons([ 8, 10, RID_CASTLE1, 10, 10, ROTATE_NONE ],plExits);

      plExits = Cons([ 9, 26, RID_CASTLE1, 7, 26, ROTATE_NONE ],plExits);
      plExits = Cons([ 8, 26, RID_CASTLE1, 10, 26, ROTATE_NONE ],plExits);

      plExits = Cons([ 9, 32, RID_CASTLE1, 7, 32, ROTATE_NONE ],plExits);
      plExits = Cons([ 8, 32, RID_CASTLE1, 10, 32, ROTATE_NONE ],plExits);

      plExits = Cons([ 4, 33, RID_DUNGEON, 24, 2, ROTATE_NONE ],plExits);

      propagate;
   }

   SomethingMoved(what = $, new_row = $, new_col = $, fine_row = FINENESS/2,
                  fine_col = FINENESS/2, cause = CAUSE_UNKNOWN, speed = 0,
                  non_monsters_only = FALSE)
   {
      % Moves monsters through the doors. If this message is modified,
      % take care not to choose new_row values that would cause an infinite
      % loop. Random check also present to help prevent that from happening
      % by mistake, and to stop the mobs potentially moving back and forward
      % quickly. This can be turned on/off via pbMobUseDoors.
      if (NOT pbMobUseDoors)
      {
         propagate;
      }

      if IsClass(what,&Monster)
      {
         if (new_row = 9
               OR new_row = 8)
            AND (new_col = 4
               OR new_col = 10
               OR new_col = 26
               OR new_col = 32)
         {
            if Random(1,5) = 1
            {
               if (new_row = 9)
               {
                  Send(self,@SomethingMoved,#what=what,
                        #new_row=7,#new_col=new_col,
                        #fine_row=fine_row,#fine_col=fine_col,
                        #speed=speed);
               }
               else
               {
                  Send(self,@SomethingMoved,#what=what,
                        #new_row=10,#new_col=new_col,
                        #fine_row=fine_row,#fine_col=fine_col,
                        #speed=speed);
               }

               return;
            }
         }
      }

      propagate;
   }

   ReqSomethingAttack(what = $,victim = $,use_weapon = $,stroke_obj = $)
   "Disallow attacks between the two parts of the room. "
   {
      local iWhatRow, iWhatCol, iVicRow, iVicCol, iTmp;
      if (what = $
         OR victim = $)
      {
         propagate;
      }

      iWhatRow = Send(what,@GetRow);
      iWhatCol = Send(what,@GetCol);
      iVicRow = Send(victim,@GetRow);
      iVicCol = Send(victim,@GetCol);
      if (iWhatRow = $
         OR iWhatCol = $
         OR iVicRow = $
         OR iVicCol = $)
      {
         propagate;
      }
      
      if (iWhatRow >= 9 AND iVicRow >= 9)
      {
         propagate;
      }

      if (iWhatRow >= 9)
      {
         iTmp = iWhatRow;
         iWhatRow = iVicRow;
         iVicRow = iTmp;
         iTmp = iWhatCol;
         iWhatCol = iVicCol;
         iVicCol = iTmp;
      }

      if (iWhatRow < 9)
      {
         if (iWhatCol < 8)
         {
            if (iVicRow > 8
               OR iVicCol > 7)
            {
               return FALSE;
            }

            propagate;
         }

         if (iWhatCol < 13)
         {
            if (iVicRow > 8
               OR iVicCol < 8
               OR iVicCol > 12)
            {
               return FALSE;
            }

            propagate;
         }

         if (iWhatCol > 23
            AND iWhatCol < 30)
         {
            if (iVicRow > 8
               OR iVicCol < 24
               OR iVicCol > 29)
            {
               return FALSE;
            }

            propagate;
         }

         if (iWhatCol > 29
            AND iWhatCol < 35)
         {
            if (iVicRow > 8
               OR iVicCol < 30
               OR iVicCol > 34)
            {
               return FALSE;
            }

            propagate;
         }

         if (iVicCol > 12
               OR iVicCol < 24)
         {
            propagate;
         }
      }

      return FALSE;
   }

end
%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
