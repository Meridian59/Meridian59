% Meridian 59, Copyright 1994-2012 Andrew Kirmse and Chris Kirmse.
% All rights reserved.
%
% This software is distributed under a license that is described in
% the LICENSE file that accompanies it.
%
% Meridian is a registered trademark.


%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
Cave2 is ObjectRoom

constants:

   include blakston.khd

resources:

   include cave2.lkod

   room_cave2 = cave2.roo
   room_name_cave2 = "A Deep, Dark, Spooky, Icky Cave"

   cave2_music = song.mp3

classvars:

   vrName = room_name_cave2

   viTeleport_row = 23
   viTeleport_col = 16

   viTerrain_type = TERRAIN_CAVES | TERRAIN_TEMPLE

properties:

   prRoom = room_cave2
   piRoom_num = RID_CAVE2

   prMusic = cave2_music

   piBaseLight = LIGHT_DARK

   piOutside_factor = OUTDOORS_2

   piGen_time = 20000
   piGen_percent = 80

   piGen_Object_Time = 1200000

   ptIllusionResetTimer = $
   piLastIllusionReplaceTime = $

messages:

   CreateStandardExits()
   {
      plExits = $;
      plExits = Cons([ 18, 30, RID_CAVE3, 74, 34, ROTATE_NONE ],plExits);

      plEdge_Exits = $;
      plEdge_Exits = Cons([LEAVE_SOUTH, RID_H7, 19, 3, ANGLE_SOUTH], plEdge_exits);
      plEdge_Exits = Cons([LEAVE_WEST, RID_ORC_CAVE1, 43, 41, ANGLE_SOUTH], plEdge_exits);

      propagate;
   }

   CreateObjectGeneration()
   {
      plObject_gen = $;
      plObject_gen = Cons([ &Chalice, GEN_ONE, 0, 23, 11, 16, 48 ],plObject_gen);
      plObject_gen = Cons([ &PurpleMushroom, GEN_INF, 3, 24, 49 ],plObject_gen);
      plObject_gen = Cons([ &PurpleMushroom, GEN_INF, 3, 25, 53 ],plObject_gen);

      propagate;
   }

   Constructed()
   {
      piLastIllusionReplaceTime = GetTime();

      plMonsters = [ [&Orc, 50], [&Spider, 50] ];

      plGenerators = [ [51, 14], [50, 34], [18, 50], [27, 47], [23, 39],
                       [19, 30], [25, 26], [25, 33], [32, 35], [31, 31],
                       [35, 58], [15, 43], [16, 56] ];

      propagate;
   }

   DispelIllusions(what=$)
   {
      Send(self,@ChangeTexture,#id=2,#new_texture=01605,
            #flags=CTF_FLOOR); %1605 is
      Send(self,@ChangeTexture,#id=3,#new_texture=01605,
            #flags=CTF_FLOOR); %cave stone 5
      Send(self,@ChangeTexture,#id=4,#new_texture=01605,
            #flags=CTF_FLOOR);
      Send(self,@ChangeTexture,#id=5,#new_texture=01605,
            #flags=CTF_FLOOR);

      Send(self,@SetSector,#sector=1,#animation=ANIMATE_FLOOR_LIFT,
            #height=24,#speed=0);
      Send(self,@SetSector,#sector=2,#animation=ANIMATE_FLOOR_LIFT,
            #height=24,#speed=0);
      Send(self,@SetSector,#sector=3,#animation=ANIMATE_FLOOR_LIFT,
            #height=24,#speed=0);
      Send(self,@SetSector,#sector=4,#animation=ANIMATE_FLOOR_LIFT,
            #height=24,#speed=0);
      Send(self,@SetSector,#sector=5,#animation=ANIMATE_FLOOR_LIFT,
            #height=24,#speed=0);

      ptIllusionResetTimer = CreateTimer(self,@ReplaceIllusions,30000);

      return;
   }

   ReplaceIllusions()
   {
      ptIllusionResetTimer = $;
      piLastIllusionReplaceTime = GetTime();

      Send(self,@ChangeTexture,#id=2,#new_texture=01722,
            #flags=CTF_FLOOR); %marble peach2
      Send(self,@ChangeTexture,#id=3,#new_texture=01701,
            #flags=CTF_FLOOR); %marble white1
      Send(self,@ChangeTexture,#id=4,#new_texture=01701,
            #flags=CTF_FLOOR);
      Send(self,@ChangeTexture,#id=5,#new_texture=01701,
            #flags=CTF_FLOOR);

      Send(self,@SetSector,#sector=1,#animation=ANIMATE_FLOOR_LIFT,
            #height=128,#speed=0);
      Send(self,@SetSector,#sector=2,#animation=ANIMATE_FLOOR_LIFT,
            #height=56,#speed=0);
      Send(self,@SetSector,#sector=3,#animation=ANIMATE_FLOOR_LIFT,
            #height=48,#speed=0);
      Send(self,@SetSector,#sector=4,#animation=ANIMATE_FLOOR_LIFT,
            #height=40,#speed=0);
      Send(self,@SetSector,#sector=5,#animation=ANIMATE_FLOOR_LIFT,
            #height=32,#speed=0);

      return;
   }

   GetLastClosedTime()
   {
      return piLastIllusionReplaceTime;
   }

   CreateStandardObjects()
   {
      Send(self,@NewHold,#what=Create(&ManaNode,#node_num=NODE_ORCCAVES),
            #new_row=23,#new_col=53,#new_angle=ANGLE_WEST);

      propagate;
   }

   OkayToGetChalice()
   {
      local i, Each_obj;

      % Must dispell the illusion to get the chalice.
      if ptIllusionResetTimer = $
      {
         return FALSE;
      }

      foreach i in plActive
      {
         each_obj=Send(self,@holderExtractObject,#data=i);
         if isClass(each_obj,&orc)
         {
            return FALSE;
         }
      }

      return TRUE;
   }

   IsMoveOK(old_row = 1, old_col = 1, old_fine_row = 1, old_fine_col = 1,
            new_row = 1, new_col = 1, new_fine_row = 1, new_fine_col = 1,
            who=$)
   {
      % This is a hardcoded movement check, which
      % verifies the movement against 3 lines to
      % make sure no one passes through them
      % while the cave entrance is not dispelled
      %
      % line1 (vertical)   (x=10 / xfine=62) 
      % line2 (vertical)   (x=14 / xfine=21)
      % line3 (horizontal) (y=17 / yfine=18)
      %
      %         | line2 (vertic.)
      %         |
      %    _____| line3 (horizont.)
      %   |
      %   |
      % < Entrance <
      %   |
      %   | line1 (vertic)

      local isOldEastOfLine1, isOldEastOfLine2, isOldSouthOfLine3;

      % Basic requirement Entrance must not be dispelled
      if ptIllusionResetTimer = $
      {
         % if we are south of line3
         if (old_row > 17 OR (old_row = 17 AND old_fine_row > 18))
         {
            % if we are east of line1
            if (old_col > 10 OR (old_col = 10 AND old_fine_col > 62))
            {
               % don't allow crossing line1 from east to west
               if (new_col < 10 OR (new_col = 10 AND new_fine_col <= 62))
               {
                  Send(self,@LogCheatingAttempt,#who=who,#old_row=old_row,
                        #old_col=old_col,#old_fine_row=old_fine_row,
                        #old_fine_col=old_fine_col,#new_row=new_row,
                        #new_col=new_col,#new_fine_row=new_fine_row,
                        #new_fine_col=new_fine_col);

                  return FALSE;
               }

               % if we are west of line2
               if (old_col < 14 OR (old_col = 14 AND old_fine_col <= 21))
               {
                  % don't allow crossing line3 from south to north
                  if (new_row < 17 OR (new_row = 17 AND new_fine_row <= 18))
                  {
                     Send(self,@LogCheatingAttempt,#who=who,#old_row=old_row,
                           #old_col=old_col,#old_fine_row=old_fine_row,
                           #old_fine_col=old_fine_col,#new_row=new_row,
                           #new_col=new_col,#new_fine_row=new_fine_row,
                           #new_fine_col=new_fine_col);

                     return FALSE;
                  }
               }
            }
         }

         % if we are north of line3 (or on line3)
         else
         {
            % don't allow crossing line2 from east to west
            if (old_col > 14 OR (old_col = 14 AND old_fine_col > 21))
               AND (new_col < 14 OR (new_col = 14 AND new_fine_col <= 21))
            {
               Send(self,@LogCheatingAttempt,#who=who,#old_row=old_row,
                     #old_col=old_col,#old_fine_row=old_fine_row,
                     #old_fine_col=old_fine_col,#new_row=new_row,
                     #new_col=new_col,#new_fine_row=new_fine_row,
                     #new_fine_col=new_fine_col);

               return FALSE;
            }
         }
      }

      return TRUE;
   }

   LogCheatingAttempt(old_row=1,old_col=1,old_fine_row=1,old_fine_col=1,
                     new_row=1,new_col=1,new_fine_row=1,new_fine_col=1,who=$)
   {
      Debug("ALERT!  ",Send(who,@GetTrueName),who,
            "tried to move through closed cave entrance, from ",
            old_row,old_col,old_fine_row,old_fine_col," to ",new_row,new_col,
            new_fine_row,new_fine_col,". Last time cave closed was ",
            GetTime() - Send(self,@GetLastClosedTime)," seconds ago.");

      return;
   }

   Delete()
   {
      if ptIllusionResetTimer <> $
      {
         DeleteTimer(ptIllusionResetTimer);
         ptIllusionResetTimer=$;
      }

      propagate;
   }

end
%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
