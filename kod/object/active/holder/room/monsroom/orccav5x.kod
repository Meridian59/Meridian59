% Meridian 59, Copyright 1994-2012 Andrew Kirmse and Chris Kirmse.
% All rights reserved.
%
% This software is distributed under a license that is described in
% the LICENSE file that accompanies it.
%
% Meridian is a registered trademark.


%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
OrcCave5Extension is MonsterRoom

constants:

   include blakston.khd

resources:

   include orccav5x.lkod

   room_OrcCave5Ext  = oc5b.roo
   room_name_OrcCave5Ext =  "A spring of vitality"

   OrcCave5Ext_music = orcpit.mp3
   
   OrcCave5Ext_drink = "You drink from the life-giving waters, and feel refreshed!"
   OrcCave5Ext_drink_sound =  drink.wav

classvars:

   vrName = room_name_OrcCave5Ext
   
   viTeleport_row = 29
   viTeleport_col = 9

   viTerrain_type = TERRAIN_CAVES
   viFilling = 20  
   viNutrition = 40

properties:

   prRoom = room_OrcCave5Ext
   piRoom_num = RID_ORC_CAVE5_EXT

   piBaseLight = LIGHT_MIN
   piOutside_factor = 0

   prMusic = OrcCave5Ext_music

messages:

   CreateStandardExits()
   {
      plEdge_exits = $;
      plEdge_Exits = Cons([LEAVE_EAST, RID_ORC_CAVE5, 12, 4, ROTATE_NONE], plEdge_exits);
      plEdge_Exits = Cons([LEAVE_WEST, RID_ORC_PIT, 64, 68, ROTATE_NONE], plEdge_exits);

      propagate;
   }

   IsBeverage()
   {
      return TRUE;
   }
   
   SomethingTryGo(what = $,row = $,col = $)
   {
      if ((row >=42) AND (row <=46))
         AND ((col >= 19) AND (col <=25))
         AND isClass(what,&User)
         AND send(self,@UserDrink,#who=what)
      {
         return TRUE;
      } 

      propagate;
   }

   UserDrink(who=$)
   {
      %% someone presses space bar in the pool, gets a cute little boost.
      if NOT Send(who,@ReqEatSomething,#filling=viFilling,#what=self)
      {
         return TRUE;
      }      

      Send(who,@EatSomething,#nutrition=viNutrition,#filling=viFilling);
      Send(who,@WaveSendUser,#wave_rsc=OrcCave5Ext_drink_sound);
      Send(who,@MsgSendUser,#message_rsc=OrcCave5Ext_drink);

      return TRUE;
   }

   ModifyChanceToImprove(who=$,oSpellSkill=$,chance=0)
   "This takes a spell/skill that who is trying to advance and modifies the improve_chance."
   {
      % Return a reduced chance.  This is a popular botting spot.
      return (chance / 3);
   }

   CreateStandardObjects()
   {
      send(self,@NewHold,#what=Create(&DynamicLight,#iColor=LIGHT_BBLUE,#iIntensity=255),
           #new_row=44,#new_col=22,#fine_row=32,#fine_col=32);

      propagate;
   }



end
%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
