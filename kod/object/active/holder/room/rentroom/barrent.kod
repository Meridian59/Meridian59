% Meridian 59, Copyright 1994-2012 Andrew Kirmse and Chris Kirmse.
% All rights reserved.
%
% This software is distributed under a license that is described in
% the LICENSE file that accompanies it.
%
% Meridian is a registered trademark.


%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
BarloqueRentableRoom is RentableRoom

constants:

   include blakston.khd

resources:

   include barrent.lkod

   BarloqueRentableRoom_name = "A tastefully appointed room"
   BarloqueRentableRoom_roo = barrent.roo

   BarloqueRentableRoom_landlord = "Pritchett"

classvars:

   vrName = BarloqueRentableRoom_name

   viTeleport_row = 16
   viTeleport_col = 6

   viBedSectorMin = 1
   viBedSectorMax = 7
   viPictureLocationMax = 4

   vrLandlord = BarloqueRentableRoom_Landlord
   viLocation = RID_BAR_INN

   viDecorator_row = 15
   viDecorator_col = 4

properties:

   prRoom = BarloqueRentableRoom_roo
   piWallpaper = 50069 % WALLPAPER_D, the default in barrent.roo.
   piCarpet = 50052 % CARPET_C, default in barrent.roo.

messages:

   Constructor(iRID=RID_RENTABLE_START)
   {
      piRoom_num = iRID;

      % Format for plBedCoords: pillow coord, "nearest" coord, pillow1 finecoord,
      %                         pillow2 finecoord, pillow angle
      % Each top-level list (one per line) is one bed position.
      plBedCoords = [ [ [16,3], [15,3], [48,16], [48,48], ANGLE_EAST ],
                      [ [14,3], [14,4], [16,16], [48,16], ANGLE_SOUTH ],
                      [ [13,3], [13,4], [16,16], [48,16], ANGLE_SOUTH ],
                      [ [13,5], [14,5], [16,16], [16,48], ANGLE_WEST ],
                      [ [13,7], [13,6], [16,48], [48,48], ANGLE_NORTH ],
                      [ [14,7], [14,6], [16,48], [48,48], ANGLE_NORTH ],
                      [ [16,7], [15,7], [48,16], [48,48], ANGLE_EAST ]
                    ];

      % Format for plPictureCoords: coordinates nearest picture, followed by
      % the next nearest coordinates.
      % Each top-level list (one per line) is one picture position
      plPictureCoords = [ [ [16,4], [16,3], [15,4], [16,5] ],
                          [ [14,3], [15,3] ],
                          [ [13,4], [13,5] ],
                          [ [14,7], [13,7], [14,6], [15,7] ]
                        ];

      propagate;
   }

   SomethingTryGo(what = $,row = $,col = $)
   {
      if row = 16 AND col = 5
      {
         Send(what,@MsgSendUser,#message_rsc=RentableRoom_teleport);
         Send(what,@EffectSendUserDuration,#effect=EFFECT_WHITEOUT,
               #duration=750);
         Send(Send(SYS,@FindRoomByNum,#num=viLocation),@NewHold,#what=what,
               #new_row=5,#new_col=12,#new_angle=ANGLE_SOUTH);

         return TRUE;
      }

      propagate;
   }

   CreateStandardObjects()
   {
      Send(self,@NewHold,#what=Create(&Bell),
            #new_row=16,#new_col=6,#fine_row=41);

      propagate;
   }

   Enter(who=$)
   {
      Send(self,@NewHold,#what=who,#new_row=15,#new_col=5,
            #new_angle=ANGLE_NORTH);

      return;
   }

end
%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
