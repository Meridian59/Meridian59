% Meridian 59, Copyright 1994-2012 Andrew Kirmse and Chris Kirmse.
% All rights reserved.
%
% This software is distributed under a license that is described in
% the LICENSE file that accompanies it.
%
% Meridian is a registered trademark.


%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
MarionRentableRoom is RentableRoom

constants:

   include blakston.khd

resources:

   include marrent.lkod

   MarionRentableRoom_name = "A comfortable room"
   MarionRentableRoom_roo = marrent.roo

   MarionRentableRoom_landlord = "Morrigan"

classvars:

   vrName = MarionRentableRoom_name

   viTeleport_row = 10
   viTeleport_col = 10

   viTerrain_Type = TERRAIN_CITY | TERRAIN_SHOP

   viBedSectorMin = 1
   viBedSectorMax = 5
   viPictureLocationMax = 5

   vrLandlord = MarionRentableRoom_landlord
   viLocation = RID_MAR_INN

   viDecorator_row = 9
   viDecorator_col = 10

properties:

   prRoom = MarionRentableRoom_roo
   piWallpaper = 50057 % WALLPAPER_B, default in marrent.roo.
   piCarpet = 50050 % CARPET_A, default in barrent.roo.

messages:

   Constructor(iRID=RID_RENTABLE_START)
   {
      % Format for plBedCoords: pillow coord, non-pillow coord, pillow1 finecoord,
      %                         pillow2 finecoord, pillow angle
      % Each top-level list (one per line) is one bed position.
      plBedCoords = [ [ [8,9], [9,9], [16,16], [16,48], ANGLE_WEST ],
                      [ [10,9], [10,10], [16,16], [48,16], ANGLE_SOUTH ],
                      [ [11,9], [11,10], [16,16], [48,16], ANGLE_SOUTH ],
                      [ [12,9], [12,10], [16,16], [48,16], ANGLE_SOUTH ],
                      [ [12,11], [11,11], [48,16], [48,48], ANGLE_EAST ]
                    ];

      % Format for plPictureCoords: coordinates nearest picture, followed by
      % the next nearest coordinates.
      % Each top-level list (one per line) is one picture position
      plPictureCoords = [ [ [8,11], [8,10], [8,12] ],
                          [ [9,9], [8,9], [9,10] ],
                          [ [11,9], [10,9], [12,9] ],
                          [ [12,10], [12,11], [11,10] ],
                          [ [10,11], [11,11], [10,10], [9,11] ]
                        ];

      propagate;
   }

   SomethingTryGo(what = $,row = $,col = $)
   {
      if row = 8 AND col = 12
      {
         Send(what,@MsgSendUser,#message_rsc=RentableRoom_teleport);
         Send(what,@EffectSendUserDuration,#effect=EFFECT_WHITEOUT,
               #duration=750);
         Send(Send(SYS,@FindRoomByNum,#num=RID_MAR_INN),@NewHold,#what=what,
               #new_row=6,#new_col=9,#new_angle=ANGLE_WEST);

         return TRUE;
      }

      propagate;
   }
   
   CreateStandardObjects()
   {
      Send(self,@NewHold,#what=Create(&Bell),
            #new_row=8,#new_col=10,#fine_row=23);

      propagate;
   }

   Enter(who=$)
   {
      Send(self,@NewHold,#what=who,#new_row=8,#new_col=11,
            #new_angle=ANGLE_WEST);

      return;
   }

end
%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
