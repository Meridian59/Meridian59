% Meridian 59, Copyright 1994-2012 Andrew Kirmse and Chris Kirmse.
% All rights reserved.
%
% This software is distributed under a license that is described in
% the LICENSE file that accompanies it.
%
% Meridian is a registered trademark.


%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
TempleRiija is Room

constants:

   include blakston.khd

   RESET_TIME = 30000            % five seconds
   DEATH_MESSAGE_DELAY = 5000
   PAIN_EFFECT_DURATION = 2000

   SECTOR_DOOR = 4

resources:

   room_name_RiijaTemple = "The Temple of Riija"
   room_RiijaTemple = KE1.roo
   riijatemple_music = bridge.mid

   riijatemple_door1 = "~kThe Monk of Riija says, \"~BNo ~rone ~B~bmay ~Ienter ~kmy ~g~Badytum~n~k.\""
   riijatemple_door2 = "~kThe Monk of Riija says, \"~BCease.  ~I~bYour ~rsanity ~gwould not remain ~B~kintact ~B~bif you were to enter ~I~rthe Library of the ~BGibbering Maelstrom.\""
   riijatemple_door3 = "~kThe Monk of Riija says, \"The inner cloister ~I~ris not for ~gyour ~bfeet ~Bto ~B~rtread~n~k.\""
   riijatemple_door4 = "~kThe Monk of Riija says, \"~b~BIt is forbidden ~r~Bfor you to ~gdescend into the ~I~kabyssal catacombs ~n~gbeneath the ~Btemple~n~k.\""
   riijatemple_door5 = "~kThe Monk of Riija says, \"~rIt would be best if you avoided ~b`Ithat`I~r door~n~k.\""
   riijatemple_door6 = "~kThe Monk of Riija says, \"~BThat ~rdoor ~gleads ~bnowhere.  ~IThat is ~I~gnot to say ~kan empty ~rroom ~Bor ~B~ga blank wall ~rlies beyond it.  ~b`INowhere`I ~gis an infinite purgatory ~Bthat ~rswallows all that enter~n~k.\""

   riijatemple_sound = down2.wav

   riijaplayer_death_male_wav = malefall822m.wav
   riijaplayer_death_female_wav = femalefall822m.wav

   riijadeadmessage0a = "Ouch, that last step was a doozy.\nYou awaken, dazed, in what appears to be an inn."
   riijadeadmessage0b = "Ouch, that last step was a doozy."

   riijadeadmessage1 = "Are you dead?"
   riijadeadmessage2 = "No, this isn't what death feels like..."
   riijadeadmessage3 = "it doesn't hurt so much."
   riijadeadmessage4 = "No, that's not it, you don't know what happened..."
   riijadeadmessage5 = "but.... you probably got very lucky."

   user_killed_bridge_faith = "### %s just hurled %sself from the cliffs of Riija to find a watery grave."

classvars:

   vrName = room_name_RiijaTemple

   viTeleport_row = 49
   viTeleport_col = 31

   viTerrain_type = TERRAIN_TEMPLE | TERRAIN_MYSTICAL | TERRAIN_JUNGLE | TERRAIN_BEACH | TERRAIN_MOUNTAIN

   viClientFlags = ROOM_OVERRIDE_DEPTH1
   viOverrideDepth1 = BRIDGE_OF_FAITH_LEVEL

properties:

   prRoom = room_RiijaTemple
   piRoom_num = RID_TEMPLE_RIIJA
   prMusic = riijatemple_music
   pbIllusioned = TRUE

   piBaseLight = LIGHT_NICE
   piOutside_factor = 3

   ptIllusionResetTimer = $
   ptDeathMessageTimer = $
   poDead = $
   
   pbPrecipitation_enabled = TRUE
   piClimate = CLIMATE_TROPICAL

messages:

   Constructor()
   {
      propagate;
   }

   Teleport(what=$)
   {
      local oRoom;
      
      if Send(what,@GetOwner) = self
      {
         % Someone is casting blink.  If on the island, blink them back to the city.
         if Send(self,@IsObjectInTempleRegion,#what=what,#row=Send(what,@GetRow),#col=Send(what,@GetCol))
         {
            oRoom = Send(SYS,@FindRoombyNum,#num=RID_KOC_NORTH);            
            Post(oRoom,@Teleport,#what=what);
            
            return TRUE;
         }
      }
      
      propagate;
   }

   NewHold(what=$,new_row=0,new_col=0)
   {
      % If someone logs on or otherwise appears in the temple, teleport them away
      if IsClass(what,&Player)
         AND Send(self,@IsObjectInTempleRegion,#what=what,#row=new_row,#col=new_col)
         AND NOT (IsClass(what,&DM) AND Send(what,@PlayerIsImmortal))
      {
         Post(self,@Teleport,#what=what);
      }

      % Catch and kill any monsters (e.g. reflections, apparitions, etc) spawned in the
      % the area stretching from the bridge's left to right buffer areas.
      if IsClass(what,&Monster) AND Send(self,@IsObjectInBridgeRegion,#what=what,#row=new_row,#col=new_col)
      {
         % `Post` to allow the monster movement to complete before killing it
         Post(what,@Killed,#what=what);
      }
      
      propagate;
   } 

   SomethingTryGo(what=$,row=$,col=$)
   {
      if pbIllusioned
      { 
         if row = 16 AND col = 31
         {
            Send(what,@MsgSendUser,#message_rsc=riijatemple_door1);

            return TRUE;
         }

         if row = 19 AND col = 31
         {
            Send(what,@MsgSendUser,#message_rsc=riijatemple_door2);

            return TRUE;
         }

         if row = 19 AND col = 30
         {
            Send(what,@MsgSendUser,#message_rsc = riijatemple_door3);

            return TRUE;
         }

         if row = 11 AND col = 33
         {
            Send(what,@MsgSendUser,#message_rsc = riijatemple_door4);

            return TRUE;
         }

         if row = 16 AND col = 30 
         {
            Send(what,@MsgSendUser,#message_rsc = riijatemple_door5);

            return TRUE;
         }

         if row = 11 AND col = 28
         {
            Send(what,@MsgSendUser,#message_rsc = riijatemple_door6);

            return TRUE;
         }
      }
      else
      {
         % Don't let people enter the front door while not illusioned
         if (row = 23 OR row = 24)
            AND (col = 30 OR col = 31)
         {
            % Return without message.  That'll drive the conspiracy theorists wild!
            return TRUE;
         }
      }

      propagate;
   }

   DispelIllusions(what=$)
   {
      local oMonk;

      oMonk = Send(self,@FindHoldingActive,#class=&RiijaMonk);

      if oMonk <> $
      {
         Send(oMonk,@DispelIllusions);
      }

      Send(self,@DoDispelIllusionEffect);

      pbIllusioned = FALSE;

      % Reappear in 30 seconds.
      ptIllusionResetTimer = CreateTimer(self,@ReplaceIllusions,RESET_TIME);
      
      propagate;
   }

   DoDispelIllusionEffect()
   {
      % Must be able to be called redundantly.
      % May be sent even if effect is already in that position.

      Send(self,@ChangeTexture,#id=2,#new_texture=02301,#flags=CTF_FLOOR);
      Send(self,@ChangeTexture,#id=4,#new_texture=02301,#flags=CTF_FLOOR);
      Send(self,@ChangeTexture,#id=6,#new_texture=02301,#flags=CTF_FLOOR); 
      Send(self,@ChangeTexture,#id=7,#new_texture=02305,#flags=CTF_FLOOR); 
      Send(self,@ChangeTexture,#id=8,#new_texture=02305,#flags=CTF_FLOOR);

      Send(self,@ChangeTexture,#id=2,#new_texture=0,#flags=CTF_CEILING); 
      Send(self,@ChangeTexture,#id=4,#new_texture=0,#flags=CTF_CEILING);
      Send(self,@ChangeTexture,#id=5,#new_texture=0,#flags=CTF_CEILING); 
      Send(self,@ChangeTexture,#id=6,#new_texture=09418,#flags=CTF_CEILING);
      Send(self,@ChangeTexture,#id=7,#new_texture=08823,#flags=CTF_CEILING); 
      Send(self,@ChangeTexture,#id=8,#new_texture=08823,#flags=CTF_CEILING);
      Send(self,@ChangeTexture,#id=9,#new_texture=08823,#flags=CTF_CEILING);

      Send(self,@ChangeTexture,#id=10,#new_texture=0,#flags=CTF_NORMALWALL);
      Send(self,@ChangeTexture,#id=10,#new_texture=09418,#flags=CTF_BELOWWALL);
      Send(self,@ChangeTexture,#id=11,#new_texture=09418,#flags=CTF_ABOVEWALL);

      Send(self,@SetSector,#sector=2,#animation=ANIMATE_FLOOR_LIFT,#height=392,#speed=0);
      Send(self,@SetSector,#sector=3,#animation=ANIMATE_FLOOR_LIFT,#height=392,#speed=0);
      Send(self,@SetSector,#sector=4,#animation=ANIMATE_FLOOR_LIFT,#height=392,#speed=0);
      Send(self,@SetSector,#sector=5,#animation=ANIMATE_FLOOR_LIFT,#height=392,#speed=0);
      Send(self,@SetSector,#sector=6,#animation=ANIMATE_FLOOR_LIFT,#height=392,#speed=0);
      Send(self,@SetSector,#sector=7,#animation=ANIMATE_FLOOR_LIFT,#height=392,#speed=0);
      Send(self,@SetSector,#sector=8,#animation=ANIMATE_FLOOR_LIFT,#height=392,#speed=0);
      Send(self,@SetSector,#sector=9,#animation=ANIMATE_FLOOR_LIFT,#height=392,#speed=0);

      Send(self,@SetSector,#sector=2,#animation=ANIMATE_CEILING_LIFT,#height=544,#speed=0);
      Send(self,@SetSector,#sector=3,#animation=ANIMATE_CEILING_LIFT,#height=544,#speed=0);
      Send(self,@SetSector,#sector=4,#animation=ANIMATE_CEILING_LIFT,#height=544,#speed=0);
      Send(self,@SetSector,#sector=5,#animation=ANIMATE_CEILING_LIFT,#height=544,#speed=0);
      Send(self,@SetSector,#sector=6,#animation=ANIMATE_CEILING_LIFT,#height=472,#speed=0);
      Send(self,@SetSector,#sector=7,#animation=ANIMATE_CEILING_LIFT,#height=488,#speed=0);
      Send(self,@SetSector,#sector=8,#animation=ANIMATE_CEILING_LIFT,#height=488,#speed=0);
      Send(self,@SetSector,#sector=9,#animation=ANIMATE_CEILING_LIFT,#height=488,#speed=0);

      return;
   }

   ReplaceIllusions()
   {
      local i, each_obj;
   
      ptIllusionResetTimer = $;

      for i in plActive
      {
         each_obj = Send(self,@HolderExtractObject,#data=i);
         
         % Don't let illusion reappear when people are in the temple!
         if Send(each_obj,@GetRow) > 8
            AND Send(each_obj,@GetRow) < 24
            AND Send(each_obj,@GetCol) > 23
            AND Send(each_obj,@GetCol) < 38
            AND IsClass(each_obj,&Player)
         {
            % Try again in 30 seconds
            ptIllusionResetTimer = CreateTimer(self,@ReplaceIllusions,RESET_TIME);  
            
            return;     
         }
      }
   
      pbIllusioned = TRUE;   

      Send(self,@DoReplaceIllusionEffect);

      return;   
   }

   DoReplaceIllusionEffect()
   {
      Send(self,@ChangeTexture,#id=2,#new_texture=09415,#flags=CTF_FLOOR); 
      Send(self,@ChangeTexture,#id=4,#new_texture=09414,#flags=CTF_FLOOR);
      Send(self,@ChangeTexture,#id=6,#new_texture=09414,#flags=CTF_FLOOR); 
      Send(self,@ChangeTexture,#id=7,#new_texture=09414,#flags=CTF_FLOOR); 
      Send(self,@ChangeTexture,#id=8,#new_texture=09414,#flags=CTF_FLOOR);

      Send(self,@ChangeTexture,#id=2,#new_texture=09415,#flags=CTF_CEILING); 
      Send(self,@ChangeTexture,#id=4,#new_texture=09416,#flags=CTF_CEILING);
      Send(self,@ChangeTexture,#id=5,#new_texture=09416,#flags=CTF_CEILING); 
      Send(self,@ChangeTexture,#id=6,#new_texture=09416,#flags=CTF_CEILING);
      Send(self,@ChangeTexture,#id=7,#new_texture=09416,#flags=CTF_CEILING); 
      Send(self,@ChangeTexture,#id=8,#new_texture=09416,#flags=CTF_CEILING);
      Send(self,@ChangeTexture,#id=9,#new_texture=09416,#flags=CTF_CEILING);

      Send(self,@ChangeTexture,#id=10,#new_texture=09417,#flags=CTF_NORMALWALL); 
      Send(self,@ChangeTexture,#id=10,#new_texture=09417,#flags=CTF_BELOWWALL);
      Send(self,@ChangeTexture,#id=11,#new_texture=09417,#flags=CTF_ABOVEWALL);

      Send(self,@SetSector,#sector=2,#animation=ANIMATE_FLOOR_LIFT,#height=408,#speed=0);
      Send(self,@SetSector,#sector=3,#animation=ANIMATE_FLOOR_LIFT,#height=536,#speed=0);
      Send(self,@SetSector,#sector=4,#animation=ANIMATE_FLOOR_LIFT,#height=408,#speed=0);
      Send(self,@SetSector,#sector=5,#animation=ANIMATE_FLOOR_LIFT,#height=480,#speed=0);
      Send(self,@SetSector,#sector=6,#animation=ANIMATE_FLOOR_LIFT,#height=408,#speed=0);
      Send(self,@SetSector,#sector=7,#animation=ANIMATE_FLOOR_LIFT,#height=408,#speed=0);
      Send(self,@SetSector,#sector=8,#animation=ANIMATE_FLOOR_LIFT,#height=408,#speed=0);
      Send(self,@SetSector,#sector=9,#animation=ANIMATE_FLOOR_LIFT,#height=480,#speed=0);

      Send(self,@SetSector,#sector=2,#animation=ANIMATE_CEILING_LIFT,#height=536,#speed=0);
      Send(self,@SetSector,#sector=3,#animation=ANIMATE_CEILING_LIFT,#height=536,#speed=0);
      Send(self,@SetSector,#sector=4,#animation=ANIMATE_CEILING_LIFT,#height=536,#speed=0);
      Send(self,@SetSector,#sector=5,#animation=ANIMATE_CEILING_LIFT,#height=536,#speed=0);
      Send(self,@SetSector,#sector=6,#animation=ANIMATE_CEILING_LIFT,#height=504,#speed=0);
      Send(self,@SetSector,#sector=7,#animation=ANIMATE_CEILING_LIFT,#height=536,#speed=0);
      Send(self,@SetSector,#sector=8,#animation=ANIMATE_CEILING_LIFT,#height=536,#speed=0);
      Send(self,@SetSector,#sector=9,#animation=ANIMATE_CEILING_LIFT,#height=536,#speed=0);

      return;
   }

   SomethingMoved(what=$,new_row=$,new_col=$)
   {
      local i;

      if IsClass(what,&Player) AND Send(self,@IsObjectInBridgeBufferZone,#what=what,#row=new_row,#col=new_col)
      {
         Send(self,@PlayerHurledSelfOffCliffs,#what=what);
      }
      
      % Catch and kill any monsters (e.g. reflections, apparitions, etc) spawned in the
      % the area stretching from the bridge's left to right buffer areas.
      if IsClass(what,&Monster) AND Send(self,@IsObjectInBridgeRegion,#what=what,#row=new_row,#col=new_col)
      {
         % `Post` to allow the monster movement to complete before killing it
         Post(what,@Killed,#what=what);
      }

      if IsClass(what,&Player) AND Send(self,@IsObjectOnBridge,#what=what,#row=new_row,#col=new_col)
         AND NOT (IsClass(what,&DM) AND Send(what,@PlayerIsImmortal))
      {
         %% on the bridge.  Do they have a sword of riija?                  
         for i in Send(what,@GetPlayerUsing)
         {
            %% checking item to see if it's a riija sword.
            if IsClass(i,&RiijaSword) AND (Send(i,@GetQuester) = what)
            {
               propagate;
            }
         } 

         % No sword.  Kill 'em
         Send(self,@PlayerHurledSelfOffCliffs,#what=what);   
      }
      
      propagate;
   }

   PlayerHurledSelfOffCliffs(what=$)
   {
      local i, dropped, row, col;

      row = Send(what,@GetRow);
      col = Send(what,@GetCol);

      % Riija shames the user by sharing their folly far and wide
      for i in Send(SYS,@GetUsersLoggedOn)
      {
         Send(i,@MsgSendUser,#message_rsc=user_killed_bridge_faith,
              #parm1=Send(what,@GetTrueName),#parm2=Send(what,@GetHisHer));
      }

      dropped = FALSE;
      for i in Send(what,@GetPlayerUsing)
      {
         if Send(self,@ShouldDropItem,#oItem=i)
         {
            Send(self,@NewHold,#what=i,#new_row=Send(what,@GetRow),#new_col=Send(what,@GetCol));
            dropped = TRUE;
            
            break;
         } 
      }
      
      if NOT dropped
      {
         for i in Send(what,@GetHolderPassive)
         {
            if Send(self,@ShouldDropItem,#oItem=i)
            {
               Send(self,@NewHold,#what=i,#new_row=Send(what,@GetRow),#new_col=Send(what,@GetCol));
               
               break;
            } 
         }
      }

      Send(what,@EffectSendUserXLat,#xlat=PT_ALLRED);
      Send(what,@EffectSendUser,#what=what,#effect=EFFECT_PARALYZE_ON);

      if poDead <> $
      {
         Send(poDead,@MsgSendUser,#message_rsc=riijadeadmessage0a);
         Send(SYS,@UtilGotoSquare,#what=what,#where=Send(SYS,@FindRoomByNum,#num=RID_KOC_INN),
              #new_row=8,#new_col=6,#new_angle=ANGLE_NORTH);
         Send(what,@LoseHealth,#amount=(Send(what,@GetHealth)-1));
         Send(Send(sys,@FindSpellByNum,#num=SID_HOLD),@DoHold,
              #what=self,#oTarget=what,#iDurationSecs=5,#report=FALSE,#bAllowFreeAction=FALSE);
         Send(what,@EffectSendUserDuration,#effect=EFFECT_PAIN,#duration=PAIN_EFFECT_DURATION);
         Send(what,@EffectSendUserXLat,#xlat=0);
         Send(what,@EffectSendUser,#what=what,#effect=EFFECT_PARALYZE_OFF);
      }
      else
      {
         poDead = what;   
         Send(poDead,@MsgSendUser,#message_rsc=riijadeadmessage0b);
   
         Send(SYS,@UtilGotoSquare,#what=what,
              #where=Send(SYS,@FindRoomByNum,#num=RID_KOC_INN),
              #new_row=8,#new_col=6,#new_angle=ANGLE_NORTH);
         Send(what,@Losehealth,#amount=(Send(what,@GetHealth)-1));
         Send(Send(sys,@FindSpellByNum,#num=SID_HOLD),@DoHold,#oTarget=what,#iDurationSecs=30,#report=FALSE);
         
         ptDeathMessageTimer = CreateTimer(self,@TimerMessage1,DEATH_MESSAGE_DELAY); 
      }
      
      if Send(what,@GetGender) = GENDER_MALE
      {
         Send(self,@SomethingWaveRoom,#row=row,#col=col,#wave_rsc=riijaplayer_death_male_wav);
         Send(what,@WaveSendUser,#what=what,#wave_rsc=riijaplayer_death_male_wav);
      }
      else
      {
         Send(self,@SomethingWaveRoom,#row=row,#col=col,#wave_rsc=riijaplayer_death_female_wav);
         Send(what,@WaveSendUser,#what=what,#wave_rsc=riijaplayer_death_female_wav);
      }
      
      return;
   }

   ShouldDropItem(oItem=$)
   {
      if oItem <> $
         AND Send(oItem,@ReqLeaveOwner)
         AND Send(oItem,@DropOnDeath)
         AND NOT IsClass(oItem,&RiijaSword)
         AND random(1,3) = 1
      {
         return TRUE;
      }

      return FALSE;
   }

   TimerMessage1()
   {
      ptDeathMessageTimer = $;
      Send(poDead,@MsgSendUser, #message_rsc=riijadeadmessage1);
      ptDeathMessageTimer=CreateTimer(self,@TimerMessage2,DEATH_MESSAGE_DELAY);
      
      return;
   }

   TimerMessage2()
   {
      ptDeathMessageTimer = $;
      Send(poDead,@MsgSendUser,#message_rsc=riijadeadmessage2);
      ptDeathMessageTimer = CreateTimer(self,@TimerMessage3,DEATH_MESSAGE_DELAY);
      
      return;
   }

   TimerMessage3()
   {
      ptDeathMessageTimer = $;
      Send(poDead,@MsgSendUser,#message_rsc=riijadeadmessage3);
      ptDeathMessageTimer = CreateTimer(self,@TimerMessage4,DEATH_MESSAGE_DELAY);
      
      return;
   }

   TimerMessage4()
   {
      ptDeathMessageTimer = $;
      Send(poDead,@MsgSendUser, #message_rsc=riijadeadmessage4);
      ptDeathMessageTimer = CreateTimer(self,@TimerMessage5,DEATH_MESSAGE_DELAY);
      
      return;
   }

   TimerMessage5()
   {
      ptDeathMessageTimer = $;
      
      Send(poDead,@MsgSendUser,#message_rsc=riijadeadmessage5);
      Send(poDead,@EffectSendUserDuration,#effect=EFFECT_PAIN,#duration=PAIN_EFFECT_DURATION);
      Send(poDead,@EffectSendUserXLat,#xlat=0);
      Send(poDead,@EffectSendUser,#what=poDead,#effect=EFFECT_PARALYZE_OFF);

      poDead = $;
      
      return;  
   }

   CreateStandardObjects()
   {
      % teacher
      Send(self,@NewHold,#what=Create(&RiijaMonk),
           #new_row=10,#new_col=31,#fine_row=32,#fine_col=0,#new_angle=ANGLE_SOUTH);

      propagate;
   }

   CreateStandardExits()
   {
      plExits = $;

      plExits = Cons([ 24, 30, RID_TEMPLE_RIIJA, 22, 30, ROTATE_NONE ],plExits);
      plExits = Cons([ 24, 31, RID_TEMPLE_RIIJA, 22, 31, ROTATE_NONE ],plExits);
      plExits = Cons([ 23, 30, RID_TEMPLE_RIIJA, 25, 30, ROTATE_NONE ],plExits);
      plExits = Cons([ 23, 31, RID_TEMPLE_RIIJA, 25, 31, ROTATE_NONE ],plExits);

      plEdge_exits = $;
      plEdge_Exits = Cons([LEAVE_WEST, RID_KD2, 12, 69, ROTATE_NONE], plEdge_exits);
      plEdge_Exits = Cons([LEAVE_SOUTH, RID_KE2, 1, 37, ROTATE_NONE], plEdge_exits);

      propagate;
   }

   Delete()
   {
      if ptIllusionResetTimer <> $
      {
         deletetimer(ptIllusionResetTimer);
         ptIllusionResetTimer = $;
      }
      
      if ptDeathMessageTimer <> $
      {
         deletetimer(ptDeathMessageTimer);
         ptDeathMessageTimer = $;
      }

      poDead = $;
      
      propagate;
   }

   ModifyChanceToImprove(who=$,oSpellSkill=$,chance=0)
   "This takes a spell/skill that who is trying to advance and modifies the improve_chance."
   {
      if IsClass(oSpellSkill,&Spell)
         AND Send(oSpellSkill,@GetSchool) = SS_RIIJA
      {
         % Give a 10% bonus.
         return ((chance * 11)/10);
      }

      return chance;
   }

   IsObjectInTempleRegion(what=$,row=0,col=0)
   "Returns TRUE if the object is in the temple region"
   {
      return ((row >= 4 AND col >= 13) AND (row <= 30 AND col <= 44));
   }

   IsObjectInBridgeRegion(what=$,row=0,col=0)
   "Returns TRUE if the object is in the bridge region or its adjacent buffer areas"
   {
      return Send(self,@IsObjectInBridgeBufferZone,#what=what,#row=row,#col=col) 
         OR Send(self,@IsObjectOnBridge,#what=what,#row=row,#col=col);
   }

   IsObjectOnBridge(what=$,row=0,col=0)
   "Returns TRUE if the object is on the bridge"
   {
      return ((row > 32 AND row < 45) AND ((col = 30 OR col = 31)));
   }

   IsObjectInBridgeBufferZone(what=$,row=0,col=0)
   "Returns TRUE if the object is in the bridge's left or right buffer areas"
   {
      return (((row >= 31 AND col >= 26) AND (row <= 44 AND col <= 29))
         OR ((row >= 31 AND col >= 32) AND (row <= 45 AND col <= 35))
         OR (row = 45 AND col = 29));
   }

end
%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
