% Meridian 59, Copyright 1994-2012 Andrew Kirmse and Chris Kirmse.
% All rights reserved.
%
% This software is distributed under a license that is described in
% the LICENSE file that accompanies it.
%
% Meridian is a registered trademark.


%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
PoisonFogCloud is ActiveWallElement

constants:

   include blakston.khd

   % Duration in milliseconds
   POISON_DURATION = 180000

   % Damage in health points * 10^-4 / second
   POISON_LOSSRATE = 5000    
   
   POISON_CHANCE = 30

resources:

   include poisfogc.lkod

   PoisonFogCloud_name_rsc = "thick fog"
   PoisonFogCloud_icon_rsc = poisoncl.bgf
   PoisonFogCloud_desc_rsc = \
      "You choke just looking at the thick putrid fog."

   PoisonFogCloud_poisoned = \
      "You suddenly feel sick as you inhale the vapors of the fog."

   PoisonFogCloud_poison_snd = psncough.wav

   PoisonFog_dissipates = \
      "Some of the poison fog fades, then clears."
   PoisonFog_unaffected = \
      "You hold your breath and the noxious cloud does not affect you."

classvars:

   vrName = PoisonFogCloud_name_rsc
   vrIcon = PoisonFogCloud_icon_rsc
   vrDesc = PoisonFogCloud_desc_rsc

   viObject_flags = OF_NOEXAMINE

   vrDissipateMessage = PoisonFog_dissipates
   vrUnaffectedMessage = PoisonFog_unaffected

properties:

   piDrawFx = DRAWFX_TRANSLUCENT_50

   piOdds = 0

messages:
   
   Constructor(Caster = $, Duration = 75, Odds = 0)
   {
      piOdds = Odds;

      propagate;
   }

   GetDuration(duration = 0)
   {
      local iDuration;

      iDuration = Random(duration-20,duration+20);
      iDuration = iDuration * 1000;
      iDuration = bound(iDuration,30000,200000);

      return iDuration;
   }

   DoEffect(what = $)
   {
      local bPoison, oSpell;
      
      if Random(1,100) < piOdds
      {
     
         oSpell = Send(SYS,@FindSpellByNum,#num=SID_POISON);

         if NOT Send(what,@IsEnchanted,#what=oSpell) or Random(1,POISON_CHANCE) = 1
         {
            if isClass(what,&User)
            {
               Send(what,@MsgSendUser,#message_rsc=PoisonFogCloud_poisoned);
               Send(what,@WaveSendUser,#wave_rsc=PoisonFogCloud_poison_snd);
            }  

            Send(oSpell,@MakePoisoned,#who=what,#lossrate=POISON_LOSSRATE,
                 #duration=POISON_DURATION);
            Send(poOwner,@SomethingAttacked,#what=poCaster,#victim=what,
                  #use_weapon=self,#stroke_obj=self);
            if IsClass(poCaster,&Player)
            {
               Send(poCaster,@SetPlayerFlag,#flag=PFLAG_DID_DAMAGE,#value=TRUE);
            }
         }
      }
      else
      {
         Send(what,@MsgSendUser,#message_rsc=PoisonFog_unaffected);
      }
      
      propagate;
   }

   SendAnimation()
   {
      AddPacket(1,ANIMATE_CYCLE, 4,random(240,280), 2,1, 2,5);

      return;
   }


end
%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
