% Meridian 59, Copyright 1994-2012 Andrew Kirmse and Chris Kirmse.
% All rights reserved.
%
% This software is distributed under a license that is described in
% the LICENSE file that accompanies it.
%
% Meridian is a registered trademark.


%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
JewelofFroz is AttackModifier

constants:

   include blakston.khd

   ANIM_FLASH = 2

resources:

   frozjewel_name_rsc = "Jewel of Froz"
   frozjewel_icon_rsc = frozjewl.bgf
   frozjewel_desc_rsc = \
      "This gem was created by the great alchemist, Froz, in a secret process "
      "known only to him.  Rumor has it that it is a conduit for mystical "
      "aggression, using raw mana to boost elemental touch spells.  "
      "Your hand tingles when you hold it."

   frozjewel_desc_broken_rsc = "The jewel seems ordinary and dull."

   frozjewel_broken = "The Jewel of Froz now seems quite ordinary."

classvars:

   vrName = frozjewel_name_rsc
   vrIcon = frozjewel_icon_rsc
   
   viUse_type = ITEM_USE_HAND
   viUse_amount = 1

   viHits_init_min = 350
   viHits_init_max = 550

   viValue_average = 400
   viBulk = 10
   viWeight = 15

properties:

   vrDesc = frozjewel_desc_rsc
   viItem_type = ITEMTYPE_GEM

   piAnimation = ANIM_NONE

messages:

   ModifyHitRoll(who = $, what = $, hit_roll = $, stroke_obj = $)
   {
      piHits = piHits - 1;
      if piHits <= 0
      {
         Send(poOwner,@MsgSendUser,#message_rsc=frozjewel_broken,
	           #parm1=Send(self,@GetCapDef),#parm2=vrName);
         Send(poOwner,@TryUnuseItem,#what=self);
         vrDesc = frozjewel_desc_broken_rsc;
      
         return hit_roll;
      }

      if IsClass(stroke_obj,&Spell)
      {
         Send(self,@DoFlash);
      
         % Adjust bonus by how strong the wielder is relative to their maximum potential.
         % Throwaway characters will get very little benefit.
         return hit_roll + (30 * Send(self,@GetHealthBonus,#who=who));
      }
      
      return hit_roll + 50;
   }
   
   ModifyDamage(who = $, what = $, damage = $, stroke_obj=$)
   {
      local i, iMana;
      
      if IsClass(stroke_obj,&Spell)
      {
         iMana = 0;
         for i in send(who,@GetPlayerUsing)
         {
            if IsClass(i,&JewelOfFroz)
            {
               % First jewel = 1 mana cost.  Second = 2 mana each (4 total).
               iMana = iMana + 1;
            }
         }

         if Send(who,@LoseMana,#amount=iMana) <> 0
         {
            % Add big damage if we spent the mana.
            % Adjust it by how strong the wielder is relative to their maximum potential.
            % Throwaway characters will get very little benefit.
            return damage + Send(self,@GetHealthBonus,#who=who);
         }

         % Add a small amount of damage.
         return damage + Random(1,2);
      }
      
      return damage;
   }
   
   GetHealthBonus(who = $)
   {
      local hp;

      hp = Send(who,@GetBaseMaxHealth);
      if hp < 50
      {
         return 1;
      }
      if hp < 75
      {
         return Random(1,2);
      }
      if hp < 100
      {
         return Random(1,3);
      }
      if hp < 125
      {
         return Random(2,3);
      }

      return Random(3,5);
   }

   WeaponHitTarget()
   {
      return;
   }
   
   DoFlash()
   {
      piAnimation = ANIM_FLASH;
      Send(poOwner,@SomethingChanged,#what=self);
      piAnimation = ANIM_NONE;
      
      return;
   }

   SendAnimation()
   {
      AddPacket(1,ANIMATE_NONE,2,2);

      return;
   }

   SendLookAnimation()
   {
      AddPacket(1,ANIMATE_NONE,2,1);

      return;
   }

   SendInventoryAnimation()
   {
      if piAnimation = ANIM_FLASH
      {
         AddPacket(1,ANIMATE_ONCE,4,300,2,3,2,3,2,1);
      }
      else
      {
         AddPacket(1,ANIMATE_NONE,2,1);
      }
      
      return;
   }


end
%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
