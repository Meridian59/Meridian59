% Meridian 59, Copyright 1994-2012 Andrew Kirmse and Chris Kirmse.
% All rights reserved.
%
% This software is distributed under a license that is described in
% the LICENSE file that accompanies it.
%
% Meridian is a registered trademark.


%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
JewelofFroz is AttackModifier

constants:

   include blakston.khd

   ANIM_FLASH = 2
   ANIMATION_FLASH_SPEED = 300
   FRAME_DEFAULT = 1
   FRAME_BROKEN_GROUND = 4
   FRAME_FLASH = 5

resources:

   frozjewel_name_rsc = "Jewel of Froz"
   frozjewel_icon_rsc = frozjewl.bgf
   frozjewel_desc_rsc = \
      "This gem was created by the great alchemist, Froz, in a secret process "
      "known only to him.  Rumor has it that it is a conduit for mystical "
      "aggression, using raw mana to boost elemental touch spells.  "
      "Your hand tingles when you hold it."

   frozjewel_desc_broken_rsc = "The jewel seems ordinary and dull."

   frozjewel_broken = "The Jewel of Froz now seems quite ordinary."

classvars:

   vrName = frozjewel_name_rsc
   vrIcon = frozjewel_icon_rsc
   
   viUse_type = ITEM_USE_HAND
   viUse_amount = 1

   viHits_init_min = 150
   viHits_init_max = 250

   viValue_average = 400
   viBulk = 10
   viWeight = 15

   viGround_group = 2
   viBroken_group = 3

properties:

   vrDesc = frozjewel_desc_rsc
   viItem_type = ITEMTYPE_GEM

   piAnimation = ANIM_NONE

messages:

   ModifyHitRoll(who = $, what = $, hit_roll = $, stroke_obj = $)
   {
      piHits = piHits - 1;
      if piHits <= 0
      {
         Send(poOwner,@MsgSendUser,#message_rsc=frozjewel_broken,
	           #parm1=Send(self,@GetCapDef),#parm2=vrName);
         Send(poOwner,@TryUnuseItem,#what=self);
         vrDesc = frozjewel_desc_broken_rsc;

         Send(poOwner,@SomethingChanged,#what=self);
      
         return hit_roll;
      }

      if IsClass(stroke_obj,&Spell)
      {
         Send(self,@DoFlash);
      
         return hit_roll + 150;
      }
      
      return hit_roll + 50;
   }
   
   ModifyDamage(who = $, what = $, damage = $, stroke_obj=$)
   {
      local i, iMana;
      
      if IsClass(stroke_obj,&Spell)
      {
         iMana = 0;
         for i in send(who,@GetPlayerUsing)
         {
            if IsClass(i,&JewelOfFroz)
            {
               % First jewel = 1 mana cost.  Second = 2 mana each (4 total).
               iMana = iMana + 1;
            }
         }

         if Send(who,@LoseMana,#amount=iMana) <> 0
         {
            % Add big damage if we spent the mana.
            return damage + Random(3,5);
         }

         % Add a small amount of damage.
         return damage + Random(1,2);
      }
      
      return damage;
   }

   WeaponHitTarget()
   {
      return;
   }
   
   DoFlash()
   {
      piAnimation = ANIM_FLASH;
      Send(poOwner,@SomethingChanged,#what=self);
      piAnimation = ANIM_NONE;
      
      return;
   }

   SendAnimation()
   {
      % Override to replace the default broken inventory graphic `viBroken_group` 
      % with a broken ground graphic.
      if piHits <= 0
      {
         AddPacket(1,ANIMATE_NONE, 2,FRAME_BROKEN_GROUND);

         return;
      }

      propagate;
   } 

   SendInventoryAnimation()
   {
      if piAnimation = ANIM_FLASH
      {
         AddPacket(1,ANIMATE_ONCE, 4,ANIMATION_FLASH_SPEED, 2,FRAME_FLASH, 2,FRAME_FLASH, 2,FRAME_DEFAULT);
       
         return;
      }
      
      propagate;
   }


end
%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
