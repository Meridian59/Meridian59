% Meridian 59, Copyright 1994-2012 Andrew Kirmse and Chris Kirmse.
% All rights reserved.
%
% This software is distributed under a license that is described in
% the LICENSE file that accompanies it.
%
% Meridian is a registered trademark.


%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
JewelofFroz is AttackModifier

constants:

   include blakston.khd

   ANIM_FLASH = 2

resources:

   frozjewel_name_rsc = "Jewel of Froz"
   frozjewel_icon_rsc = frozjewl.bgf
   frozjewel_desc_rsc = \
      "This gem was created by the great alchemist, Froz, in a secret process "
      "known only to him.  Rumor has it that it is a conduit for mystical "
      "aggression, that allows its user to channel their magical energy into "
      "devastating touch spells.  Obviously, this would hinder the caster when "
      "trying to use said energy for less destructive purposes.  "
      "Your hand tingles when you hold it."

   frozjewel_desc_broken_rsc = "The jewel seems ordinary and dull."

   frozjewel_broken = "The Jewel of Froz now seems quite ordinary."

classvars:

   vrName = frozjewel_name_rsc
   vrIcon = frozjewel_icon_rsc
   
   viUse_type = ITEM_USE_HAND
   viUse_amount = 1

   viHits_init_min = 750
   viHits_init_max = 1250

   viValue_average = 400
   viBulk = 10
   viWeight = 15

properties:

   vrDesc = frozjewel_desc_rsc
   viItem_type = ITEMTYPE_GEM

   piAnimation = ANIM_NONE

messages:

   ModifyHitRoll(who = $, what = $, hit_roll = $, stroke_obj = $)
   {
      piHits = piHits - 1;
      if piHits <= 0
      {
         Send(poOwner,@MsgSendUser,#message_rsc=frozjewel_broken,
	           #parm1=Send(self,@GetCapDef),#parm2=vrName);
         Send(poOwner,@TryUnuseItem,#what=self);
         vrDesc = frozjewel_desc_broken_rsc;
      
         return hit_roll;
      }

      return hit_roll;
   }
   
   ModifyDamage(who = $, what = $, damage = $, stroke_obj=$)
   {
      % all calculations are now done in touchatk.kod	
      return damage;
   }

   WeaponHitTarget()
   {
      return;
   }
   
   DoFlash()
   {
      piAnimation = ANIM_FLASH;
      Send(poOwner,@SomethingChanged,#what=self);
      piAnimation = ANIM_NONE;
      
      return;
   }

   SendAnimation()
   {
      AddPacket(1,ANIMATE_NONE,2,2);

      return;
   }

   SendLookAnimation()
   {
      AddPacket(1,ANIMATE_NONE,2,1);

      return;
   }

   SendInventoryAnimation()
   {
      if piAnimation = ANIM_FLASH
      {
         AddPacket(1,ANIMATE_ONCE,4,300,2,3,2,3,2,1);
      }
      else
      {
         AddPacket(1,ANIMATE_NONE,2,1);
      }
      
      return;
   }

   GetBaseSpellModifier(oSpell=$)
   {
      if oSpell <> $ AND send(oSpell,@GetSchool) = SS_FAREN
      {
         return 0;
      }

      return -20;
   }


end
%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
