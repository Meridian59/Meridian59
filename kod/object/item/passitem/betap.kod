% Meridian 59, Copyright 1994-2012 Andrew Kirmse and Chris Kirmse.
% All rights reserved.
%
% This software is distributed under a license that is described in
% the LICENSE file that accompanies it.
%
% Meridian is a registered trademark.


%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
BetaPotion is PassiveItem

constants:

   include blakston.khd

resources:

   BetaPotion_icon_rsc = potion01.bgf

   BetaPotion_name_rsc = "The infamous BETA POTION!"
   BetaPotion_desc_rsc = \
	   "This potion is to facilitate beta testing by allowing "
       "the testers to test the more advanced parts of the game.\n\n"
	   "This potion will boost stats including your Mana and Hit Points. "
	   "Once you drink the potion, you can also select your schools of magic "
	   "by saying the name of the schools and the levels you wish to recieve. "
	   "You may not have both Shal'ille and Qor spells. Limited by your Intellect you will "
	   "only recieve a certain amount of points before the potion effect wears off. \n\n"
       "If you lose the potion in any way, shape or form (including if you give "
       "it to a friend and s/he quaffs it, or you step through the portal in "
       "Raza, or your little sister suicides your character just to spite you), "
       "or you just threw it away, you will NOT get another potion.  So don't ask.\n\n"

   BetaPotion_gulp_sound = drkptn.wav

   BetaPotion_drink = "You quaff the contents of the vial in a single gulp."
   BetaPotion_worked = "You suddenly feel a little taller."
   BetaPotion_bad_drink = \
      "You choke down the foul-tasting contents of the vial, realizing almost "
      "immediately that this was a pretty bad idea. Time has caused it to sour."

   BetaPotion_ask = \
	  "Add your schools until your out of points. Say one of the following:\n"
      "~B\"[SHAL'ILLE, QOR, KRAANAN, FAREN, RIIJA, JALA, WEAPONCRAFT]\"~n "
	  "followed by ~B\"LEVEL [ONE, TWO, THREE, FOUR, FIVE, SIX]\""

classvars:
   
   vrDesc = BetaPotion_desc_rsc

   viBulk = 20
   viWeight = 20
   viValue_average = 60

   viItem_type = ITEMTYPE_POTION | ITEMTYPE_SUNDRY
   viUse_type = ITEM_SINGLE_USE

   viPoisonPercent = 0
   viGoBadDamage = 0

properties:

   vrName = BetaPotion_name_rsc
   vrIcon = BetaPotion_icon_rsc
   
   piGoBadTime = 0
   piItem_flags = PT_GRAY_TO_RED

   % By default we will give these boosts
   piHealth = 150
   piHealthCap = 150
   pbGiveHealth = TRUE
   pbGiveMana = TRUE

messages:

   Constructor(health = 150, healthCap = 150, giveHealth = TRUE, giveMana = TRUE)
   {
      piHealth = health;
      piHealthCap = healthCap;
      pbGiveHealth = giveHealth;
      pbGiveMana = giveMana;

      propagate;
   }

   ApplyPotionEffects(apply_on = $)
   {
	  local iBaseMaxHealth, HealthBoost;
	  
      if Send(Send(SYS,@GetLore),@BetaPotionsEnabled) OR 
	     isClass(apply_on,&Guest)
      {
		 % Give Mana
		 if pbGiveMana
         {
            Send(&ManaNode,@Meld,#who=apply_on);
		 }
		 
		 % Give Health
		 iBaseMaxHealth = Send(apply_on,@GetBaseMaxHealth);
		 
		 if pbGiveHealth AND (iBaseMaxHealth < piHealthCap)
         {
            % How many hps?  Don't give them over potion max, and don't give
            %  them over stamina max.
            HealthBoost = bound(piHealth,0,(100+Send(apply_on,@GetStamina)));
            HealthBoost = bound(HealthBoost,0,(piHealthCap-iBaseMaxHealth));
            Send(apply_on,@GainBaseMaxHealth,#amount=HealthBoost);
		 }
		 
		 %  Don't use iBaseMaxHealth, because it changed.
         if (Send(apply_on,@GetBaseMaxHealth) >= PKILL_ENABLE_HP)
            AND (NOT Send(apply_on,@CheckPlayerFlag,#flag=PFLAG_PKILL_ENABLE))
         {
             Send(apply_on,@PkillEnable);
         }
			
		 Send(apply_on,@MsgSendUser,#message_rsc=BetaPotion_worked);
		 post(apply_on,@MsgSendUser,#message_rsc=BetaPotion_ask);
		 send(apply_on,@DrankBetaPotion);
	  }

	  return;
   }

   %%% Old Potion stuff

   ReqNewApply(what = $,apply_on = $)
   {
      if what = apply_on
      {
         return TRUE;
      }
      
      return FALSE;
   }
   
   NewApplied(what = $,apply_on = $)
   {
      local oSpell;
      if isClass(apply_on,&User) 
      {
         Send(apply_on,@waveSenduser,#wave_rsc=BetaPotion_gulp_sound);
      }
	  
      Send(apply_on,@MsgSendUser,#message_rsc=BetaPotion_drink);
      Send(self,@ApplyPotionEffects,#apply_on=apply_on);

      Send(self,@Delete);

      return;
   }

   IsBeverage()
   {
      return TRUE;
   }

   CanIdentify()
   {
      return TRUE;
   }

   RevealHiddenAttributes()
   {
      return FALSE;
   }
  
end
%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
