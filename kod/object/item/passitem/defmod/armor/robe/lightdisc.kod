% Meridian 59, Copyright 1994-2012 Andrew Kirmse and Chris Kirmse.
% All rights reserved.
%
% This software is distributed under a license that is described in
% the LICENSE file that accompanies it.
%
% Meridian is a registered trademark.


%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
LightDiscipleRobe is Robe

%% This item is significant in that it is a torso with something
%% hotspotted onto it.  WHy they did it that way, but I guess we 
%% all learn from other people's stupid implementations, neh?

constants:

   include blakston.khd

resources:

   LightRobeDiscipleInsignia_name_rsc = "light robes of the disciple"
   LightRobeDiscipleInsignia_icon_rsc = ltrobe.bgf
   LightRobeDiscipleInsignia_desc_rsc = \
   	"Light and comfortable, these disciple robes typically denote a fervent (and some would say "
      "unhealthy) dedication to one school or another."

   LightRobeDiscipleInsignia_school_info1 = "  These robes bear the insignia of "
   LightRobeDiscipleInsignia_school_info2 = " and will give bonuses to spells in that school."

   lightdisciplerobe_riija_female = emblm-rf.bgf
   lightdisciplerobe_riija_male = emblm-rm.bgf
   lightdisciplerobe_faren_female = emblm-ff.bgf
   lightdisciplerobe_faren_male = emblm-fm.bgf
   lightdisciplerobe_qor_female = emblm-qf.bgf
   lightdisciplerobe_qor_male = emblm-qm.bgf
   lightdisciplerobe_shalille_female = emblm-sf.bgf
   lightdisciplerobe_shalille_male = emblm-sm.bgf
   lightdisciplerobe_jala_female = emblm-jf.bgf
   lightdisciplerobe_jala_male = emblm-jm.bgf
   lightdisciplerobe_kraanan_female = emblm-kf.bgf
   lightdisciplerobe_kraanan_male = emblm-km.bgf

classvars:

   vrName = LightRobeDiscipleInsignia_name_rsc
   vrIcon = LightRobeDiscipleInsignia_icon_rsc
   vrDesc = LightRobeDiscipleInsignia_desc_rsc

   vrBroken_rsc = defmod_broken_plural_clothes
   vrItem_broken = item_broken_battle_plural

   viGround_group = 1
   viInventory_group = 2
   viBroken_group = 3

   viSpell_modifier = 10
   viBulk = 160
   viWeight = 130
   viValue_average = 2000

   vrLeftarm_male = $
   vrRightarm_male = $
   vrLeftarm_female = $
   vrRightarm_female = $

properties:   

   viXLAT_immortal =  XLAT_TO_SKY

   viXLAT_no_knowledge =  XLAT_TO_YELLOW
   viXLAT_level_one = XLAT_TO_SKIN1
   viXLAT_level_two = XLAT_TO_SKIN2
   viXLAT_level_three = XLAT_TO_ORANGE 
   viXLAT_level_four = XLAT_TO_SKIN4
   
   viXLAT_qor_master = XLAT_TO_BLACK
   viXLAT_shalille_master = XLAT_TO_GRAY
   viXLAT_faren_master = XLAT_TO_RED
   viXLAT_kraanan_master = XLAT_TO_BLUE
   viXLAT_riija_master = XLAT_TO_PURPLE
   viXLAT_jala_master = XLAT_TO_GREEN

   piSchool = SS_RIIJA
   piLevel = 0
   prInsignia_male =  lightdisciplerobe_riija_male
   prInsignia_female = lightdisciplerobe_riija_female
   piDefense_bonus = 5

   pbImmortalColor = TRUE

messages:

   Constructor(school = -1)
   {
      if school = -1
      {  
         school = random(1,6); 
      }
      
      send(self,@SetSchool,#school=school);
      send(self,@SetXLATTranslation,#translation=viXLAT_no_knowledge);
      
      propagate;
   }

   ItemChangesArms()
   {
      return FALSE;
   }

   GetBaseSpellModifier(oSpell=$)
   {
      if send(oSpell,@GetSchool) = piSchool
      {
         return bound(4*piLevel,0,20);
      }

      return 5;
    }

   SetXLATTranslation(translation=$)
   {
      local skin_color, XLAT;

      if poOwner <> $
      {
         skin_color = send(poOwner,@GetSkinColor);      
         skin_color = send(poOwner,@ConvertSkinColorToXLAT,#skin_color=skin_color);
      }
      else
      {
         skin_color = XLAT_TO_SKIN2;
      }
      
      XLAT = send(SYS,@EncodeTwoColorXLAT,#color1=translation,#color2=skin_color);
      send(self,@SetPaletteTranslation,#translation=XLAT);

      if poOwner <> $
      {
         if isClass(poOwner,&room)
         {
            send(send(SYS,@UtilGetRoom,#what=self),@SomethingChanged,#what=self);
         }
         
         if isClass(poOwner,&user)
         {
            send(poOwner,@SomethingChanged,#what=self);
            send(self,@DoPlayerArt);
         }
      }

      return;
   }
    
   ResetColors()
   {
      local i, oWarEvent, iSide, oSpell, iHigh_level, skin_color, bFound;
      
      iHigh_level = 0;

      oWarEvent = send(SYS,@GetWarEvent);
      if send(oWarEvent,@IsActive)
      {
         iSide = send(oWarEvent,@GetPlayerSide,#who=poOwner);
         send(self,@SetXLATTranslation,#translation=send(oWarEvent,@GetSideColor,#side=iSide));

         return;
      }      

      if (isClass(poOwner,&DM) and pbImmortalColor)
      {
         send(self,@SetXLATTranslation,#translation=viXLAT_immortal);

         return;
      }

      foreach i in send(poOwner,@GetSpellList)
      {         
         oSpell = send(SYS,@FindSpellByNum,#num = send(poOwner,@DecodespellNum,#compound=i));
         if piSchool = send(oSpell,@GetSchool)
         {
            if send(oSpell,@GetLevel) > iHigh_level
            {
               iHigh_Level = send(oSpell,@GetLevel);
            }
         }
      }

      piLevel = iHigh_level;

      bFound = FALSE;
      
      %% Okay, decipher the high level.
      if iHigh_level = 0
      {
         send(self,@SetXLATTranslation,#translation=viXLAT_no_knowledge);         
      }

      if iHigh_level = 1
      {
         send(self,@SetXLATTranslation,#translation=viXLAT_level_one);         
      }

      if iHigh_level = 2
      {
         send(self,@SetXLATTranslation,#translation=viXLAT_level_two);         
      }

      if iHigh_level = 3
      {
         send(self,@SetXLATTranslation,#translation=viXLAT_level_three);         
      }

      if iHigh_level = 4
      {
         send(self,@SetXLATTranslation,#translation=viXLAT_level_four);         
      }

      % Okay, it's above level four.  Let's be school specific, shall we?
      if iHigh_level > 4
      {
         if piSchool = SS_JALA
         {
            send(self,@SetXLATTranslation,#translation=viXLAT_jala_master);         
         }
         
         if piSchool = SS_FAREN
         {
            send(self,@SetXLATTranslation,#translation=viXLAT_faren_master);            
         }
         
         if piSchool = SS_KRAANAN
         {
            send(self,@SetXLATTranslation,#translation=viXLAT_kraanan_master);
         }
         
         if piSchool = SS_SHALILLE
         {
            send(self,@SetXLATTranslation,#translation=viXLAT_shalille_master);
         }
         
         if piSchool = SS_QOR
         {
            send(self,@SetXLATTranslation,#translation=viXLAT_qor_master);            
         }
         
         if piSchool = SS_RIIJA
         {
            send(self,@SetXLATTranslation,#translation=viXLAT_riija_master);         
         }
      }
      
      return;
   }

   ReqUse()
   "Called when the player tries to use the item"
   {
      local i, oItemAtt;
          
      % Check to be sure nothing in ItemAtts disallows this.
      foreach i in plItem_Attributes
      {
         oItemAtt = send(SYS,@FindItemAttByNum,#num=send(self,@GetNumFromCompound,#compound=first(i)));
         if not send(oItemAtt,@ItemReqUse,#oItem=self,#oPlayer=poOwner)
         {
            return FALSE;
         }
      }
      
      return TRUE;
   }

   NewUsed()
   {      
      if prInsignia_male <> $
      {
         Send(poOwner,@SetOverlay,#what=self);
      }

      send(self,@ResetColors);

      propagate;
   }
  
   NewUnused()
   {    
      if prInsignia_male <> $
      {
         Send(poOwner,@RemoveOverlay,#what=self);
      }

      propagate;
   }

   GetResistanceModifiers()
   {
      return [ [ ATCK_WEAP_ALL, -10],
               [-ATCK_SPELL_FIRE,-20],
               [-ATCK_SPELL_SHOCK,15]
             ];
   }

   GetOverlay()
   {
      if send(poOwner,@GetGender) = GENDER_MALE
      {
         return prInsignia_male;
      }

      return prInsignia_female;       
   }

   GetOverlayHotspot()
   {
      return HS_EMBLEM;
   }

   SendOverlayAnimation(iAnimation = $)
   {      
      if (piItem_flags & ITEM_PALETTE_MASK) <> 0
      {
         AddPacket(1,ANIMATE_TRANSLATION,1,piItem_flags & ITEM_PALETTE_MASK);
      }

      if iAnimation = PANM_WEAPON_ATTACK
      {
         AddPacket(1,ANIMATE_ONCE,4,300,2,2,2,4,2,1);

         return;
      }

      if iAnimation = PANM_FIST_ATTACK
      {
         AddPacket(1,ANIMATE_ONCE,4,600,2,3,2,4,2,1);

         return;
      }

      if iAnimation = PANM_BOW_FIRE
      {
         AddPacket(1,ANIMATE_ONCE,4,1200,2,5,2,5,2,1);

         return;
      }

      if iAnimation = PANM_DANCE
      {
         AddPacket(1,ANIMATE_CYCLE,4,150,2,6,2,21);

         return;
      }

      AddPacket(1,ANIMATE_NONE,2,1);

      return;
   }

   SetSchool(school=SS_RIIJA)
   {
      piSchool = School;
      
      if school = SS_RIIJA
      {
         prInsignia_male = lightdisciplerobe_riija_male;
         prInsignia_female = lightdisciplerobe_riija_female;
      }
      
      if school = SS_JALA
      {
         prInsignia_male = lightdisciplerobe_jala_male;
         prInsignia_female = lightdisciplerobe_jala_female;
      }
      
      if school = SS_FAREN
      {
         prInsignia_male = lightdisciplerobe_faren_male;
         prInsignia_female = lightdisciplerobe_faren_female;
      }
      
      if school = SS_QOR
      {
         prInsignia_male = lightdisciplerobe_qor_male;
         prInsignia_female = lightdisciplerobe_qor_female;
      }
      
      if school = SS_SHALILLE
      {
         prInsignia_male = lightdisciplerobe_shalille_male;
         prInsignia_female = lightdisciplerobe_shalille_female;         
      }
      
      if school = SS_KRAANAN
      {
         prInsignia_male = lightdisciplerobe_kraanan_male;
         prInsignia_female = lightdisciplerobe_kraanan_female;         
      }

      if poOwner <> $
      {
         send(poOwner,@SomethingChanged,#what=self);
      }
      
      return;
   }

   SendOverlayInformation(ianimation = $)
   {
      AddPacket(4,Send(self,@GetOverlay));
      AddPacket(1,Send(self,@GetOverlayHotspot));
      Send(self,@SendOverlayAnimation,#iAnimation=iAnimation);
      
      return;
   }

   AppendDesc()
   {
      local oSpell;

      % Find a spell, any spell, so we can get the string out of it for which school we are.
      oSpell = send(SYS,@FindSpellByNum,#num=SID_BLINK);
   
      AppendTempString(LightRobeDiscipleInsignia_school_info1);
      AppendTempString(Send(oSpell,@GetSchoolStr,#iSchool=piSchool));
      AppendTempString(LightRobeDiscipleInsignia_school_info2);
   
      propagate;
   }

   GetSchool()
   {
      return piSchool;
   }


end
%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
