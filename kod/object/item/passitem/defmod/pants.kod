% Meridian 59, Copyright 1994-2012 Andrew Kirmse and Chris Kirmse.
% All rights reserved.
%
% This software is distributed under a license that is described in
% the LICENSE file that accompanies it.
%
% Meridian is a registered trademark.


%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
Pants is DefenseModifier

constants:

   include blakston.khd

resources:

   pants_name_rsc = "pants"   
   pants_desc_rsc = \
   "These pants will cover your legs but won't help much in the way of protection."

classvars:

   vbShow_condition = FALSE

   viUse_type = ITEM_USE_LEGS
   viUse_amount = 1
   vrPoss_article = object_article_cap_these_rsc

   vrDesc = pants_desc_rsc
   vrName = pants_name_rsc

   viHits_init_min = 450
   viHits_init_max = 650

   viValue_average = 50
   viWeight = 10
   viBulk = 10

   viGround_group = 1
   viInventory_group = 2

   vrItem_broken = defmod_broken_plural_clothes

   viDefense_base = 5

properties:

messages:

   Constructor(translation = $,model = $, color = $)
   {      
      if color <> $
      {
         translation = send(SYS,@EncodeTwoColorXLAT,
                            #color1=color,#color2=XLAT_TO_SKIN1);
      }
      
      if model <> $
      {
         translation = send(model,@GetPaletteTranslation);    
      }
      
      if translation = $
      {
         translation = send(SYS,@EncodeTwoColorXLAT,
                            #color1=XLAT_TO_BLUE,#color2=XLAT_TO_SKIN1);
      }

      Send(self,@SetPaletteTranslation,#translation=translation);
      
      propagate;
   }

   Randomize()
   {
      local translation;

      translation = send(SYS,@EncodeTwoColorXLAT,
                         #color1=random(XLAT_TO_RED, XLAT_TO_SKY),#color2=XLAT_TO_SKIN1);
      Send(self,@SetPaletteTranslation,#translation=translation);

      if poOwner <> $
      {
         if isClass(poOwner,&Room)
         {
            send(send(SYS,@UtilGetRoom,#what=self),@SomethingChanged,#what=self);
         }

         if isClass(poOwner,&User)
         {
            send(poOwner,@SomethingChanged,#what=self);
            send(self,@DoPlayerArt);
         }
      }

      return;
   }

   ReqUseSomething(what = $)
   {
      if send(what,@getitemusetype) & ITEM_USE_LEGS
      {
         if Send(poOwner,@TryUnuseItem,#what=self)
         {
            propagate;
         }
         else
         {
            return FALSE;
         }
      }
      
      propagate;
   }
      
   ReqRepair()
   {
      return FALSE;
   }

   CanMend()
   {
      return FALSE;
   }

   DefendingHit()
   {
      return;
   }

   IsPlural()
   {
      return TRUE;
   }


end
%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%


