% Meridian 59, Copyright 1994-2012 Andrew Kirmse and Chris Kirmse.
% All rights reserved.
%
% This software is distributed under a license that is described in
% the LICENSE file that accompanies it.
%
% Meridian is a registered trademark.


%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
OrcShield is Shield

constants:

   include blakston.khd

resources:

   OrcShield_name_rsc = "orc shield"
   OrcShield_icon_rsc = osh.bgf
   OrcShield_desc_rsc = \
      "Fearsome to behold, the infamous orc shields strike terror the enemy's heart, "
      "while also proving highly useful in combat. Remarkably light yet sturdy, this "
      "particular one promises some light protection in any battle. As you hold the shield "
      "in your hand, you are amazed to find that it does not obstruct your spellcasting in "
      "the least - on the contrary, it almost feels as if the shield tried to feed your darker "
      "arcane powers. Is it just you, or does the sun seem even more repulsive than usual?"

   orcshield_cant = "You try to grab the handle of the shield, but it insistently slips out of your fingers."

   OrcShield_window_overlay_rsc = povosh.bgf

classvars:

   vrName = OrcShield_name_rsc
   vrIcon = OrcShield_icon_rsc
   vrDesc = OrcShield_desc_rsc

   viHits_init_min = 350
   viHits_init_max = 450

   viValue_average = 2000
   viWeight = 100
   viBulk = 100

   vrShield_window_overlay = OrcShield_window_overlay_rsc
   vrShield_overlay = OrcShield_icon_rsc

   viBroken_group = 4
   viGround_group = 3
   viInventory_group = 1

   viDefense_base = 50
   viDamage_base = 1

properties:

messages:
   
   ReqUse()
   {
      if send(poOwner,@GetKarma) > -50  
      { 
         send(poOwner,@MsgSendUser,#message_rsc=orcshield_cant);
         
         return FALSE; 
      }
      
      propagate;
   }

   GetBaseSpellModifier(oSpell=$)
   {
      if send(oSpell,@GetSchool) = SS_SHALILLE
      {
         return -15;
      }
      
      if send(oSpell,@GetSchool) = SS_QOR
      {
         return 15;
      }

      return 0;
   }

   GetResistanceModifiers()
   {
      return [ [ATCK_WEAP_PIERCE,10],
               [ATCK_WEAP_THRUST,10],
               [ATCK_WEAP_BLUDGEON,10],
               [ATCK_WEAP_SLASH,10],
               [-ATCK_SPELL_HOLY,-20]
             ];
   }

   SendOverlayAnimation(animation = $)
   {
      AddPacket(1,ANIMATE_NONE,2,2);
      if (piItem_flags & ITEM_PALETTE_MASK) <> 0
      {
         AddPacket(1,ANIMATE_TRANSLATION,1,piItem_flags & ITEM_PALETTE_MASK);
      }

      return;
   }   


end
%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
