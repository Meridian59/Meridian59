% Meridian 59, Copyright 1994-2012 Andrew Kirmse and Chris Kirmse.
% All rights reserved.
%
% This software is distributed under a license that is described in
% the LICENSE file that accompanies it.
%
% Meridian is a registered trademark.


%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
PrincessShield is SoldierShield

% Stats affected: Mysticism and Agility
% Special bonus: Using a Princess shield makes your spells cost up to 2 less mana.

constants:

   include blakston.khd

resources:

   include prinshld.lkod

   PrincessShield_name_rsc = "shield of the Princess' army"

   PrincessShield_desc_rsc = \
      "This shield identifies you as a proud member of the Princess' army."

   PrincessShield_ArmyName = "the Royal army"

   PrincessShield_emblem_over = gshprov.bgf
   PrincessShield_emblem_drop = gshpr.bgf
   PrincessShield_emblem_back = gshprbk.bgf

   PrincessShield_welcome_mail = "Subject: Welcome to the Royal army.\n"
      "Greetings.  I welcome you as a new ally in our fight to keep the throne from the "
      "forces that would see us come to harm.\n\n"
      "There are a few rules you must observe while acting as a soldier.\n\n"
      "SOLDIER SHIELD: Your shield is the sign that you are an official part of the "
      "Royal army.  You must keep this equipped at all times in order to demonstrate "
      "your position.\n\n"
      "ENEMY SOLDIERS: As agreed, you can be attacked by any enemy soldier in any place "
      "that is not a haven (such as a inn or hall).  Your attacker will not be recognized "
      "as a criminal for attacking or killing you.  Likewise, you can attack enemies "
      "without fear of penalty.  Note that attacking any fellow soldiers will vex me "
      "greatly, and I will remove you as a soldier and a member of my faction.  Also, our "
      "righteousness allows you to do additional damage with all your attacks to these "
      "enemy soldiers.  Please realize that deaths at the hands of enemy soldiers are "
      "real, and you will suffer the usual penalties.\n\n"
      "RANKS: Your position within the army is determined by your actions.  Killing enemy "
      "soldiers will allow you promotion within the ranks of the army.  Being killed or "
      "remaining inactive for long periods of time will show you a decrease in rank.  "
      "Do not try to escape combat by leaving this world in unsafe areas as this will "
      "also lower your rank.\n\n"
      "ABILITIES: With your status as a faction soldier, I will show you additional ways "
      "to increase your power.  I will make you more intelligent and able to comprehend "
      "all things mystical.  I will also teach you ways to reduce the cost of your "
      "spells.  The potence of these abilities will increase as you gain ranks.\n\n"
      "THE COUNCILORS: The councilors have declared that the tokens are too delicate to "
      "be entrusted to the hands of soldiers.  Therefore, you are instructed not to pick "
      "up any tokens you may find.  I encourage you to find an ally to take the token to "
      "the proper councilor should you find one.  Likewise, the councilos have declared "
      "that only faction soldiers may claim territory now.  The flags in my chambers may "
      "now only be carried by recognized soldiers of my faction.\n\n"
      "LOYALTY: While in the service of the royal army, I will not ask you to prove your "
      "loyalty as before.  You should concentrate on maintaining your rank.\n\n"
      "May Shal'ille guide your actions to ensure our victory!\n\n"
      "Signed, The Princess Kateriina, regent to Meridian."

classvars:

   vrName = PrincessShield_name_rsc
   vrDesc = PrincessShield_desc_rsc

   prShield_emblem_held = PrincessShield_emblem_over
   prShield_emblem_drop = PrincessShield_emblem_drop
   prShield_emblem_back = PrincessShield_emblem_back

   viFaction = FACTION_PRINCESS
   viColor = XLAT_TO_ORANGE

   % Stuff for rank
   vrArmyName = PrincessShield_ArmyName

   vrWelcomeMessage = PrincessShield_welcome_mail

properties:

   piIntellectChange = 0
   piMysticismChange = 0

messages:

   RemoveStats()
   {
      if poOwner <> $
         AND IsClass(poOwner,&Player)
      {
         % Reset owner's stats
         send(poOwner,@AddIntellect,#points=-piIntellectChange);
         send(poOwner,@AddMysticism,#points=-piMysticismChange);
         piIntellectChange = 0;
         piMysticismChange = 0;
      }

      return;
   }

   UpdateRankPowers()
   "Updates any rank powers.  Affects stats."
   {
      local iStatAdjustment, iNewAdjustment;

      if poOwner = $
         OR NOT IsClass(poOwner,&Player)
      {
         return;
      }

      if piFactionRank <= 2
      {
         iStatAdjustment = 0;
      }
      else
      {
         if piFactionRank <= 6
         {
            iStatAdjustment = 3;
         }
         else
         {
            iStatAdjustment = 5;
         }
      }

      % In the following:
      %  iNewAdjustment is how much the stat changed this time around.
      %  pi(stat)Change is the cumulative change to that stat.
      if iStatAdjustment <> piIntellectChange
      {
         iNewAdjustment = send(poOwner,@AddIntellect,#points=(iStatAdjustment-piIntellectChange));
         piIntellectChange = piIntellectChange + iNewAdjustment;
      }

      if iStatAdjustment <> piMysticismChange
      {
         iNewAdjustment = send(poOwner,@AddMysticism,#points=(iStatAdjustment-piMysticismChange));
         piMysticismChange = piMysticismChange + iNewAdjustment;
      }

      return;
   }

   GetSpellManaReduction()
   {
      local iReduction;

      iReduction = ((piFactionRank/3) - 1);

      return iReduction;
   }


end
%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
