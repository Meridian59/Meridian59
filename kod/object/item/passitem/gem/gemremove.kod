% Meridian 59, Copyright 1994-2012 Andrew Kirmse and Chris Kirmse.
% All rights reserved.
%
% This software is distributed under a license that is described in
% the LICENSE file that accompanies it.
%
% Meridian is a registered trademark.


%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
GemRemove is Gem

constants:

   include blakston.khd

resources:

   gem_remove_name_rsc = "jeweller's chisel"
   gem_remove_icon_rsc = wand6.bgf
   gem_remove_desc_rsc = \
      "This wand has a rather mundane use - rather than casting a spell, "
      "its sturdy construction and anti-magic properties will help you "
      "physically pry gems from the armor to which they are attached."
    gem_remove_not_while_wearing = \
      "You can't pry gems from your armor while you're wearing it!"
    gem_remove_work_complete = \
      "The chisel shatters as the final gem pops free."

classvars:

   vrName = gem_remove_name_rsc
   vrIcon = gem_remove_icon_rsc
   vrDesc = gem_remove_desc_rsc
   viValue_average = 25000
   viWeight = 0
   viBulk = 0

properties:
   
messages:

   ReqNewApply(what = $,apply_on = $)
   {
      local oObjectAtt, i;

      if Send(apply_on,@GetOwner) <> what
      {
         return FALSE;
      }

      if IsClass(what,&Player)
      {
         foreach i in Send(what,@GetPlayerUsing)
         {
            if apply_on = i
            {
               Send(what,@MsgSendUser,#message_rsc=gem_remove_not_while_wearing);
               return FALSE;
            }
         }
      }

      foreach oObjectAtt in Send(apply_on,@GetObjectAttributes)
      {
         if IsClass(oObjectAtt,&DefModSockets)
            AND Send(oObjectAtt,@GetSocketed) <> $
         {
            return TRUE;
         }
      }

      return FALSE;
   }

   NewApplied(what = $,apply_on = $)
   {
      local oObjectAtt;

      foreach oObjectAtt in Send(apply_on,@GetObjectAttributes)
      {
         if IsClass(oObjectAtt,&DefModSockets)
         {
            Send(oObjectAtt,@RemoveGems,#who=what);

            return;
         }
      }

      Send(what,@MsgSendUser,#message_rsc=gem_remove_work_complete);
      Send(self,@Delete);

      return;
   }

end
%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
