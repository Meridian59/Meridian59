% Meridian 59, Copyright 1994-2012 Andrew Kirmse and Chris Kirmse.
% All rights reserved.
%
% This software is distributed under a license that is described in
% the LICENSE file that accompanies it.
%
% Meridian is a registered trademark.


%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
NameChangeScroll is InscriptionItem

constants:

   include blakston.khd

   DEED_POLL_DESTROY_TIME = 3600000

resources:

   include namechscroll.lkod

   deedpoll_name_rsc = "deed poll"
   deedpoll_icon_rsc = scr02.bgf
   deedpoll_desc_rsc = \
      "This is an official document of the Office of the Justicar.\n"
      "The Royal Justicar's office is responsible for organizing legal "
      "name changes.  Fill out your requested name and return this scroll "
      "to Caramo in the Office of the Justicar in North Barloque, and if "
      "your proposed name is valid, it shall be yours.\n"
      "\n"
      "    =[C]=   Caramo, Assistant to the Royal Justicar"
   deedpoll_cant_drop_rsc = \
      "You find yourself unable to get rid of the deed poll."
   deedpoll_destroyed_rsc = \
      "Your deed poll suddenly crumbles into dust!"

classvars:

   vrName = deedpoll_name_rsc
   vrIcon = deedpoll_icon_rsc
   vrDesc = deedpoll_desc_rsc

   viWeight = 2

properties:

   ptDestroy = $

messages:

   NewOwner(what=$)
   {
      if ptDestroy <> $
      {
         DeleteTimer(ptDestroy);
         ptDestroy = $;
      }

      if IsClass(what,&Player)
      {
         ptDestroy = CreateTimer(self,@Destroy,DEED_POLL_DESTROY_TIME);
      }

      propagate;
   }

   Destroy()
   {
      ptDestroy = $;

      if poOwner <> $
         AND IsClass(poOwner,&User)
      {
         Send(poOwner,@MsgSendUser,#message_rsc=deedpoll_destroyed_rsc);
      }

      Send(self,@Delete);

      return;
   }

   Delete()
   {
      if ptDestroy <> $
      {
         DeleteTimer(ptDestroy);
         ptDestroy = $;
      }

      if poOwner <> $
      {
         % Take user off Caramo's name change warn list.
         Send(Send(SYS,@GetCaramo),@RemoveFromWarnList,#who=poOwner);
      }

      propagate;
   }

   ShowDesc()
   {
      AddPacket(4, deedpoll_desc_rsc);

      return;
   }

   ReqNewOwner(what = $)
   {
      if poOwner = $
         OR what = $
      {
         return TRUE;
      }

      if IsClass(what,&BarloqueClerk)
      {
         return TRUE;
      }

      if IsClass(poOwner,&User)
      {
         Send(poOwner,@MsgSendUser,#message_rsc=deedpoll_cant_drop_rsc);
      }

      return FALSE;
   }

   CanSwap()
   {
      % No more using these for swap mules
      return FALSE;
   }

   DropOnDeath()
   {
      % No drop so that people can't load up on these and avoid real penalties
      return FALSE;
   }

   CanBeStoredInVault()
   {
       return FALSE;
   }

end
%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
