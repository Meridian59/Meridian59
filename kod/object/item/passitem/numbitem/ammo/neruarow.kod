% Meridian 59, Copyright 1994-2012 Andrew Kirmse and Chris Kirmse.
% All rights reserved.
%
% This software is distributed under a license that is described in
% the LICENSE file that accompanies it.
%
% Meridian is a registered trademark.


%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
NeruditeArrow is Ammo

constants:

   include blakston.khd

resources:

   include neruarow.lkod

   nerudite_arrow_name_rsc = "nerudite arrows"
   nerudite_arrow_icon_rsc = arrowner.bgf
   nerudite_arrow_desc_rsc = \
      "These carved arrowheads do a little more damage than standard arrows, "
      "but should the small green stone shatter under the impact, it could "
      "prove fatal to the victim."

   nerudite_arrow_explodes = "The nerudite arrowhead shatters!"

classvars:

   vrName = nerudite_arrow_name_rsc
   vrIcon = nerudite_arrow_icon_rsc
   vrDesc = nerudite_arrow_desc_rsc

   viAmmo_type = AMMO_ARROW
   viValue_average = 20

   viGround_group = 4
   viInventory_group = 3
   viBroken_group = 2

   viBulk = 4
   viWeight = 5

   viMinDamage = 2
   viMaxDamage = 6

   viMinShatterDamage = 3
   viMaxShatterDamage = 8

properties:

   piNumber = 5
   piAttack_type = ATCK_WEAP_PIERCE + ATCK_WEAP_NERUDITE

messages:

   GetDamage(who=$,target=$)
   {
      if target <> $ AND Random(1,100) <= 10
      {
         Send(who,@MsgSendUser,#message_rsc=nerudite_arrow_explodes);
         if IsClass(target,&Player)
         {
            Send(target,@MsgSendUser,#message_rsc=nerudite_arrow_explodes);
         }

         return Random(viMinShatterDamage,viMaxShatterDamage);
      }

      propagate;
   }

   GetMinSpecialDamage()
   {
      return viMinShatterDamage;
   }

   GetMaxSpecialDamage()
   {
      return viMaxShatterDamage;
   }

   SendProjectileAnimation()
   {
      AddPacket(1,ANIMATE_NONE,2,1);

      return;
   }

   GetProjectileSpeed()
   {
      return 7;
   }

   GetProjectileIcon()
   {
      return vrIcon;
   }

end
%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
