% Meridian 59, Copyright 1994-2012 Andrew Kirmse and Chris Kirmse.
% All rights reserved.
%
% This software is distributed under a license that is described in
% the LICENSE file that accompanies it.
%
% Meridian is a registered trademark.


%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
%an Arrow is a NumberItem used by a bow--it needs properties and procedures
%for ShowSomething() and GetDamage()
%
SilverArrow is Ammo

constants:

   include blakston.khd

resources:

   include slvarrow.lkod

   silver_arrow_name_rsc = "silver arrows"
   silver_arrow_icon_rsc = arrowsil.bgf
   silver_arrow_desc_rsc = \
      "The sharp clean edge of the arrowhead makes for nasty wounds.  "
      "Some archers have found these to be the nock of choice for "
      "battling the undead."

   silver_arrow_burns_you = "The silver arrowhead stings your undead flesh!"

classvars:

   vrName = silver_arrow_name_rsc
   vrIcon = silver_arrow_icon_rsc
   vrDesc = silver_arrow_desc_rsc

   viAmmo_type = AMMO_ARROW
   viValue_average = 30

   viGround_group = 4
   viInventory_group = 3
   viBroken_group = 2

   viBulk = 4
   viWeight = 5

   viMinDamage = 2
   viMaxDamage = 6

   viMinUndeadDamage = 5
   viMaxUndeadDamage = 10

properties:

   piNumber = 10
   piAttack_type = ATCK_WEAP_PIERCE + ATCK_WEAP_SILVER

messages:

   GetDamage(who=$,target=$)
   {
      if (target <> $) AND Send(target,@IsUndead)
      {
         if IsClass(target,&Player)
         {
            Send(target,@MsgSendUser,#message_rsc=silver_arrow_burns_you);
         }

         return Random(viMinUndeadDamage,viMaxUndeadDamage);
      }

      propagate;
   }

   GetMinSpecialDamage()
   {
      return viMinUndeadDamage;
   }

   GetMaxSpecialDamage()
   {
      return viMaxUndeadDamage;
   }

   SendProjectileAnimation()
   {
      AddPacket(1,ANIMATE_NONE,2,1);

      return;
   }

   GetProjectileSpeed()
   {
      return 7;
   }

   GetProjectileIcon()
   {
      return vrIcon;
   }

end
%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
