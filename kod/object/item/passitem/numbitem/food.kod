% Meridian 59, Copyright 1994-2012 Andrew Kirmse and Chris Kirmse.
% All rights reserved.
%
% This software is distributed under a license that is described in
% the LICENSE file that accompanies it.
%
% Meridian is a registered trademark.


%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
Food is NumberItem

constants:

   include blakston.khd

resources:

   include food.lkod

   food_desc_rsc = "%s\n\nThis will cure the munchies.%s"

   food_use = "Yum-yum good!"
   food_cant_use_on = "You can't feed %s%s"

   food_eat_wav = eat.wav

classvars:

   viItem_type = ITEMTYPE_FOOD | ITEMTYPE_SUNDRY

   viFilling = 50
   viNutrition = 10

   vrEat_wav = food_eat_wav

properties:

   viObject_flags = OF_GETTABLE

messages:

   GetFilling()
   {
      return viFilling;

   }

   ReqNewApply(what = $,apply_on = $)
   {
      if not IsClass(apply_on,&User)
      {
	 Send(what,@MsgSendUser,#message_rsc=food_cant_use_on, 
	      #parm1=Send(apply_on,@GetIndef),
		  #parm2=Send(apply_on,@GetName));
	 return False;
      }
      if not Send(apply_on,@ReqEatSomething,#filling=viFilling,#what=self)
      {
	 return False;
      }
      return True;
   }

   SendTaste(what = $,apply_on = $)
   {
      Send(apply_on,@MsgSendUser,#message_rsc=food_use);
	  return;
   }

   NewApplied(what = $,apply_on = $)
   {
      Send(self,@SendTaste,#what=what,#apply_on=apply_on);
      Send(what,@WaveSendUser,#wave_rsc=vrEat_wav);
      Send(apply_on,@EatSomething,#nutrition=viNutrition,#filling=viFilling);
      Send(self,@SubtractNumber,#number=1);
      return;
   }

   IsAlcohol()
   {	 
      return FALSE;
   }

   IsBeverage()
   {
      return FALSE;
   }

end
%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
