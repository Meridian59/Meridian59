% Meridian 59, Copyright 1994-2012 Andrew Kirmse and Chris Kirmse.
% All rights reserved.
%
% This software is distributed under a license that is described in
% the LICENSE file that accompanies it.
%
% Meridian is a registered trademark.


%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
ShamanBlood is Reagent

constants:

   include blakston.khd

resources:

   include shamblod.lkod

   ShamanBlood_name_rsc = "shaman blood"
   ShamanBlood_icon_rsc = avblood.bgf
   ShamanBlood_desc_rsc = \
      "Spellcasters of the birdlike avars do not learn their "
      "abilities. Quite the opposite, the magic courses through "
      "their veins - which means that it is magic that can be borrowed." 

   ShamanBlood_name_plural_rsc = "shaman blood"
   ShamanBlood_vial_name_plural_rsc = "vials of shaman blood"
   ShamanBlood_vial_name_rsc = "vial of shaman blood"

classvars:

   vrName = ShamanBlood_name_rsc
   vrIcon = ShamanBlood_icon_rsc
   vrDesc = ShamanBlood_desc_rsc

   vrName_plural = ShamanBlood_name_plural_rsc

   viValue_average = 70
   viWeight = 4
   viBulk = 5

properties:

   piNumber = 2
   pbGathered = TRUE

messages:

   Constructor(Gathered=TRUE)
   {
      pbGathered = Gathered;

      propagate;
   }

   NewOwner(what = $)
   {
      if poOwner <> $
         AND (what = $ OR IsClass(what,&Battler))
      {
         pbGathered = TRUE;
      }

      propagate;
   }

   SendAnimation()
   {
      if pbGathered
      {
         if poOwner <> $ AND IsClass(poOwner,&Player)
         {
            AddPacket(1,ANIMATE_NONE,2,1);
         }
         else
         {
            AddPacket(1,ANIMATE_NONE,2,2);
         }

         return;
      }
      else
      {
         AddPacket(1,ANIMATE_NONE,2,3);
      }

      return;
   }

   SendLookAnimation()
   {
      if pbGathered
      {
         if poOwner <> $ AND IsClass(poOwner,&player)
         {
            AddPacket(1,ANIMATE_NONE,2,1);
         }
         else
         {
            AddPacket(1,ANIMATE_NONE,2,2);
         }

         return;
      }
      else
      {
         AddPacket(1,ANIMATE_NONE,2,3);
      }

      return;
   }

   SendInventoryAnimation()
   {
      AddPacket(1,ANIMATE_NONE,2,1);

      return;
   }

   GetName()
   {
      if pbGathered
      {
         if piNumber = 1
         {
            return ShamanBlood_vial_name_rsc;
         }
         else
         {
            return ShamanBlood_vial_name_plural_rsc;
         }
      }
      else
      {
         if piNumber = 1
         {
            return vrName;
         }
         else
         {
            return vrName_plural;
         }
      }

      propagate;
   }

end
%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
