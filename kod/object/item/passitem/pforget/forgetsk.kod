% Meridian 59, Copyright 1994-2012 Andrew Kirmse and Chris Kirmse.
% All rights reserved.
%
% This software is distributed under a license that is described in
% the LICENSE file that accompanies it.
%
% Meridian is a registered trademark.


%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%ForgetPotionSkills is ForgetPotion
forgetpotionskills is forgetpotion

constants:

   MESSAGE_DELAY = 3000    %% three seconds

   include blakston.khd

resources:

   forgetpotionskills_desc_rsc = "Drinking this potion causes one to forget all skills that he or she possesses.\n\nMay have very serious side effects."

   forgetpotionskills_forget_skill = "The art of '%s' slips from your mind!"
   forgetpotionskills_vulnerable = "You suddenly feel more vulnerable!"

classvars:

   vrDesc = forgetpotionskills_desc_rsc
   viForget_school = SKS_FENCING

properties:

messages:

   QuaffTimer()
   {
      local i, skills, oSkill, iSkillnum, ihp_loss, howmuch;

      ptQuaff = $;
      Skills = send(poApply_target,@getskilllist);
      piCount = 0;
      iHP_loss = 0;

      for i in Skills
      {
         iSkillNum = send(poApply_target,@DecodeSkillNum,#compound=i);
         if iSkillNum = SKID_SLASH OR iSkillnum = SKID_DODGE
         {
            iHP_loss = iHp_loss + send(poApply_target,@DecodeSkillAbility,#compound=i);
         }

         oSkill = send(SYS,@FindSkillByNum,#num=iSkillNum);

         % delete all skills!
         send(poApply_target,@RemoveSkill,#num=iSkillNum);
         send(poApply_target,@MsgSendUser,#message_rsc=forgetpotionskills_forget_skill,
              #parm1=send(oSkill,@GetName));
         piCount = piCount + 1;
      }
      
      if piCount = 0
      {
         Send(poApply_target,@MsgSendUser,#message_rsc=forgetpotion_nothing_happens);
      }
      else
      {
         send(poApply_target,@MsgSendUser,#message_rsc=forgetpotionskills_vulnerable);

         howmuch = -random((iHP_loss/40),iHP_loss/20);
         howmuch = bound(howmuch,$,-1);
         send(poApply_target,@GainBaseMaxHealth,#amount=howmuch);
         
         if send(poApply_target,@CheckLog)
         {
            DEBUG("LOG:  ",send(poApply_Target,@getname)," drank a forgetpotion.  Lost ",howmuch);
         }
         
         send(self,@DoSideEffects);
         send(poApply_target,@tocliStats,#group=2);
      }

      %% check if player should regain guardian angel from HP loss.
      Send(poApply_target,@EvaluatePKstatus);
      
      Send(self,@Delete);
      
      return;
   }

end
%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
