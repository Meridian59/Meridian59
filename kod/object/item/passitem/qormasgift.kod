% Meridian 59, Copyright 1994-2012 Andrew Kirmse and Chris Kirmse.
% All rights reserved.
%
% This software is distributed under a license that is described in
% the LICENSE file that accompanies it.
%
% Meridian is a registered trademark.


%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
QormasGift is PassiveItem

constants:

   include blakston.khd

resources:

   include qormasgift.lkod

   QormasGift_icon_rsc = gift1.bgf
   QormasGift_icon_rsc1 = gift1.bgf
   QormasGift_icon_rsc2 = gift2.bgf
   QormasGift_icon_rsc3 = gift3.bgf
   QormasGift_icon_rsc4 = gift4.bgf
   QormasGift_name_rsc = "Qormas Present"
   QormasGift_desc_rsc = \
      "This gift is intricately wrapped in the finest paper available in the"
      " land."
   QormasGift_desc_append = "  The tag reads:\n\nTo: %q\nFrom: Qor The Vile"
   
   Qormas_gift_msg = "You tear away the wrapping paper to reveal %s"
   Qormas_gift1_msg = "a Rose!"
   Qormas_gift2_msg = "twenty Inky-Cap Mushrooms!"
   Qormas_gift3_msg = "three Scrolls of Dispelling!"
   Qormas_gift4_msg = "three Brown Potions!"
   Qormas_gift5_msg = "three Potions of Deliverance!"
   Qormas_gift6_msg = "a Bonk Stick!"
   Qormas_gift7_msg = "three Wands of Charming!"
   Qormas_gift8_msg = "twenty Orc Teeth!"
   Qormas_gift9_msg = "a Shrunken Head!"
   
   Qormas_gift10_msg = "a Cow Mask!"
   Qormas_gift11_msg = "a Daemon Skeleton Mask!"
   Qormas_gift12_msg = "a Mummy Mask!"
   Qormas_gift13_msg = "a Fey Mask!"
   Qormas_gift14_msg = "a Shrunken-Head Mask!"
   Qormas_gift15_msg = "a Xeochicatl Mask!"
   Qormas_gift16_msg = "a Skull Mask!"
   Qormas_gift17_msg = "a Rat Mask!"
   Qormas_gift18_msg = "an Ant Mask!"
   Qormas_gift19_msg = "a Troll Mask!"
   Qormas_gift20_msg = "a Kriipa Mask!"

classvars:
   
   vrName = QormasGift_name_rsc
   vrDesc = QormasGift_desc_rsc

   viUse_type = ITEM_SINGLE_USE
   viUse_amount = 1
   viWeight = 90
   viBulk = 90

properties:

   viObject_flags = OF_NOEXAMINE | OF_GETTABLE
   vrIcon = QormasGift_icon_rsc

   poQormas = $

   poRecipient = $

   % individual gift payload toggles
   pbRose = TRUE
   pbBonkStick = TRUE
   pbSeduceWand = TRUE
   pbShrunkenHead = TRUE
   pbCowMask = TRUE
   pbDaemonMask = TRUE
   pbMummyMask = TRUE
   pbFeyMask = TRUE
   pbShrunkenHeadMask = TRUE
   pbXeoMask = TRUE
   pbSkullMask = TRUE
   pbRatMask = TRUE
   pbAntMask = TRUE
   pbTrollMask = TRUE
   pbKriipaMask = TRUE

   % Set this in qormas kod, leave this one alone
   pbMaskPossible = FALSE   
   
messages:   

   Constructor(mask=FALSE,qormas_obj=$)
   {
      Send(self,@SetRandomPaper);
      
      if (mask)
      {
         pbMaskPossible = TRUE;
      }
      if(qormas_obj)
      {
         poQormas = qormas_obj;
      }
      
      propagate;
   }

   DestroyDisposable()
   {
      return;
   }
   
   GetRecipient()
   {
      return poRecipient;
   }
   
   SetRecipient(who=$)
   {
      poRecipient = who;
      
      return;
   }
   
   SetRandomPaper()
   {
      local iRandom;

      iRandom = Random(1,4);
      
      if iRandom = 1
      {
         vrIcon = QormasGift_icon_rsc1;
      }
      if iRandom = 2
      {
         vrIcon = QormasGift_icon_rsc2;
      }
      if iRandom = 3
      {
         vrIcon = QormasGift_icon_rsc3;
      }
      if iRandom = 4
      {
         vrIcon = QormasGift_icon_rsc4;
      }
      
      return;
   }

   ReqNewApply()
   {
      return TRUE;
   }

   NewApplied(what = $)
   {
      local iCount, rand, bSet, oPayload, rMsg;

      bSet = FALSE;
      iCount = 0;
      rMsg = $;

      while (NOT bSet AND iCount++ < 30)
      {
         if (pbMaskPossible)
         {
            rand = Random(1,60);
         }
         else
         {
            rand = Random(1,49);
         }

         if rand = 1
         {
            if (pbRose)
            {
               Send(poOwner,@NewHold,#what=Create(&Rose));
               rMsg = Qormas_gift1_msg;
               bSet = TRUE;
            }
         }
         else if rand >= 2 AND rand <= 12
         {
            Send(poOwner,@NewHold,#what=Create(&InkyCap,#number=20));
            rMsg = Qormas_gift2_msg;
            bSet = TRUE;
         }
         else if rand >= 13 AND rand <= 18
         {
            oPayload = Create(&DispellIllusionScroll);
            Send(oPayload,@RevealHiddenAttributes);
            Send(poOwner,@NewHold,#what=oPayload);
            oPayload = Create(&DispellIllusionScroll);
            Send(oPayload,@RevealHiddenAttributes);
            Send(poOwner,@NewHold,#what=oPayload);
            oPayload = Create(&DispellIllusionScroll);
            Send(oPayload,@RevealHiddenAttributes);
            Send(poOwner,@NewHold,#what=oPayload);
            rMsg = Qormas_gift3_msg;
            bSet = TRUE;
         }
         else if rand >= 19 AND rand <= 24
         {
            Send(poOwner,@NewHold,#what=Create(&IllusionaryFormPotion));
            Send(poOwner,@NewHold,#what=Create(&IllusionaryFormPotion));
            Send(poOwner,@NewHold,#what=Create(&IllusionaryFormPotion));
            rMsg = Qormas_gift4_msg;
            bSet = TRUE;
         }
         else if rand >= 25 AND rand <= 30
         {
            oPayload = Create(&RescuePotion);
            Send(oPayload,@RevealHiddenAttributes);
            Send(poOwner,@NewHold,#what=oPayload);
            oPayload = Create(&RescuePotion);
            Send(oPayload,@RevealHiddenAttributes);
            Send(poOwner,@NewHold,#what=oPayload);
            oPayload = Create(&RescuePotion);
            Send(oPayload,@RevealHiddenAttributes);
            Send(poOwner,@NewHold,#what=oPayload);
            rMsg = Qormas_gift5_msg;
            bSet = TRUE;
         }
         else if rand >= 31 AND rand <= 36
         {
            if (pbBonkStick)
            {
               Send(poOwner,@NewHold,#what=Create(&BonkStick));
               rMsg = Qormas_gift6_msg;
               bSet = TRUE;
            }
         }
         else if rand >= 37 AND rand <= 42
         {
            if (pbSeduceWand)
            {
               oPayload=Create(&SeduceWand);
               Send(oPayload,@RevealHiddenAttributes);
               Send(poOwner,@NewHold,#what=oPayload);
               oPayload=Create(&SeduceWand);
               Send(oPayload,@RevealHiddenAttributes);
               Send(poOwner,@NewHold,#what=oPayload);
               oPayload=Create(&SeduceWand);
               Send(oPayload,@RevealHiddenAttributes);
               Send(poOwner,@NewHold,#what=oPayload);
               rMsg = Qormas_gift7_msg;
               bSet = TRUE;
            }
         }
         else if rand >= 43 AND rand <= 48
         {
            Send(poOwner,@NewHold,#what=Create(&OrcTooth,#number=20));
            rMsg = Qormas_gift8_msg;
            bSet = TRUE;
         }
         else if rand = 49
         {
            if (pbShrunkenHead)
            {
               Send(poOwner,@NewHold,#what=Create(&ShrunkenHead));
               rMsg = Qormas_gift9_msg;
               bSet = TRUE;
            }
         }
         else if rand = 50
         {
            if (pbCowMask)
            {
               Send(poOwner,@NewHold,#what=Create(&CowMask));
               rMsg = Qormas_gift10_msg;
               bSet = TRUE;
            }
         }
         else if rand = 51
         {
            if (pbDaemonMask)
            {
               Send(poOwner,@NewHold,#what=Create(&DaemonMask));
               rMsg = Qormas_gift11_msg;
               bSet = TRUE;
            }
         }
         else if rand = 52
         {
            if (pbMummyMask)
            {
               Send(poOwner,@NewHold,#what=Create(&MummyMask));
               rMsg = Qormas_gift12_msg;
               bSet = TRUE;
            }
         }
         else if rand = 53
         {
            if (pbFeyMask)
            {
               Send(poOwner,@NewHold,#what=Create(&FeyMask));
               rMsg = Qormas_gift13_msg;
               bSet = TRUE;
            }
         }
         else if rand = 54
         {
            if (pbShrunkenHeadMask)
            {
               Send(poOwner,@NewHold,#what=Create(&ShrunkenHeadMask));
               rMsg = Qormas_gift14_msg;
               bSet = TRUE;
            }
         }
         else if rand = 55
         {
            if (pbXeoMask)
            {
               Send(poOwner,@NewHold,#what=Create(&XeoMask));
               rMsg = Qormas_gift15_msg;
               bSet = TRUE;
            }
         }
         else if rand = 56
         {
            if (pbSkullMask)
            {
               Send(poOwner,@NewHold,#what=Create(&SkullMask));
               rMsg = Qormas_gift16_msg;
               bSet = TRUE;
            }
         }
         else if rand = 57
         {
            if (pbRatMask)
            {
               Send(poOwner,@NewHold,#what=Create(&RatMask));
               rMsg = Qormas_gift17_msg;
               bSet = TRUE;
            }
         }
         else if rand = 58
         {
            if (pbAntMask)
            {
               Send(poOwner,@NewHold,#what=Create(&AntMask));
               rMsg = Qormas_gift18_msg;
               bSet = TRUE;
            }
         }
         else if rand = 59
         {
            if (pbTrollMask)
            {
               Send(poOwner,@NewHold,#what=Create(&TrollMask));
               rMsg = Qormas_gift19_msg;
               bSet = TRUE;
            }
         }
         else if rand = 60
         {
            if (pbKriipaMask)
            {
               Send(poOwner,@NewHold,#what=Create(&KriipaMask));
               rMsg = Qormas_gift20_msg;
               bSet = TRUE;
            }
         }
      }

      if rMsg <> $
      {
         Send(poOwner,@MsgSendUser,#message_rsc=Qormas_gift_msg,#parm1=rMsg);
      }

      Send(self,@Delete);

      return;
   }

   AppendDesc()
   {
      if poRecipient = $
      {
         AddPacket(4,system_blank_resource);
      }
      else
      {
         AddPacket(4,QormasGift_desc_append,
                   6,Send(poRecipient,@GetTrueName));
      }

      return;
   }

   NewOwner(what=$)
   {
      if (poQormas <> $)
      {
         if (IsClass(what,&User))
         {
            Send(poQormas,@GiftClaimed,#what=self,#who=what);
         }
      }

      propagate;
   }

   ReqNewOwner(what = $)
   {
      if IsClass(what,&Room)
      {
         return TRUE;
      }

      if (what = poRecipient)
      {
         return TRUE;
      }

      return FALSE;
   }

end
%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
