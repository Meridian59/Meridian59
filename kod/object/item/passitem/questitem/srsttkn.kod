% Meridian 59, Copyright 1994-2012 Andrew Kirmse and Chris Kirmse.
% All rights reserved.
%
% This software is distributed under a license that is described in
% the LICENSE file that accompanies it.
%
% Meridian is a registered trademark.


%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
StatsResetToken is QuestItem

constants:

   include blakston.khd

resources:

   include srsttkn.lkod

   StatsResetToken_name_rsc = "ancient trinket"
   StatsResetToken_icon_rsc = srtoken.bgf
   StatsResetToken_desc_rsc = \
      "This is a Trinket of the Elders, said to be handed to them by the Gods. "
      "Legend holds that these Ancient Trinkets contain the power to change "
      "the very structure of a Mortal. Surely the Elders would greatly "
      "reward anyone who returns this Trinket to them."
   StatsResetToken_desc_append = \
      "\n\nThis token will expire in %i Meridian years.%s"
   StatsResetToken_desc_noTrade = \
      "\n\nNo Trade"

classvars:

   vrName = StatsResetToken_name_rsc
   vrIcon = StatsResetToken_icon_rsc
   vrDesc = StatsResetToken_desc_rsc
   viIndefinite = ARTICLE_AN
   viValue_average = 0
   viWeight = 0
   viBulk = 0

properties:

   pbTemporary
   piGameYearsRemaining
   pbTradeable

messages:

   Constructor(temporary=TRUE,lifetime=-1,tradeable=FALSE)
   {
      pbTradeable = tradeable;
      pbTemporary = temporary;

      % Just assign 2 years if not temporary, it won't decrease anyway.
      if NOT pbTemporary
      {
         piGameYearsRemaining = 2;

         propagate;
      }

      if lifetime <> -1
      {
         piGameYearsRemaining = lifetime;
      }
      else
      {
         piGameYearsRemaining = Send(SETTINGS_OBJECT,@GetStatResetTokenLife);
      }

      propagate;
   }

   ReqNewOwner(what = $)
   {
      if IsClass(poOwner,&Room)
      {
         return TRUE;
      }

      if IsClass(what,&MarionElder)
         OR IsClass(what,&JasperElder)
         OR IsClass(what,&HazarElder)
      {
         return TRUE;
      }

      return pbTradeable;
   }

   CanWeaken()
   {
      return FALSE;
   }

   CanSwap()
   {
      return FALSE;
   }

   CanShatter()
   {
      return FALSE;
   }

   CanEnchant()
   {
      return FALSE;
   }

   CanBeStoredInVault()
   {
      return FALSE;
   }

   DropOnDeath()
   {
      return FALSE;
   }

   NewYear()
   {
      if pbTemporary
      {
         piGameYearsRemaining = piGameYearsRemaining - 1;
      
         if piGameYearsRemaining <= 0
         {
            Send(self,@Delete);
         }
      }

      return;
   }

   AppendDesc()
   {
      if pbTemporary
      {
         if pbTradeable
         {
            AddPacket(4,StatsResetToken_desc_append,4,piGameYearsRemaining,
                      4,system_blank_resource);

            return;
         }
         else
         {
            AddPacket(4,StatsResetToken_desc_append,4,piGameYearsRemaining,
                      4,StatsResetToken_desc_noTrade);

            return;
         }
      }

      if pbTradeable
      {
         AddPacket(4,system_blank_resource);
      }
      else
      {
         AddPacket(4,StatsResetToken_desc_noTrade);
      }

      return;
   }

end
%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
