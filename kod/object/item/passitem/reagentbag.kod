% Meridian 59, Copyright 1994-2012 Andrew Kirmse and Chris Kirmse.
% All rights reserved.
%
% This software is distributed under a license that is described in
% the LICENSE file that accompanies it.
%
% Meridian is a registered trademark.


%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
ReagentBag is PassiveItem

constants:

   include blakston.khd

resources:

   include reagentbag.lkod

   reagentbag_name_rsc = "reagent bag"
   reagentbag_icon_rsc = baghold.bgf

   reagentbag_desc_rsc = "This lumpy bag is magical in nature, and able to "
   "hold large amounts of objects that themselves have inherent power."

   stash_in_bag = "You stash %d %s in your reagent bag."
   get_from_bag = "You get %d %s from your reagent bag."

classvars:

   vrName = reagentbag_name_rsc
   vrIcon = reagentbag_icon_rsc
   vrDesc = reagentbag_desc_rsc

   viValue_average = 500
   viBulk_hold_max = 12000
   viWeight_hold_max = $

properties:

   plReagents = $

   viObject_flags = OF_CONTAINER | OF_GETTABLE

messages:

   Constructor(owner=$)
   {
      if owner <> $
         AND IsClass(owner,&Player)
         AND Send(owner,@GetReagentBag) = $
      {
         Send(owner,@NewHold,#what=self);
      }

      propagate;
   }

   Delete()
   {
      local i;

      foreach i in plReagents
      {
         Send(i,@Delete);
      }

      propagate;
   }
   
   GetReagentBagContents()
   {
      return plReagents;
   }

   HolderExtractObject(data=$)
   {
      return data;
   }

   IsHolding(what=$)
   {
      local i,each_obj;

      foreach i in plReagents
      {
         if what = i
         {
            return TRUE;
         }
      }

      return FALSE;
   }

   GetBulk()
   {
      local i, iSum;

      iSum = 0;

      foreach i in plReagents
      {
         iSum = iSum + Send(i,@GetBulk);
      }

      return iSum;
   }

   GetWeight()
   {
      local i, iSum;

      iSum = 0;

      foreach i in plReagents
      {
         iSum = iSum + Send(i,@GetWeight);
      }

      return iSum;
   }

   ReqTaker(what=$,taker=$)
   {
      if taker <> poOwner
      {
         return FALSE;
      }

      Send(poOwner,@MsgSendUser,#message_rsc=get_from_bag,
            #parm1=Send(what,@GetNumber),
            #parm2=Send(what,@GetName));

      return TRUE;
   }

   ReqNewHold(what=$,who=$)
   {
      if who <> poOwner
      {
         return FALSE;
      }

      if NOT IsClass(what,&NumberItem)
         OR IsClass(what,&Food)
         OR IsClass(what,&Ammo)
         OR IsClass(what,&Money)
      {
         return FALSE;
      }

      return;
   }

   NewHold(what=$)
   {
      local i;

      if what = $
         OR what = self
      {
         return;
      }

      Send(poOwner,@MsgSendUser,#message_rsc=stash_in_bag,
            #parm1=Send(what,@GetNumber),
            #parm2=Send(what,@GetName));

      if IsClass(what,&NumberItem)
      {
         foreach i in plReagents
         {
            if i <> what
            {
               if GetClass(what) = GetClass(i)
               {
                  % Should only be one of these, so can quit loop if found
                  Send(i,@AddNumber,#number=Send(what,@GetNumber));
                  Send(what,@Delete);

                  return;
               }
            }
         }
      }

      Send(what,@NewOwner,#what=self);
      plReagents = Cons(what,plReagents);

      % If our owner is a player, update the weight carried stat.
      if poOwner <> $
         AND IsClass(poOwner,&Player)
      {
         Post(poOwner,@DrawCapacity);
      }

      return;
   }

   ReqNewOwner(what=$)
   {
      if what <> $
         AND IsClass(what,&Player)
         AND Send(what,@GetReagentBag) <> $
      {
         return FALSE;
      }

      propagate;
   }

   ReqLeaveHold(what=$)
   {
      return TRUE;
   }

   LeaveHold(what=$)
   {
      local i;

      foreach i in plReagents
      {
         if i = what
         {
            plReagents = DelListElem(plReagents,i);
         }
      }

      return;
   }

   ChangeBulkAndWeight()
   {
      return;
   }

   SendAnimation()
   {
      if poOwner <> $ AND IsClass(poOwner,&Room)
      {
         AddPacket(1,ANIMATE_NONE, 2,2);
      }
      else
      {
         AddPacket(1,ANIMATE_NONE, 2,1);
      }

      return;
   }

   CanBeGivenToNPC()
   {
      return FALSE;
   }

   CanSwap()
   {
      return FALSE;
   }

   CanShatter()
   {
      return FALSE;
   }

   ReqDMDelete()
   "Keys are not deleted with the DM clear inventory command."
   {
      return FALSE;
   }
   
   CanBeStoredInVault()
   {
      return FALSE;
   }

   DropOnDeath()
   {
      return FALSE;
   }

end
%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
