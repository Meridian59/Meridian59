% Meridian 59, Copyright 1994-2012 Andrew Kirmse and Chris Kirmse.
% All rights reserved.
%
% This software is distributed under a license that is described in
% the LICENSE file that accompanies it.
%
% Meridian is a registered trademark.


%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
VampireWand is Rod

constants:

   include blakston.khd

resources:

   include vamprod.lkod

   vampireRod_name_rsc = "rod of vampiric shock"
   vampireRod_icon_rsc = wand3.bgf
   vampireRod_desc_rsc = \
   "This ancient rod is cold to the touch.  Its dark, smooth surface seems to "
   "draw in all of the life force around it. "
   "You sense that this wand cannot be charged by places of safety, and is "
   "instead fed by the energy of good souls slain in battle."
   vampireRod_success_wav_rsc = vamp_wand.wav

   vampireRod_success_rsc = \
   "You shiver uncontrollably as you gleefully consume slivers of raw soul "
   "energy! Your black feast darkens your karma."

classvars:

   vrName = vampireRod_name_rsc
   vrIcon = vampireRod_icon_rsc

   viBulk = 25
   viWeight = 25
   viValue_average = 200
   viGround_group = 2
   
   vrGoodDesc = vampireRod_desc_rsc
   
   use_success_wav = vampireRod_success_wav_rsc
   use_success_msg = vampireRod_success_rsc

properties:

messages:

   CastSpell()
   "Changes the user's karma toward evil."
   {
      local iKarma_change;

      % User moves toward (-80) karma.
      iKarma_change=Send(poOwner,@CalculateKarmaChangeFromAct,#karma_act=-80,
         #karma_doer=Send(poOwner,@GetKarma),#swing_factor=10);
      Send(poOwner,@AddKarma,#amount=iKarma_Change);

      propagate;
   }

   SomethingKilled(what = $, victim = $)
   {
      % Gain a charge upon our owner killing a monster that has good karma.
      if what = poOwner
         AND IsClass(victim,&Monster)
         AND Send(self,@GetHits) < Send(self,@GetMaxHits)
         AND Send(victim,@GetKarma) > 0
      {
         Send(self,@SetHits,#number=Send(self,@GetHits)+1);
         return TRUE;
      }

      return FALSE;
   }

   FullyRecharge()
   {
      % Can only be charged by kills.
      return FALSE;
   }
   

end
%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%

