% Meridian 59, Copyright 1994-2012 Andrew Kirmse and Chris Kirmse.
% All rights reserved.
%
% This software is distributed under a license that is described in
% the LICENSE file that accompanies it.
%
% Meridian is a registered trademark.


%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
RoomKeyCopy is PassiveItem

constants:

   include blakston.khd

resources:

   RoomKeyCopy_name_rsc = "silver room key"
   RoomKeyCopy_icon_rsc = roomkeyc.bgf
   RoomKeyCopy_desc_rsc = "This crudely-fashioned silver key copy is used to gain "
      "access to a private room at an inn."
   RoomKeyCopy_append_renter_rsc = "  It bears an inscription which reads: \""
   RoomKeyCopy_append_renter2_rsc = "'s room at "
   RoomKeyCopy_append_renter3_rsc = ".\"  "
   RoomKeyCopy_appendspecial_rsc = "On the back is an additional inscription: \""
      "If found, please return this key to them or the innkeeper.\""
   RoomKeyCopy_append_days_remaining_rsc = "Rent for this room has been paid for the next "
   RoomKeyCopy_append_days_remaining2_rsc = " days."

   RoomKeyCopy_vanishes = \
      "Your silver room key dissolves into a strange silver liquid, then "
      "evaporates into a blue mist and is gone."

   RoomKeyCopy_teleport = \
      "As you slide the key into the lock, a strange white glow envelops "
      "you, and you find yourself somewhere else."

   RoomKeyCopy_no_use = "You cannot find a keyhole that would fit this key."

   RoomKeyCopy_no_drop_room = "You cannot drop your key here."

   RoomKeyCopy_no_token = "You can't let go of your token to fit your key into the lock."

classvars:

   vrName = RoomKeyCopy_name_rsc
   vrIcon = RoomKeyCopy_icon_rsc
   vrDesc = RoomKeyCopy_desc_rsc
   vrVanish = RoomKeyCopy_vanishes

   viItem_type = ITEMTYPE_SPECIAL

   viBulk = 10
   viWeight = 10
   viValue_average = 80

   viUse_type = ITEM_SINGLE_USE

properties:

   piRID = $

messages:

   Constructor(iRID=$)
   {
      if iRID = $
      {
         post(self,@Delete);

         return;
      }

      piRID = iRID;

      propagate;
   }

   CanBeGivenToNPC()
   {
      return FALSE;
   }

   GetRID()
   {
      return piRID;
   }

   GetRenter()
   "Attempts to look up the room's renter, if one exists."
   {
      local oRoom, oRenter;

      oRenter = $;
      oRoom = Send(SYS, @FindRoomByNum, #num=piRID);
      if oRoom <> $
      {
         oRenter = Send(oRoom, @GetRenter);      
      }

      return oRenter;
   }

   OriginalDeleted()
   {
      if (poOwner <> $) and isClass(poOwner,&User)
      {
         if send(poOwner,@IsLoggedOn)
         {
            send(poOwner,@MsgSendUser,#message_rsc=vrVanish);
         }
         else
         {
            if Send(poOwner,@GetSaveRoom) = piRID
            {
               Send(poOwner,@AdminGoToSafety);
            }
         }
      }
      
      send(self,@Delete);
      
      return;
   }

   AppendDesc()
   {
      local oRoom, oRenter, rLocation, iDaysLeft;

      oRoom = Send(SYS,@FindRoomByNum,#num=piRID);
      if (oRoom = $)
      {
         Send(self,@Delete);
         return;
      }

      oRenter = Send(oRoom, @GetRenter);
      rLocation = Send(oRoom, @GetLocationName);
      iDaysLeft = Send(oRoom, @GetDaysLeft);

      if (oRenter <> $)
      {
         AppendTempString(RoomKeyCopy_append_renter_rsc);
         AppendTempString(Send(oRenter, @GetName));
         AppendTempString(RoomKeyCopy_append_renter2_rsc);
         AppendTempString(rLocation);
         AppendTempString(RoomKeyCopy_append_renter3_rsc);
         Send(self, @AppendSpecial);
      }

      AppendTempString("\n\n");
      AppendTempString(RoomKeyCopy_append_days_remaining_rsc);
      AppendTempString(iDaysLeft);
      AppendTempString(RoomKeyCopy_append_days_remaining2_rsc);

      return;
   }

   AppendSpecial()
   "Appends text to the description relevant to specific key classes."
   {
      AppendTempString(RoomKeyCopy_appendspecial_rsc);
      return;
   }
   
   SendAnimation()
   {
      if (piItem_flags & ITEM_PALETTE_MASK) <> 0
      {
         AddPacket(1, ANIMATE_TRANSLATION, 1, piItem_flags & ITEM_PALETTE_MASK);
      }
      AddPacket(1,ANIMATE_NONE,2,2);

      return;
   }

   SendLookAnimation()
   {
      if (piItem_flags & ITEM_PALETTE_MASK) <> 0
      {
         AddPacket(1, ANIMATE_TRANSLATION, 1, piItem_flags & ITEM_PALETTE_MASK);
      }
      AddPacket(1,ANIMATE_NONE,2,2);

      return;
   }

   SendInventoryAnimation()
   {
      if (piItem_flags & ITEM_PALETTE_MASK) <> 0
      {
         AddPacket(1, ANIMATE_TRANSLATION, 1, piItem_flags & ITEM_PALETTE_MASK);
      }
      AddPacket(1,ANIMATE_NONE,2,1);

      return;
   }

   ReqNewApply()
   {
      return TRUE;
   }

   NewApplied(what = $,apply_on = $)
   {
      local oRoom, oInn;
      
      oRoom = Send(SYS,@FindRoomByNum,#num=piRID);
      oInn = Send(SYS,@FindRoomByNum,#num=send(oRoom,@GetLocation));

      if send(what,@GetOwner) = oInn
         AND send(oInn,@EnterRentableRoom,#who=what)
      {
         % Can't bring a token in; no one can follow you
         if Send(what, @PossessesA, #class = &Token) 
         {
            Send(what, @MsgSendUser, #message_rsc = RoomKeyCopy_no_token);
            return;
         }

         send(what,@MsgSendUser,#message_rsc=RoomKeyCopy_teleport);
         send(what,@EffectSendUserDuration,#effect=EFFECT_WHITEOUT,#duration=750);
         send(send(SYS,@FindRoomByNum,#num=piRID),@Enter,#who=what);
      }
      else
      {
         send(what,@MsgSendUser,#message_rsc=RoomKeyCopy_no_use);
      }

      return;
   }

   NoDropMessage()
   {
      if (poOwner <> $) AND IsClass(poOwner,&Player)
      {
         Send(poOwner,@MsgSendUser,#message_rsc=RoomKeyCopy_no_drop_room);
      }

      return;
   }

   ReqNewOwner(what=$)
   {
      % No dropping in rentable rooms to circumvent the room expiration code
      if what <> $ AND isClass(what,&RentableRoom)
      {
         send(self,@NoDropMessage);
           
         return FALSE;
      }

      propagate;
   }

   IsKeyCopy()
   "Asks a key whether it's a copy or the original."
   {
      return TRUE;
   }

   CanSwap()
   {
      return FALSE;
   }

   CanShatter()
   {
      return FALSE;
   }

   ReqDMDelete()
   "Keys are not deleted with the DM clear inventory command."
   {
      return FALSE;
   }
   
   CanBeStoredInVault()   
   {
      return FALSE;
   }

   DropOnDeath()
   {
      return FALSE;
   }


end
%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
