% Meridian 59, Copyright 1994-2012 Andrew Kirmse and Chris Kirmse.
% All rights reserved.
%
% This software is distributed under a license that is described in
% the LICENSE file that accompanies it.
%
% Meridian is a registered trademark.


%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
Shield is PassiveItem

constants:

   include blakston.khd

   USED = 1
   UNUSED = 2
   DEFENDING = 3
   
   SHIELD_DAMAGE_CHANCE = 25

resources:

   shield_broken = "Your %s falls to pieces."   

   shield_condition_exc = " shines with smooth perfection."
   shield_condition_exc_mended = " is in great condition, but has been patched before."
   shield_condition_good = " is marred by a deep gouge but otherwise is in fine condition."
   shield_condition_med = " is deeply scarred from battle but still functional."
   shield_condition_poor = " is cracked in several places and may not last much longer."
   shield_condition_broken = " has been shattered by a forceful blow, leaving it useless."

classvars:

   viItem_type = ITEMTYPE_ARMOR
   vrItem_broken = shield_broken

   viUse_type = ITEM_USE_HAND
   viUse_amount = 1

   vrposs_article = object_article_cap_this_rsc

   vrShield_window_overlay = $
   vrShield_overlay = $

   viBroken_group = 4
   vbShow_condition = TRUE
   vrCondition_exc = shield_condition_exc 
   vrCondition_exc_mended = shield_condition_exc_mended 
   vrCondition_good = shield_condition_good 
   vrCondition_med = shield_condition_med 
   vrCondition_poor = shield_condition_poor 
   vrCondition_broken = shield_condition_broken
   
   viWeaponType = WEAPON_TYPE_STRIKE
   viAttack_Type = ATCK_WEAP_STRIKE
	viProficiency_needed = SKID_BASH
   viAttack_spell = 0
   viBlock_chance = 0
   viRangedBlock_chance = 0
   viBlock_damage = 0
   viSpell_modifier = 0
   
properties:
   
   piUsed = UNUSED

messages:

   GetProf(who=$)
   {
      return send(who,@GetSkillAbility,#skill_num=viProficiency_needed);
   }

   GetProfNumber()
   {
      return viProficiency_needed;
   }

   GetStroke(who=$)
   {
      return send(who,@GetSkillAbility,#skill_num=Send(self,@GetDefaultStrokeNumber));
   }

   GetAttackName()
   {
      return vrName;
   }

   GetDefaultStrokeNumber()
   {
      return SKID_BASH;
   }
	
   GetDamage(target=$)
   {
      return viBlock_damage*100;
   }
   
   DamageGear(who = $,what = $)
   {
      % 25% chance to get damaged.
      if (random(1,100) < SHIELD_DAMAGE_CHANCE)
      {
         propagate;
      }
      
      return;
   }

   % General Combat messages:
   
   DoBlockAttack(who=$,what=$)
   {
      local iDamage,oStroke;
		
		oStroke = Send(SYS,@FindSkillByNum,#num=SKID_BASH);
	  
	   iDamage = Send(oStroke,@FindDamage,#weapon_used=self,#who=who,#victim=what);

		% Handle damage modifications that are usually handled in TryAttack here, since we bypass chance to hit.
		iDamage = bound(iDamage,0,$);
		
      % After figuring damage, call AssessDamage and AssessHit. These will apply damage and handle various other stuff.
      iDamage = Send(what,@AssessDamage,#what=who,#damage=iDamage,
                     #atype=Send(self,@GetAttackType),#aspell=Send(self,@GetAttackSpell),
                     #absolute=FALSE);

      Send(who,@AssessHit,#what=what,#damage=iDamage,#use_weapon=self,#stroke_obj=oStroke);
		
		% Bashing doesn't interfere with other weapon skills, and doesn't user the weaponswingtimer,
		% so we have to call ImproveStroke here instead.
      Send(oStroke,@ImproveStroke,#who=who,#target=what,#use_weapon=self);

      if iDamage = $
      {
         Send(who,@KilledSomething,#what=what,#use_weapon=self,#stroke_obj=Send(SYS,@FindSkillByNum,#num=Send(self,@GetDefaultStrokeNumber)));
      } 

      Send(send(who,@GetOwner),@SomethingAttacked,#what=who,#victim=what,#use_weapon=self);
	  
	  return;
   }

   % Called here, different shields can give different bonuses.
   GetBlockAbility(who = $)
   {
      return send(who,@GetSkillAbility,#skill_num=SKID_BLOCK) + 1 + viBlock_chance + send(who,@GetMight);
   }
	
   GetRangedBlockAbility(who = $)
   {
      return send(who,@GetSkillAbility,#skill_num=SKID_BLOCK) + 1 + viRangedBlock_chance + send(who,@GetMight);
   }

   DoBlockAnimation()
   {
	  piUsed = DEFENDING;
	  
      if vrShield_window_overlay <> $
      {
         Send(poOwner,@ChangeWindowOverlay,#what=self);
      }
         
      if vrShield_overlay <> $
      {
         piUsed = USED;
      }
	  return;
   }

   % General unsorted messages:

   ReqUseSomething(what = $)
   {
      if isClass(what,&Shield)
      {
         if not send(poOwner,@TryUnuseItem,#what=self)
         {
            return FALSE;
         }
      }
      
      propagate;
   }

   NewUsed()
   {
      piUsed = USED;

      % do first person overlay if we have one
      if vrShield_window_overlay <> $
      {
         Send(poOwner,@SetWindowOverlay,#what=self);
      }

      % do third person overlay if we have one
      if vrShield_overlay <> $
      {
         Send(poOwner,@SetOverlay,#what=self);
      }

      propagate;
   }

   NewUnused()
   {
      piUsed = UNUSED;
      if vrShield_window_overlay <> $
      {
         Send(poOwner,@RemoveWindowOverlay,#what=self);
      }

      Send(poOwner,@RemoveOverlay,#what=self);

      propagate;
   }

   ReqRepair()
   {
      return TRUE;
   }

   % These 5 called by player, for window overlays.
   % Shield subclasses--do NOT set a window overlay unless you either
   %  set vrShield_window_overlay correctly or override all these message handlers.

   GetWindowOverlay()
   {
      return vrShield_window_overlay;
   }

   GetWindowOverlayID()
   {
      return PWO_LEFT_HAND;
   }

   GetWindowOverlayHotspot()
   {
      % turns off the window overlay
      if piUsed = UNUSED
      {
         return 0;
      } 

      return HS_SW;
   }

   SendWindowOverlayAnimation()
   {
      if piUsed = DEFENDING
      { 
         AddPacket(1,ANIMATE_ONCE,4,1000,2,2,2,2,2,1);
         return;
      }
      
      AddPacket(1,ANIMATE_NONE,2,1);
      
      return;      
   }

   SendWindowOverlayOverlays()
   {
      AddPacket(1,0); % no first person overlays on top of shield 

      return; 
   }

   % These 3 called by player, for normal 3rd-person overlays on user.
   % Shield subclasses--do NOT set an overlay unless you either
   %  set vrShield_overlay correctly or override all these message handlers.

   GetOverlay()
   {
      return vrShield_overlay;
   }
   
   GetOverlayHotspot()
   {
      return HS_LEFT_WEAPON;
   }

   SendOverlayAnimation()
   {
      if (piItem_flags & ITEM_PALETTE_MASK) <> 0
      {
         AddPacket(1,ANIMATE_TRANSLATION,1,piItem_flags & ITEM_PALETTE_MASK);
      }
      
      AddPacket(1,ANIMATE_NONE,2,2); % group 2 is the player-overlay group
      
      return;
   }
   
   %%% Attack Type Functions for Block Damage
   

   GetAttackSpell()
   {
      return viAttack_spell;
   }

   GetAttackType()
   {
      return viAttack_type;
   }

end
%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
