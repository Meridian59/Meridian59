% Meridian 59, Copyright 1994-2012 Andrew Kirmse and Chris Kirmse.
% All rights reserved.
%
% This software is distributed under a license that is described in
% the LICENSE file that accompanies it.
%
% Meridian is a registered trademark.


%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
Wand is SpellItem

constants:

   include blakston.khd

resources:

   Wand_name_rsc = "wand"
   Wand_label_name_rsc = "generic wand"
   Wand_icon_rsc = wand6.bgf
   Wand_desc_rsc = "Magical energy crackles about this slender wand."
   Wand_broken = "%s%s shatters into pieces!"
   Wand_useless = "The wand has spent its magical energy and is now charred and useless."
   Wand_desc_broken_rsc = "The once pristine wand is now a blackened mess."

   Wand_target_gone = "You carefully aim your wand at %s%s, but you suddenly realize your target is gone."
   Wand_fails = "You point your wand but nothing happens."

classvars:

   vrLabelName = Wand_label_name_rsc
   vrIcon = Wand_icon_rsc
   vrBroken =    Wand_broken
   vrFails = Wand_fails
   viSpellEffect = SID_FIREBALL
   viColor = XLAT_TO_RED
   viItem_Type = ITEMTYPE_WAND

   viGround_group = 2
   viInventory_group = 1

   vbNoBrokenView = TRUE

   viHits_init_min = 4
   viHits_init_max = 9

properties:

   vrName = Wand_name_rsc
   vrDesc = Wand_desc_rsc

   viObject_flags = APPLY_YES | GETTABLE_YES

   % Set to negative so it doesn't expire naturally.
   piGoBadTime = -1

messages:

   CastSpell(what = $, apply_on = $)
   {
      % If our owner and the target share the same owner (a room), then there needs to
      %  be line of sight.  Also, make sure the target is within sight and range.
      if (NOT ((send(poOwner,@GetOwner) <> send(apply_on,@GetOwner))
               OR send(send(poOwner,@GetOwner),@LineOfSight,#obj1=poOwner,#obj2=apply_on)))
         OR (IsClass(poOwner,&Player)
             AND NOT send(poOwner,@TargetWithinSightAndRange,#oTarget=apply_on))
      {
         send(self,@DoFailure,#what=what,#lTargets=[apply_on]);

         return;
      }

      propagate;
   }

   DoFailure(what = $,lTargets=$)
   {
      Send(what,@MsgSendUser,#message_rsc=vrFails,#parm1=send(first(ltargets),@GetinDef),
           #parm2=send(first(ltargets),@GetName));

      return;
   }

   AppendDesc()
   {
      if piSpellPower <> $
         AND piHits <> 0
      {
         AppendTempString("\n\nThis wand coruscates with the energies of ");
         AppendTempString(piHits);
         
         if piHits > 1
         {
            AppendTempString(" charges");
         }
         else
         {
            AppendTempString(" charge");
         }

         AppendTempString(". The magical force within averages ");
         AppendTempString(piSpellPower);
         AppendTempString(" power.");
      }

      propagate;
   }


end
%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
