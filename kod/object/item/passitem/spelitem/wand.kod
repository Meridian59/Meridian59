% Meridian 59, Copyright 1994-2012 Andrew Kirmse and Chris Kirmse.
% All rights reserved.
%
% This software is distributed under a license that is described in
% the LICENSE file that accompanies it.
%
% Meridian is a registered trademark.


%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
Wand is SpellItem

constants:

   include blakston.khd

resources:

   include wand.lkod

   Wand_name_rsc = "wand"
   Wand_label_name_rsc = "generic wand"
   Wand_icon_rsc = wand6.bgf
   Wand_desc_rsc = "Magical energy crackles about this slender wand."
   Wand_broken = "%s%s shatters into pieces!"
   Wand_useless = \
      "The wand has spent its magical energy and is now charred and useless."
   Wand_desc_broken_rsc = "The once pristine wand is now a blackened mess."
   wand_append_hits = "\n\nThis wand coruscates with the energies of %i %s."
   wand_hits_plural = "charges"
   wand_hits_singular = "charge"
   wand_append_spellpower = "  The magical force within averages %i spellpower."
   Wand_target_gone = \
      "You carefully aim your wand at %s%s, but you suddenly "
      "realize your target is gone."
   Wand_fails = "You point your wand but nothing happens."

classvars:

   vrLabelName = Wand_label_name_rsc
   vrIcon = Wand_icon_rsc
   vrBroken =    Wand_broken
   vrDescHits = wand_append_hits
   vrDescHitsSingular = wand_hits_singular
   vrDescHitsPlural = wand_hits_plural
   vrDescSpellpower = wand_append_spellpower
   vrFails = Wand_fails
   viSpellEffect = SID_FIREBALL
   viColor = XLAT_TO_RED
   viItem_Type = ITEMTYPE_WAND
   viGround_group = 2
   viInventory_group = 1
   vbNoBrokenView = TRUE
   viHits_init_min = 4
   viHits_init_max = 9

properties:

   vrName = Wand_name_rsc
   vrDesc = Wand_desc_rsc
   viObject_flags = OF_APPLYABLE | OF_GETTABLE

   % Set to negative so it doesn't expire naturally.
   piGoBadTime = -1

messages:

   CastSpell(what=$,apply_on=$)
   {
      % If our owner and the target share the same owner (a room),
      % then there needs to be line of sight.  Also, make sure the 
      % target is within sight and range.
      if (NOT ((Send(poOwner,@GetOwner) <> Send(apply_on,@GetOwner))
               OR Send(Send(poOwner,@GetOwner),@LineOfSight,#obj1=poOwner,#obj2=apply_on)))
         OR (IsClass(poOwner,&Player)
             AND NOT Send(poOwner,@TargetWithinSightAndRange,#oTarget=apply_on))
      {
         Send(self,@DoFailure,#what=what,#lTargets=[apply_on]);

         return;
      }

      propagate;
   }

   DoFailure(what=$,lTargets=$)
   {
      Send(what,@MsgSendUser,#message_rsc=vrFails,
            #parm1=Send(First(lTargets),@GetinDef),
            #parm2=Send(First(lTargets),@GetName));

      return;
   }

end
%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
