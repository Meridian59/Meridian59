% Meridian 59, Copyright 1994-2012 Andrew Kirmse and Chris Kirmse.
% All rights reserved.
%
% This software is distributed under a license that is described in
% the LICENSE file that accompanies it.
%
% Meridian is a registered trademark.


%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
GoldSword is Weapon

constants:

   include blakston.khd

resources:

   goldsword_name_rsc = "gold sword"
   goldsword_icon_rsc = gsword.bgf
   goldsword_desc_rsc = \
      "This sword is delicate and ornamental.  It seems like it might be "
      "good for a fanciful duel, but it is certainly not suitable for the "
      "rigors of blood combat."

   goldsword_window_overlay_rsc = povgswrd.bgf
   goldsword_player_overlay = gswordov.bgf

classvars:
   vrName = goldsword_name_rsc
   vrIcon = goldsword_icon_rsc
   vrDesc = goldsword_desc_rsc

   viValue_average = 600
   viWeight = 90
   viBulk = 60

   % Gold swords are average quality thrusting weapons
   viWeaponType = WEAPON_TYPE_THRUST
   viWeaponQuality = WEAPON_QUALITY_NORMAL

   viProficiency_Needed = SKID_PROFICIENCY_SWORD

   vrWeapon_window_overlay = goldsword_window_overlay_rsc
   vrWeapon_overlay = goldsword_player_overlay

   viGround_group = 1
   viInventory_group = 3
   viBroken_group = 2

   viMin_damage = 2
   viMax_damage = 5

   % Adds 150 defense
   % No bonus outside of arena, or if player does not have parry.
   % Gold sword is fantastic in the arena, but useless elsewhere.
   viArenaParryBonus = 75

properties:

   piAttack_type = ATCK_WEAP_NONMAGIC+ATCK_WEAP_THRUST

messages:

   GetBaseDamage()
   {
      local oOwner, iDamage;

      iDamage = random(viMin_damage,viMax_damage);

      oOwner = send(send(self,@GetOwner),@getowner);
      if send(oOwner,@IsArena)
      {
         iDamage = iDamage + 7;
      }

      return iDamage;
   }

   GetParryAbility(who=$)
   {
      local oOwner, iParry;

      iParry = send(who,@GetSkillAbility,#skill_num=SKID_PARRY);

      if iParry = 0
      {
         return 0;
      }
      
      oOwner = send(send(self,@GetOwner),@GetOwner);
      if send(oOwner,@IsArena)
      {
         iParry = iParry + viArenaParryBonus;
      }
      
      return iParry + piParryBonus;
   }

end
%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
