% Meridian 59, Copyright 1994-2012 Andrew Kirmse and Chris Kirmse.
% All rights reserved.
%
% This software is distributed under a license that is described in
% the LICENSE file that accompanies it.
%
% Meridian is a registered trademark.


%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
NeruditeSword is Weapon

constants:

   include blakston.khd

resources:

   include neruswd.lkod

   NeruditeSword_name_rsc = "nerudite sword"
   NeruditeSword_icon_rsc = nerswd.bgf
   NeruditeSword_desc_rsc = \
      "Swift and strong, the nerudite weapon feels good in your hand."

   NeruditeSword_window_overlay_rsc = povnersw.bgf
   NeruditeSword_player_overlay = nerswdov.bgf

   nerusword_shatters = "The nerudite sword shatters into a flurry of splinters!"

classvars:

   vrName = NeruditeSword_name_rsc
   vrIcon = NeruditeSword_icon_rsc
   vrDesc = NeruditeSword_desc_rsc

   vrWeaponBroke = nerusword_shatters

   % Nerudite swords are nerudite quality thrusting weapons
   viWeaponType = WEAPON_TYPE_THRUST
   viWeaponQuality = WEAPON_NERUDITE

   viProficiency_needed = SKID_PROFICIENCY_SWORD

   viValue_average = 1000
   viWeight = 80
   viBulk = 70

   vrWeapon_window_overlay = NeruditeSword_window_overlay_rsc
   vrWeapon_overlay = NeruditeSword_player_overlay

   viGround_group = 1
   viInventory_group = 3
   viBroken_group = 2

   viRange = 3 * FINENESS

properties:

   piAttack_type = ATCK_WEAP_THRUST + ATCK_WEAP_NERUDITE
   piAttack_Spell = 0

messages:

   DoShatter()
   {
      send(self,@WeaponBroke);

      propagate;
   }

   ShatterToShards()
   {
      if poOwner <> $ AND isClass(poOwner,&User)
      {
         if send(poOwner,@AssessDamage,#what=self,#damage=random(10,25),
            #report=FALSE,#atype=ATCK_WEAP_NERUDITE) = $
         {
            Send(Send(poOwner,@GetOwner),@SomethingKilled,#what=self,#victim=poOwner);
            Send(poOwner,@Killed,#what=self);
         }
      }

      return;
   }

   WeaponBroke()
   {
      send(self,@ShatterToShards);

      propagate;
   }


end
%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
