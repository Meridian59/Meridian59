% Meridian 59, Copyright 1994-2012 Andrew Kirmse and Chris Kirmse.
% All rights reserved.
%
% This software is distributed under a license that is described in
% the LICENSE file that accompanies it.
%
% Meridian is a registered trademark.


%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
% A ranged weapon is a weapon which you equip in your hand, type Ctrl to use, 
% then a target icon comes up a la apply() with which you choose your victim.
% Well, at least ideally.  At this point there is no good way to code that,
% so ranged weapons are like normal weapons with a range of 13 (the max range
% for weapons).  
% Ranged weapons do not break as often as melee weapons (not as much wear and
% tear).
%
% code by Justin Houk, 98jsh@williams.edu

RangedWeapon is Weapon

constants:

   include blakston.khd

resources:

   ranged_desc_rsc = "Nothing but the wielder's precision will determine the effectiveness of this weapon."
   ranged_wrong_ammo_rsc = "You have not equipped the proper missile to attack."

   % This is the name for the type of ammo used.  Used in combat messages.
   ranged_ammo_name = "arrow"

classvars:

   viHits_init_min = 400
   viHits_init_max = 500
   
   viProficiency_Needed = SKID_PROFICIENCY_BOW
   viAmmo_type = AMMO_ARROW
	
   viRange = 100
   viWeaponDamage = 0
   viHit_roll_modifier = 0
	viRangedAccuracy = 15
   viStrikethrough = 0
   viProf_bonus = 0
   viSpell_modifier = -40
	
   viBulk = 70
   viWeight = 70
   
   viUse_Amount = 1
   viUse_type = ITEM_USE_MAIN_HAND | ITEM_USE_OFF_HAND

   vrExtendedDescription = ranged_desc_rsc

properties:
   
   piAttack_Type = ATCK_WEAP_PIERCE

messages:

   % General combat messages:

   % Overrides message from superclass.
   % Ranged weapons provide NO parry bonus.
   GetParryAbility(who=$)
   {
      return 0;
   }

   GetStrikethrough()
   {
      local iStrikethrough;
	  
	  iStrikethrough = send(send(self,@GetOwner),@GetAbility,#num=viProficiency_needed) * (100 + viStrikethrough) / 100;
	  	  
	  return iStrikethrough;
   }

   ModifyHitRoll(who=$,target=$)
	{
		local iDistance, iMalus;
		
		iDistance = send(who,@SquaredDistanceTo,#what=target);
		
		if iDistance > 0
		{
			iMalus = bound(-100 + 100 * viRangedAccuracy * viRangedAccuracy / send(who,@SquaredDistanceTo,#what=target),-100,0);
		}
		else
		{
			iMalus = 0;
		}
		
		return iMalus;
	}
   
   % General unsorted messages:

   GetStrokeNumber()
   {
      return SKID_FIRE;
   }

   GetAmmoType()
   {
      return viAmmo_type;
   }


   GetAttackType()
   {
      local iAttackType, ammotype, i;

      if poOwner = $
      {
         return 0;
      }

      iAttackType = 0;
      ammotype = send(self,@GetAmmoType);
      for i in send(poOwner,@GetPlayerUsing)
      {
         if send(i,@GetItemUseType) = ITEM_USE_QUIVER
         {
            if send(i,@GetAmmoType) = ammotype
            { 
               iAttackType = send(i,@GetAttackType);
               
               break;
            }
         }
      }
      
      iAttackType = iAttackType |  piAttack_Type;

      return iAttackType;
   }

   GetAttackSpell()
   {
      local iAttackType, ammotype, i;

      if poOwner = $
      {
         return 0;
      }

      iAttackType = 0;
      ammotype = send(self,@GetAmmoType);

      for i in send(poOwner,@GetPlayerUsing)
      {
         if send(i,@GetItemUseType) = ITEM_USE_QUIVER
         {
            if (send(i,@GetAmmoType) = ammotype)
            { 
               iAttackType = send(i,@GetAttackSpell);
               
               break;
            }
         }
      }
      
      iAttackType = iAttackType | piAttack_Spell;

      return iAttackType;
   }

   GetBaseDamage(who=$,target=$)
   {
      local iDamage, ammotype, i;

      if poOwner = $
      {
         return 0;
      }

      iDamage = 0;

      ammotype = send(self,@GetAmmoType);
      for i in send(poOwner,@GetPlayerUsing)
      {
         if send(i,@GetItemUseType) = ITEM_USE_QUIVER
         {
            if (send(i,@GetAmmoType) = ammotype)
            {
               iDamage = send(i,@GetDamage,#who=who,#target=target);
               break;
            }
         }
      }
	  
	  iDamage = iDamage + viWeaponDamage;
	  
      return iDamage;
   }

   GetRange()
   {
      return viRange;
   }   

   % This returns the weapon's attack name.  For ranged weapons, return the ammo name.
   GetAttackName()
   {
      return ranged_ammo_name;      
   }
   
end
%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%







