% Meridian 59, Copyright 1994-2012 Andrew Kirmse and Chris Kirmse.
% All rights reserved.
%
% This software is distributed under a license that is described in
% the LICENSE file that accompanies it.
%
% Meridian is a registered trademark.


%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
VileAxe is Weapon

constants:

   include blakston.khd

resources:

   vileaxe_name_rsc = "vile axe"
   vileaxe_icon_rsc = axe.bgf
   vileaxe_desc_rsc = \
      "Inherently having unholy blessings, this axe is even more powerful "
      "than the standard variety."

   vileaxe_expires = "Your vile axe disappears in a puff of foul-smelling smoke!"

   vileaxe_window_overlay_rsc = povaxe.bgf
   vileaxe_player_overlay = vaxeov.bgf

classvars:
   vrName = vileaxe_name_rsc
   vrIcon = vileaxe_icon_rsc
   vrDesc = vileaxe_desc_rsc

   % Vile Axes are high quality slashing weapons.
   viWeaponType = WEAPON_TYPE_SLASH
   viWeaponQuality = WEAPON_QUALITY_HIGH

   viProficiency_Needed = SKID_PROFICIENCY_AXE

   viValue_average = 0
   % Light because it's magical.
   viWeight = 17
   viBulk = 72

   viHits_init_min = 350
   viHits_init_max = 400

   vrWeapon_window_overlay = vileaxe_window_overlay_rsc
   vrWeapon_overlay = vileaxe_player_overlay

   viGround_group = 1
   viInventory_group = 3
   viBroken_group = 2

properties:

   piAttack_type = ATCK_WEAP_MAGIC+ATCK_WEAP_SLASH
   ptLongevity % Used to keep track of time before expiration of the vile axe
   piItem_flags = PT_GRAY_TO_DGREEN

   piHitBonus = 0

messages:

   Constructor(ilongevity = $, iToHitBonus = $)
   {
      if ilongevity = $
      {
         % Default is 1 hour.
         ilongevity = 3600000;
      }

      if iToHitBonus <> $
      {
         piHitBonus = iToHitBonus;
      }

      ptLongevity = CreateTimer(self,@WeaponExpires,ilongevity);
 
      send(self,@SetSpellFlag,#flag=ATCK_SPELL_UNHOLY,#value=TRUE);

      propagate;
   }

   WeaponExpires()
   {
      ptLongevity = $;
      
      if isclass(poOwner,&Player)
      {
         send(poOwner,@MsgSendUser,#message_rsc=vileaxe_expires);
      }
      
      send(self,@Delete);
      
      return;
   }

   Delete()
   {
      if ptLongevity <> $
      {
         DeleteTimer(ptLongevity);
         ptLongevity = $;
      }
      
      propagate;
   }

   WeaponHitTarget()
   {
      % Return here so that vile axes don't get damaged.
      return;
   }

   ModifyHitRoll(hit_roll = 0, target = $)
   {
      local iBaseHit, oWeapAtt, i;

      iBaseHit = hit_roll;

      iBaseHit = iBaseHit + piHitBonus;

      for i in plItem_Attributes
      {
         oWeapAtt = send(SYS,@FindItemAttByNum, #num=send(self,@GetNumFromCompound,#compound=first(i)));
         if isClass(oWeapAtt,&WeaponAttribute)
         {
            iBaseHit = send(oWeapAtt,@ModifyHitRoll,#hitroll=hit_roll,#wielder=poOwner,
	                         #target=target,#lData=i);
         }
      }     

      return iBaseHit;
   }

   CanEnchant()
   {
      return FALSE;
   }
   
   CanBeGivenToNPC()
   {
      return FALSE;
   }

   CanShatter()
   {
      return FALSE;
   }

   CanSwap()
   {
      return FALSE;
   }

   CanWeaken()
   {
      return FALSE;
   }

   GetBaseSpellModifier()
   {
      % It's mystically created and temporary.  No penalties.
      return 0;
   }


end
%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
