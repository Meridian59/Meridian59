% Meridian 59, Copyright 1994-2012 Andrew Kirmse and Chris Kirmse.
% All rights reserved.
%
% This software is distributed under a license that is described in
% the LICENSE file that accompanies it.
%
% Meridian is a registered trademark.


%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
GuildForgeAlliance is GuildCommand

constants:

   include blakston.khd
   include protocol.khd

resources:

   include gcally.lkod

   guildforgealliance_name_rsc = "Forge Alliance"
   guildforgealliance_icon_rsc = light.bgf
   guildforgealliance_desc_text_rsc = "Used to team up with other guilds.  Primary benefits of being in an alliance - sharing guild halls."

   guildforgealliance_to_inductor = "You extend to %s a treaty between your guild and %s%s."
   guildforgealliance_to_inductee = "%s extends to you a treaty between your guild and %s%s!"
   guildforgealliance_already_your_guild = "%s is a member of your guild!"

   ally_not_user = "You'd feel pretty silly marching into battle with %s%q at your side!"     
   ally_cant_ally_self = "You want to forge an alliance with yourself?  Very well, you accept."
   ally_only_one_treaty = "You may only attempt to forge one treaty at a time."

   ally_on_enemy_list = "You may not become allies with %s%q until you cease hostilities with them."
   ally_on_ally_list = "The fierce warriors of %s%q are already your allies!"

   ally_too_low = "%s is not a high ranking guild official, and cannot discuss an alliance with you."

classvars:

   vrName = guildforgealliance_name_rsc
   vrIcon = guildforgealliance_icon_rsc
   vrDesc = guildforgealliance_desc_text_rsc

   viIndefinite = ARTICLE_NONE
   viDefinite = ARTICLE_NONE

   viGuildcommand_num = GCID_FORGE_ALLIANCE
   viShow_enchantment_icon = ENCHANTMENT_SHOW_ALL
   viRank_needed = RANK_LIEUTENANT

properties:

messages:

   GetNumSpellTargets()
   {
      return 1;
   }

   DoCommand(who=$, otarget = $)
   "Create invitation and give it to the target."

   {
      local oGuild, ownguild, rank, i;


      %debug("reaching gcally.kod");
      oGuild= oTarget;
      ownguild = send(who, @getguild);

      if oGuild = ownguild
        {
           send(who,@msgsenduser,#message_rsc = guildforgealliance_already_your_guild);
           return FALSE;
        }
      foreach i in send(ownguild,@getenemylist)
        {
           if i = oGuild
             {
                send(who,@msgsenduser,#message_rsc = ally_on_enemy_list,
                  #parm1=send(ownguild,@getdef),#parm2=send(ownguild,@getname));
                return FALSE;
             }
        }
      foreach i in send(ownGuild,@getallylist)
        {
           if i = oguild
             {
                send(who,@msgsenduser,#message_rsc = ally_on_ally_list,
                  #parm1=send(oguild,@getcapdef),#parm2=send(oguild,@getname));
                return FALSE;
             }
        }

      if send(ownguild,@canally,#otherguild=oGuild,#rep=who)
        {
           send(ownguild,@newally,#otherguild=oGuild);
        }
      propagate;
   }
  
end
%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
