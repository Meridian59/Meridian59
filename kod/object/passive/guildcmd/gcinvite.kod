% Meridian 59, Copyright 1994-2012 Andrew Kirmse and Chris Kirmse.
% All rights reserved.
%
% This software is distributed under a license that is described in
% the LICENSE file that accompanies it.
%
% Meridian is a registered trademark.


%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
GuildInvite is GuildCommand

constants:

   include blakston.khd
   include protocol.khd

resources:

   include gcinvite.lkod

   guildInvite_name_rsc = "Invite"
   guildInvite_icon_rsc = light.bgf
   guildinvite_desc_text_rsc = "Used to invite prospective members into the guild.  Invited person has 1 minute to accept the invitation."

   guildinvite_already_your_guild = "This person already belongs to your guild."
   invite_cant_invite_self = "You cannot invite yourself to join your guild!"

   invite_non_user = "%s%s respectfully declines your tempting offer."

   guildinvitation_to_inductor = "~BYou extend to %s%q the opportunity to join %s%q."
   guildinvitation_to_inductee = "~B%s%s extends to you an invitation to join %s%q!"

   guildinvitation_guild_full = "%s%q's ranks are full, and may not take new members."
   guildinvitation_cannot_rejoin = "~BThis player may not rejoin your guild's ranks at this time."
   guildinvitation_necro_no_amulet = "~BOnly a necromancer may join the Order!  This player is only fit for "
      "consumption."

classvars:

   vrName = guildinvite_name_rsc
   vrIcon = guildinvite_icon_rsc
   vrDesc = guildinvite_desc_text_rsc

   viIndefinite = ARTICLE_NONE
   viDefinite = ARTICLE_NONE

   viGuildcommand_num = GCID_INVITE
   viShow_enchantment_icon = ENCHANTMENT_SHOW_ALL
   viRank_needed = RANK_LORD

properties:

messages:

   GetNumSpellTargets()
   {
      return 1;
   }

   DoCommand(who=$, otarget = $)
   "Create invitation and give it to the target."

   {
      local oGuild, newguild;

      if not isClass(oTarget,&player)
        {
           send(who,@msgsenduser,#message_rsc=invite_non_user,
             #parm1=send(oTarget,@getcapdef),#parm2=send(oTarget,@getname));
           return FALSE;
        }

      if oTarget=who
        {
           DEBUG("player tried to invite self into guild!  Shouldn't have been an option!");
           send(who,@msgsenduser,#message_rsc=invite_cant_invite_self);
           return FALSE;
        }

      oGuild= send(oTarget,@getguild);
      newGuild = send(who, @getguild);

      if not send(newguild,@checkinvitationlist,#inductor = who,#inductee=oTarget)
        {
           return FALSE;
        }

      if oGuild = newguild
        {
           send(who,@msgsenduser,#message_rsc = guildinvite_already_your_guild);
           return FALSE;
        }

      if length(send(newGuild,@getmemberlist)) >= send(newGuild,@getmaxmembers)
        {
           send(who,@msgsenduser,#message_rsc = guildinvitation_guild_full,
             #parm1=send(newGuild,@getcapdef),#parm2=send(newGuild,@getname));
           return FALSE;
        }
      if isClass(newGuild,&NecromancerGuild) and not send(otarget,@IsUsingA,#class=&NecromancerAmulet)
        {
           send(who,@msgsenduser,#message_rsc = guildinvitation_necro_no_amulet);
           return FALSE;
        }
      if send(newGuild,@checkformermemberlist,#who=oTarget)
        {
           send(who,@msgsenduser,#message_rsc = guildinvitation_cannot_rejoin);
           return FALSE;
        }
      send(who,@msgsenduser,#message_rsc=guildinvitation_to_inductor,
	   #parm1=send(otarget,@getdef),
	   #parm2=send(otarget,@getname), #type2=STRING_RESOURCE,
	   #parm3=send(newguild,@getdef),#parm4=send(newguild,@getname));
      send(oTarget,@msgsenduser,#message_rsc=guildinvitation_to_inductee,
         #parm1=send(who,@getcapindef),#parm2=send(who,@getname), 
		 #parm3=send(newguild,@getdef),#parm4=send(newguild,@getname));

    %% put invitation object in inductees inventory

      send(oTarget,@newhold,#what=create(&guildinvitation,#inductor=who,
                        #inductee=oTarget));
      propagate;
   }
  
end
%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
