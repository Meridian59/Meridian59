% Meridian 59, Copyright 1994-2012 Andrew Kirmse and Chris Kirmse.
% All rights reserved.
%
% This software is distributed under a license that is described in
% the LICENSE file that accompanies it.
%
% Meridian is a registered trademark.


%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
ItemAttCorpsePointer is ItemAttribute

%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
%
%  The job of corpsepointer is simply to point at the monster that 
%    formerly 'carried' the loot, allowing whoever killed the monster
%    to get first crack at the loot.  
% 
%  Form is: 
%
%      [IA_CORPSEPOINTER, $, oCorpse]
%   
%       oCorpse :: the corpse that the pointer is remembering
%
%  A final word: this pointer will stick around for only 20 seconds
%     after a kill, then it will delete itself, removing it from the
%     item's plItem_attribute's list.
%
%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%

constants:

   include blakston.khd   

resources:
 
   corpsepointer_no_loot = "You reach for %s%s, but you can't get past %s picking over the loot of the kill."
   itematt_corpse_desc = "This item can only be picked up by "
   itematt_corpse_desc_generic = "This item can only be picked up by its killer."
   
classvars:

   viItem_Att_Num = IA_CORPSEPOINTER
   vrDesc = itematt_corpse_desc

properties:
  
messages:
  
%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
%%
%%  Effect Functions
%%
%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
   
   %% A player can only pick up an item from a monster he has 
   %% recently killed himself, or that has sat around for a bit.

   CanGetAffectedItem(lData = $, who = $, oItem = $)
   {
      local oCorpse;
     
      oCorpse = nth(lData,3);
     
      % If the corpse object is pointing at who, then 'who' can pick up this item.
      if NOT send(oCorpse,@CanGetMe,#what=who)
      {
         send(who,@MsgSendUser,#message_rsc=corpsepointer_no_loot,
              #parm1=send(oItem,@GetDef),#parm2=send(oItem,@GetName),
              #parm3=send(oCorpse,@GetCorpseName));

         return FALSE;
      }   

      return TRUE;
   }

   AppendDesc(oItem=$, lData=$)
   "Append a human-readable description for corpse-pointer items."
   {
      local oCorpse, rKillerName;

      % If we don't have proper attribute data, fall back to a generic
      % description so the attribute still shows up as visible.
      if lData = $ OR NOT IsList(lData)
      {
         AppendTempString(itematt_corpse_desc_generic);
         return;
      }

      oCorpse = Nth(lData,3);
      if oCorpse = $
      {
         AppendTempString(itematt_corpse_desc_generic);
         return;
      }

      % GetCorpseName actually returns the killer's name, not the corpse's name.
      % The corpse object stores who killed the monster in prPlayer_name.
      rKillerName = Send(oCorpse,@GetCorpseName);
      if rKillerName = $
      {
         AppendTempString(itematt_corpse_desc_generic);
         return;
      }

      AppendTempString("This item can only be picked up by ");
      AppendTempString(rKillerName);
      AppendTempString(".");

      return;
   }

%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
%%
%%  Infrastructure
%%  (Adding and removing a ItemAtt from an item.)
%%
%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%

   SetPrimaryState(state1=$)
   "State1 contains the corpse that used to own us."
   {
      if state1 = $
      {
         debug("SetPrimaryState called with nil state!");

         return FALSE;
      }

      return state1;
   }

   AddToTreasureTable()
   {
      %% do nothing
      return FALSE;
   }

   SetItemsToAttribute()
   {
      %% plItems_to_attribute should never be used by this itematt
      plItems_to_Attribute = $;

      return;
   }

   InitiallyIdentified()
   {
      return TRUE;
   }   

   CanBeSpoofed()
   {
      return FALSE;
   }

   GetNamePriority()
   {
      return -1;
   }


end
%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
