% Meridian 59, Copyright 1994-2012 Andrew Kirmse and Chris Kirmse.
% All rights reserved.
%
% This software is distributed under a license that is described in
% the LICENSE file that accompanies it.
%
% Meridian is a registered trademark.


%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
WeapAttPunisher is WeaponAttribute

%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
%
%  This weapon has a 2x bonus against murderers, 1.5x bonus against outlaws,
%    and a -1x bonus against all other players.
%  Also, outlaws and murderers may not wield this weapon.  They get wounded
%    if they try.
%
%  Form is: 
%
%       [ WA_PUNISHER ]
%	
%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%

constants:

   include blakston.khd   

resources:

   weapattpunisher_desc = "  The emblem of a fist holding an ankh rests in the pommel of this weapon, promising "
   weapattpunisher_desc2 = " advantage in the fight for justice."
   punisher_fail_use = "As you grab the hilt of %s%s, you scream out in pain and let go!"
   weapattpunisher_name = "%s of the just"

   punisher_dm = "punisher"

classvars:

   vrDesc = weapattpunisher_desc
   vrDesc2 = weapattpunisher_desc2
   viItem_Att_Num = WA_PUNISHER

   viDifficulty = 7
   vrName = weapattpunisher_name
   vrDM_trigger = punisher_dm

   vbAppears_in_Happyland = FALSE

properties:
  
messages:

%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
%%
%%  Initialization Functions
%%
%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%

   InitialPower(oItem=$)
   {
      if oItem = $ { DEBUG("This shouldn't have happened!"); return; }

      return send(self,@InitialWeaponPower,#oItem=oItem);
   }
  
%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
%%
%%  Effect Functions
%%
%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
      
   ModifyDamage(damage = 0, target = $, wielder = $, lData= $)
   "Wielder gets a damage bonus, equal to (proficiency/4) - 2."
   {
      local iNum, iPower;
          
      %% here, iNum = power itself
      iPower = send(self,@TranslatePowerFromCompound,#iCompound=first(lData));
      iPower = send(self,@DecipherDamageFromPower,#iPower=iPower);

      %% no special effects for monsters
      if isClass(target,&monster)
      {
         return Damage;
      }

      % Double standard effect on murderers.
      if send(target,@CheckPlayerFlag,#flag = PFLAG_MURDERER)
      {
         return (damage + ((iPower*3)/2));
      }

      % One and one half effect on outlaws.
      if send(target,@CheckPlayerFlag,#flag = PFLAG_OUTLAW)
      {
         return iPower*10;
      }

      return (-iPower)*10;    %% player is fighting a white named target.
   }

   ModifyHitRoll(hitroll = 0, target = $, wielder = $, lData= $)
   "Wielder gets a hit bonus based on whether or not the target is a murderer or outlaw."
   {
      local iNum, iPower;
          
      %% here, iNum = power itself
      iPower = send(self,@TranslatePowerFromCompound,#iCompound=first(lData));
      iPower = send(self,@DecipherHitRollFromPower,#iPower=iPower);

      %% no special effects for monsters
      if isClass(target,&monster)
      {
         return 0;
      }

      % Double standard effect on murderers.
      if send(target,@CheckPlayerFlag,#flag = PFLAG_MURDERER)
      {
         return (((iPower*3)/20));
      }

      % One and one half effect on outlaws.
      if send(target,@CheckPlayerFlag,#flag = PFLAG_OUTLAW)
      {
         return iPower/10;
      }

      return (-iPower/10);    %% player is fighting a white named target.
   }

   ItemReqUse(oItem=$,oPlayer=$)
   "Murderers and Outlaws may not use.  In fact, they are wounded "
   "if they even try."
   {
      local lUsing, bUsingAmulet, oUsedItem;
      
      lUsing = send(oPlayer,@GetPlayerUsing);
      bUsingAmulet = FALSE;
      
      for oUsedItem in lUsing
      {
         if IsClass(oUsedItem,&NecromancerAmulet)
         {
            bUsingAmulet = TRUE;
            
            break;
         }
      }
      
      if send(oPlayer,@checkplayerflag,#flag = PFLAG_MURDERER)
         OR send(oPlayer,@checkplayerflag,#flag = PFLAG_OUTLAW)
         OR bUsingAmulet
      {
	      send(oPlayer, @MsgSendUser, #message_rsc = punisher_fail_use,
	           #parm1=send(oItem,@getdef),#parm2=send(oItem,@GetName));

         if Send(oPlayer, @AssessDamage, #what=self,#damage=Random(3,7),
                 #report=False) = $
	      {
            % we killed someone off... heh heh heh
            Send(Send(oPlayer,@GetOwner),@SomethingKilled,#what=self,#victim=oPlayer);
            Send(oPlayer,@Killed,#what=oItem);
            Send(oItem,@Delete);
         }

         %% this person is a murderer and may not wield it.
         return FALSE;      
      }
      
      return TRUE;
   }

end
%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
