% Meridian 59, Copyright 1994-2012 Andrew Kirmse and Chris Kirmse.
% All rights reserved.
%
% This software is distributed under a license that is described in
% the LICENSE file that accompanies it.
%
% Meridian is a registered trademark.


%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
NecroWarnEvilQuest is QuestTemplate
% Quest template QST_ID_NECRO_WARNING = 46.
% Quest nodes 146, 147, 148.
% QNT_ID_NECRO_WARN_EVIL_ONE, QNT_ID_NECRO_WARN_EVIL_TWO,
% QNT_ID_NECRO_WARN_EVIL_THREE

% QuestTemplate is
% [ quest template index
%   quest template object
%   number of players,
%   type of quest,
%   eligible player restrictions,
%   quest nodes,
%   max number active,
%   currently active quests,
%   percent chance of scheduling (on timer firing)
%   special restrictions ]

constants:

   include blakston.khd

resources:

   include necrowarnevilqt.lkod

   % Quest description.
   necrowarnevilqt_icon_rsc = necscrol.bgf
   necrowarnevilqt_name_rsc = "necromancer warning quest"
   necrowarnevilqt_desc_rsc = \
      "Maleval has information about a dark power coming to these lands.  "
      "Help him spread the word of the coming darkness."

   necrowarnevilqt_nodetwo_rsc = \
      "Maleval has information about a dark power coming to these lands.  "
      "Help him spread the word of the coming darkness by delivering a letter "
      "to %s%s for him.  If you fail to deliver the letter, Maleval promises "
      "to make you suffer."
   necrowarnevilqt_nodethree_rsc = \
      "%s%s thought nothing of the warning.  Return to Maleval and let him "
      "know you have delivered the letter."

   necrowarnevilqt_completed_rsc = \
      "You have completed the necromancer warning quest."

   % Quest resources.
   necrowarning_assign_2 = \
      "Yes, I have need of a messenger.  It would seem we have a new power in "
      "the land, or rather a very old power, full of darkness.  Perhaps those "
      "fools in the temples will finally get what they deserve for their "
      "arrogance.  How could they possibly think that their gods are the "
      "only gods?  Hopefully this letter will help speed the inevitable "
      "along.  Take it to %NPC and return to me for your... reward.  Fail me "
      "and you will suffer."
   necrowarning_success_2 = \
      "You have been the tool of an unsound person.  I expect this from him, "
      "but there is no excuse for your depravity.  Can you not find a better "
      "sponsor for your enthusiasm?  Well, perhaps not."
   necrowarning_failure_2 = \
      "Subject: My letter\n"
      "So you would trifle with me.  Your insolence is either the act of a "
      "fool or the act of a coward.  Either way you should hope we do not "
      "meet again.  Please accept this curse as a token of my displeasure."
   necrowarning_success_3 = \
      "I suppose you have returned to collect your reward.  In truth, your "
      "reward is the knowledge of the message you delivered, but... I "
      "understand your peoples desire for payment of a more material sort "
      "so here it is."

classvars:

   vrIcon = necrowarnevilqt_icon_rsc
   vrName = necrowarnevilqt_name_rsc
   vrDesc = necrowarnevilqt_desc_rsc

   vrNodeTwo = necrowarnevilqt_nodetwo_rsc
   vrNodeThree = necrowarnevilqt_nodethree_rsc

   vrCompleted = necrowarnevilqt_completed_rsc

   % The QST_ID constant for this quest.
   viQuestID = QST_ID_NECRO_WARNING

   viIcon_Animation_start = 1

properties:

   piNumPlayers = 1
   piPlayerRestrict = \
      Q_PLAYER_NOTTRIED_RECENTLY | Q_PLAYER_NOTNEWBIE | Q_PLAYER_KARMA_EVIL
   plSpecialRestrict = $
   piMaxPlayers = 20
   piSchedulePct = 100

messages:

   Constructor()
   {
      % Quest node list to send to QuestEngine.
      plQuestNodes = [QNT_ID_NECRO_WARN_EVIL_ONE,QNT_ID_NECRO_WARN_EVIL_TWO,
                      QNT_ID_NECRO_WARN_EVIL_THREE];

      % Quest nodes with matching descriptions for the player.
      plQuestNodeDesc = [ [QNT_ID_NECRO_WARN_EVIL_TWO, vrNodeTwo],
                          [QNT_ID_NECRO_WARN_EVIL_THREE, vrNodeThree] ];

      propagate;
   }

   SendQuestNodeTemplates()
   {
      local lNPCs, oNPC, oQE, oLib;

      oQE = Send(SYS,@GetQuestEngine);
      oLib = Send(SYS,@GetLibrary);

      % Template #146 is necro warning quest
      % QNT_ID_NECRO_WARN_EVIL_ONE
      if Send(oQE,@AddQuestNodeTemplate,#questnode_type=QN_TYPE_MESSAGE,
               #NPC_modifier=QN_NPCMOD_NONE,#cargolist=[ letter_trigger ],
               #quest_node_index=QNT_ID_NECRO_WARN_EVIL_ONE)
      {
         lNPCs = $;
         foreach oNPC in Send(oLib,@GetOccupationList,#cNPC_class=&DarkWizard)
         {
            lNPCs = Cons(oNPC, lNPCs);
         }
         Send(oQE,@SetQuestNodeNPCList,#index=QNT_ID_NECRO_WARN_EVIL_ONE,
               #new_NPC_list=lNPCs);
      }
      else
      {
         Debug("Error building QN: QNT_ID_NECRO_WARN_EVIL_ONE");
      }

      % Template #147 is necro warning quest
      % QNT_ID_NECRO_WARN_EVIL_TWO
      if Send(oQE,@AddQuestNodeTemplate,#questnode_type=QN_TYPE_ITEM,
               #NPC_modifier=QN_NPCMOD_NONE,
               #cargolist=[ [ QN_PRIZETYPE_ITEMCLASS, &NecromancerLetter, 1 ] ],
               #penaltylist=[ [QN_PRIZETYPE_BOON, QN_PRIZE_BOON_INTELLECT , -2, 1 ] ],
               #timelimit=2 * 3600,#quest_node_index=QNT_ID_NECRO_WARN_EVIL_TWO)
      {
         lNPCs = $;
         foreach oNPC in Send(oLib,@GetOccupationList,#cNPC_class=&KraananPriestess)
         {
            lNPCs = Cons(oNPC, lNPCs);
         }
         foreach oNPC in Send(oLib,@GetOccupationList,#cNPC_class=&FarenPriestess)
         {
            lNPCs = Cons(oNPC, lNPCs);
         }
         foreach oNPC in Send(oLib,@GetOccupationList,#cNPC_class=&ShalillePriestess)
         {
            lNPCs = Cons(oNPC, lNPCs);
         }
         foreach oNPC in Send(oLib,@GetOccupationList,#cNPC_class=&QorPriestess)
         {
            lNPCs = Cons(oNPC, lNPCs);
         }
         Send(oQE,@SetQuestNodeNPCList,#index=QNT_ID_NECRO_WARN_EVIL_TWO,
               #new_NPC_list=lNPCs);
         Send(oQE,@SetQuestNodeAssignHint,#index=QNT_ID_NECRO_WARN_EVIL_TWO,
               #new_hint=necrowarning_assign_2);
         Send(oQE,@SetQuestNodeSuccessHint,#index=QNT_ID_NECRO_WARN_EVIL_TWO,
               #new_hint=necrowarning_success_2);
         Send(oQE,@SetQuestNodeFailureHint,#index=QNT_ID_NECRO_WARN_EVIL_TWO,
               #new_hint=necrowarning_failure_2);
      }
      else
      {
         Debug("Error building QN: QNT_ID_NECRO_WARN_EVIL_TWO");
      }

      % Template #148 is necro warning quest
      % QNT_ID_NECRO_WARN_EVIL_THREE
      if Send(oQE,@AddQuestNodeTemplate,#questnode_type=QN_TYPE_SHOWUP,
               #NPC_modifier=QN_NPCMOD_PREVIOUS,
               #prizelist=[ [ [ QN_PRIZETYPE_ITEMCLASS, &Money, 800 ],
                              [ QN_PRIZETYPE_ITEMCLASS, &Money, 1000 ],
                              [ QN_PRIZETYPE_ITEMCLASS, &Money, 1200 ],
                              [ QN_PRIZETYPE_ITEMCLASS, &Money, 1400 ],
                              [ QN_PRIZETYPE_ITEMCLASS, &Money, 1600 ],
                              [ QN_PRIZETYPE_ITEMCLASS, &SilverArrow, 10],
                              [ QN_PRIZETYPE_ITEMCLASS, &SilverArrow, 15],
                              [ QN_PRIZETYPE_ITEMCLASS, &SilverArrow, 20],
                              [ QN_PRIZETYPE_ITEMCLASS, &Prism, 1 ] ] ],
               #timelimit=4 * 3600,#quest_node_index=QNT_ID_NECRO_WARN_EVIL_THREE)
      {
         Send(oQE,@SetQuestNodeSuccessHint,#index=QNT_ID_NECRO_WARN_EVIL_THREE,
               #new_hint=necrowarning_success_3);
      }
      else
      {
         Debug("Error building QN: QNT_ID_NECRO_WARN_EVIL_THREE");
      }

      return;
   }

   ExtraDesc(index=$,oQuestNode=$)
   {
      local oDestNPC, oSourceNPC;

      if oQuestNode = $
         OR index = $
      {
         return;
      }

      if index = QNT_ID_NECRO_WARN_EVIL_TWO
      {
         oDestNPC = Send(oQuestNode,@GetDestNPC);
         AddPacket(4,Send(oDestNPC,@GetDef), 4,Send(oDestNPC,@GetName));

         return;
      }

      if index = QNT_ID_NECRO_WARN_EVIL_THREE
      {
         oSourceNPC = Send(oQuestNode,@GetSourceNPC);
         AddPacket(4,Send(oSourceNPC,@GetCapDef), 4,Send(oSourceNPC,@GetName));

         return;
      }

      return;
   }

end
%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
