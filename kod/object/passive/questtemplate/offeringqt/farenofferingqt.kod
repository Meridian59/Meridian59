% Meridian 59, Copyright 1994-2012 Andrew Kirmse and Chris Kirmse.
% All rights reserved.
%
% This software is distributed under a license that is described in
% the LICENSE file that accompanies it.
%
% Meridian is a registered trademark.


%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
FarenOfferingQuest is OfferingQuestTemplate
% Quest template QST_ID_FAREN_OFFERING = 28
% Quest nodes 73, 74, 75, 76, 77.
% QNT_ID_FAREN_OFFERING_ONE, QNT_ID_FAREN_OFFERING_TWO, QNT_ID_FAREN_OFFERING_THREE
% QNT_ID_FAREN_OFFERING_FOUR, QNT_ID_FAREN_OFFERING_FIVE

% QuestTemplate is
% [ quest template index
%   quest template object
%   number of players,
%   type of quest,
%   eligible player restrictions,
%   quest nodes,
%   max number active,
%   currently active quests,
%   percent chance of scheduling (on timer firing)
%   special restrictions ]

constants:

   include blakston.khd

resources:

   include farenofferingqt.lkod

   % Quest description.
   farenofferingqt_icon_rsc = burnwate.bgf
   farenofferingqt_name_rsc = "Faren offering quest"
   farenofferingqt_desc_rsc = \
      "Priestess Tenuv'vyal has invited you to chant with her, to "
      "give some of your strength to Faren."

   farenofferingqt_completed = \
      "You have given part of your life essence in order to strengthen "
      "Faren.  In return, you have received a powerful magical "
      "item.  When placed at one of the Shrines located around the "
      "world, the Shrine will be dedicated to Faren and his spells "
      "will have more power."

   % Quest resources.
   farenoffering_assign_2 = \
      "`B`g%NAME, child of our Mistress of the Wilds, will you give "
      "of yourself so that your Mother may be stronger?"
   farenoffering_assign_3 = \
      "`B`gWorship with me, then.  Together, we will draw upon our own "
      "life essence to form a symbol of the power of nature.  Rarely are "
      "the stars so well aligned for the ritual we shall undertake, so we "
      "must speak quickly.  Incant these words with me: %CARGO"
   farenoffering_trigger_3 = \
      "By the earth, the winds, the seas, and the fire inside, I summon "
      "thee, Faren, Mistress of the Wilds"
   farenoffering_failure_3 = \
      "Subject: The stars were off\n"
      "I miscalculated the positioning of various forces;  the time was "
      "not right to manifest Faren's power.  Perhaps another time, when "
      "the celestial clocks tick around once more."
   farenoffering_assign_4 = "`B`g%CARGO!"
   farenoffering_success_5 = \
      "`B`gBehold, the Turbulence of Nature manifest!  Go now, carry "
      "forth the power of our Mistress into the world!"

classvars:

   vrIcon = farenofferingqt_icon_rsc
   vrName = farenofferingqt_name_rsc
   vrDesc = farenofferingqt_desc_rsc

   vrCompleted = farenofferingqt_completed

   % The QST_ID constant for this quest.
   viQuestID = QST_ID_FAREN_OFFERING

   viIcon_Animation_start = 1

properties:

   piPlayerRestrict = Q_PLAYER_NOTTRIED_RECENTLY | Q_PLAYER_KARMA_NEUTRAL \
                      | Q_PLAYER_NOTNEWBIE

messages:

   Constructor()
   {
      % Quest node list to send to QuestEngine.
      plQuestNodes = [QNT_ID_FAREN_OFFERING_ONE, QNT_ID_FAREN_OFFERING_TWO,
                      QNT_ID_FAREN_OFFERING_THREE, QNT_ID_FAREN_OFFERING_FOUR,
                      QNT_ID_FAREN_OFFERING_FOUR, QNT_ID_FAREN_OFFERING_FOUR,
                      QNT_ID_FAREN_OFFERING_FIVE];

      % Quest nodes with matching descriptions for the player.
      % No description needed for quest node QNT_ID_SHAL_DISCIPLE_ONE.
      plQuestNodeDesc = [ [QNT_ID_FAREN_OFFERING_TWO, vrAccept],
                          [QNT_ID_FAREN_OFFERING_THREE, vrChant],
                          [QNT_ID_FAREN_OFFERING_FOUR, vrChant],
                          [QNT_ID_FAREN_OFFERING_FIVE, vrChant] ];

      plSpecialRestrict = [ [ Q_R2_HAS_SPELL_SCHOOL, SS_FAREN, 15 ] ];

      propagate;
   }

   SendQuestNodeTemplates()
   {
      local oQE, oLib;

      oQE = Send(SYS,@GetQuestEngine);
      oLib = Send(SYS,@GetLibrary);

      % Template #73 is faren offering quest kickoff
      % QNT_ID_FAREN_OFFERING_ONE
      if Send(oQE,@AddQuestNodeTemplate,#questnode_type=QN_TYPE_SHOWUP,
               #quest_node_index=QNT_ID_FAREN_OFFERING_ONE)
      {
         Send(oQE,@SetQuestNodeNPCList,#index=QNT_ID_FAREN_OFFERING_ONE,
               #new_NPC_list=Send(oLib,@GetOccupationList,
                                 #cNPC_class=&FarenPriestess));
      }
      else
      {
         Debug("Error building QN: QNT_ID_FAREN_OFFERING_ONE");
      }

      % Template #74 is faren offering quest, say "yes"
      % QNT_ID_FAREN_OFFERING_TWO
      if Send(oQE,@AddQuestNodeTemplate,#questnode_type=QN_TYPE_MESSAGE,
               #NPC_modifier=QN_NPCMOD_SAME,#cargolist=[offering_trigger_2],
               #timelimit=piTimeLimit,#quest_node_index=QNT_ID_FAREN_OFFERING_TWO)
      {
         Send(oQE,@SetQuestNodeAssignHint,#index=QNT_ID_FAREN_OFFERING_TWO,
               #new_hint=farenoffering_assign_2);
      }
      else
      {
         Debug("Error building QN: QNT_ID_FAREN_OFFERING_TWO");
      }

      % Template #75 is faren offering quest, incant
      % QNT_ID_FAREN_OFFERING_THREE
      if Send(oQE,@AddQuestNodeTemplate,#questnode_type=QN_TYPE_MESSAGE,
               #NPC_modifier=QN_NPCMOD_SAME,#cargolist=[farenoffering_trigger_3],
               #prizelist=[ [QN_PRIZETYPE_BOON, QN_PRIZE_BOON_MYSTICISM , -4, 4 ],
                            [QN_PRIZETYPE_BOON, QN_PRIZE_BOON_MYSTICISM , -1, 7 ] ],
               #penaltylist=[ [QN_PRIZETYPE_BOON, QN_PRIZE_BOON_MYSTICISM , -5, 2 ] ],
               #timelimit=piTimeLimit,#quest_node_index=QNT_ID_FAREN_OFFERING_THREE)
      {
         Send(oQE,@SetQuestNodeAssignHint,#index=QNT_ID_FAREN_OFFERING_THREE,
               #new_hint=farenoffering_assign_3);
         Send(oQE,@SetQuestNodeFailureHint,#index=QNT_ID_FAREN_OFFERING_THREE,
               #new_hint=farenoffering_failure_3);
      }
      else
      {
         Debug("Error building QN: QNT_ID_FAREN_OFFERING_THREE");
      }

      % Template #76 is faren offering quest, incant
      % QNT_ID_FAREN_OFFERING_FOUR
      if Send(oQE,@AddQuestNodeTemplate,#questnode_type=QN_TYPE_MESSAGE,
               #NPC_modifier=QN_NPCMOD_SAME,#cargolist=[farenoffering_trigger_3],
               #prizelist=[ [QN_PRIZETYPE_BOON, QN_PRIZE_BOON_MYSTICISM , -4, 4 ],
                            [QN_PRIZETYPE_BOON, QN_PRIZE_BOON_MYSTICISM , -1, 7 ] ],
               #penaltylist=[ [QN_PRIZETYPE_BOON, QN_PRIZE_BOON_MYSTICISM , -5, 2 ] ],
               #timelimit=piTimeLimit,#quest_node_index=QNT_ID_FAREN_OFFERING_FOUR)
      {
         Send(oQE,@SetQuestNodeAssignHint,#index=QNT_ID_FAREN_OFFERING_FOUR,
               #new_hint=farenoffering_assign_4);
         Send(oQE,@SetQuestNodeFailureHint,#index=QNT_ID_FAREN_OFFERING_FOUR,
               #new_hint=farenoffering_failure_3);
      }
      else
      {
         Debug("Error building QN: QNT_ID_FAREN_OFFERING_FOUR");
      }

      % Template #77 is faren offering quest, incant
      % QNT_ID_FAREN_OFFERING_FIVE
      if Send(oQE,@AddQuestNodeTemplate,#questnode_type=QN_TYPE_MESSAGE,
               #NPC_modifier=QN_NPCMOD_SAME,#cargolist=[farenoffering_trigger_3],
               #prizelist=[ [ QN_PRIZETYPE_ITEMCLASS, &OfferingFaren, 1 ],
                             [QN_PRIZETYPE_BOON, QN_PRIZE_BOON_MYSTICISM , -4, 4 ],
                             [QN_PRIZETYPE_BOON, QN_PRIZE_BOON_MYSTICISM , -1, 7 ] ],
               #penaltylist=[ [QN_PRIZETYPE_BOON, QN_PRIZE_BOON_MYSTICISM , -5, 2 ] ],
               #timelimit=piTimeLimit,#quest_node_index=QNT_ID_FAREN_OFFERING_FIVE)
      {
         Send(oQE,@SetQuestNodeAssignHint,#index=QNT_ID_FAREN_OFFERING_FIVE,
               #new_hint=farenoffering_assign_4);
         Send(oQE,@SetQuestNodeSuccessHint,#index=QNT_ID_FAREN_OFFERING_FIVE,
               #new_hint=farenoffering_success_5);
         Send(oQE,@SetQuestNodeFailureHint,#index=QNT_ID_FAREN_OFFERING_FIVE,
               #new_hint=farenoffering_failure_3);
      }
      else
      {
         Debug("Error building QN: QNT_ID_FAREN_OFFERING_FIVE");
      }

      return;
   }

end
%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
