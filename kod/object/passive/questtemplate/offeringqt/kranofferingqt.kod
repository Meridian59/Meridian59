% Meridian 59, Copyright 1994-2012 Andrew Kirmse and Chris Kirmse.
% All rights reserved.
%
% This software is distributed under a license that is described in
% the LICENSE file that accompanies it.
%
% Meridian is a registered trademark.


%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
KranOfferingQuest is OfferingQuestTemplate
% Quest template QST_ID_KRAANAN_OFFERING = 29
% Quest nodes 78, 79, 80, 81, 82.
% QNT_ID_KRAN_OFFERING_ONE, QNT_ID_KRAN_OFFERING_TWO, QNT_ID_KRAN_OFFERING_THREE
% QNT_ID_KRAN_OFFERING_FOUR, QNT_ID_KRAN_OFFERING_FIVE

% QuestTemplate is
% [ quest template index
%   quest template object
%   number of players,
%   type of quest,
%   eligible player restrictions,
%   quest nodes,
%   max number active,
%   currently active quests,
%   percent chance of scheduling (on timer firing)
%   special restrictions ]

constants:

   include blakston.khd

resources:

   include kranofferingqt.lkod

   % Quest description.
   kranofferingqt_icon_rsc = sweat.bgf
   kranofferingqt_name_rsc = "Kraanan offering quest"
   kranofferingqt_desc_rsc = \
      "Priestess Qerti'nya has invited you to chant with her, to give "
      "strength to your Lord."

   kranofferingqt_completed = \
      "You have given part of your life essence in order to strengthen "
      "your Lord.  In return, you have received a powerful magical "
      "item.  When placed at one of the Shrines located around the "
      "world, the Shrine will be dedicated to Kraanan and his spells "
      "will have more power."

   % Quest resources.
   kraananoffering_assign_2 = \
      "`B`b%NAME, heroic champion of the Fist, will you give of yourself "
      "so that your Lord may be stronger?"
   kraananoffering_assign_3 = \
      "`B`bWorship with me, then.  Together, we will draw upon our own "
      "life essence to forge a symbol of our Lord's power.  Rarely are "
      "the stars so well aligned for the ancient ceremony we shall undertake, "
      "so we must speak quickly.  Incant these words with me: %CARGO"
   kraananoffering_trigger_3 = \
      "By the spark of steel against steel, I summon thee, Kraanan the Fist"
   kraananoffering_failure_3 = \
      "Subject: You must strengthen thyself!\n"
      "The window of time has passed, and now we have both suffered for "
      "your hesitation.  The ritual begun but left unfinished has drawn "
      "away a piece of our power.  You were weaker than I had hoped;  Fortify "
      "thy spirit, and perhaps another day we shall be successful."
   kraananoffering_assign_4 = "`B`b%CARGO!"
   kraananoffering_success_5 = \
      "`B`bAh, this is a great day for Kraanan!  Go now, carry forth the "
      "strength of our Lord into the world!"

classvars:

   vrIcon = kranofferingqt_icon_rsc
   vrName = kranofferingqt_name_rsc
   vrDesc = kranofferingqt_desc_rsc

   vrCompleted = kranofferingqt_completed

   % The QST_ID constant for this quest.
   viQuestID = QST_ID_KRAANAN_OFFERING

   viIcon_Animation_start = 1

properties:

   piPlayerRestrict = Q_PLAYER_NOTTRIED_RECENTLY | Q_PLAYER_NOTNEWBIE

messages:

   Constructor()
   {
      % Quest node list to send to QuestEngine.
      plQuestNodes = [QNT_ID_KRAN_OFFERING_ONE, QNT_ID_KRAN_OFFERING_TWO,
                      QNT_ID_KRAN_OFFERING_THREE, QNT_ID_KRAN_OFFERING_FOUR,
                      QNT_ID_KRAN_OFFERING_FOUR, QNT_ID_KRAN_OFFERING_FOUR,
                      QNT_ID_KRAN_OFFERING_FIVE];

      % Quest nodes with matching descriptions for the player.
      % No description needed for quest node QNT_ID_SHAL_DISCIPLE_ONE.
      plQuestNodeDesc = [ [QNT_ID_KRAN_OFFERING_TWO, vrAccept],
                          [QNT_ID_KRAN_OFFERING_THREE, vrChant],
                          [QNT_ID_KRAN_OFFERING_FOUR, vrChant],
                          [QNT_ID_KRAN_OFFERING_FIVE, vrChant] ];

      plSpecialRestrict = [ [ Q_R2_HAS_SPELL_SCHOOL, SS_KRAANAN, 15 ] ];

      propagate;
   }

   SendQuestNodeTemplates()
   {
      local oQE, oLib;

      oQE = Send(SYS,@GetQuestEngine);
      oLib = Send(SYS,@GetLibrary);

      % Template #78 is kraanan offering quest kickoff
      % QNT_ID_KRAN_OFFERING_ONE
      if Send(oQE,@AddQuestNodeTemplate,#questnode_type=QN_TYPE_SHOWUP,
               #quest_node_index=QNT_ID_KRAN_OFFERING_ONE)
      {
         Send(oQE,@SetQuestNodeNPCList,#index=QNT_ID_KRAN_OFFERING_ONE,
               #new_NPC_list=Send(oLib,@GetOccupationList,
                                 #cNPC_class=&KraananPriestess));
      }
      else
      {
         Debug("Error building QN: QNT_ID_KRAN_OFFERING_ONE");
      }

      % Template #79 is kraanan offering quest, say "yes"
      % QNT_ID_KRAN_OFFERING_TWO
      if Send(oQE,@AddQuestNodeTemplate,#questnode_type=QN_TYPE_MESSAGE,
               #NPC_modifier=QN_NPCMOD_SAME,#cargolist=[offering_trigger_2],
               #timelimit=piTimeLimit,#quest_node_index=QNT_ID_KRAN_OFFERING_TWO)
      {
         Send(oQE,@SetQuestNodeAssignHint,#index=QNT_ID_KRAN_OFFERING_TWO,
               #new_hint=kraananoffering_assign_2);
      }
      else
      {
         Debug("Error building QN: QNT_ID_KRAN_OFFERING_TWO");
      }

      % Template #80 is kraanan offering quest, incant
      % QNT_ID_KRAN_OFFERING_THREE
      if Send(oQE,@AddQuestNodeTemplate,#questnode_type=QN_TYPE_MESSAGE,
               #NPC_modifier=QN_NPCMOD_SAME,#cargolist=[kraananoffering_trigger_3],
               #prizelist=[ [QN_PRIZETYPE_BOON, QN_PRIZE_BOON_AGILITY , -4, 4 ],
                            [QN_PRIZETYPE_BOON, QN_PRIZE_BOON_AGILITY , -1, 7 ] ],
               #penaltylist=[ [QN_PRIZETYPE_BOON, QN_PRIZE_BOON_AGILITY , -5, 2 ] ],
               #timelimit=piTimeLimit,#quest_node_index=QNT_ID_KRAN_OFFERING_THREE)
      {
         Send(oQE,@SetQuestNodeAssignHint,#index=QNT_ID_KRAN_OFFERING_THREE,
               #new_hint=kraananoffering_assign_3);
         Send(oQE,@SetQuestNodeFailureHint,#index=QNT_ID_KRAN_OFFERING_THREE,
               #new_hint=kraananoffering_failure_3);
      }
      else
      {
         Debug("Error building QN: QNT_ID_KRAN_OFFERING_THREE");
      }

      % Template #81 is kraanan offering quest, incant
      % QNT_ID_KRAN_OFFERING_FOUR
      if Send(oQE,@AddQuestNodeTemplate,#questnode_type=QN_TYPE_MESSAGE,
               #NPC_modifier=QN_NPCMOD_SAME,#cargolist=[kraananoffering_trigger_3],
               #prizelist=[ [QN_PRIZETYPE_BOON, QN_PRIZE_BOON_AGILITY , -4, 4 ],
                            [QN_PRIZETYPE_BOON, QN_PRIZE_BOON_AGILITY , -1, 7 ] ],
               #penaltylist=[ [QN_PRIZETYPE_BOON, QN_PRIZE_BOON_AGILITY , -5, 2 ] ],
               #timelimit=piTimeLimit,#quest_node_index=QNT_ID_KRAN_OFFERING_FOUR)
      {
         Send(oQE,@SetQuestNodeAssignHint,#index=QNT_ID_KRAN_OFFERING_FOUR,
               #new_hint=kraananoffering_assign_4);
         Send(oQE,@SetQuestNodeFailureHint,#index=QNT_ID_KRAN_OFFERING_FOUR,
               #new_hint=kraananoffering_failure_3);
      }
      else
      {
         Debug("Error building QN: QNT_ID_KRAN_OFFERING_FOUR");
      }

      % Template #82 is kraanan offering quest, incant
      % QNT_ID_KRAN_OFFERING_FIVE
      if Send(oQE,@AddQuestNodeTemplate,#questnode_type=QN_TYPE_MESSAGE,
               #NPC_modifier=QN_NPCMOD_SAME,#cargolist=[kraananoffering_trigger_3],
               #prizelist=[ [ QN_PRIZETYPE_ITEMCLASS, &OfferingKraanan, 1 ],
                             [QN_PRIZETYPE_BOON, QN_PRIZE_BOON_AGILITY , -4, 4 ],
                             [QN_PRIZETYPE_BOON, QN_PRIZE_BOON_AGILITY , -1, 7 ] ],
               #penaltylist=[ [QN_PRIZETYPE_BOON, QN_PRIZE_BOON_AGILITY , -5, 2 ] ],
               #timelimit=piTimeLimit,#quest_node_index=QNT_ID_KRAN_OFFERING_FIVE)
      {
         Send(oQE,@SetQuestNodeAssignHint,#index=QNT_ID_KRAN_OFFERING_FIVE,
               #new_hint=kraananoffering_assign_4);
         Send(oQE,@SetQuestNodeSuccessHint,#index=QNT_ID_KRAN_OFFERING_FIVE,
               #new_hint=kraananoffering_success_5);
         Send(oQE,@SetQuestNodeFailureHint,#index=QNT_ID_KRAN_OFFERING_FIVE,
               #new_hint=kraananoffering_failure_3);
      }
      else
      {
         Debug("Error building QN: QNT_ID_KRAN_OFFERING_FIVE");
      }

      return;
   }

end
%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
