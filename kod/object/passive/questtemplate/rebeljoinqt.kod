% Meridian 59, Copyright 1994-2012 Andrew Kirmse and Chris Kirmse.
% All rights reserved.
%
% This software is distributed under a license that is described in
% the LICENSE file that accompanies it.
%
% Meridian is a registered trademark.


%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
RebelJoinQuest is QuestTemplate
% QST_ID_REBEL_JOIN = 70
% Quest nodes 199, 200
% QNT_ID_REBEL_JOIN_ONE, QNT_ID_REBEL_JOIN_TWO

% QuestTemplate is
% [ quest template index
%   quest template object
%   number of players,
%   type of quest,
%   eligible player restrictions,
%   quest nodes,
%   max number active,
%   currently active quests,
%   percent chance of scheduling (on timer firing)
%   special restrictions ]

constants:

   include blakston.khd

resources:

   include rebeljoinqt.lkod

   % Quest description.
   rebeljoin_icon_rsc = wngenera.bgf
   rebeljoin_name_rsc = "rebel join faction quest"
   rebeljoin_desc_rsc = \
      "Before Jonas D'Accor will let you join his ranks, he must be convinced "
      "you are not a spy. Bring him an item, and you will gain his trust."

   rebeljoin_deliver_rsc = \
      "Before Jonas D'Accor will let you join his ranks, he must be convinced "
      "you are not a spy. Bring him %s%s, and you will gain his trust."

   rebeljoin_completed = \
      "You have earnt the trust of Jonas D'Accor, and are now a member "
      "of his faction."

   % Quest resources.
   rebel_join2_assign = \
      "You wish to join our fight for freedom?  I cannot trust you, for "
      "you may be a spy for my enemies.  Prove your value by bringing "
      "me %INDEF_CARGO%CARGO."
   rebel_join2_success = \
      "Excellent work!  This will equip the troops I field in battle.  "
      "Consider yourself one of the freedom fighters for the land."
   rebel_join2_failure = \
      "Subject: Your lack of action.\n"
      "Your lack of actions probably confirms my fears that you were a spy "
      "for the other weak leaders.  Do not bother me again with your "
      "false desires to fight for true freedom."

classvars:

   vrIcon = rebeljoin_icon_rsc
   vrName = rebeljoin_name_rsc
   vrDesc = rebeljoin_desc_rsc

   vrNodeTwo = rebeljoin_deliver_rsc

   vrCompleted = rebeljoin_completed

   % The QST_ID constant for this quest.
   viQuestID = QST_ID_REBEL_JOIN

   viIcon_Animation_start = 1

properties:

   piNumPlayers = 1
   piPlayerRestrict = Q_PLAYER_INTRIGUING | Q_PLAYER_FACTION_NEUTRAL \
                      | Q_PLAYER_NOTTRIED_RECENTLY
   plSpecialRestrict = $
   piMaxPlayers = 30
   piSchedulePct = 100
   piTimeLimit = 3600 % 1 hour.

messages:

   Constructor()
   {
      % Quest node list to send to QuestEngine.
      plQuestNodes = [QNT_ID_REBEL_JOIN_ONE, QNT_ID_REBEL_JOIN_TWO];

      % Quest nodes with matching descriptions for the player.
      plQuestNodeDesc = [ [QNT_ID_REBEL_JOIN_TWO, vrNodeTwo] ];

      propagate;
   }

   SendQuestNodeTemplates()
   {
      local lNPCs, oQE, oLib;

      oQE = Send(SYS,@GetQuestEngine);
      oLib = Send(SYS,@GetLibrary);

      % Template #199 is 1st Rebel Join node, say "join" to Jonas
      % QNT_ID_REBEL_JOIN_ONE
      if Send(oQE,@AddQuestNodeTemplate,#questnode_type=QN_TYPE_MESSAGE,
               #cargolist=[duke_join1_trigger],
               #quest_node_index=QNT_ID_REBEL_JOIN_ONE)
      {
         Send(oQE,@SetQuestNodeNPCList,#index=QNT_ID_REBEL_JOIN_ONE,
               #new_NPC_list=Send(oLib,@GetOccupationList,
                        #iJob=MOB_ROLE_ROYALTY,#cNPC_class=&RebelLiege));
      }
      else
      {
         Debug("Error building QN: QNT_ID_REBEL_JOIN_ONE");
      }

      % Template # 200 is 2nd Rebel Join node, bring an item to Jonas
      % QNT_ID_REBEL_JOIN_TWO
      if Send(oQE,@AddQuestNodeTemplate,#NPC_modifier=QN_NPCMOD_SAME,
               #questnode_type=QN_TYPE_ITEMFINDCLASS,\
               #cargolist=[ [ QN_PRIZETYPE_ITEMCLASS, &ScaleArmor, 1 ],\
                            [ QN_PRIZETYPE_ITEMCLASS, &SimpleHelm, 1 ],\
                            [ QN_PRIZETYPE_ITEMCLASS, &Knightshield, 1 ],\
                            [ QN_PRIZETYPE_ITEMCLASS, &Gauntlet, 1 ],\
                            [ QN_PRIZETYPE_ITEMCLASS, &MysticSword, 1 ],\
                            [ QN_PRIZETYPE_ITEMCLASS, &Scimitar, 1 ],\
                            [ QN_PRIZETYPE_ITEMCLASS, &Scimitar, 1 ] ],\
               #prizelist=[ [ QN_PRIZETYPE_FACTION, QN_PRIZE_FACTION_REBEL ] ],
               #timelimit=piTimeLimit,#quest_node_index=QNT_ID_REBEL_JOIN_TWO)
      {
         Send(oQE,@SetQuestNodeAssignHint,#index=QNT_ID_REBEL_JOIN_TWO,
               #new_hint=rebel_join2_assign);
         Send(oQE,@SetQuestNodeSuccessHint,#index=QNT_ID_REBEL_JOIN_TWO,
               #new_hint=rebel_join2_success);
         Send(oQE,@SetQuestNodeFailureHint,#index=QNT_ID_REBEL_JOIN_TWO,
               #new_hint=rebel_join2_failure);
      }
      else
      {
         Debug("Error building QN: QNT_ID_REBEL_JOIN_TWO");
      }

      return;
   }

   ExtraDesc(index=$,oQuestNode=$)
   {
      local oCargo;

      if oQuestNode = $
         OR index = $
      {
         return;
      }

      % Quest node QNT_ID_REBEL_JOIN_TWO dialog needs
      % cargo indef article and name.
      if index = QNT_ID_REBEL_JOIN_TWO
      {
         oCargo = Send(oQuestNode,@GetCargo);
         AddPacket(4,Send(oCargo,@GetIndef), 4,Send(oCargo,@GetName));
      }

      return;
   }

end
%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
