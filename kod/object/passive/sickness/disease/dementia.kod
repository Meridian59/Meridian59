% Meridian 59, Copyright 1994-2012 Andrew Kirmse and Chris Kirmse.
% All rights reserved.
%
% This software is distributed under a license that is described in
% the LICENSE file that accompanies it.
%
% Meridian is a registered trademark.


%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
Dementia is Disease

constants:

   include blakston.khd

   DEMENTIA_INTELLECT_LOSS = 15
   DEMENTIA_DURATION = 600000        % 10 minutes

resources:

   dementia_name_rsc = "dementia"
   dementia_icon_rsc = idement.bgf
   dementia_desc_rsc = "Formerly, you didn't feel so... so... stupid...."

   dementia_start = "Somehow you feel less sharp than you used to be."
   dementia_stop  = "The fullness of your mental capacities returns to bear."
   
classvars:

   vrName = dementia_name_rsc
   vrIcon = dementia_icon_rsc
   vrDesc = dementia_desc_rsc
	
    viShow_enchantment_icon = 0x02

properties:

messages:

   GetSpellNum()
   {
      return SID_DEMENTIA;
   }

   MakeSick(who = $,iAmount = DEMENTIA_INTELLECT_LOSS,iDuration = DEMENTIA_DURATION,report = TRUE)
   {
      local iIntellectChange;

      if IsClass(who,&Player)
      {
         iIntellectChange = send(who,@AddIntellect,#points=-iAmount);
         send(who,@StartEnchantment,#what=self,
              #time=iDuration,#state=[-iIntellectChange]);
         if report
         {
            send(who,@MsgSendUser,#message_rsc=dementia_start);
         }
      }
      else %%% class is monster
      {
         Send(who,@StartDementia);
         Send(who,@StartEnchantment,#what=self,#time=iDuration);
      }
      return;
   }

   EndEnchantment( who = $, state = 0, report = TRUE )
   {
      if IsClass(who,&Player)
      {
         send(who,@AddIntellect,#points=first(state));
         if report
         {
            send(who,@MsgSendUser,#message_rsc=dementia_stop);
         }
      }
      else  %%% class is monster
      {
         Send(who,@EndDementia);
      }
      
      return;
   }

end
%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
