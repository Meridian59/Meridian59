% Meridian 59, Copyright 1994-2012 Andrew Kirmse and Chris Kirmse.
% All rights reserved.
%
% This software is distributed under a license that is described in
% the LICENSE file that accompanies it.
%
% Meridian is a registered trademark.


%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
Palsy is Disease

constants:

   include blakston.khd

   PALSY_MIGHT_LOSS = 15
   PALSY_DURATION = 600000        % 10 minutes

resources:

   palsy_name_rsc = "palsy"
   palsy_icon_rsc = ienfeebl.bgf
   palsy_desc_rsc = "Where once you were like a titan, now it seems you have the strength of a feeble old lady."

   palsy_start = "Your muscles turn to water."
   palsy_stop  = "You feel your old strength coming back."
   
classvars:

   vrName = palsy_name_rsc
   vrIcon = palsy_icon_rsc
   vrDesc = palsy_desc_rsc
   	
    viShow_enchantment_icon = 0x02

properties:

   poStrength_spell = $

messages:

   GetSpellNum()
   {
      return SID_PALSY;
   }

   MakeSick(who = $,iDuration = PALSY_DURATION,iAmount = PALSY_MIGHT_LOSS, report = TRUE)
   {
      local iMightChange;
      
      if poStrength_spell = $ 
      { poStrength_spell = Send(SYS,@FindSpellByNum,#num=SID_SUPERSTRENGTH); }

      %%% all super strength spells are now dispelled before palsy begins!
      Send(who,@RemoveEnchantment,#what=poStrength_spell);

      if IsClass(who,&Player)
      {
         iMightChange = send(who,@AddMight,#points=-iAmount);
         send(who,@StartEnchantment,#what=self,
              #time=iDuration,#state=[-iMightChange]);
         if report
         {
            send(who,@MsgSendUser,#message_rsc=palsy_start);
         }
      }
      else
      {
         %%% class is &Monster
         Send(who,@StartPalsy);
         Send(who,@StartEnchantment,#what=self,#time=iDuration);
      }
      
      return;
   }

   EndEnchantment(who = $,state = 0,report = TRUE)
   {
      if IsClass(who,&Player)
      {
         send(who,@AddMight,#points=first(state));
         if report
         {
            send(who,@MsgSendUser,#message_rsc=palsy_stop);
         }
      }
      else
      {
         %%% class is &Monster
         Send(who,@EndPalsy);
      }
      
      return;
   }

end
%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
