% Meridian 59, Copyright 1994-2012 Andrew Kirmse and Chris Kirmse.
% All rights reserved.
%
% This software is distributed under a license that is described in
% the LICENSE file that accompanies it.
%
% Meridian is a registered trademark.


%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
DualWielding is Proficiency

constants:

   include blakston.khd

resources:

   DualWield_name_rsc = "dual wielding"
   DualWield_icon_rsc = dualico.bgf
   DualWield_desc_rsc = "This ability is for the truly skilled. It allows a warrior to wield multiple weapons at once allowing for more damage."

classvars:

   vrName = DualWield_name_rsc
   vrIcon = DualWield_icon_rsc
   vrDesc = DualWield_desc_rsc

   viSchool = SKS_FENCING
   viSkill_num = SKID_PROFICIENCY_DUAL
   viSkill_level = 6
   viChance_to_increase = 15

properties:

   plPrerequisites = $

messages:

   GetRequisiteStat(who=$)
   {
      return send(who,@GetAgility);
   }
   
   DoDualWield(who = $, WeaponLeft = $, WeaponRight = $)
   {
	   local oDualWeapon, oSoldierShield, oVault;
   
	   % Only dual wield if they are of the same class
	   if GetClass(WeaponLeft) <> GetClass(WeaponRight)
	   {
		   return FALSE;
	   }
	   
	   % Free up both hands
	   Send(who,@FreeHands);

	   % Place weapons in player's vault
	   oVault = Send(SYS,@FindVaultByNum,#num=VID_PLAYER);
	   Post(oVault,@DepositItems,#lItems=[WeaponLeft, WeaponRight],#who=who);
	   
	   % Sling it on the back if it's there.
	   oSoldierShield = send(who,@FindUsing,#class=&SoldierShield);
	   if oSoldierShield <> $
	   {
		  send(oSoldierShield,@SlingOnBack);
	   }
	   
	   oDualWeapon = Create(&DualWeapon,#WeaponLeft=WeaponLeft,#WeaponRight=WeaponRight);
	   Send(who,@NewHold,#what=oDualWeapon);
	   Post(who,@TryUseItem,#what=oDualWeapon);
   
	   return TRUE;
   }


end
%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
