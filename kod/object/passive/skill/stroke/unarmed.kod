% Meridian 59, Copyright 1994-2012 Andrew Kirmse and Chris Kirmse.
% All rights reserved.
%
% This software is distributed under a license that is described in
% the LICENSE file that accompanies it.
%
% Meridian is a registered trademark.


%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
Unarmed is Stroke

constants:

   include blakston.khd

resources:

   unarmed_name_rsc = "Unarmed Combat"
   unarmed_icon_rsc = light.bgf
   unarmed_desc_rsc = "This is a hand to hand combat move."

classvars:

   vrName = unarmed_name_rsc
   vrIcon = unarmed_icon_rsc
   vrDesc = unarmed_desc_rsc

   viIndefinite = ARTICLE_NONE
   viDefinite = ARTICLE_NONE

   viSkill_num = SKID_DEFAULT
   viSchool = SKS_BRAWLING

   viskillExertion = 10

   viRange = 2

   viAttack_type = 0
   viSpell_type = 0

properties:

   % Format of reagents:  Each element of plReagents is a list, describing a single reagent.
   % First elem of this list is class of reagent; second is # of reagent required for skill.
   plReagents = $
   plPrerequisites = $

messages:

   ImproveStroke(who=$,Use_weapon=$,target=$)
   {
		send(self,@ImproveAbility,#who=who,#target=target);        
		send(send(SYS,@FindSkillByNum,#num=SKID_BRAWLING),@ImproveAbility,#who=who,#target=target);

     return;
   }

   GetAttackType(use_weapon=$)
   {
     return viAttack_Type;
   }

   GetAttackSpell(use_weapon=$)
   {
     return viSpell_type;
   }

   CheckWeaponAndDoAnimation(who=$)
   "Unarmed strokes may be performed even with a weapon in hand."
   "To be added, different animations for different attack types."
   {
      send(who,@DoFistAttack);

      return TRUE;
   }

   GetRange()
   "If the person is unarmed, then that person's range is determined solely"
   "by his stroke.  This may vary from stroke to stroke."
   {
      return viRange;      
   }

   FindDamage(use_weapon=$, who=$, victim=$)
   "If we aren't using a fist weapon, come up with the damage here."
   {
      local oMonster, damage, might, iAbility;
		
      % Morphed? If so, get the damage from the monster we are morphed into.
      oMonster = Send(who,@GetIllusionForm);
      if oMonster <> $ AND Send(who,@CheckPlayerFlag,#flag=PFLAG_MORPHED)
      {
			return Send(oMonster,@GetDamage);
      }
		
      % base weapon damage
		if use_weapon <> $
		{
			damage = send(use_weapon,@GetDamage,#who=who,#target=victim);
		}
		else		
		{
			damage = 800;
		}
		
      % scale the basedamage with the attacker's ability in the stroke
      iAbility = Send(who,@GetAbility,#num=viSkill_num);
      damage = damage * iAbility / 100;
      
      % Take various damage factors into consideration.
      damage = send(self,@DamageFactors,#damage=damage,#who=who,#use_weapon=use_weapon);

      return damage;
   }

end
%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
