% Meridian 59, Copyright 1994-2012 Andrew Kirmse and Chris Kirmse.
% All rights reserved.
%
% This software is distributed under a license that is described in
% the LICENSE file that accompanies it.
%
% Meridian is a registered trademark.


%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
Anonymity is Spell

constants:

   include blakston.khd

resources:

   anonymity_start = "Your features fade, and you begin to feel indistinct."
   anonymity_stop  = "You suddenly feel more like yourself again."
   anonymity_already_enchanted = "You already have %s%s."

   anonymity_name_rsc = "anonymity"
   anonymity_icon_rsc = ianonymo.bgf
   anonymity_desc_rsc = \
      "Makes you indistinct, and unrecognizable to all but those who you'd like to know its you.  "
      "Note that Shal'ille can not punish transgressors against you when you are anonymous.  "
      "Requires rainbow ferns and firesand to cast."
   anonymity_sound = ranonym.wav
   
classvars:

   viPersonal_ench = True
   vrName = anonymity_name_rsc
   vrIcon = anonymity_icon_rsc
   vrDesc = anonymity_desc_rsc

   viSpell_num = SID_ANONYMITY
   viSchool = SS_RIIJA
   viSpell_level = 1
   
   viMana = 15
   viSpellExertion = 10      
   viChance_To_Increase = 40
   viMeditate_ratio = 20
   viCast_time = 5000
   
   viFlash = FLASH_GOOD_SELF
   vrSucceed_wav = anonymity_sound

properties:
   
messages:

   ResetReagents()
   {
      plReagents = $;
      plReagents = Cons([&RainbowFern,2],plReagents);
      plReagents = Cons([&FireSand,1],plReagents);

      return;
   }

   GetNumSpellTargets()
   {
      return 0;
   }

   CanPayCosts(who = $, lTargets = $)
   {
      if lTargets = $
      {
         lTargets = [who];
      }
      
      % check for enchantment already applied
      if Send(first(lTargets),@IsEnchanted,#what=self)
      {
         Send(first(lTargets),@MsgSendUser,#message_rsc=anonymity_already_enchanted,
              #parm1=Send(self,@GetIndef),#parm2=Send(self,@GetName));

         return FALSE;
      }

      propagate;
   }

   CastSpell( who = $, iSpellPower = 1 )
   {
      local iduration;

      % Spell lasts 1 to 10 minutes.
      iduration = ((iSpellPower / 10) + 1) * 60000; 
      
      send(who,@StartEnchantment,#what=self,#time=iduration,#state=1);
      send(who,@MsgSendUser,#message_rsc=anonymity_start);
      send(who,@ResetPlayerFlagList);

      propagate;
   }

   SetSpellPlayerFlag(who=$)
   {
      Send(who,@SetPlayerFlag,#flag=PFLAG_ANONYMOUS,#value=TRUE);
      
      return;
   }

   EndEnchantment( who = $, state = 0, report = TRUE )
   {
      local oRoom;
      %% just in case something snuck by

      send(who,@MsgSendUser,#message_rsc=anonymity_stop);
      post(who,@ResetPlayerFlagList);

      oRoom = send(who,@GetOwner);
      if oRoom <> $
      {
         post(oRoom,@SomethingChanged,#what=who);
      }
      
      return;
   }

   GetPotionClass()
   {
      RETURN &AnonymityPotion;
   }


end
%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
