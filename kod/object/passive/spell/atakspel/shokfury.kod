% Meridian 59, Copyright 1994-2012 Andrew Kirmse and Chris Kirmse.
% All rights reserved.
%
% This software is distributed under a license that is described in
% the LICENSE file that accompanies it.
%
% Meridian is a registered trademark.


%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
ShockingFury is AttackSpell

constants:

   include blakston.khd

resources:

   ShockingFury_name_rsc = "shocking fury"

   ShockingFury_icon_rsc = ishkfury.bgf

   ShockingFury_desc_rsc = \
      "Allows you to overcharge your magical energy into a devastating torrent of electrical destruction. "
      "Requires nothing but violent magical energy, but will leave you unable to cast for a little while."

   ShockingFury_sound = fzap.wav

classvars:

   vrName = ShockingFury_name_rsc
   vrIcon = ShockingFury_icon_rsc
   vrDesc = ShockingFury_desc_rsc
   
   viSpell_num = SID_SHOCKING_FURY

   viAttack_spell = ATCK_SPELL_ALL + ATCK_SPELL_SHOCK

   viSchool = SS_FAREN
   viSpell_level = 3
   viMana = 30

   vrSucceed_wav = ShockingFury_sound

   % In seconds, since it works off GetTime().
   viPostCast_time = 10

   viChance_To_Increase = 5

properties:

   piDamageMin = 30
   piDamageMax = 30
   piRange = 5

messages:

   ResetReagents()
   {
      % separate message so can change, then set from admin mode

      plReagents = $;

      return;
   }

   CastSpell(who = $, lTargets = $)
   {
      % Do the invert effect
      Send(who,@EffectSendUser,#what=self,#effect=EFFECT_INVERT);
      Send(First(lTargets),@EffectSendUser,#what=self,#effect=EFFECT_INVERT);

      propagate;
   }

   SendEffectData()
   {
      % for EFFECT_INVERT, 4 bytes of time to be inverted
      AddPacket(4,250);
      
      return;
   }


end
%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
