% Meridian 59, Copyright 1994-2012 Andrew Kirmse and Chris Kirmse.
% All rights reserved.
%
% This software is distributed under a license that is described in
% the LICENSE file that accompanies it.
%
% Meridian is a registered trademark.


%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
CreateFood is Spell

constants:
   include blakston.khd

resources:

   include creafood.lkod

   createfood_name_rsc = "create food"
   createfood_icon_rsc = icrefood.bgf
   createfood_desc_rsc = \
      "The power of Kraanan is summoned to materialize various nourishing foods.  "
      "Requires elderberries and herbs to cast."

   createfood_spell_intro = "Kraanan Lv. 1: Turns basic elderberries into food for you to eat and increase your vigor."

   createfood_sound = kcreatef.wav

   createfood_cast_rsc = "%s%s appears."
   createfood_inv_full_rsc = \
      "%s%s appears in the air before you, but before you can drop something and grab it, it vanishes."

classvars:

   vrName = createfood_name_rsc
   vrIcon = createfood_icon_rsc
   vrDesc = createfood_desc_rsc

   vrSpell_intro = createfood_spell_intro
   viSpell_num = SID_CREATE_FOOD
   viSchool = SS_KRAANAN
   viSpell_level = 1
   viMana = 10 
   viSpellExertion = 0
   viCast_time = 500
   viChance_To_Increase = 15
   viMeditate_ratio = 10

   vrSucceed_wav = createfood_sound

properties:

messages:

   ResetReagents()
   {
      % separate message so can change, then set from admin mode

      plReagents = $;
      plReagents = Cons([&ElderBerry,2],plReagents);
      plReagents = Cons([&Herbs,2],plReagents);

      return;
   }

   GetNumSpellTargets()
   {
      return 0;
   }

   CastSpell(who = $, lTargets = $,iSpellpower = $)
   {
      local oFood, iNum, iAmount;

      iNum = Random(iSpellPower/2,iSpellpower);

      if iNum < 30 
      {
         oFood = Create(&Apple,#number=1);
      }
      else
      {
         if iNum < 40
         {
            oFood = Create(&Bread,#number=1);
         }
         else
         {
            if iNum < 60
            {
               % If they're on the island, let them create 5 grapes instead
               %  of 1.  This allows people to accomplish part of the Riija
               %  sword quest easier.
               iAmount = 1;
               if send(send(who,@GetOwner),@GetRegion) = RID_KOCATAN
               {
                  iAmount = 5;
               }

               oFood = Create(&Grapes,#number=iAmount);
            }
            else
            {
               if iNum < 70
               {
                  oFood = Create(&TurkeyLeg,#number=1);
               }
               else
               {
                  if iNum < 80
                  {
                     oFood = Create(&Meatpie,#number=1);
                  }
                  else
                  {
                     % Create cheese if number is less than 95
                     %  Also create cheese (instead of an inky) 66% of the time over 95%
                     if iNum < 95 OR random(1,3) <> 1
                     {
                        oFood = Create(&Cheese,#number=1);
                     }
                     else
                     {
                        oFood = Create(&InkyCap,#number=1);
                     }
                  }
               }
            }
         }
      }
      
      if Send(who,@ReqNewHold,#what=oFood)
      {
	      Send(who,@NewHold,#what=oFood);
	      Send(who,@MsgSendUser,#message_rsc=createfood_cast_rsc,
	           #parm1=send(oFood,@GetCapIndef),#parm2=send(oFood,@GetName));
      }
      else
      {
	      Send(oFood,@Delete);
	      Send(who,@MsgSendUser,#message_rsc=createfood_inv_full_rsc,
	           #parm1=send(oFood,@GetCapIndef),#parm2=send(oFood,@GetName));
      }

      propagate;
   }

%  If we use the IA_MADE attribute again:
%  GetDuration(iSpellpower=$)
%     iDuration = iSpellpower;  %%In minutes

end

%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
