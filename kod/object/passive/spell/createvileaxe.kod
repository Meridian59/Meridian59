% Meridian 59, Copyright 1994-2012 Andrew Kirmse and Chris Kirmse.
% All rights reserved.
%
% This software is distributed under a license that is described in
% the LICENSE file that accompanies it.
%
% Meridian is a registered trademark.


%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
CreateVileAxe is Spell

constants:
   include blakston.khd

resources:

   createvileaxe_cast_rsc = "A vile axe appears."
   createvileaxe_inv_full_rsc = "A vile axe in the air before you, "
      "but before you can drop something and grab it, it vanishes."

   createvileaxe_name_rsc = "vile axe"
   createvileaxe_icon_rsc = vileaxe.bgf
   createvileaxe_desc_rsc = \
      "Materializes a vile axe of Qor. "
      "Requires entroots and an orc tooth to cast."

   createvileaxe_sound = qvileaxe.wav

classvars:

   vrName = createvileaxe_name_rsc
   vrIcon = createvileaxe_icon_rsc
   vrDesc = createvileaxe_desc_rsc
   
   viSpell_num = SID_VILE_AXE
   viSchool = SS_QOR
   viSpell_level = 3
   viMana = 15
   viSpellExertion = 5
   viCast_time = 5000

   vrSucceed_wav = createvileaxe_sound

properties:


messages:

   ResetReagents()
   {
      % separate message so can change, then set from admin mode

      plReagents = $;
      plReagents = Cons([&EntrootBerry,2],plReagents);
      plReagents = Cons([&OrcTooth,1],plReagents);

      return;
   }

   GetNumSpellTargets()
   {
      return 0;
   }

   GetLongevity(iSpellPower = 0)
   {
      local iLongevity;

      iLongevity = 10 + ((50 * iSpellpower) / 100);  % lasts 10 - 60 minutes
      iLongevity = iLongevity * 60 * 1000;  % Convert to ms

      return iLongevity;   
   }


   CastSpell(who = $, lTargets = $, iSpellPower = $)
   {
      local oVileAxe, iLongevity, iToHitBonus;

      iLongevity = send(self,@GetLongevity,#iSpellPower=iSpellPower);
      iToHitBonus = (iSpellPower * 3) / 2;
      oVileAxe = Create(&VileAxe,#iLongevity=iLongevity,#iToHitBonus=iToHitBonus);

      if Send(who,@ReqNewHold,#what=oVileAxe)
      {
         Send(who,@NewHold,#what=oVileAxe);
         Send(who,@MsgSendUser,#message_rsc=createvileaxe_cast_rsc);
      }
      else
      {
         Send(oVileAxe,@Delete);
         Send(who,@MsgSendUser,#message_rsc=createvileaxe_inv_full_rsc);
      }

      propagate;
   }

end

%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
