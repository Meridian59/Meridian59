% Meridian 59, Copyright 1994-2012 Andrew Kirmse and Chris Kirmse.
% All rights reserved.
%
% This software is distributed under a license that is described in
% the LICENSE file that accompanies it.
%
% Meridian is a registered trademark.


%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
CreateWeapon is Spell

constants:
   include blakston.khd

resources:

   createweapon_cast_rsc = "%s%s appears."
   createweapon_inv_full_rsc = "%s%s appears in the air before you, "
   "but before you can drop something and grab it, it vanishes."
   createweapon_name_rsc = "create weapon"
   createweapon_icon_rsc = icremace.bgf
   createweapon_desc_rsc = \
      "Materializes a weapon. "
	  "Requires sapphires."

   createweapon_spell_intro = "Kraanan Lv. 1: Creates a variety of weapons for use in defeating your foes." 

   createweapon_sound = kraanan.wav

classvars:

   vrName = createweapon_name_rsc
   vrIcon = createweapon_icon_rsc
   vrDesc = createweapon_desc_rsc

   vrSpell_intro = createweapon_spell_intro

   viSpell_num = SID_CREATE_WEAPON
   viSchool = SS_KRAANAN
   viSpell_level = 1
   viMana = 15
   viCast_time = 500

   vrSucceed_wav = createweapon_sound

properties:

messages:

   ResetReagents()
   {
      plReagents = $;
      plReagents = Cons([&Sapphire,1],plReagents);
      return;
   }

   GetNumSpellTargets()
   {
      return 0;
   }

   CastSpell(who = $,lTargets = $,iSpellPower=$)
   {
      local oWeapon,iNum, oItemAtt;

      % Keep the bottom end a bit low because the player might want a lower weapon.
      iNum = Random(iSpellPower/3,iSpellPower);
      
      if iNum < 20 
      {
         oWeapon = Create(&Mace);
      }
      else
      {
         if iNum < 30
         {
            oWeapon = Create(&ShortSword);
         }
         else
         {
            if iNum < 45
            {
               oWeapon = Create(&Hammer);
            }
            else
            {
               if iNum < 60
               {
                  oWeapon = create(&Axe);
               }
               else
               {
                  if iNum < 75
                  {
                     oWeapon = Create(&LongSword);
                  }
                  else
                  {
                     if iNum < 95
                     {
                        oWeapon = Create(&Scimitar);
                     }
                     else
                     {
                        oWeapon = Create(&MysticSword);
                     }
                  }
               }
            }
         }
      }

      % Weapon has half it's hits, + iSpellPower% of the other half.
      Send(oWeapon,@SetHits,#number=(Send(oWeapon,@GetHits)*(iSpellPower+100))/200);

      if Send(who,@ReqNewHold,#what=oWeapon)
      {
	      Send(who,@NewHold,#what=oWeapon);
	      Send(who,@MsgSendUser,#message_rsc=createweapon_cast_rsc,
	           #parm1=send(oWeapon,@GetCapDef),#parm2=send(oWeapon,@GetName));
      }
      else
      {
	      Send(oWeapon,@Delete);
	      Send(who, @MsgSendUser, #message_rsc=createweapon_inv_full_rsc,#parm1=send(oWeapon,@GetCapDef),#parm2=send(oWeapon,@GetName));
      }
      
      oItemAtt = send(SYS,@FindItemAttByNum,#Num=IA_MADE);

      send(oItemAtt,@AddtoItem,#oitem=oWeapon,#timer_duration=send(self,@getduration,#iSpellpower=iSpellpower));

      propagate;
   }

   GetDuration(iSpellpower=$)
   {
      local iDuration;

      iDuration = iSpellpower * 2;  %%In minutes
      iDuration = iDuration * 60 * 1000;

      return iDuration;
   }


end
%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
