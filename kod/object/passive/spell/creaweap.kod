% Meridian 59, Copyright 1994-2012 Andrew Kirmse and Chris Kirmse.
% All rights reserved.
%
% This software is distributed under a license that is described in
% the LICENSE file that accompanies it.
%
% Meridian is a registered trademark.


%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
CreateWeapon is Spell

constants:
   include blakston.khd

resources:

   include creaweap.lkod

   createweapon_cast_rsc = "%s%s appears."
   createweapon_inv_full_rsc = \
      "%s%s appears in the air before you but since you are unable "
      "to carry it, it falls on the floor with a loud clang."
   createweapon_name_rsc = "create weapon"
   createweapon_icon_rsc = icremace.bgf
   createweapon_desc_rsc = \
      "Materializes a weapon. Requires nothing but a violent will."

   createweapon_spell_intro = \
      "Kraanan Lv. 1: Creates a variety of weapons for use in "
      "defeating your foes."

   createweapon_sound = kraanan.wav

classvars:

   vrName = createweapon_name_rsc
   vrIcon = createweapon_icon_rsc
   vrDesc = createweapon_desc_rsc

   vrSpell_intro = createweapon_spell_intro

   viSpell_num = SID_CREATE_WEAPON
   viSchool = SS_KRAANAN
   viSpell_level = 1
   viMana = 15
   viCast_time = 5000
   viMeditate_ratio = 10
   

   vrSucceed_wav = createweapon_sound

properties:

messages:

   ResetReagents()
   {
      plReagents = $;

      return;
   }

   GetNumSpellTargets()
   {
      return 0;
   }

   TranceBlocksSay()
   {
      return TRUE;
   }

   SomethingSaidDuringTrance(who=$,string=$)
   {
      local i, iSpellPower, iCount,isWeaponCreateable, oWeapon, oItemAtt,iSpellPercent;

      iCount = 0;
      isWeaponCreateable = false;

      iSpellPower = Send(self,@GetSpellPower,#who=who);
      iSpellPercent = Send(who,@GetSpellAbility,#spell_num=SID_CREATE_WEAPON);

      foreach i in Send(SYS,@GetItemTemplates)
      {
         if(StringEqual(string,Send(i,@GetName)))
         {
            if(NOT isClass(i,&Weapon))
            {
               return;
            }

            if(NOT Send(self,@CanCreateWeapon,#sWeapon=Send(i,@GetName),
                  #iSpellPower=iSpellPower,#iSpellPercent=iSpellPercent))
            {
               return;
            }

            oWeapon = Create(getClass(i));
            isWeaponCreateable = true;
            break;
         }
      }

      if(isWeaponCreateable = true)
      {
         Send(who,@BreakTrance,#event=EVENT_STEER);
         % Weapon has half its hits, + iSpellPower% of the other half.
         Send(oWeapon,@SetHits,#number=(Send(oWeapon,@GetHits)*(iSpellPower+100))/200);

         if Send(who,@ReqNewHold,#what=oWeapon)
         {
            Send(who,@NewHold,#what=oWeapon);
            Send(who,@MsgSendUser,#message_rsc=createweapon_cast_rsc,
                  #parm1=Send(oWeapon,@GetCapIndef),#parm2=Send(oWeapon,@GetName));
         }
         else
         {
            Send(Send(who,@GetOwner),@NewHold,#what=oWeapon,
                  #new_row=Send(who,@GetRow),#new_col=Send(who,@GetCol),
                  #fine_row=(Send(who,@GetFineRow)+2),
                  #fine_col=(Send(who,@GetFineCol)+2));
            Send(who,@MsgSendUser,#message_rsc=createweapon_inv_full_rsc,
                  #parm1=Send(oWeapon,@GetCapIndef),#parm2=Send(oWeapon,@GetName));
         }

         oItemAtt = Send(SYS,@FindItemAttByNum,#num=IA_MADE);
         Send(oItemAtt,@AddtoItem,#oItem=oWeapon,
               #timer_duration=Send(self,@GetDuration,#iSpellpower=iSpellpower));
      }

      return;
   }

   CanCreateWeapon(sWeapon=$,iSpellPower=$,iSpellPercent=$)
   {

      if(StringEqual("Mace",sWeapon) AND iSpellPercent > 0)
      {
         return true;
      }
      if(StringEqual("Short Sword",sWeapon) AND iSpellPercent >= 30)
      {
         return true;
      }
      if(StringEqual("Hammer",sWeapon) AND iSpellPercent >= 45)
      {
         return true;
      }
      if(StringEqual("Axe",sWeapon) AND iSpellPercent >= 45)
      {
         return true;
      }
      if(StringEqual("Long Sword",sWeapon) AND iSpellPercent >=45)
      {
         return true;
      }
      if(StringEqual("Scimitar",sWeapon) AND iSpellPercent >= 55)
      {
         return true;
      }
      if(StringEqual("Mystic Sword",sWeapon) AND iSpellPercent >= 95)
      {
         return true;
      }
      if(StringEqual("Practice Bow",sWeapon) AND iSpellPercent >= 98)
      {
         return true;
      }
      return false;
   }

   CastSpell(who=$,lTargets=$,iSpellPower=$)
   {
      local oWeapon,iNum, oItemAtt;

      % Keep the bottom end a bit low because the player might want a lower weapon.
      iNum = Random(iSpellPower/3,iSpellPower);

      if iNum < 20
      {
         oWeapon = Create(&Mace);
      }
      else
      {
         if iNum < 30
         {
            oWeapon = Create(&ShortSword);
         }
         else
         {
            if iNum < 45
            {
               oWeapon = Create(&Hammer);
            }
            else
            {
               if iNum < 60
               {
                  oWeapon = create(&Axe);
               }
               else
               {
                  if iNum < 75
                  {
                     oWeapon = Create(&LongSword);
                  }
                  else
                  {
                     if iNum < 95
                     {
                        oWeapon = Create(&Scimitar);
                     }
                     else
                     {
                        oWeapon = Create(&MysticSword);
                     }
                  }
               }
            }
         }
      }

      % Weapon has half its hits, + iSpellPower% of the other half.
      Send(oWeapon,@SetHits,#number=(Send(oWeapon,@GetHits)*(iSpellPower+100))/200);

      if Send(who,@ReqNewHold,#what=oWeapon)
      {
         Send(who,@NewHold,#what=oWeapon);
         Send(who,@MsgSendUser,#message_rsc=createweapon_cast_rsc,
               #parm1=Send(oWeapon,@GetCapIndef),#parm2=Send(oWeapon,@GetName));
      }
      else
      {
         Send(Send(who,@GetOwner),@NewHold,#what=oWeapon,
               #new_row=Send(who,@GetRow),#new_col=Send(who,@GetCol),
               #fine_row=(Send(who,@GetFineRow)+2),
               #fine_col=(Send(who,@GetFineCol)+2));
         Send(who,@MsgSendUser,#message_rsc=createweapon_inv_full_rsc,
               #parm1=Send(oWeapon,@GetCapIndef),#parm2=Send(oWeapon,@GetName));
      }

      oItemAtt = Send(SYS,@FindItemAttByNum,#num=IA_MADE);
      Send(oItemAtt,@AddtoItem,#oItem=oWeapon,
            #timer_duration=Send(self,@GetDuration,#iSpellpower=iSpellpower));

      propagate;
   }

   GetDuration(iSpellpower=$)
   {
      local iDuration;

      iDuration = iSpellpower * 2; %%In minutes
      iDuration = iDuration * 60 * 1000;

      return iDuration;
   }

end
%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
