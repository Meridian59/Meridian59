% Meridian 59, Copyright 1994-2012 Andrew Kirmse and Chris Kirmse.
% All rights reserved.
%
% This software is distributed under a license that is described in
% the LICENSE file that accompanies it.
%
% Meridian is a registered trademark.


%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
Vertigo is Debuff

constants:

   include blakston.khd

resources:

   include vertigo.lkod

   Vertigo_name_rsc = "vertigo"
   Vertigo_icon_rsc = ivertigo.bgf
   Vertigo_desc_rsc = \
      "This spell causes the victim to become so ill that they can hardly "
      "stand, much less fight. "
      "Requires two dragonfly eyes and a vial of solagh to cast."

   Vertigo_caster = "%s%s looks strangely dazed... and confused."
   Vertigo_already_enchanted = "%s%s already appears to be freaking."

   Vertigo_on = \
      "Your stomach leaps into your throat and feel as though "
      "someone hit you on the head with a very large hammer."
   Vertigo_off = "You recover from that terrible feeling."

   Vertigo_sound = rvertigo.wav

classvars:

   vrName = Vertigo_name_rsc
   vrIcon = Vertigo_icon_rsc
   vrDesc = Vertigo_desc_rsc

   vrAlreadyEnchanted = Vertigo_already_enchanted
   vrEnchantment_On = Vertigo_on
   vrEnchantment_Off = Vertigo_off
   vrSuccess = Vertigo_caster

   viSpell_num = SID_VERTIGO
   viSchool = SS_RIIJA
   viSpell_level = 3
   viMana = 12
   viSpellExertion = 2

   viChance_to_increase = 15
   viMeditate_ratio = 30

   vrSucceed_wav = Vertigo_sound

properties:

   piBaseDuration = 4000
   piSpellPowerModifier = 101
   piDeviation = 10

messages:

   ResetReagents()
   {
      plReagents = $;
      plReagents = Cons([&DragonflyEye,2],plReagents);
      plReagents = Cons([&Solagh,1],plReagents);

      return;
   }

   CastSpell(who=$,lTargets=$,iSpellPower=0)
   {
      local oTarget;

      oTarget = First(lTargets);

      Send(self,@DoSpell,#what=who,#oTarget=oTarget,#iSpellPower=iSpellPower,
           #iDuration=Send(self,@GetDuration,#iSpellPower=iSpellPower));

      propagate;
   }

   DoSpell(what=$,oTarget=$,iDuration=0,iSpellPower=$)
   {
      local iAmount,iAgil,iStr,iAim;

      iAmount = Send(self,@GetAmount,#iSpellPower=iSpellPower);

      Send(what,@MsgSendUser,#message_rsc=vrSuccess,
           #parm1=Send(oTarget,@GetCapDef),#parm2=Send(oTarget,@GetName));

      % Spell effects
      if IsClass(oTarget,&Player)
      {
         Send(oTarget,@MsgSendUser,#message_rsc=vrEnchantment_On);
         Send(oTarget,@EffectSendUserDuration,#effect=EFFECT_WAVER,#duration=iDuration);

         iAgil = Send(oTarget,@AddAgility,#points=-iAmount);
         iStr = Send(oTarget,@AddMight,#points=-iAmount);
         iAim = Send(oTarget,@AddAim,#points=-iAmount);

         Send(oTarget,@StartEnchantment,#what=self,#time=iDuration,#state=[-iAgil,-iStr,-iAim]);
      }
      else
      {
         iAgil = Send(oTarget,@AddMonsterAgility,#points=-iAmount);
         iAim = Send(oTarget,@AddMonsterAim,#points=-iAmount);

         Send(oTarget,@StartEnchantment,#what=self,#time=iDuration,#state=[-iAgil,-iAim]);
      }

      propagate;
   }

   GetAmount(iSpellPower=0)
   {
      local iAmount;

      % Stats are reduced between 15-24
      iAmount = 15+(iSpellPower/10);

      return iAmount;
   }

   GetDuration(iSpellPower=0)
   {
      local iDuration;

      % Duration calculation now uses class properties, modifiable in game
      % By default, base duration 4000ms, total time 4000-14000ms using 101x
      % spellpower multiplier, with the final duration 90-100% max

      iDuration = (piBaseDuration + (iSpellPower+1)*piSpellPowerModifier);
      iDuration = random(iDuration*(100-piDeviation)/100,iDuration);

      return iDuration;
   }

   EndEnchantment(who=$,state=$,report=TRUE)
   {
      if report
      {
         Send(who,@MsgSendUser,#message_rsc=vrEnchantment_Off);
      }

      % Handle transition from 3-element enchantment list elements to
      % 4-element ones.
      if state = $
         OR state = 0
      {
         state = Send(who,@GetRawState,#what=self);
      }

      if IsClass(who,&Player)
      {
         Send(who,@AddAgility,#points=Nth(state,1));
         Send(who,@AddMight,#points=Nth(state,2));
         Send(who,@AddAim,#points=Nth(state,3));
      }
      else
      {
         Send(who,@AddMonsterAgility,#points=Nth(state,1));
         Send(who,@AddMonsterAim,#points=Nth(state,2));
      }

      return;
   }

   RestartEnchantmentEffect(who=$,state=$)
   {
      local i, iDuration, oList;

      oList = Send(who,@GetEnchantmentList);

      foreach i in oList
      {
         if Nth(i,2) = self
         {
            iDuration = GetTimeRemaining(Nth(i,1));
         }
      }

      Send(who,@MsgSendUser,#message_rsc=vrEnchantment_On);
      Send(who,@EffectSendUserDuration,#effect=EFFECT_WAVER,#duration=iDuration);

      return;
   }

end
%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
