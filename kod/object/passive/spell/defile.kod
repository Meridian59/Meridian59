% Meridian 59, Copyright 1994-2012 Andrew Kirmse and Chris Kirmse.
% All rights reserved.
%
% This software is distributed under a license that is described in
% the LICENSE file that accompanies it.
%
% Meridian is a registered trademark.


%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
Defile is Spell

constants:
   include blakston.khd

   DEFILE_RECENT_LIST = 5

resources:

   defile_name_rsc = "defile"
   defile_icon_rsc = idefile.bgf
   defile_desc_rsc = \
      "In this darkest of rituals, the very spirit of Qor is "
      "summoned to defile the corpse of a vanquished soul, be it "
      "innocent or beast, transforming flesh and blood into magical "
      "energy for the caster.  "
      "Requires fairy wings and the slain."

   defile_cast_rsc = "Your nausea betokens the imminence of Qor."

   defile_corpse_gone = \
      "Your defile spell fails because the corpse has already returned to the earth."

   defile_max = \
      "If you were to invite more eldritch energy inside of you, there is no "
      "telling what would happen!"
   defile_not_good = "Qor may only harvest the souls of the good at heart."

   defile_sound = qdefile.wav

classvars:

   vrName = defile_name_rsc
   vrIcon = defile_icon_rsc
   vrDesc = defile_desc_rsc

   viSpell_num = SID_DEFILE
   viSchool = SS_QOR

   viSpell_level = 3
   viMana = 0

   viCast_time = 10000
   viChance_to_Increase = 100

   vrSucceed_wav = defile_sound
   vbCastable_in_HappyLand = FALSE

   % Mana Effect Variables

   % Changing this value will adjust the base mana. 
   % Increasing it raises the minimum mana across the board.
   viKarmaEffect_player_min = 5

   % This determines how strongly karma and spellpower influence the mana change. 
   % Increasing it will result in a larger difference between the minimum and maximum mana.
   viKarmaEffect_player_factor = 25

   % Changing this value will adjust the base mana. 
   % Increasing it raises the minimum mana across the board.
   viKarmaEffect_monster_min = 3
   % This determines how strongly karma and spellpower influence the mana change. 
   % Increasing it will result in a larger difference between the minimum and maximum mana.
   viKarmaEffect_monster_factor = 5

properties:

   plRecent = $

messages:

   ResetReagents()
   {
      % separate message so can change, then set from admin mode

      plReagents = $;
      plReagents = Cons([&FairyWing, 1],plReagents);

      return;
   }

   GetNumSpellTargets()
   {
      return 1;
   }

   CanPayCosts(who = $, lTargets = $)
   {
      local target;
      
      % Can cast spell if the 1 target item is a corpse
      if Length(lTargets) <> 1
      {
	      return FALSE;
      }

      if send(who,@GetMana) >= (send(who,@GetMaxMana) * 3)
      {
	      Send(who,@MsgSendUser,#message_rsc=defile_max);

	      return FALSE; 
      }

      target = First(lTargets);
      if NOT IsClass(target, &DeadBody)
      {
	      Send(who,@MsgSendUser,#message_rsc=spell_bad_target, 
              #parm1=Send(self,@GetName),#parm2=Send(target,@GetDef),
              #parm3=Send(target,@GetName));

	      return FALSE;
      }

      if NOT Send(target,@WasGood)
      {
	      Send(who,@MsgSendUser,#message_rsc=defile_not_good);           

	      return FALSE;
      }

      propagate;
   }

   CastSpell(who = $, lTargets = $, iSpellPower = 0)
   {
      local oTarget, oRoom, rRecent, bRecent, iKarmaAct, iTargetKarma, iManaAmount, bCapMana;

      bCapMana = FALSE;

      oTarget = First(lTargets);
      oRoom = Send(who,@GetOwner);
      
      if Send(oTarget,@GetOwner) <> oRoom
      {
	      Send(who,@MsgSendUser,#message_rsc=defile_corpse_gone);

	      return;
      }

      bRecent = FALSE;
      for rRecent in plRecent
      {
	      if send(oTarget,@GetCorpseName) = rRecent
	      {
	         bRecent = TRUE;

	         break;
	      }
      }

      if NOT bRecent
      {
         iKarmaAct = bound(abs(send(who,@GetKarma)), 0, iSpellPower+1);
         send(who,@AddKarma,#amount=send(who,@CalculateKarmaChangeFromAct,
              #karma_act=-iKarmaAct,#karma_doer=send(who,@GetKarma),#swing_factor=4));
      }

      iTargetKarma = Send(oTarget,@GetKarma);
      
      % Calculate and apply a mana increase/decrease depending on whether 
      % corpse was a player or monster.
      iManaAmount = 0;

      if Send(oTarget,@WasPlayer)
      {
         % Player corpses
         % Scaling mana impact
         iManaAmount =  viKarmaEffect_player_min + 
            (viKarmaEffect_player_factor * (iTargetKarma * iSpellPower) / 
            (100 * 99)); % Karma max and spellpower max
      }
      else
      {
         % Monster corpses
         % Scaling mana formula
         iManaAmount = viKarmaEffect_monster_min + 
            (viKarmaEffect_monster_factor * (iTargetKarma * iSpellPower) / 
            (100 * 99)); % Karma max and spellpower max
         
         % Do not allow monsters to raise mana above player cap
         bCapMana = TRUE;
      }
      
      
      Send(who,@GainMana,#amount=iManaAmount,#bCapped=bCapMana);
      Send(who,@MsgSendUser,#message_rsc=defile_cast_rsc);
      
      % Corpse cleanup
      Send(oTarget,@Delete);

      propagate;
   }

   ImproveAbility(who=$,target=$)
   {
      local rRecent, rTarget;

      rTarget = send(target,@GetCorpseName);

      % plRecent is names of corpses defiled across whole server

      for rRecent in plRecent
      {
	      if rRecent = rTarget
	      {
	         % no advancement if same body name was defiled recently
	         return;
	      }
      }

      % Chop tail if recent is too long, then add new name to head
      if length(plRecent) >= DEFILE_RECENT_LIST
      {
	      rRecent = Nth(plRecent,DEFILE_RECENT_LIST);
	      plRecent = DelListElem(plRecent,rRecent);
      }

      plRecent = cons(rTarget, plRecent);

      propagate;
   }


end
%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
