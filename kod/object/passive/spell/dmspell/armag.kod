% Meridian 59, Copyright 1994-2012 Andrew Kirmse and Chris Kirmse.
% All rights reserved.
%
% This software is distributed under a license that is described in
% the LICENSE file that accompanies it.
%
% Meridian is a registered trademark.


%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
Armageddon is DMSpell

constants:
   include blakston.khd

resources:

   include armag.lkod

   armageddon_name_rsc = "armageddon"
   armageddon_icon_rsc = ifrclite.bgf

   armageddon_monster_death_msg = \
      "~bYou hear a muffled scream as %s%s is vaporized instantly."

   armageddon_casted_msg = \
      "With a flourish and a warlike cry, %s%s throws %s hands to the heavens!"
   armageddon_unworthy = \
      "You have been deemed unworthy to cast so powerful a spell, "
      "and it has been stripped from you."

   armageddon_desc_rsc = \
      "Kills all monsters in a room with a flourish."

classvars:
   vrName = armageddon_name_rsc
   vrIcon = armageddon_icon_rsc
   vrDesc = armageddon_desc_rsc
   
   viSpell_num = SID_ARMAGEDDON

   viMana = 0

properties:

messages:

   SuccessChance(who=$)
   "If they are not a creator, then they do not get this spell off."
   {
      if Send(who,@PlayerIsImmortal,#gn=TRUE) <> 2 AND NOT isclass(who,&Creator)
      {
         Send(who,@MsgSendUser,#message_rsc=armageddon_unworthy);
         Send(who,@RemoveSpell,#num=viSpell_num);
         Send(who,@ToCliStats,#group=3);

         return FALSE;
      }

      return TRUE;
   }

   ResetReagents()
   {
      plReagents = $;

      return;
   }

   CastSpell(who=$,lTargets=$)
   {
      local  i, j, oRoom, lActive, each_obji, each_objj;

      oRoom = Send(who,@GetOwner);
      lActive = Send(oRoom,@GetHolderActive);

      Send(oRoom,@SomeoneSaid,#type=SAY_MESSAGE,#string=armageddon_casted_msg,
            #what=who,
            #parm1=Send(who,@GetInDef),
            #parm2=Send(who,@GetName),
            #parm3=Send(who,@GetHisHer));

      foreach i in lActive
      {
         each_obji = Send(oRoom,@HolderExtractObject,#data=i);

         if (IsClass(each_obji,&Monster)) 
            AND Send(each_obji,@CanMonsterFight,#oStroke=self,#who=who)
         {
            Send(oRoom,@SomethingShot,#who=who,#target=each_obji,
                  #projectile=self,
                  #flags=PROJ_FLAG_LIGHT_SOURCE);

            if Send(each_obji,@DMCanArmageddon)
            {
               Send(oRoom,@SomeoneSaid,#type=SAY_MESSAGE,
                     #string=armageddon_monster_death_msg,#what=self,
                     #parm1=Send(each_obji,@GetIndef),
                     #parm2=Send(each_obji,@GetName),
                     #parm3=Send(each_obji,@GetHisHer));
               Send(each_obji,@Delete);
            }
         }
      }

      propagate;
   }

   SendLookAnimation()
   {
      AddPacket(1,ANIMATE_NONE,2,1);

      return;
   }

   GetProjectileIcon()
   {
      return vrIcon;
   }

   GetProjectileSpeed()
   {
      return 12;
   }

   SendProjectileAnimation()
   {
      % 40ms between animations
      AddPacket(1, ANIMATE_CYCLE, 4, 40, 2, 1, 2, 2);

      return;
   }

   GetProjectileLightFlags()
   {
      % General lighting information.
      return LIGHT_FLAG_ON | LIGHT_FLAG_DYNAMIC;
   }

   GetProjectileLightIntensity()
   {
      % Medium light radius for projectiles.  Out of 255 levels.
      return 100;
   }

   GetProjectileLightColor()
   {
      local iColor, iNumber;
      
      iNumber=Random(1,7);

      if iNumber = 1
      {
         iColor = LIGHT_WHITE;
      }
      else if iNumber = 2
      {
         iColor = LIGHT_RED;
      }
      else if iNumber = 3
      {
         iColor = LIGHT_ORANGE;
      }
      else if iNumber = 4
      {
         iColor = LIGHT_YELLOW;
      }
      else if iNumber = 5
      {
         iColor = LIGHT_GREEN;
      }
      else if iNumber = 6
      {
         iColor = LIGHT_BLUE;
      }
      else
      {
         iColor = LIGHT_PURPLE;
      }

      % Color is actually random!
      return iColor;
   }

end
%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
