% Meridian 59, Copyright 1994-2012 Andrew Kirmse and Chris Kirmse.
% All rights reserved.
%
% This software is distributed under a license that is described in
% the LICENSE file that accompanies it.
%
% Meridian is a registered trademark.


%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
DMRescue is DMSpell

constants:

   include blakston.khd

resources:

   include dmrescue.lkod

   dmrescue_name_rsc = "deliverance"
   dmrescue_icon_rsc = light.bgf
   dmrescue_only_oog = \
       "You may only deliver players from Out of Grace."

classvars:

   vrName = dmrescue_name_rsc
   vrIcon = dmrescue_icon_rsc

   viSpell_num = SID_DMRESCUE

properties:

messages:

   GetNumSpellTargets()
   {
      return 1;
   }

   CastSpell(who=$,lTargets=$)
   {
      local i, j, oRoom, oHell;

      % Unless the player is a 'SeniorGuide' or above, all they can
      % do with this spell is send players home from OoG.
      oHell = Send(SYS,@FindRoomByNum,#num=RID_OUTOFGRACE);

      if NOT Send(who,@IsActor)
      {
         if NOT (Send(who,@GetOwner) = oHell)
         {
            Send(who,@MsgSendUser,#message_rsc=dmrescue_only_oog);

            return;
         }
      }

      foreach j in lTargets
      {
         i = j;

         if IsClass(i,&LogoffGhost)
         {
            i = Send(i,@GetGhostedPlayer);
            Send(i,@AdminGoToSafety);
            oRoom = Send(SYS,@FindRoomByNum,#num=Send(i,@GetSaveRoom));
            Send(SYS,@UtilGoToSquare,#what=j,#where=oRoom,
                 #row=Send(i,@GetSaveRow),#col=Send(i,@GetSaveCol));

            % If they have the damned string, this will remove it.
            Send(i,@RemoveHonorString,#string=damn_last_annoyed);

            continue;
         }

         if IsClass(i,&Player)
         {
            Send(i,@AdminGoToSafety);

            % If they have the damned string, this will remove it.
            Send(i,@RemoveHonorString,#string=damn_last_annoyed);

            continue;
         }

         Send(who,@MsgSendUser,#message_rsc=spell_bad_target,
              #parm1=Send(self,@GetName),#parm2=Send(i,@GetIndef),
              #parm3=Send(i,@GetName));
      }

      propagate;
   }

end
%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
