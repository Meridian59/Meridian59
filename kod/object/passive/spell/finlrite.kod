% Meridian 59, Copyright 1994-2012 Andrew Kirmse and Chris Kirmse.
% All rights reserved.
%
% This software is distributed under a license that is described in
% the LICENSE file that accompanies it.
%
% Meridian is a registered trademark.


%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
FinalRites is Spell

constants:

   include blakston.khd

resources:

   include finlrite.lkod

   FinalRites_name_rsc = "final rites"
   FinalRites_icon_rsc = ifinrite.bgf
   FinalRites_desc_rsc = \
      "This is the lengthy formal ceremony performed at funerals to help the "
      "recently departed soul pass smoothly on to the Underworld.  It may "
      "also be used to force nearby undead back in their graves where they "
      "belong.  "
      "Requires emeralds and dried elderberries, which during the ceremony "
      "are crushed together into a symbolic handful of dust and scattered "
      "into the air."
   
   FinalRites_success = \
      "You crush the reagents into a fine powder, and with a murmur "
      "you cast the handful of dust heavenward."
   FinalRites_success_others = \
      "With a solemn murmur %s%s casts a handful of dust heavenward."

   FinalRites_hurt = \
      "%s%s shrieks in pain as the sand showers down like a holy rain."
   FinalRites_hurt_necro = \
      "%s%s casts a handful of dust into the air, and it falls down on you "
      "like a scalding rain."

   FinalRites_sound = sfrites.wav

classvars:

   vrName = FinalRites_name_rsc
   vrIcon = FinalRites_icon_rsc
   vrDesc = FinalRites_desc_rsc

   viSpell_num = SID_FINAL_RITES
   viSchool = SS_SHALILLE
   viSpell_level = 5
   viMana = 20
   viSpellExertion = 4

   % This will disallow people from trucing, and then killing undead.
   viHarmful = TRUE   

   viCast_time = 10000
   viChance_To_Increase = 20
   viMeditate_ratio = 50

   vrSucceed_wav = FinalRites_sound

properties:

   viOutlaw = TRUE

messages:

   Constructor()
   {
      if NOT Send(SYS,@IsPKAllowed)
      {
         viOutlaw = FALSE;
      }

      propagate;
   }

   ResetReagents()
   {
      plReagents = $;
      plReagents = Cons([&Emerald,2],plReagents);
      plReagents = Cons([&Elderberry,3],plReagents);

      return;
   }

   GetNumSpellTargets()
   {
      return 0;
   }

   GetTargets(who=$,lTargets=$)
   "This returns a list of undead targets."
   {
      local oRoom, i, each_obj, lFinalTargets;

      lFinalTargets = $;

      oRoom = Send(who,@GetOwner);
      foreach i in Send(oRoom,@GetHolderActive)
      {
         each_obj = Send(oRoom,@HolderExtractObject,#data=i);
         if IsClass(each_obj,&Battler)
            AND Send(each_obj,@IsUndead)
         {
            lFinalTargets = Cons(each_obj,lFinalTargets);
         }
      }

      return lFinalTargets;
   }

   CastSpell(who=$, lTargets = $, iSpellPower=0)
   {
      local oUndead,iDamage,oRoom;
      
      oRoom = Send(who,@GetOwner);
      Send(who,@MsgSendUser,#message_rsc=FinalRites_success);

      Send(oRoom,@SomeoneSaid,#type=SAY_MESSAGE,#what=self,
           #string=FinalRites_success_others,
           #parm1=Send(who,@GetDef),#parm2=Send(who,@GetName));

      foreach oUndead in lTargets
      {
         if isClass(oUndead,&Player)
         {
            Send(oUndead,@MsgSendUser,#message_rsc=FinalRites_hurt_necro,
                  #parm1=Send(who,@GetCapDef),#parm2=Send(who,@GetName));
         }
            
         % Do damage to them.
         iDamage = random(1,5) + (iSpellpower/20) + (2*((Send(who,@GetKarma,#detect=TRUE)
            - Send(oUndead,@GetKarma,#detect=TRUE))/40));
         if Send(oUndead,@AssessDamage,#what=who,#damage=iDamage,#aspell=ATCK_SPELL_HOLY) = $
         {
            Send(who,@KilledSomething,#what=oUndead,#use_weapon=self);
         }

         Send(oRoom,@SomethingAttacked,#what=who,#victim=oUndead,#use_weapon=self);
      }
      
      propagate;
   }


end
%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
