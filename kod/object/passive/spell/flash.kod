% Meridian 59, Copyright 1994-2012 Andrew Kirmse and Chris Kirmse.
% All rights reserved.
%
% This software is distributed under a license that is described in
% the LICENSE file that accompanies it.
%
% Meridian is a registered trademark.


%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
Flash is Spell

constants:

   include blakston.khd

resources:

   include flash.lkod

   Flash_name_rsc = "flash"
   Flash_icon_rsc = iflash.bgf
   Flash_desc_rsc = \
      "Causes a bright flash, likely to interrupt the concentration of any "
      "spellcasters in the area or confuse monsters temporarily.  "
      "Requires a set of eyes from a dragonfly to cast."

   flash_sound = rflash.wav

classvars:

   vrSucceed_wav = flash_sound

   vrName = Flash_name_rsc
   vrIcon = Flash_icon_rsc
   vrDesc = Flash_desc_rsc

   viSpell_num = SID_FLASH
   viSchool = SS_RIIJA
   viSpell_level = 1
   viMana = 15
   viSpellExertion = 1
   viChance_To_Increase = 15
   viMeditate_ratio = 20

   viOutlaw = TRUE
   viHarmful = TRUE
   viNoNewbieOffense = TRUE

properties:

messages:

   ResetReagents()
   {
      plReagents = $;
      plReagents = Cons([&DragonflyEye,2],plReagents);

      return;
   }

   GetNumSpellTargets()
   {
      return 0;
   }

   GetTargets(who=$,lTargets=$)
   "This returns a list of valid targets in the room."
   {
      local oRoom, i, each_obj, lFinalTargets;

      lFinalTargets = $;

      oRoom = Send(who,@GetOwner);
      foreach i in Send(oRoom,@GetHolderActive)
      {
         each_obj = Send(oRoom,@HolderExtractObject,#data=i);
         if IsClass(each_obj,&Battler)
         {
            lFinalTargets = Cons(each_obj,lFinalTargets);
         }
      }

      return lFinalTargets;
   }

   CastSpell(who=$, lTargets = $, iSpellPower=0)
   {
      local oRoom, i, iChance, oHold;
      
      oRoom = Send(who,@GetOwner);

      % What's our chance to affect the target?
      iChance = 33 + ((2 * iSpellPower) / 3);

      % Find the hold spell to temporarily hold monsters
      oHold = Send(SYS,@FindSpellByNum,#num=SID_HOLD);

      if IsClass(who,&Player)
      {
         Send(who,@EffectSendUserDuration,#effect=EFFECT_WHITEOUT,#duration=750);
      }

      foreach i in lTargets
      {
         if Random(1,100) > iChance
         {
            continue;
         }

         if IsClass(i,&Player) 
         {
            if Send(i,@CheckPlayerFlag,#flag=PFLAG_TRANCE)
            {
               Send(i,@BreakTrance,#event=EVENT_DISRUPT); 
            }

            Send(i,@EffectSendUserDuration,#effect=EFFECT_WHITEOUT,#duration=750);
         }
         else
         {
            % Is a monster, stun it.
            Send(oHold,@DoSpell,#what=who,#oTarget=i,#iDuration=1000,
                  #report=FALSE,#bAllowFreeAction=FALSE);
         }
      }

      propagate;
   }

end
%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
