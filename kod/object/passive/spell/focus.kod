% Meridian 59, Copyright 1994-2012 Andrew Kirmse and Chris Kirmse.
% All rights reserved.
%
% This software is distributed under a license that is described in
% the LICENSE file that accompanies it.
%
% Meridian is a registered trademark.


%%%%%%%%%%%%%%%%%%%%%%%%%S%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
% The only way to lose this enchantment is to break the trance.
ManaFocus is Spell

constants:

   include blakston.khd

resources:

   manafocus_name_rsc = "mana focus"
   manafocus_icon_rsc = imanafoc.bgf
   manafocus_desc_rsc = \
      "Focuses your magical energy, "
      "regenerating your mana at an accelerated rate and "
      "increasing the damage of natural elemental attack spells. "
      "Dissipates if your concentration is broken.  "
      "Requires mushrooms to cast."

   manafocus_on = "You feel newly focused magic raging through your body."
   manafocus_break_trance = "You have lost your mana focus."
   manafocus_already_enchanted = "You already have %s%s."

classvars:

   viPersonal_ench = TRUE

   vrName = manafocus_name_rsc
   vrIcon = manafocus_icon_rsc
   vrDesc = Manafocus_desc_rsc

   viSpell_num = SID_MANA_FOCUS
   viMana = 1
   viSchool = SS_FAREN
   viSpell_level = 3

   viCast_time = 7000            %% 7 seconds
   viChance_To_Increase = 15
   viMeditate_ratio = 20

properties:

messages:

   ResetReagents()
   {
      plReagents = $;
      plReagents = cons([&Mushroom,1],plReagents);

      return;
   }

   GetNumSpellTargets()
   {
      return 0;
   }

   CanPayCosts(who = $, lTargets = $)
   {
      % check for enchantment already applied
      if Send(who,@IsEnchanted,#what=self)
      {
         Send(who,@MsgSendUser,#message_rsc=manafocus_already_enchanted,
              #parm1=Send(self,@GetIndef),#parm2=Send(self,@GetName));
              
         return FALSE;
      }

      propagate;
   }


   SetSpellPlayerFlag(who = $)
   {
      Send(who,@SetTranceFlag);
      Send(who,@SetPlayerFlag,#flag=PFLAG_MANA_FOCUS,#value=TRUE);
      
      return;
   }

   CastSpell(who = $, iSpellPower = 0)
   {
      Send(who,@MsgSendUser,#message_rsc=manafocus_on);

      % Save out iSpellPower as our state so that the spells affected by this spell can use it.
      Send(who,@StartEnchantment,#what=self,#time=SPELL_INDEFINITE_DURATION,
           #state=[iSpellPower]);
           
      propagate;
   }

   EndEnchantment(who = $, state = $, report = TRUE)
   {
      if report
      {
         Send(who,@MsgSendUser,#message_rsc=manafocus_break_trance);
      }
      
      return;
   }

   BreakTrance( who = $, state = $, event = 0, what = $ )
   {
      %% Attack Spells (fireballs and lightning bolts) must not break this trance
      if event = EVENT_CAST
         AND IsClass(what,&AttackSpell)
      {
         return;
      }

      Send(who,@RemoveEnchantment,#what=self);
      return;
   }
   
   AdjustManaTime(time=0, iSpellPower=0)
   "Reduces time passed in so that mana regens faster.  Returns reduced time."
   {
      local iTime;

      iTime = (time * (200-iSpellPower)) / 200;
      iTime = bound(iTime,500,60000);

      return iTime;
   }

end

%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
