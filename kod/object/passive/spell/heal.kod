% Meridian 59, Copyright 1994-2012 Andrew Kirmse and Chris Kirmse.
% All rights reserved.
%
% This software is distributed under a license that is described in
% the LICENSE file that accompanies it.
%
% Meridian is a registered trademark.


%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
Heal is Spell

constants:
   include blakston.khd

resources:

   heal_name_rsc = "minor heal"
   heal_icon_rsc = iheal.bgf
   heal_desc_rsc = \
      "The compassionate power of Shal'ille heals the wounds of the caster or another "
      "person.  Is particularly potent on those protected by guardian angels.  "
      "Shal'ille gives a small reward toward the karma of the mage who heals another "
      "good soul.  "
      "Requires herbs to cast."

   heal_cast_rsc = "%s%s's spell heals some minor injuries, restoring ~g~B%i~B~n hitpoints."
   heal_cast_self = "Shal'ille's holy magic heals some minor injuries, restoring ~g~B%i~B~n hitpoints."
   heal_cast_on_other = "You heal %s%s for ~k~B%i~B~n hitpoints."
   heal_unnecessary_rsc = "%s%s is perfectly healthy."

   heal_spell_intro = "Shal'ille Lv. 1: Bestows the healing touch of Shal'ille, restoring points of health."

classvars:

   vrName = heal_name_rsc
   vrIcon = heal_icon_rsc
   vrDesc = heal_desc_rsc

   vrSpell_intro = heal_spell_intro

   viSpell_num = SID_MINOR_HEAL
   viSchool = SS_SHALILLE
   viSpell_level = 1
   viMana = 3

   viCast_time = 600

   viFlash = FLASH_GOOD

properties:

messages:

   ResetReagents()
   {
      % separate message so can change, then set from admin mode

      plReagents = $;
      plReagents = Cons([&Herbs,1],plReagents);

      return;
   }

   GetNumSpellTargets()
   {
      return 1;
   }

   CanPayCosts(who = $, lTargets = $)
   {
      local target, i;
      
      % Can cast spell if the 1 target item is a user
      if Length(lTargets) <> 1
      {
         return FALSE;
      }

      target = First(lTargets);
      if not IsClass(target,&User)
      {
         Send(who,@MsgSendUser,#message_rsc=spell_bad_target, 
              #parm1=vrName,#parm2=Send(target,@GetDef),#parm3=Send(target,@GetName));
           
         return FALSE;
      }
      
      if Send(target,@GetHealth) >= Send(target,@GetMaxHealth)
      {
         Send(who,@MsgSendUser,#message_rsc=heal_unnecessary_rsc,
              #parm1=Send(target,@GetDef),#parm2=Send(target,@GetName));
              
         return FALSE;
      }

      propagate;
   }

   CastSpell(who = $, lTargets = $, iSpellPower=0)
   {
      local iHeal, oTarget;

      oTarget = First(lTargets);
      
      iHeal = random(1,5) + ((iSpellPower + 1)/20) + (Send(oTarget,@GetKarma)/20);
      iHeal = bound(iHeal,1,10);

      % A little boost for casting on newbies, even if you're the newbie!
      if NOT send(oTarget,@CheckPlayerFlag,#flag=PFLAG_PKILL_ENABLE)
      {
         iHeal = iHeal + random(2,5);
      }
      
      if who <> oTarget
      {
         Send(who,@MsgSendUser,#message_rsc=heal_cast_on_other,
              #parm1=Send(oTarget,@GetDef),#parm2=Send(oTarget,@GetName),#parm3=iHeal);
              
         Send(oTarget,@MsgSendUser,#message_rsc=heal_cast_rsc,
              #parm1=Send(who,@GetCapDef),#parm2=Send(who,@GetName),#parm3=iHeal);

         % Give them a little boost if they help someone of higher Karma.
         if send(who,@GetKarma) < send(oTarget,@GetKarma)
         {
            send(who,@AddKarma,#amount = send(who,@CalculateKarmaChangeFromAct,
                #karma_doer=send(who,@GetKarma),#karma_act=send(oTarget,@GetKarma),
                #Swing_factor = 1));
         }
      }

      else
      {
         Send(who,@MsgSendUser,#message_rsc=heal_cast_self,#parm1=iHeal);
      }
           
      Send(oTarget,@GainHealthNormal,#amount=iHeal);

      propagate;
   }


end
%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
