% Meridian 59, Copyright 1994-2012 Andrew Kirmse and Chris Kirmse.
% All rights reserved.
%
% This software is distributed under a license that is described in
% the LICENSE file that accompanies it.
%
% Meridian is a registered trademark.


%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
Jig is Jala

constants:

   include blakston.khd

resources:

   jig_name_rsc = "jig"
   jig_icon_rsc = ijig.bgf
   jig_desc_rsc = \
      "This spell causes everyone in the room to break out into dance "
      "and is lots of fun at parties.  Requires one diamond to cast."

   jig_song = jala2.mid
 
   jig_enter_rsc = "The nearby ditty compels you to begin dancing with yourself."
   jig_leave_rsc = "As the ditty fades, you resume your wallflower status with a sigh of relief."
   
   jig_prevented_rsc = "Your urge to dance causes you to misfire completely!" 

classvars:

   vrName = jig_name_rsc
   vrIcon = jig_icon_rsc
   vrDesc = jig_desc_rsc

   jala_song_enter = jig_enter_rsc
   jala_song_leave = jig_leave_rsc
   
   viMana = 18
   viManaDrain = 10
   viDrainTime = 5000
   viSpell_num = SID_JIG
   viSpellExertion = 18      
   viChance_To_Increase = 40

   viSchool = SS_JALA
   viSpell_level = 4

   viHarmful = TRUE   
   viBaseRange = 2
   viAffectsGuildmates = TRUE

properties:

   prMusic = jig_song
      
messages:

   ResetReagents()
   {
      plReagents = $;
      plReagents = Cons([&Diamond,1],plReagents);

      return;
   }

   EnterSong(who=$,iVolume=0)
   {   
      Send(who,@FreeHands);
      Send(who,@DoDance);
      propagate;
   }

   LeaveSong(who=$,iVolume=0)
   {   
      Send(who,@StopDancing);

      propagate;
   }

   TryPreventAttack(who=$, state=$, stroke_obj=$)
   {
      local oWeapon;
      oWeapon = Send(who,@GetWeapon);
         
      if IsClass(oWeapon,&RangedWeapon)
         AND random(1,100) <= ((Nth(state,2)+1)/2)
      {
         Send(who,@MsgSendUser,#message=jig_prevented_rsc);
         return TRUE;
      }
      return FALSE;
   }
   
end
%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
