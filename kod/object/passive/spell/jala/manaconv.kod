% Meridian 59, Copyright 1994-2012 Andrew Kirmse and Chris Kirmse.
% All rights reserved.
%
% This software is distributed under a license that is described in
% the LICENSE file that accompanies it.
%
% Meridian is a registered trademark.


%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
ManaConvergence is Jala

constants:

   include blakston.khd

resources:

   ManaConvergence_name_rsc = "mana convergence"
   ManaConvergence_icon_rsc = imanacon.bgf
   ManaConvergence_desc_rsc = \
      "The words of this song form an enchantment which increases the effectiveness "
      "of all spells cast.  Requires one perfect polished seraphym."

   ManaConvergence_song = jala5.mid

   ManaConvergence_on = "The flow of magic in this place suddenly becomes stronger."
   ManaConvergence_new_occupant = "You can feel a strong flow of magic in this place."
 
classvars:

   vrName = ManaConvergence_name_rsc
   vrIcon = ManaConvergence_icon_rsc
   vrDesc = ManaConvergence_desc_rsc

   viMana = 10          % Mana is amount used upon inititiation
   viManaDrain = 9      % Drain is amount used every viDrainTime milliseconds
   viDrainTime = 5000    % Drain some mana every viDrainTime milliseconds
   viSpell_num = SID_MANA_CONVERGENCE

   viSchool = SS_JALA
   viSpell_level = 5

   viHarmful = FALSE

properties:

   prMusic = ManaConvergence_song
      
messages:

   ResetReagents()
   {
      plReagents = $;
      plReagents = Cons([&PolishedSeraphym,1],plReagents);

      return;
   }

   CastSpell( who = $, iSpellPower = 0 )
   "Initiation point for the spell."
   {
      local oRoom;

      oRoom = Send(who,@GetOwner);
      
      % global effects of the enchantment
      Send(oRoom,@SomeoneSaid,#type=SAY_MESSAGE,#string=ManaConvergence_on,#what=self);
      
      propagate;
   }

   BuildState(thisVolume=0,who=$,iSpellPower=0)
   {
      return [thisVolume,who,((iSpellPower/2)+25)];
   }

   StartEnchantmentNewOccupant( who = $ )
   "Called on new occupants of the enchanted room."
   {
      Send(who,@MsgSendUser,#message_rsc=ManaConvergence_new_occupant);

      return;
   }

   AffectsSpellPower()
   {
      return TRUE;
   }

   ModifySpellPower(oRoom = $, oSpell = $)
   {
      local iChange;

      iChange = 0;

      if oRoom <> $
      {
         iChange = send(oRoom,@GetEnchantedState,#what=self);
         if iChange <> $
         {
            iChange = nth(iChange,3);
         }
      }

      % Spell won't affect Anti-Magic Aura
      if iChange = $
         OR IsClass(oSpell,&AntiMagicAura)
      {
         iChange = 0;
      }

      % This is added to spell power in spell.kod
      return iChange;
   }


end
%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
