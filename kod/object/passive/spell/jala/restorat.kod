% Meridian 59, Copyright 1994-2012 Andrew Kirmse and Chris Kirmse.
% All rights reserved.
%
% This software is distributed under a license that is described in
% the LICENSE file that accompanies it.
%
% Meridian is a registered trademark.


%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
Restorate is Jala

constants:

   include blakston.khd

   SPELL_POWER_INTERCEPT = 40    % Hypothetical zero spell power restorate effect coefficient
   SPELL_POWER_SLOPE = 400       % 1.12 to 1.51 times faster HP regen, linear effect on time per tick

resources:

   Restorate_name_rsc = "restorate"
   Restorate_icon_rsc = irestor.bgf
   Restorate_desc_rsc = \
      "This song allows all listeners to regain health at an accelerated rate.  "
      "Requires one diamond and several herbs to cast."

   Restorate_song = song05.mid
 
classvars:

   vrName = Restorate_name_rsc
   vrIcon = Restorate_icon_rsc
   vrDesc = Restorate_desc_rsc

   viSpell_num = SID_RESTORATE
   viSchool = SS_JALA
   viSpell_level = 3

   viMana = 5           % Mana is amount used upon inititiation
   viManaDrain = 3      % Drain is amount used every viDrainTime milliseconds
   viDrainTime = 5000   % Drain some mana every viDrainTime milliseconds
      
   viSpellExertion = 10      
   viChance_To_Increase = 20

   viHarmful = FALSE

properties:

   prMusic = Restorate_song
      
messages:

   ResetReagents()
   {
      plReagents = $;
      plReagents = Cons([&Diamond,1],plReagents);
      plReagents = Cons([&Herbs,5],plReagents);

      return;
   }

   AdjustHealthTime(time=0, iSpellPower=0)
   "Reduces time passed in so that health regens faster.  Returns reduced time."
   {
      local iTime;
      % Bound time to normal HP regen range before applying spell effect
      time = bound(time,1000,60000);
      iTime = (time * (SPELL_POWER_SLOPE - (SPELL_POWER_INTERCEPT + iSpellPower))) / SPELL_POWER_SLOPE;
      return bound(iTime,670,60000);
   }


end
%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
