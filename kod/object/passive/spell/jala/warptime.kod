% Meridian 59, Copyright 1994-2012 Andrew Kirmse and Chris Kirmse.
% All rights reserved.
%
% This software is distributed under a license that is described in
% the LICENSE file that accompanies it.
%
% Meridian is a registered trademark.


%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
WarpTime is Jala

constants:

   include blakston.khd

resources:

   WarpTime_name_rsc = "warp time"
   WarpTime_icon_rsc = iwarptim.bgf
   WarpTime_desc_rsc = \
      "The words of this song form an enchantment which increases the time "
      "that a spellcaster must hold trance to cast a spell."
   WarpTime_StartEnchantment = \
      "A woozy feeling passes through your head, slowing your mental spell preparations, "
      "your thoughts dripping like honey through your brain."
   WarpTime_RemoveEnchantment = \
      "A rushing sensation passes through your head, speeding your mental spell preparations "
      "back to normal."

   Warptime_song = jala4.mid
 
classvars:

   vrName = WarpTime_name_rsc
   vrIcon = WarpTime_icon_rsc
   vrDesc = WarpTime_desc_rsc

   viMana = 5           % Mana is amount used upon initiation
   viManaDrain = 5      % Drain is amount used every `viDrainTime` milliseconds
   viDrainTime = 5000   % Drain some mana every `viDrainTime` milliseconds
   viSpell_num = SID_WARP_TIME

   viSchool = SS_JALA
   viSpell_level = 2

   viHarmful = FALSE

properties:
   
   pbUserEffects = TRUE
   prMusic= warptime_song

messages:

   ResetReagents()
   {
      plReagents = $;

      return;
   }

   StartEnchantment( who = $ )
   "Starts enchantment effect on one player"
   {
      local i, oEnch, state, bFound, tranceTimeLeft;

      if (who = $) OR NOT IsClass(who,&Player) 
      {
         propagate;
      }

      bFound = FALSE;

      for i in Send(who,@GetEnchantmentsByClass,#enchClass=&Trance)
      {
         bFound = TRUE;
         oEnch = Nth(i,2);
         state = $;

         if Length(i) >= 3
         {
            state = Nth(i,3);
         }
         
         tranceTimeLeft = GetTimeRemaining(first(i));
         tranceTimeLeft = Send(self,@GetStretchedTime,#oldTime=tranceTimeLeft,#oRoom=Send(who,@getOwner));

         % break the old trance
         Send(oEnch,@BreakTrance,#who=who,#state=state,#event=EVENT_STEER);
         
         % start a new one, identical save for the stretched time
         Send(who,@StartEnchantment,#what=oEnch,#time=tranceTimeLeft,#addicon=FALSE,#state=state);            
      }

      if bFound
      {
         Send(who,@MsgSendUser,#message_rsc=warptime_StartEnchantment);
      }
      
      propagate;
   }

   GetStretchedTime( oldtime=0, iSpellPower=0, oRoom=$ )
   {
      if oRoom <> $
      {
         iSpellPower = Nth(Send(oRoom,@GetEnchantmentState,#what=self),3);
      }
      
      return oldtime * (100 + iSpellPower) / 100;
   }
   
   GetUnstretchedTime(oldtime=0, iSpellPower=0, oRoom=$)
   {
      if oRoom <> $
      {
         iSpellPower = Nth(Send(oRoom,@GetEnchantmentState,#what=self),3);
      }
      
      return oldtime * 100 / (100 + iSpellPower);
   }

   RemoveEnchantment( who = $ )
   "Called instead of EndEnchantment when trance is broken"
   {
      local i, oEnch, state, bFound, tranceTimeLeft;

      if (who = $) OR NOT IsClass(who,&Player)
      {
         propagate;
      }

      bFound = FALSE;

      for i in Send(who,@GetEnchantmentsByClass,#enchClass=&Trance)
      {
         oEnch = Nth(i,2);
         bFound = TRUE;
         state = $;

         if Length(i) >= 3
         {
            state = Nth(i,3);
         }

         tranceTimeLeft = GetTimeRemaining(first(i));
         tranceTimeLeft = Send(self,@GetUnstretchedTime,#oldTime=tranceTimeLeft,#oRoom=Send(who,@GetOwner));

         % break the old trance
         Send(oEnch,@BreakTrance,#who=who,#state=state,#event=EVENT_STEER);

         % start a new one, identical save for the unstretched time
         Send(who,@StartEnchantment,#what=oEnch,#time=tranceTimeLeft,#addicon=FALSE,#state=state);            
      }

      if bFound
      {
         Send(who,@MsgSendUser,#message_rsc=warptime_RemoveEnchantment);
      }
      
      propagate;
   }

end
%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
