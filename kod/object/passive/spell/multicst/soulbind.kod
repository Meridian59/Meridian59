% Meridian 59, Copyright 1994-2012 Andrew Kirmse and Chris Kirmse.
% All rights reserved.
%
% This software is distributed under a license that is described in
% the LICENSE file that accompanies it.
%
% Meridian is a registered trademark.


%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
SoulBind is MultiCastSpell

constants:

   include blakston.khd

resources:

   SoulBind_name_rsc = "soul bind"
   SoulBind_icon_rsc = isbind.bgf
   SoulBind_desc_rsc = \
      "Infuses an item with a heavenly oratorio.  However, the "
      "binding requires terrific amounts of energy to form.  A heartstone and "
      "dozens of polished serphym and diamonds must be crushed to amplify "
      "the caster's mystical energy, which must then be focused through a "
      "prism.  The item to be soulbound must be targetted.  Once the casting "
      "is complete the item will follow its owner to hell itself."
   
   SoulBind_sound = sbond.wav

   SoulBind_bad_target = "You may only soul bind items to a player."
   SoulBind_no_prism = "The magic energies of the prism do not allow it to be soulbound."
   SoulBind_failed = \
      "You feel the magical bond begin to reach out in vain for one of its targets, "
      "but finding nothing it dissipates."
   SoulBind_already_bonded = "This item is already bonded."
   % Following not yet in use.  Can be used to exclude certain items from bonding
   SoulBind_no_item = "You cannot bind this item to a player."

   SoulBind_successful_casters = "You feel a magical bond form between %s%s and %s."
   SoulBind_successful_bondee = "You feel a magical bond form between your %s and your soul."

classvars:

   vrName = SoulBind_name_rsc
   vrIcon = SoulBind_icon_rsc
   vrDesc = SoulBind_desc_rsc

   vrSucceed_wav = SoulBind_sound

   viMana = 50

   viSpell_num = SID_SOUL_BIND
   viSchool = SS_SHALILLE
   viSpell_level = 5

   viSpellExertion = 50
   viCast_time = 20000   % Removed redundant line item.  This is the time to START casting
   viChance_To_Increase = 66

   % Drain is amount used every viDrainTime milliseconds
   viManaDrain = 1
   viDrainTime = 1000

   % Moved to the proper location from bad template.  Lessen total power needed to speed up casting
   % as desired.  
   % 10 minutes for worst case, 2.3 mins for best case.
   viMultiCast_Spellpower = 6000   

properties:                     


messages:      

   ResetReagents()
   {
      plReagents = $;
      plReagents = Cons([&PolishedSeraphym,50],plReagents);
      plReagents = Cons([&Diamond,50],plReagents);
      plReagents = Cons([&Heartstone,1],plReagents);

      return;
   }
   
   CanPayCosts(who = $, lTargets = $, iSpellPower = 0)
   {
      local oTarget;

      oTarget = First(lTargets);

      if NOT IsClass(oTarget,&Item)
      {
         Send(who,@MsgSendUser,#message_rsc=SoulBind_bad_target);

         return FALSE;
      }

      propagate;
   }

   CastSpell(who = $, lTargets = $, iSpellPower = 0)
   {
      local oTarget;

      oTarget = First(lTargets);

      if NOT IsClass(oTarget,&Item)
      {
         Send(who,@MsgSendUser,#message_rsc=SoulBind_bad_target);

         return FALSE;
      }

      if IsClass(oTarget,&Prism)
      {
         Send(who,@MsgSendUser,#message_rsc=SoulBind_no_prism);

         return FALSE;
      }

      if Send(oTarget,@HasAttribute,#ItemAtt=IA_SOULBOUND)
      {
         Send(who,@MsgSendUser,#message_rsc=SoulBind_already_bonded);

         return FALSE;
      }

      propagate;
   }

   PrismCast(spellpower = 0, lCasters=$, lTargets=$) 
   {
      local oOwner, oCaster;
      oOwner = Send(First(lTargets),@GetOwner);

      Send(Send(SYS,@FindItemAttByNum,#Num=IA_SOULBOUND),@AddToItem,
           #oItem=First(lTargets));

      for oCaster in lCasters
      {
         Send(oCaster,@MsgSendUser,#message_rsc=SoulBind_successful_bondee,
              #parm1=Send(First(lTargets),@GetName));
      }
      
      return;
   }

   GetNumSpellTargets()
   {
      return 1;
   }

end
%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
