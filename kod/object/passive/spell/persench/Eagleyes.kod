% Meridian 59, Copyright 1994-2012 Andrew Kirmse and Chris Kirmse.
% All rights reserved.
%
% This software is distributed under a license that is described in
% the LICENSE file that accompanies it.
%
% Meridian is a registered trademark.


%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
EagleEyes is PersonalEnchantment

constants:

   include blakston.khd

resources:

   include eagleyes.lkod

   EagleEyes_name_rsc = "eagle eyes"
   EagleEyes_icon_rsc = ieagleye.bgf
   EagleEyes_desc_rsc = \
      "Bestows a supernatural acuity of vision on the caster, protecting "
      "against spells that attack sight and improving accuracy at long "
      "range.  Requires three mushrooms and one purple mushroom."
   
   EagleEyes_on_rsc = \
      "Your eyes burn and the world leaps into surreal clarity."
   EagleEyes_off_rsc = "The world seems somehow less distinct and vivid."
   EagleEyes_already_enchanted_rsc = "You already have Eagle Eyes."
   EagleEyes_Success_rsc = "You grant %s%s preternatural sight."
   eagleeyes_enchantment_rsc = \
      "\n\nYour current %s enchantment adds %i offense and 1 damage to your "
      "ranged attacks.  It will completely resist an attempt to render you "
      "sightless %i%% of the time.  Successful attempts to remove your sight "
      "will be reduced by %i%%."
   EagleEyes_resist = \
      "Krannan's magic fights to penetrate the veil of darkness."
   EagleEyes_evade = "The magic of Krannan preserves your sight."
   EagleEyes_resist_caster = \
      "You sense a force resisting your blinding spell."
   EagleEyes_evade_caster = \
      "The magic of Krannan preserves the sight of your target."

   EagleEyes_sound = keagleye.wav

classvars:

   vrName = EagleEyes_name_rsc
   vrIcon = EagleEyes_icon_rsc
   vrDesc = EagleEyes_desc_rsc
   vrSucceed_wav = EagleEyes_sound

   vrAlreadyEnchanted = EagleEyes_already_enchanted_rsc
   vrEnchantment_On = EagleEyes_On_rsc
   vrEnchantment_Off = EagleEyes_Off_rsc
   vrSuccess = EagleEyes_Success_rsc

   viSpell_num = SID_EAGLE_EYES
   viSchool = SS_KRAANAN
   viSpell_level = 4
   viMana = 5
   viSpellExertion = 3

   viCast_time = 2000
   viChance_To_Increase = 20
   viMeditate_ratio = 30

properties:

   % This controls how effective EE is at reducing CC duration.
   % Higher value makes EE less effective.
   % 200 = 50% reduction, 300 = 33% reduction.
   piReductionFactor = 200 % Recommended value 300.

   % What percent of spellpower should we take in order to
   % give the player a chance to completely resist a blinding spell?
   piResistPercent = 5

messages:

   ResetReagents()
   {
      plReagents = $;
      plReagents = Cons([&Mushroom,3],plReagents);
      plReagents = Cons([&PurpleMushroom,1],plReagents);

      return;
   }

   GetStateValue(who=$,iSpellpower=$,target=$)
   {
      local iFactor;

      Send(target,@AddAttackModifier,#what=self);
      iFactor = Bound(iSpellPower+1,10,100);

      return iFactor;
   }

   EndEnchantment(who=$,report=TRUE,state=$)
   {
      Send(who,@RemoveAttackModifier,#what=self);

      propagate;
   }

   GetDuration(iSpellpower=0)
   {
      local iDuration;

      % 2 - 8.6 minutes max.
      iDuration = 120 + (4 * iSpellPower);
      % Convert to ms.
      iDuration = iDuration * 1000;

      return iDuration;
   }

   OfferToNewCharacters()
   {
      return FALSE;
   }

   GetPotionClass()
   {
      return &EagleEyesPotion;
   }

   DoEagleEyes(oCaster=$,oTarget=$,iDuration=0,iFactor=1)
   "Calculates adjustment of blinding spell duration.  Returns $ if resisted "
   "completely.  iFactor is the factor by which we want to increase the effect."
   {
      local i, iNewDuration, iChance, oEagleEyesStrength;

      oEagleEyesStrength = Send(oTarget,@GetEnchantedState,#what=self);

      % Give players a decent chance to improve the spell!
      foreach i in Send(oTarget,@GetPassiveImprovementList)
      {
         if First(i) = self AND random(1,3) = 3
         {
            Post(First(i),@ImproveAbility,#who=oTarget);
         }
      }

      % Check if they repel the blinding spell outright 1-10% base chance,
      % modified by factor.
      iChance = (oEagleEyesStrength * (piResistPercent * iFactor)) / 100;
      if Random(0,100) <= iChance
      {
         Send(oTarget,@MsgSendUser,#message_rsc=EagleEyes_evade);
         if IsClass(oCaster,&Player)
         {
            Send(oCaster,@MsgSendUser,#message_rsc=EagleEyes_evade_caster);
         }

         return $;
      }

      % Duration reduced 0-50%, depending on spellpower.
      iNewDuration = iDuration - (iDuration * oEagleEyesStrength)
                        / piReductionFactor;
      Post(oTarget,@MsgSendUser,#message_rsc=EagleEyes_resist);
      
      if IsClass(oCaster,&Player)
      {
         Post(oCaster,@MsgSendUser,#message_rsc=EagleEyes_resist_caster);
      }

      return iNewDuration;
   }

   %%% Attack Modifier stuff

   ModifyHitRoll(who=$,what=$,hit_roll=$)
   {
      local iFactor, oWeapon;

      iFactor = Send(who,@GetEnchantedState,#what=self);

      oWeapon = Send(who,@LookupPlayerWeapon);

      if oWeapon <> $ AND IsClass(oWeapon,&RangedWeapon)
      {
         return hit_roll + iFactor;
      }

      return hit_roll;
   }

   ModifyDamage(who=$,what=$,damage=$)
   {
      local iFactor, oWeapon;

      iFactor = Send(who,@GetEnchantedState,#what=self);

      oWeapon = Send(who,@LookupPlayerWeapon);

      if oWeapon <> $ AND IsClass(oWeapon,&RangedWeapon)
      {
         return damage + 1;
      }

      return damage;
   }

   EffectDesc(who=$)
   {
      local iState;

      iState = Send(who,@GetEnchantedState,#what=self);

      AddPacket(4,eagleeyes_enchantment_rsc, 4,Send(self,@GetName),
                4,iState, 4,iState * piResistPercent / 100,
                4,100 * iState / piReductionFactor);
      return;
   }

end
%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
