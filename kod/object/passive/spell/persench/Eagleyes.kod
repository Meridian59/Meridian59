% Meridian 59, Copyright 1994-2012 Andrew Kirmse and Chris Kirmse.
% All rights reserved.
%
% This software is distributed under a license that is described in
% the LICENSE file that accompanies it.
%
% Meridian is a registered trademark.


%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
EagleEyes is PersonalEnchantment

constants:

   include blakston.khd

resources:

   EagleEyes_name_rsc = "eagle eyes"
   EagleEyes_icon_rsc = ieagleye.bgf
   EagleEyes_desc_rsc = \
      "Bestows a supernatural acuity of vision on the caster, protecting "
      "against spells that attack sight and improving accuracy at long range.  "
      "Requires mushrooms and purple mushrooms."
   
   EagleEyes_on_rsc = "Your eyes burn and the world leaps into surreal clarity."
   EagleEyes_off_rsc = "The world seems somehow less distinct and vivid."
   EagleEyes_already_enchanted_rsc = "You already have Eagle Eyes."
   EagleEyes_Success_rsc = "You grant %s%s preternatural sight."

   EagleEyes_resist = "Krannan's magic fights to penetrate the veil of darkness."
   EagleEyes_evade = "The magic of Krannan preserves your sight."
   EagleEyes_resist_caster = "You sense a force resisting your blinding spell."
   EagleEyes_evade_caster = "The magic of Krannan preserves the sight of your target."

   EagleEyes_sound = keagleye.wav
   EagleEyes_evade_caster_sound = spelfail.wav

classvars:

   vrName = EagleEyes_name_rsc
   vrIcon = EagleEyes_icon_rsc
   vrDesc = EagleEyes_desc_rsc
   vrSucceed_wav = EagleEyes_sound

   vrAlreadyEnchanted = EagleEyes_already_enchanted_rsc
   vrEnchantment_On = EagleEyes_On_rsc
   vrEnchantment_Off = EagleEyes_Off_rsc
   vrSuccess = EagleEyes_Success_rsc

   viSpell_num = SID_EAGLE_EYES
   viSchool = SS_KRAANAN
   viSpell_level = 4
   viMana = 5
   viSpellExertion = 10

   viCast_time = 2000
   viChance_To_Increase = 20

properties:


messages:

   ResetReagents()
   {
      plReagents = $;
      plReagents = cons([&Mushroom,3],plReagents);
      plReagents = cons([&PurpleMushroom,1],plReagents);

      return;
   }

   GetStateValue(who=$,iSpellpower=$,target = $)
   {
      local iFactor;
      
      Send(target,@AddAttackModifier,#what=self);
      iFactor = bound(iSpellPower+1,10,100);
      
      return iFactor;
   }  

   EndEnchantment(who=$,report=TRUE,state=$)
   {
      Send(who,@RemoveAttackModifier,#what=self);
      
      propagate;
   }

   GetDuration(iSpellpower = 0)
   {
      local iDuration;

      % 2 - 5.3 minutes max.
      iDuration = 120 + (2 * iSpellPower);
      % Convert to ms.
      iDuration = iDuration * 1000;
      
      return random(iDuration/2,iDuration);
   }   

   OfferToNewCharacters()
   {
      return FALSE;
   }

   GetPotionClass()
   {
      return &EagleEyesPotion;
   }

   DoEagleEyes(oCaster=$, oTarget=$, iDuration=0, iFactor=1)
   "Calculates adjustment of blinding spell duration.  Returns $ if resisted "
   "completely.  iFactor adjusts the individual thresholds for dazzle and blind."
   {
      local iNewDuration, iChance, oEagleEyesStrength, iThreshold;

      oEagleEyesStrength = Send(oTarget,@GetEnchantedState,#what=self);
      
      % minimum blind duration before returning an outright repel
      iThreshold = 2500 + ((oEagleEyesStrength + 1) * 25 * iFactor);

      % Duration reduced by up to 2.5 seconds, depending on spellpower.
      iNewDuration = bound(iDuration - (25 * oEagleEyesStrength),iThreshold,15000);
      
      if iNewDuration = iThreshold
      {
         Post(oTarget,@MsgSendUser,#message_rsc=EagleEyes_evade);
         
         if IsClass(oCaster,&Player)
         {
            Post(oCaster,@MsgSendUser,#message_rsc=EagleEyes_evade_caster);
            Post(oCaster,@SomethingWaveRoom,#what=self,#wave_rsc=EagleEyes_evade_caster_sound);
         }
         return $;
      }
      else
      {
         Post(oTarget,@MsgSendUser,#message_rsc=EagleEyes_resist);
         
         if IsClass(oCaster,&Player)
         {
            Post(oCaster,@MsgSendUser,#message_rsc=EagleEyes_resist_caster);
         }
         return iNewDuration;
      }
   }

   %%% Attack Modifier stuff

   ModifyHitRoll(who = $,what = $,hit_roll = $)
   {
      local iFactor, oWeapon;

      iFactor = Send(who,@GetEnchantedState,#what=self);

      oWeapon = Send(who,@LookupPlayerWeapon);

      if oWeapon <> $ AND IsClass(oWeapon,&RangedWeapon)
      {
         return hit_roll + iFactor;
      }

      return hit_roll;
   }

   ModifyDamage(who = $,what = $,damage = $)
   {
      local iFactor, oWeapon;

      iFactor = Send(who,@GetEnchantedState,#what=self);

      oWeapon = Send(who,@LookupPlayerWeapon);

      if oWeapon <> $ AND IsClass(oWeapon,&RangedWeapon)
      {
         return damage + 1;
      }

      return damage;
   }


end
%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
