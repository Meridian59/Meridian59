% Meridian 59, Copyright 1994-2012 Andrew Kirmse and Chris Kirmse.
% All rights reserved.
%
% This software is distributed under a license that is described in
% the LICENSE file that accompanies it.
%
% Meridian is a registered trademark.


%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
ResistanceEnchantment is PersonalEnchantment

constants:

   include blakston.khd

resources:

classvars:

   viPersonal_ench = True
   viFlash = FLASH_GOOD_SELF

   viResistanceType = -ATCK_SPELL_ALL

   vrAlreadyEnchanted = $
   vrEnchantment_On = $
   vrEnchantment_Off = $
   vrSuccess = $

properties:

messages:

   CanPayCosts(who = $, lTargets = $)
   {
      
      lTargets = [who];
         
      if (vrAlreadyEnchanted = $) or (vrEnchantment_On = $) or (vrEnchantment_Off = $) or (vrSuccess = $)
      {
         DEBUG("Haven't set messages for this spell!  Aborting cast.");
         
         return FALSE;
      }

      propagate;
   }

   GetStateValue(who = $, iSpellPower = 0, target = $)
   {
      local iValue;
      
      iValue = send(self,@GetResistanceStrength,#iSpellPower=iSpellPower);
      send(self,@AddResistances,#who=target,#value=iValue);
      
      return iValue;
   }

   AddResistances(who=$,value=$)
   {
      Send(who,@AddResistance,#what=viResistanceType,#value=value);
      
      return;
   }

   GetResistanceStrength(iSpellpower = 0)
   {
      %% strength varies from 1 to 50
      return iSpellPower/2 + 1;
   }

   GetDuration(iSpellPower = 0)
   {
      local iDuration;	   %%% 5 to 10 minutes
      
      iDuration = 300 + iSpellPower * 3;
      iDuration = iDuration * 1000;	  %% translate to milliseconds
      
      return iDuration;
   }

   EndEnchantment(who = $, report = TRUE, state = -1)
   {
      if state = -1  { DEBUG("Bad data!"); state = 0; }
      Send(self,@RemoveResistances,#who=who,#value=state);

      propagate;
   }

   RemoveResistances(who=$,value=$)
   {
      Send(who,@RemoveResistance,#what=viResistanceType,#value=value);
      
      return;
   }

   GetNumSpellTargets()
   {
      return 0;
   }
   
end

%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
