% Meridian 59, Copyright 1994-2012 Andrew Kirmse and Chris Kirmse.
% All rights reserved.
%
% This software is distributed under a license that is described in
% the LICENSE file that accompanies it.
%
% Meridian is a registered trademark.


%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
TouchAttackSpell is PersonalEnchantment

% A touch attack spell is essentially a 'combat stroke' enchantment.

constants:

   include blakston.khd

   HITVALUE_MAX = 100
   POISON_DURATION = 30000 %%% in milliseconds
   POISON_LOSSRATE = 10000 %%% in health points * 10^-4 / second

resources:

   TouchAttackSpell_name_rsc = "Touch Spell"
   TouchAttackSpell_icon_rsc = light.bgf
   TouchAttackSpell_desc_rsc = "This is a touch spell that does damage."

   TouchAttack_Succeed_rsc = \
      "You have cast a touch attack on another player.  This is a bug.  "
      "Please report it.  Have a nice day."
   TouchAttack_missed1_sound = swswish1.wav
   TouchAttack_missed2_sound = swswish2.wav
   TouchAttack_missed3_sound = swswish3.wav
   
   TouchAttack_hit_sound = patk.wav
   
   purger_worked_user = "Your violent blast of electricity disrupts the magic surrounding %s%s!"
   purger_worked_target = "%s%s blasts you with electricity, disrupting the magic surrounding you!"
   poison_worked_user = "Your acidic touch eats into %s%s's flesh, polluting %s blood!"
   poison_worked_target = "%s%s's acidic touch eats into your flesh and pollutes your blood!"
   blinder_worked = "%s%s stumbles away, screaming and clutching %s eyes!"
   holder_worked = "%s%s is frozen solid, unable to move even a finger."
   heal_worked = "Shal'ille aids you in battle, healing you for ~g~B%i~B~n damage."

classvars:

   vrName = TouchAttackSpell_name_rsc
   vrIcon = TouchAttackSpell_icon_rsc
   vrDesc = TouchAttackSpell_desc_rsc

   vrMiss_sound = $
   
   vrMissed_message_attacker = $
   vrMissed_message_defender = $

   vrAlreadyEnchanted = PersonalEnchantment_already_enchanted_rsc
   vrEnchantment_On = PersonalEnchantment_On_rsc
   vrEnchantment_Off = PersonalEnchantment_Off_rsc
   vrSuccess = PersonalEnchantment_Success_rsc
   vrSucceed = TouchAttack_Succeed_rsc

   vrPlayer_hit_something = $
   vrPlayer_was_hit = $
   vrPlayer_killed_something = $
   vrPlayer_was_killed = $

   viIndefinite = ARTICLE_NONE
   viDefinite = ARTICLE_NONE

   viSchool = 0
   viSpell_num = 0
   viStroke = 0

   viSpellExertion = 2

   viChance_To_Increase = 10

   % typically, spells have hit probs that are high, provided you're good at
   %  the skill.
   viHit_Factor = 80

   % Touch spells typically do good damage but are unaffected by typical weapon
   %  bonuses.
   viMin_Damage = 3
   viMax_Damage = 6

   % Touch spell range can vary, but are typically good     
   viRange = 2

   % Touch spells are now linked to punch attacks, so make it do punch damage
   %  for default.
   viAttackType = ATCK_WEAP_UNARMED+ATCK_WEAP_PUNCH
   % Touch spells typically DO have a spell type.
   viSpellType = 0          

   viPostCast_time = 1

   % Cannot enchant others with touch attacks.
   vbCanCastOnOthers = FALSE

properties:

   plPrerequisites = $
   plReagents = $
   
   % The following are chances to proc special effects
   piBlind_chance = 5
   piHold_chance = 5
   piPurge_chance = 5
   piPoison_chance = 5
   piHeal_chance = 5
   
messages:

   ImproveStroke(who=$,target=$)
   {
      Send(self,@ImproveAbility,#who=who,#target=target);
      
      return;
   }

   CanPayCosts(who = $, lTargets = $, iSpellPower = 0)
   {
      propagate;
   }

   CastSpell(who = $,iSpellPower=0,lTargets=$)
   {
      % Remove other touch attack spells.  Won't remove self, because
      %  we've checked for re-enchantment in CanPayCosts.
      Send(who,@RemoveEnchantmentClass,#class=&TouchAttackSpell);
      
      propagate;
   }

   GetNumSpellTargets()
   {
      return 0;
   }

   CheckWeaponAndDoAnimation(who=$,weapon_used=$,victim=$)
   "Spell does not require a weapon, but a player may use this spell even"
   "if they are armed."
   "Also does the cast animation.  In future, may provide different grfx"
   "for different touch spells."
   {
      Send(who,@DoCast);
      
      return TRUE;
   }

   ToHitImprovers(raw_hitroll=0)
   "A weapon stroke may have IMPROVERS, which may boost the performance of "
   "the stroke.  Example, if a player has renweed, that may boost his thrust "
   "Default, return the hitroll raw."
   {
      return Raw_hitroll; 
   }

   DamageFactors(damage = 0, who = $)
   "Attack spells typically are unaffected by strength, although they may "
   "be affected by other factors."
   {
      return damage;
   }

   GetHitsound()
   {
      return touchattack_hit_sound;
   }

   %%% This section for combat

   %%% These are the weapon functions

   % What's the stroke number?
   % Touch attacks are "backwards", they use Punch as the proficiency, or the
   %  secondary bonus to hit.  Added bonus for "pure" mages, can substitute
   %  1.5 * Mysticism instead of punch.  This allows for a more focused
   %  character, but you can reach higher levels if you learn punch.
   GetProf(who=$)
   {
      local iProf, iMysticism;

      iProf = Send(who,@GetSkillAbility,#skill_num=SKID_PUNCH);
      Send(who,@FlipSkillAtrophyFlag,#SKID=SKID_PUNCH);
      
      iMysticism = (Send(who,@GetMysticism)*3)/2;
      
      if iProf < iMysticism
      {
         iProf = iMysticism;
      }

      return iProf;
   }

   GetStroke(who=$)
   {
      % Touch attacks are "backwards", they use the specialized spell as the
      %  stroke, or primary bonus to hit.
      Send(who,@FlipSpellAtrophyFlag,#SID=viSpell_num);

      return Send(who,@GetSpellAbility,#spell_num=viSpell_num);
   }

   GetAttackType(weapon_used=$)
   {
      return viAttackType;
   }

   GetAttackSpell(weapon_used=$)
   {
      return viSpellType;
   }

   GetParryAbility()
   {
      % Touch attacks have no parrying ability.
      return 0;
   }

   %%% These are the stroke functions

   % Can this touch attack be blocked?
   CanBlock()
   {
      return TRUE;
   }

   % Can this touch attack be parried?
   CanParry()
   {
      return TRUE;
   }

   % Can this touch attack be blocked?
   CanDodge()
   {
      return TRUE;
   }

   % This returns the "weapon" name for combat attacks.
   GetAttackName()
   {
      return vrName;
   }

   GetRange(weapon_used = $)
   "This returns the range of the spell.  Usually, this hovers around 2-3."
   {
      return viRange;
   }

   CheckSpecial(who=$,victim=$)
   "Checks to see if there are any special circumstances which prevents"
   "the target player from performing this spell."
   {
      return TRUE;
   }

   FindDamage(weapon_used=$,who=$,victim=$)
   "Damage for attack spells is determined largely by expertise in the spell."
   {
      local i, damage, iMysticism, iAbility, iProcfactor;

      % base weapon damage
      damage = random(viMin_Damage,viMax_damage);
      iProcfactor = 10;

      for i in send(who,@GetPlayerUsing)
      {
         if IsClass(i,&JewelOfFroz)
         {
            % Each jewel of Froz adds up to 3 to base damage depending on HPs.
            damage = damage + bound(send(who,@GetBaseMaxHealth)-40,0,60)/20;
            % Each equipped JoFincreases the chance to proc effects.
            iProcfactor = iProcfactor + 10;
         }
      }
      
      % scale the basedamage with the caster's ability in the spell
      iAbility = Send(who,@GetSpellAbility,#spell_num=viSpell_num);
      damage = damage * (iAbility + 1)/100;
      
      % scale proficiency bonus with HPs instead of spell ability
      damage = damage + bound(send(who,@GetBaseMaxHealth)-50,0,50)/10;
      
      % touchspells are boosted by mysticism instead of might
      iMysticism = send(who,@GetMysticism);
      damage = damage + ((100+bound(iMysticism-25,0,40))*damage)/100;
      
      % allow individual adjustments by a specific spell
      damage = Send(self,@DamageFactors,#damage=damage,#who=who,
                          #victim=victim);
      
      % check for a proc if we have a JoF equipped
      if iProcfactor <> 0
      {
         Send(who,@DoTouchProc,#spell_num=viSpell_num,#procfactor=iProcfactor,#target=victim,#damage=damage,#who=who);
      }
      
      return bound(damage,1,$);
   }

   DoTouchProc(spell_num = $, procfactor = 0, target=$, damage=0, who=$)
   "Here, we handle procs that JoFs unlock within touch spells."
   {
      local oSpell;
      
      procfactor = procfactor * bound(send(who,@GetBaseMaxHealth)-50,0,50)/50;
      
      if spell_num = SID_ZAP
      {
         oSpell = send(SYS,@FindSpellByNum,#num=SID_PURGE);
         
         if random(1,1000) <= piPurge_chance*procfactor
         {
            %% Gotta have something to try to remove....
            if Send(target,@IsEnchanted)
            {
               % Try to purge off random(25,75)% of the enchantments.
               if send(oSpell,@DoPurge,#who=target,#iChance=random(25,75))
               {
                  % Only tell our victim if they lost enchantments.
                  send(target,@MsgSendUser,#message_rsc=purger_worked_target,
                       #parm1=send(who,@GetCapDef),#parm2=send(who,@GetName));
               }

               % Tell the zapper.
               send(who,@MsgSendUser,#message_rsc=purger_worked_user,
                    #parm1=send(target,@GetCapDef),#parm2=send(target,@GetName));
            }
         }
      }
      
      if spell_num = SID_TOUCH_OF_FLAME
      {
         oSpell = send(SYS,@FindSpellByNum,#num=SID_BLIND);
         
         if random(1,1000) <= piBlind_chance*procfactor
         {
            %% no duplicates
            if not Send(target,@IsEnchanted,#what=oSpell)
            {        
               send(oSpell,@CastSpell,#who=self,#ltargets=[target],#iSpellPower=50);
               send(who, @MsgSendUser, #message_rsc = blinder_worked,
                     #parm1=send(target,@GetCapDef),#parm2=send(target,@GetName),
                     #parm3=send(target,@GetHisher));
            }
         }
      }
      
      if spell_num = SID_ICY_FINGERS
      {
         oSpell = send(SYS,@FindSpellByNum,#num=SID_HOLD);

         if random(1,1000) <= piHold_chance*procfactor
         {
            %% no duplicates
            if not Send(target,@IsEnchanted,#what=oSpell)
            {        
               send(oSpell,@CastSpell,#who=self,#ltargets=[target],#ispellpower=99);
               send(who,@MsgSendUser,#message_rsc=holder_worked,
                  #parm1=send(target,@GetCapDef),#parm2=send(target,@GetName));
            }
         }
      }

      if spell_num = SID_ACID_TOUCH
      {
         oSpell = Send(SYS,@FindSpellByNum,#num=SID_POISON);
         
         if random(1,1000) <= piPoison_chance*procfactor
         {

            send(oSpell,@MakePoisoned,#who=target,
                #lossrate=POISON_LOSSRATE,#duration=POISON_DURATION);
            send(who,@MsgSendUser,#message_rsc=poison_worked_user,
                 #parm1=send(target,@GetCapDef),#parm2=send(target,@GetName),
                 #parm3=send(target,@GetHisher));
            send(target,@MsgSendUser,#message_rsc=poison_worked_target,
                 #parm1=send(target,@GetCapDef),#parm2=send(target,@GetName));
         }
      }

      if spell_num = SID_HOLY_TOUCH
      {
         if random(1,1000) <= piHeal_chance*procfactor
         {
            send(who,@GainHealth,#amount = damage);
            send(who,@MsgSendUser,#message_rsc=heal_worked,#parm1=damage);
         }

      }
      return damage;
   }

   PlayerWasHitMsg(who=$,attacker=$,damage=$)
   "The stroke may choose it's own damage give or recieve message.  Otherwise "
   "player.kod will just choose a default - which is true for most weapon-"
   "based combat strokes (but not true for most punch strokes or touch spells."
   {
      if vrPlayer_was_hit = $
      {
         return FALSE;
      }

      Send(who,@MsgSendUser,#message_rsc=vrPlayer_was_hit,
           #parm1=Send(attacker,@GetCapDef),#parm2=Send(attacker,@GetName));
           
      return TRUE;
   }

   PlayerHitSomethingMsg(who=$,victim=$,damage=$,color_rsc=$)
   "The stroke may choose it's own damage give or recieve message.  Otherwise "
   "player.kod will just choose a default - which is true for most weapon-"
   "based combat strokes (but not true for most punch strokes or touch spells."
   {
      if vrPlayer_hit_something = $
      {
         return FALSE;
      }
       
      Send(who,@MsgSendUser,#message_rsc=vrPlayer_hit_something,
           #parm1=color_rsc,#parm2=Send(victim,@GetDef),
           #parm3=Send(victim,@GetName));
          
      return TRUE;
   }

   SendMissMessageToAttacker(who=$,victim=$,color_rsc=$)
   {
      local iRandom, rSound;
      
      % 'miss' sound
      if vrMiss_Sound = $
      {
         % Play a random miss sound.
         iRandom = random(1,3);
         if iRandom = 1
         {
            rSound = TouchAttack_missed1_sound;
         }
         else
         {
            if iRandom = 2
            {
               rSound = TouchAttack_missed2_sound;
            }
            else
            {
               rSound = TouchAttack_missed3_sound;
            }
         }
      }
      else
      {
         rSound = vrMiss_Sound;
      }
      
      Send(Send(who,@GetOwner),@SomethingWaveRoom,#what=who,#wave_rsc=rSound);
      
      % Text messages
      if vrMissed_Message_Attacker <> $
      {
         Send(who,@MsgSendUser,#message_rsc=vrMissed_Message_Attacker,
              #parm1=color_rsc,#parm2=vrName,#parm3=Send(victim,@GetDef),
              #parm4=Send(victim,@GetName));
              
         return TRUE;
      }
      
      return;
   }

   SendMissMessageToDefender(who=$,attacker=$)
   {
      if vrMissed_message_Defender <> $
      {
         Send(who,@MsgSendUser,#message_rsc=vrMissed_Message_Defender,
              #parm1=Send(attacker,@GetCapDef),#parm2=Send(attacker,@GetName),
              #parm3=vrName);
              
         return TRUE;
      }

      return;
   }

   PlayerKilledSomethingMsg(who=$,victim=$,damage=$)
   "The stroke may choose it's own damage give or recieve message.  Otherwise"
   "player.kod will offer the default 'You killed the whatever' message."
   {
       if vrPlayer_killed_Something = $
       {
         return FALSE ;
       }
       
       Send(who,@MsgSendUser,#message_rsc=vrPlayer_killed_something,
            #parm1=Send(victim,@GetCapDef),#parm2=Send(victim,@GetName));
                
       return TRUE;
   }

   PlayerWasKilledMsg(who=$,attacker=$,damage=$)
   "The stroke may choose it's own damage give or recieve message.  Otherwise"
   "player.kod will just choose a default - as it does for most strokes."
   {
       if vrPlayer_was_hit = $
       {
         return FALSE ;
       }
       
       Send(who,@MsgSendUser,#message_rsc=vrPlayer_was_killed,
            #parm1=Send(attacker,@GetDef),#parm2=Send(attacker,@GetName));
            
       return TRUE;
   }

   %%% Enchantment support

   GetDuration(iSpellpower=$)
   {
      local iDuration;

      iDuration = 60;
      % 60 minutes flat
      iDuration = iDuration * 60 * 1000;  

      return iDuration;
   }  

   GetStateValue(iSpellpower=$)
   {
      Return iSpellpower;
   }


end
%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
