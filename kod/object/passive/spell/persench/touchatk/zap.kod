% Meridian 59, Copyright 1994-2012 Andrew Kirmse and Chris Kirmse.
% All rights reserved.
%
% This software is distributed under a license that is described in
% the LICENSE file that accompanies it.
%
% Meridian is a registered trademark.


%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
Zap is TouchAttackSpell

constants:

   include blakston.khd

resources:

   Zap_name_rsc = "zap"
   Zap_icon_rsc = izap.bgf
   Zap_desc_rsc = \
      "Channels natural electrical energy into a powerful charge transmitted with a touch.  "
      "Requires blue mushrooms to cast."

   Zap_already_enchanted_rsc = "Your hands already crackle with electric charge."
   Zap_On_rsc = "Sparks jump and crackle from your fingertips."
   Zap_Off_rsc = "Your fingers are no longer charged with electrical power."

   zap_player_was_hit = "%s%s's touch jolts you with a surge of electric current!"
   zap_player_hit_something = "%sThe smell of burning flesh fills your nostrils as your touch singes %s%s!"
   zap_player_was_killed = "%s%s's electric touch robs the last bit of life from your failing body!"
   zap_player_killed_something = "%s%s lets out a final scream in protest to your electric touch."
   zap_spell_intro = "Faren Lv. 1: Channels natural electrical energy into a powerful charge which singes the target."
   
   zap_sound = fzap.wav

   ta_purge_worked_user = "The violent blast of electricity disrupts the magic surrounding %s%s!"
   ta_purge_worked_target = "%s%s blasts you with electricity, disrupting the magic surrounding you!"
	
classvars:

   vrName = Zap_name_rsc
   vrIcon = Zap_icon_rsc
   vrDesc = Zap_desc_rsc

   vrAlreadyEnchanted = Zap_already_enchanted_rsc
   vrEnchantment_On = Zap_On_rsc
   vrEnchantment_Off = Zap_Off_rsc

   vrPlayer_was_hit = zap_player_was_hit
   vrPlayer_hit_something = zap_player_hit_something
   vrPlayer_was_killed = zap_player_was_killed
   vrPlayer_killed_something = zap_player_killed_something

   vrSpell_intro = zap_spell_intro

   viIndefinite = ARTICLE_NONE
   viDefinite = ARTICLE_NONE

   viSchool = SS_FAREN
   viSpell_num = SID_ZAP
   viSpellExertion = 2
   viSpell_level = 1
   viMana = 6

   viSpellType = ATCK_SPELL_SHOCK + ATCK_SPELL_ALL

   vbAutomatic = FALSE

   vrSucceed_wav = zap_sound

properties:

   plPrerequisites = $
   plReagents = $

messages:

  ResetReagents()
  {
     plReagents = $;
     plReagents = cons([&BlueMushroom,1],plReagents);
     return;
  }

  GetProf(who=$)
  {
     return Send(who,@GetAbility,#num=SID_MANA_FOCUS) * bound(Send(who,@GetBaseMaxHealth),0,100) / 100;
  }

   PlayerWasHitMsg(who=$,attacker=$,damage=$)
   "The stroke may choose it's own damage give or recieve message.  Otherwise"
   "player.kod will just choose a default - which is true for most weapon-based"
   "combat strokes (but not true for most punch strokes or touch spells."
   {
      if vrPlayer_was_hit = $
        { return FALSE; }
      send(who,@msgsenduser,#message_rsc=vrPlayer_was_hit,
           #parm1=send(attacker,@getdef),#parm2=send(attacker,@getname));

      return TRUE;
   }

	DoSpellProc(damage=0,who=$,victim=$,prof=0)
	{
		local oSpell;
	
		% Purge the victim.
		oSpell = send(SYS,@FindSpellByNum,#num=SID_PURGE);

		if random(1,1000) <= prof*viSpell_proc_chance/100
		{
			%% Gotta have something to try to remove....
			if Send(victim,@IsEnchanted)
			{
				% Try to purge off random(25,75)% of the enchantments.
				if send(oSpell,@DoPurge,#who=victim,#iChance=random(25,75))
				{
					% Only tell our victim if they lost enchantments.
					send(victim,@MsgSendUser,#message_rsc=ta_purge_worked_target,
						  #parm1=send(who,@GetCapDef),#parm2=send(who,@GetName));
				}

				% Tell the zapper.
				send(who,@MsgSendUser,#message_rsc=ta_purge_worked_user,
					  #parm1=send(victim,@GetDef),#parm2=send(victim,@GetName));
			}
		}

		return;
	}
	
end
%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
