% Meridian 59, Copyright 1994-2012 Andrew Kirmse and Chris Kirmse.
% All rights reserved.
%
% This software is distributed under a license that is described in
% the LICENSE file that accompanies it.
%
% Meridian is a registered trademark.


%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
RadiusEnchantment is Spell

% Radius Enchantments are spells that apply an effect to players within a certain distance of the caster.
% Caster must typically maintain a trance, but requirements to keep the trance are variable.
%
% Internally, the trance uses a state of the form [time elapsed, players enchanted, power, range].
% This should not be altered. It is used to track the radius and who should be affected.
% Power and Range have their own Calculate functions if they need to be modified.
%
% EnterRadius and LeaveRadius add or remove from a player's plRadiusEnchantments list.
% Each element is of the form [spell object, spellpower, caster].
% These states should be used when effects are actually being performed.
%
% Useful Radius enchantment functions to call (these are in Player.kod):
% IsAffectedByRadiusEnchantment(what=$,byClass=&RadiusEnchantment)  -- returns TRUE or FALSE
% GetMostPowerfulRadiusEnchantmentState(byClass=&RadiusEnchantment) -- returns [spell object, power, caster] with highest power
% GetRadiusEnchantments()                                           -- returns player's list of [spell object, power, caster]

constants:

   include blakston.khd
   
   RADIUS_CHECK_TIME = 250   % Time between checks for who is affected, no worse than an active object lag-wise

resources:

   radius_ench_default_cast = "A circle of magic expands outward from you."
   radius_ench_default_starts = "A circle of magic expands outward from %s."
   radius_ench_default_ends = "The circle of magic maintained by %s collapses."
   radius_ench_default_caster_ends = "Your circle of magic collapses."
   radius_ench_default_caster_enter = "You feel your circle of magic having an effect."
   radius_ench_default_enter = "You enter a circle of magical effect maintained by %s."
   radius_ench_default_leave = "You leave a circle of magical effect maintained by %s."
   radius_ench_no_newbie = \
      "Your guardian angel tells you, \"You are not ready to cast spells "
      "which may hinder other players.\""
      
   radius_ench_default_already_cast = "You are already maintaining that radius enchantment."

   radius_ench_need_instrument = "You need an instrument!"

   radius_ench_old_style_aleady_cast = "You are already under the effects of that magic."

   radius_ench_cant_fight_here = "You can't fight here."
   
classvars:

   radius_ench_already_cast = radius_ench_default_already_cast
   
   radius_ench_cast = radius_ench_default_cast
   radius_ench_starts = radius_ench_default_starts
   radius_ench_ends = radius_ench_default_ends
   radius_ench_caster_ends = radius_ench_default_caster_ends
   radius_ench_caster_enter = radius_ench_default_caster_enter
   radius_ench_enter = radius_ench_default_enter
   radius_ench_leave = radius_ench_default_leave

   viSchool = SS_JALA
   viNeedInstrument = FALSE

   viMana = 5
   viManaDrain = 3
   viDrainTime = 5000
   viPostCast_time = 1
   viBaseRange = 5

   viLightColor = LIGHT_SPECTRAL
   viBaseLightIntensity = 60

	% Chance to increase is one in viChance_To_Increase.
   viChance_To_Increase = 6

   viAffectsCasterOnly = FALSE  % Override for self-only spell
   viAffectsEveryone = FALSE    % Override for affect all

   viAffectsCaster = TRUE       % Caster is affected by the spell
   viAffectsGuildmates = FALSE  % Guildmates of caster
   viAffectsAllies = FALSE      % Members of guilds allied with caster's guild
   viAffectsEnemies = FALSE     % Hostile, can cause outlaw status
   viAffectsInnocents = FALSE   % Unaffiliated white characters
   viAffectsNewbies = FALSE     % Angeled characters
                                
   viCasterPersist = FALSE      % If true, spell will 'follow' caster between rooms

properties:
   
   piOldAreaEnchStyle = FALSE   % If true, spell functions as an old-style Area Enchantment
                                % No trance required, and will remain in initial room for duration

messages:

   ResetReagents()
   {
      return;
   }

   GetNumSpellTargets()
   {
      % Enchantment is applied by radius, not by target
      return 0;
   }

   CanPayCosts(who=$, lTargets=$)  
   {
      local i, each_obj, oRoom;

      oRoom = Send(who,@GetOwner);
      if oRoom = $
         OR NOT IsClass(oRoom,&Room)
      {
         % Don't cast without a room, may create hanging objects
         return FALSE;
      }

      if viNeedInstrument
      {
          if Send(who,@GetInstrumentLevel) = 0
          { 
             Send(who,@MsgSendUser,#message_rsc=radius_ench_need_instrument);
             return FALSE; 
          }
      }
      
      if piOldAreaEnchStyle
         AND IsClass(who,&User)
         AND Send(who,@IsAffectedByRadiusEnchantment,#what=self)
      {
         Send(who,@MsgSendUser,#message_rsc=radius_ench_old_style_aleady_cast);
         return FALSE;
      }

      if Send(oRoom,@CheckRoomFlag,#flag=ROOM_NO_COMBAT)
         AND viAffectsEnemies
      {
         if IsClass(who,&User)
         {
            Send(who,@MsgSendUser,#message_rsc=radius_ench_cant_fight_here);
         }
         return FALSE;
      }
      
      % Make sure monsters don't spam area enchantments of the same type
      if piOldAreaEnchStyle
         OR IsClass(who,&Monster)
      {
         for i in Send(oRoom,@GetplActive)
         {
            each_obj = Send(oRoom,@HolderExtractObject,#data=i);
            if IsClass(each_obj,&RadiusEnchantmentTotem)
               AND Send(each_obj,@GetSpell) = self
            {
               return FALSE;
            }
         }
      }

      propagate;
   }

   CastSpell(who=$, iSpellPower=0)
   "Initiation point for the spell."
   {
      local i, lMaintaining, iState, oObj, oUser, oCasterGuild, oRoom, iDrain, lEnchanted, iPower, iRange;

      % Old style Area Enchantments use a RadiusEnchantmentTotem as the center of the spell.
      % This creates a timed invisible object that perpetuates the spell regardless of what the player does.
      % The totem is linked to the caster for all characteristics (flags and affiliations).
      if (piOldAreaEnchStyle
         AND IsClass(who,&User))
         OR IsClass(who,&Monster)
      {
         Create(&RadiusEnchantmentTotem,#totem_spell=self,#totem_caster=who,#totem_power=iSpellPower);
         propagate;
      }

      iDrain = 0;
      lEnchanted = $;
      iPower = Send(self,@CalculatePower,#iSpellPower=iSpellPower);
      iRange = Send(self,@CalculateRange,#iSpellPower=iSpellPower);

      % Cancel previous trance if this spell requires one as well
      if NOT piOldAreaEnchStyle
         AND IsClass(who,&User)
         AND Send(who,@IsEnchanted,#byClass=&RadiusEnchantment)
      {
         lMaintaining = Send(who,@GetEnchantmentsByClass,#enchClass=&RadiusEnchantment);
         for i in lMaintaining
         {
            iState = Send(who,@GetEnchantedState,#what=Nth(i,2));
            Send(Nth(i,2),@BreakTrance,#who=who,#state=iState,#event=EVENT_STEER);
         }
      }

      oRoom = Send(who,@GetOwner);
      oCasterGuild = Send(who,@GetGuild);

      for oObj in Send(oRoom,@GetplActive)
      {
         oUser = Send(oRoom,@HolderExtractObject,#data=oObj);

         if IsClass(oUser,&User)
         {
            if oUser = who
            {
               Send(oUser,@MsgSendUser,#message_rsc=radius_ench_cast);
               Send(oUser,@SetFlickerFlag);
            }
            else
            {
               Send(oUser,@MsgSendUser,#message_rsc=radius_ench_starts,#parm1=Send(who,@GetName));
            }

            % Important note: FindListElem returns nil on a passed nil value, not zero.
            if (lEnchanted = $ OR FindListElem(lEnchanted,oUser) = 0)
               AND (Send(who,@SquaredDistanceTo,#what=oUser) <= (iRange * iRange)
                   OR piOldAreaEnchStyle)
            {
               if Send(self,@TargetIsValid,#target=oUser,#caster=who)
               {
                  Send(self,@EnterRadius,#who=oUser,#iPower=iPower,#caster=who);
                  lEnchanted = cons(oUser,lEnchanted);
                  if oUser = who
                  {
                     Send(oUser,@MsgSendUser,#message_rsc=radius_ench_caster_enter);
                  }
                  else
                  {
                     Send(oUser,@MsgSendUser,#message_rsc=radius_ench_enter,#parm1=Send(who,@GetName));
                  }
               }
            }
         }
      }

      % Put spell maintenance info in caster's enchantment list.
      Send(who,@StartEnchantment,#what=self,#time=RADIUS_CHECK_TIME,
           #state=[iDrain,lEnchanted,iPower,iRange]);      

      propagate;
   }

   TargetIsValid(target=$,caster=$)
   {
      if viAffectsCasterOnly
      {
         if target=caster
         {
            return TRUE;
         }
         else
         {
            return FALSE;
         }
      }
      
      if viAffectsEveryone
      {
         return TRUE;
      }

      if target = caster
      {
         if viAffectsCaster
         {
            return TRUE;
         }
         else
         {
            return FALSE;
         }
      }
      
      if viAffectsGuildmates
         AND Send(target,@GetGuild) <> $
         AND Send(target,@GetGuild) = Send(caster,@GetGuild)
      {
         return TRUE;
      }
      
      if viAffectsAllies
         AND Send(target,@GetGuild) <> $
         AND Send(Send(target,@GetGuild),@IsAlly,#otherguild=Send(caster,@GetGuild))
      {
         return TRUE;
      }

      if viAffectsEnemies
         AND Send(caster,@AllowPlayerAttack,#victim=target,#report=FALSE)
      {
         return TRUE;
      }
   
      if viAffectsNewbies
         AND NOT Send(target,@CheckPlayerFlag,#flag=PFLAG_PKILL_ENABLE)
      {
         return TRUE;
      }

      if viAffectsInnocents
         AND Send(target,@CheckPlayerFlag,#flag=PFLAG_PKILL_ENABLE)
         AND NOT Send(target,@CheckPlayerFlag,#flag=PFLAG_OUTLAW)
         AND NOT Send(target,@CheckPlayerFlag,#flag=PFLAG_MURDERER)
      {
         return TRUE;
      }

      return FALSE;
   }

   EnterRadius(who=$,iPower=0,caster=$)
   {      
      Send(who,@AddRadiusEnchantment,#what=self,#iPower=iPower,#caster=caster);
      Send(self,@StartSpecialEffect,#who=who,#iPower=iPower,#caster=caster);

      return;
   }
   
   LeaveRadius(who=$,iPower=0,caster=$)
   {      
      Send(who,@RemoveRadiusEnchantment,#what=self,#iPower=iPower,#caster=caster);
      Send(self,@EndSpecialEffect,#who=who,#iPower=iPower,#caster=caster);
      
      return;
   }
   
   StartSpecialEffect(who=$,iPower=0,caster=$)
   {
      return;
   }
   
   EndSpecialEffect(who=$,iPower=0,caster=$)
   {
      return;
   }

   EndEnchantment( who = $, state = $ )
   "Called only for caster. Refreshes itself to recalculate affected players."
   {
      local iDrain, lEnchanted, iPower, iRange, oUser, oCasterGuild, oRoom, lActive;

      iDrain = Nth(state,1);
      lEnchanted = Nth(state,2);
      iPower = Nth(state,3);
      iRange = Nth(state,4);

      oRoom = Send(who,@GetOwner);
      oCasterGuild = Send(who,@GetGuild);

      % Check for players that have left the radius of effect
      for oUser in lEnchanted
      {
         if (Send(who,@SquaredDistanceTo,#what=oUser) > (iRange * iRange)
            AND NOT piOldAreaEnchStyle)
            OR Send(who,@GetOwner) <> Send(oUser,@GetOwner)
            OR NOT Send(self,@TargetIsValid,#target=oUser,#caster=who)
         {
            Send(self,@LeaveRadius,#who=oUser,#iPower=iPower,#caster=who);
            lEnchanted = DelListElem(lEnchanted,oUser);
            Send(oUser,@MsgSendUser,#message_rsc=radius_ench_leave,#parm1=Send(who,@GetName));
         }
      }
      
      % Check for players that have entered the radius of effect
      for lActive in send(oRoom,@GetplActive)
      {
         oUser = Send(oRoom,@HolderExtractObject,#data=lActive);

         if IsClass(oUser,&User)
            AND (lEnchanted = $ OR FindListElem(lEnchanted,oUser) = 0)
            AND (Send(who,@SquaredDistanceTo,#what=oUser) <= (iRange * iRange)
                OR piOldAreaEnchStyle)
         {
            if Send(self,@TargetIsValid,#target=oUser,#caster=who)
            {
               send(self,@EnterRadius,#who=oUser,#iPower=iPower,#caster=who);
               lEnchanted = cons(oUser,lEnchanted);
               Send(oUser,@MsgSendUser,#message_rsc=radius_ench_enter,#parm1=Send(who,@GetName));
            }
         }
      }

      if IsClass(who,&User)
         AND iDrain > 1000
      {
         if Send(who,@GetMana) >= 1
         {
            Send(who,@LoseMana,#amount=1);
            iDrain = iDrain - 1000;
         }
         else
         {
            Post(self,@BreakTrance,#who=who,#state=state,#event=EVENT_STEER);
         }
      }

      Send(who,@StartEnchantment,#what=self,#time=RADIUS_CHECK_TIME,
          #state=[iDrain + (((viManaDrain*1000 / viDrainTime)*1000) / (1000 / RADIUS_CHECK_TIME)),lEnchanted,iPower,iRange],#Report=FALSE);

      return;
   }

   RemoveEnchantment(who = $, state=$)
   "Remove enchantment effects on this occupant"
   {
      return;
   }
   
   BreakTrance(who=$, event=$, state=$, location=$, iPower=$)
   "If caster runs out of mana or loses trance, spell ends."
   {
      local oUser, oObj, oRoom, lEnchanted;

      % The spell keeps going if you get damaged, move, cast, use an item, or make an attack.
      % Players must rest to end radius spells.
      if event = EVENT_DAMAGE
         OR event = EVENT_ATTACK
         OR event = EVENT_RUN
         OR event = EVENT_DISRUPT
         OR event = EVENT_CAST
         OR event = EVENT_USE
      {
         return FALSE;
      }
      
      oRoom = Send(who,@GetOwner);

      % Only cancel Persist spells if they are offensive & caster is entering a non-combat room
      if viCasterPersist
         AND event = EVENT_NEWOWNER
      {
         if oRoom <> $
            AND NOT (viAffectsEnemies
                     AND Send(oRoom,@CheckRoomFlag,#flag=ROOM_NO_COMBAT))
         {
            return FALSE;
         }
      }
   
      iPower = Nth(state,3);

      % Clean up Trance.
      if IsClass(who,&User)
      {
         Send(who,@ClearTranceFlag);
         Send(who,@RemoveEnchantment,#what=self);
      }
      lEnchanted = Nth(state,2);
      for oUser in lEnchanted
      {
         Send(self,@LeaveRadius,#who=oUser,#iPower=iPower,#caster=who);
         
         if piOldAreaEnchStyle
         {
            if oUser = who
            {
               Send(oUser,@MsgSendUser,#message_rsc=radius_ench_caster_ends,#parm1=Send(who,@GetName));
               Send(oUser,@ClearFlickerFlag);
               Send(oUser,@RecalcFlickerFlag);
            }
            else
            {
               Send(oUser,@MsgSendUser,#message_rsc=radius_ench_ends,#parm1=Send(who,@GetName));
            }
         }
      }
      
      if NOT piOldAreaEnchStyle
      {
         for oObj in Send(oRoom,@GetplActive)
         {
            oUser = Send(oRoom,@HolderExtractObject,#data=oObj);

            if IsClass(oUser,&User)
            {
               if oUser = who
               {
                  Send(oUser,@MsgSendUser,#message_rsc=radius_ench_caster_ends,#parm1=Send(who,@GetName));
                  Send(oUser,@ClearFlickerFlag);
                  Send(oUser,@RecalcFlickerFlag);
               }
               else
               {
                  Send(oUser,@MsgSendUser,#message_rsc=radius_ench_ends,#parm1=Send(who,@GetName));
               }
            }
         }
      }

      propagate;
   }

   SetSpellPlayerFlag(who=$)
   {
      Send(who,@SetTranceFlag);
      
      return;
   }
   
   RemoveEnchantmentEffects()
   "Need to override spell.kod's thing, since we do this ourselves specially."
   {
      return;
   }

   % Replace this to modify power calculations - currently on 0 to 99 scale
   CalculatePower(who=$, iSpellPower=25)
   {
      return iSpellPower;
   }

   % Replace this to modify range by spellpower calculations
   CalculateRange(who=$, iSpellPower=25)
   {
      local iRange;

      % Default is up to twice base range      
      iRange = viBaseRange + ((viBaseRange*iSpellPower)/99);
      
      return iRange;
   }

   CalculateLightIntensity(iSpellPower=25)
   {
      return viBaseLightIntensity * ((iSpellpower*2) / 99);
   }

   CanBeRemovedByPlayer()
   "Returns if a spell can be removed by normal Purge/Purify"
   {
      return FALSE;
   }
   
   GetLightColor()
   {
      return viLightColor;
   }
   
   GetLightIntensity()
   {
      return viBaseLightIntensity;
   }

   % Radius spells that use totems need to naturally time out.
   GetDuration(iSpellPower=0)
   {
      return ((50 + iSpellpower)*1000);
   }

   %%%%% Effect Functions
   
   ModifyDefensePower(attacker=$,iPower=0,caster=$,defense_power=0)
   {
      return defense_power;
   }

   ModifyResistance(attacker=$,atype=-ATCK_SPELL_ALL,iPower=0,caster=$,resistance=$)
   {
      return resistance;
   }

   ModifyHitRoll(who = $,what = $,hit_roll = $)
   {
      return hit_roll;
   }
   
   ModifyDamage(who = $,what = $,damage = $)
   {
      return damage;
   }

   ModifySpellPower(who=$,state=$,oSpell=$,iSpellPower=0)
   {
      return iSpellPower;
   }
   
   TryRuinShot(who=$,iSpellPower=0)
   {
      return FALSE;
   }

end
%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
