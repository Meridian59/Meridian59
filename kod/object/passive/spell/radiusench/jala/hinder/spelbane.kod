% Meridian 59, Copyright 1994-2012 Andrew Kirmse and Chris Kirmse.
% All rights reserved.
%
% This software is distributed under a license that is described in
% the LICENSE file that accompanies it.
%
% Meridian is a registered trademark.


%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
Spellbane is HinderSpell

constants:

   include blakston.khd

resources:

   include spelbane.lkod

   Spellbane_name_rsc = "spellbane"
   Spellbane_icon_rsc = ispebane.bgf
   Spellbane_desc_rsc = \
      "The words of this song form an enchantment which hinders the casting "
      "of any other spells.  Requires one perfect polished seraphym."
   Spellbane_no_towns = "You are prevented from singing this song currently."

   Spellbane_song = jala1.mp3

   Spellbane_cast = \
      "You begin to play a song full of clicks and layered mechanical sounds."
   Spellbane_starts = \
      "%s begins to play a song full of clicks and layered mechanical sounds."
   Spellbane_ends = \
      "The strangely mechanical song played by %s fades away."
   Spellbane_caster_ends = \
      "Your strangely mechanical song fades."
   Spellbane_caster_enter = \
      "Your mechanical song creates a maze of magical energies in the "
      "area around you."
   Spellbane_enter = \
      "The song sung by %s fills the air with strange clicks "
      "and mechanical sounds."
   Spellbane_leave = \
      "You can no longer hear the strange mechanical noises of %s's song."

   % Resources from spell.kod/lkod for old version of spell
   %spell_spellbane_stopped = \
   %   "The notes of %s's song tangle with the words of your spell, unraveling "
   %   "it faster than your tongue can weave it."
   %spell_spellbane_stopped = de \
   %   "Die Noten des Liedes, gesungen von %s vermischen sich mit den Worten "
   %   "Deines Zauberspruches und lösen diese schneller auf, als Deine Zunge "
   %   "sie weben kann."

classvars:

   radius_ench_cast = Spellbane_cast
   radius_ench_starts = Spellbane_starts
   radius_ench_ends = Spellbane_ends
   radius_ench_caster_ends = Spellbane_caster_ends
   radius_ench_caster_enter = Spellbane_caster_enter
   radius_ench_enter = Spellbane_enter
   radius_ench_leave = Spellbane_leave

   vrName = Spellbane_name_rsc
   vrIcon = Spellbane_icon_rsc
   vrDesc = Spellbane_desc_rsc

   viSpell_num = SID_SPELLBANE
   viSchool = SS_JALA
   viSpell_level = 5

   viMana = 12          % Mana is amount used upon inititiation
   viManaDrain = 8      % Drain is amount used every viDrainTime milliseconds
   viDrainTime = 5000   % Drain some mana every viDrainTime milliseconds

   viSpell_Exertion = 6
   viMeditate_ratio = 50

properties:

   prMusic = Spellbane_song

messages:

   ResetReagents()
   {
      plReagents = $;
      plReagents = Cons([&PolishedSeraphym,1],plReagents);

      return;
   }

   GetAlteredChance(chance = 0, what = $, lRadiusState = 0)
   {
      local iAltChance, iSongSpellpower;

      if (what = $)
      {
         return chance;
      }

      iSongSpellpower = Nth(lRadiusState,2);

      iAltChance = chance - Random(iSongSpellpower/2,(iSongSpellpower*2)/3);

      % Don't worry about bounding, that's done the level above us.
      return iAltChance;
   }

end
%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
