% Meridian 59, Copyright 1994-2012 Andrew Kirmse and Chris Kirmse.
% All rights reserved.
%
% This software is distributed under a license that is described in
% the LICENSE file that accompanies it.
%
% Meridian is a registered trademark.


%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
Rejuvenate is Jala

constants:

   include blakston.khd

resources:

   include rejuven.lkod

   Rejuvenate_name_rsc = "rejuvenate"
   Rejuvenate_icon_rsc = irejuven.bgf
   Rejuvenate_desc_rsc = \
      "This song allows all listeners to regain mana at an accelerated rate.  "
      "Requires one diamond and a raw, uncut seraphym to cast."

   Rejuvenate_song = song04.mp3

   Rejuvenate_cast = "You begin to play an energizing song."
   Rejuvenate_starts = "%s begins to play an energizing song."
   Rejuvenate_ends = "The energizing song played by %s fades away."
   Rejuvenate_caster_ends = "Your energizing song fades."
   Rejuvenate_caster_enter = "You feel the weariness of your soul diminish."
   Rejuvenate_enter = "%s's energizing song eases the weariness of your soul."
   Rejuvenate_leave = "You can no longer hear the energizing notes of %s's song."

classvars:

   radius_ench_cast = Rejuvenate_cast
   radius_ench_starts = Rejuvenate_starts
   radius_ench_ends = Rejuvenate_ends
   radius_ench_caster_ends = Rejuvenate_caster_ends
   radius_ench_caster_enter = Rejuvenate_caster_enter
   radius_ench_enter = Rejuvenate_enter
   radius_ench_leave = Rejuvenate_leave

   vrName = Rejuvenate_name_rsc
   vrIcon = Rejuvenate_icon_rsc
   vrDesc = Rejuvenate_desc_rsc

   viSpell_num = SID_REJUVENATE
   viSchool = SS_JALA
   viSpell_level = 5

   viMana = 5           % Mana is amount used upon inititiation
   viManaDrain = 8      % Drain is amount used every viDrainTime milliseconds
   viDrainTime = 5000   % Drain some mana every viDrainTime milliseconds

   viPeriodicEffect = TRUE

   viSpellExertion = 3
   viChance_To_Increase = 20
   viMeditate_ratio = 50

   viAffectsEveryone = TRUE

properties:

   prMusic = Rejuvenate_song

messages:

   ResetReagents()
   {
      plReagents = $;
      plReagents = Cons([&Diamond,1],plReagents);
      plReagents = Cons([&UncutSeraphym,1],plReagents);

      return;
   }
   
   CalculatePeriodicEffectTime(iPower=0)
   {
      return 100000 / (iPower+1);
   }

   PeriodicEffect(lEnchanted=$,iPower=0,iRange=0,source=$)
   {
      local oUser, lState;

      foreach oUser in lEnchanted
      {
         if IsClass(oUser,&User)
         {
            lState = Send(oUser,@GetMostPowerfulRadiusEnchantmentState,#byClass=&Rejuvenate);
            
            if Nth(lState,3) = source
               AND Nth(lState,2) = iPower
            {
               Send(oUser,@GainMana,#amount=1,#bRespectMax=TRUE);
            }
         }
      }
      
      return;
   }

end
%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
