% Meridian 59, Copyright 1994-2012 Andrew Kirmse and Chris Kirmse.
% All rights reserved.
%
% This software is distributed under a license that is described in
% the LICENSE file that accompanies it.
%
% Meridian is a registered trademark.


%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
Mirth is RadiusEnchantment

constants:

   include blakston.khd

resources:

   Mirth_name_rsc = "mirth"
   Mirth_icon_rsc = imirth.bgf
   Mirth_desc_rsc = \
      "This song makes people happy!  It is a lively tune, which makes people "
      "feel as if they have a new start on life.  The song causes some people "
      "to smile, often resulting in better moods.  A good mood allows others "
      "to fight evil more effectively.  "
      "It requires 1 sapphire to cast."

   Mirth_on = "%s%s's mirthful song lifts your spirits a bit."

   Mirth_cast = "You begin to play a lively tune."
   Mirth_starts = "%s begins to play a lively tune."
   Mirth_ends = "The happy song fades."
   Mirth_caster_ends = "Your cheery song fades."
   Mirth_caster_enter = "The happy song reminds you of good times."
   Mirth_enter = "%s's happy song makes you feel a little better."
   Mirth_leave = "The lively tune fades from your ears."

   Mirth_song = main.mid
 
classvars:

   radius_ench_cast = Mirth_cast
   radius_ench_starts = Mirth_starts
   radius_ench_ends = Mirth_ends
   radius_ench_caster_ends = Mirth_caster_ends
   radius_ench_caster_enter = Mirth_caster_enter
   radius_ench_enter = Mirth_enter
   radius_ench_leave = Mirth_leave

   vrName = Mirth_name_rsc
   vrIcon = Mirth_icon_rsc
   vrDesc = Mirth_desc_rsc

   viSpell_num = SID_MIRTH
   viSchool = SS_JALA
   viSpell_level = 1
   viBaseRange = 10

   viMana = 2
   viManaDrain = 2
   viDrainTime = 5000

   viSpellExertion = 2      
   viChance_To_Increase = 25

   viAffectsEveryone = TRUE

   viCasterPersist = TRUE

properties:

   prMusic = Mirth_song
      
messages:

   ResetReagents()
   {
      plReagents = $;
      plReagents = Cons([&Sapphire,1],plReagents);

      return;
   }

   StartSpecialEffect(who=$,iPower=0,caster=$)
   {
      If IsClass(who,&User)
         AND iPower <> 0
      {
         if random(1,100) < iPower
         {
            Send(who,@SetAction,#action=UA_HAPPY);
         }
         else
         {
            % No frowning during the happy song.
            if Send(who,@GetAction) = UA_SAD
            {
               Send(who,@SetAction,#action=UA_NORMAL);
            }
         }
      }

      return;
   }

   ModifyHitRoll(who = $,what = $,hit_roll = $)
   {
      % Only affect the evil ones...
      if Send(what,@GetKarma,#detect=TRUE) < 0
      {
         % Give a small bonus
         return hit_roll + 100;
      }

      return hit_roll;
   }
   
   ModifyDamage(who = $,what = $,damage = $)
   {
      % Only affect the evil ones...
      if Send(what,@GetKarma,#detect=TRUE) < 0
      {
         % Roughly equal to Killing Fields
         return damage + Random(1,5);
      }

      return damage;
   }

end
%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
