% Meridian 59, Copyright 1994-2012 Andrew Kirmse and Chris Kirmse.
% All rights reserved.
%
% This software is distributed under a license that is described in
% the LICENSE file that accompanies it.
%
% Meridian is a registered trademark.


%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
SandStorm is RadiusEnchantment

constants:

   include blakston.khd

   SANDSTORM_VIGOR_LOSS = 5          % 5 vigor / 10 sec
   SANDSTORM_PERIOD = 10000          % damage every 15 secs
   SANDSTORM_TIME = 120000           % 2 realtime minutes

resources:

   include sandstor.lkod

   sandstorm_name_rsc = "sandstorm"
   sandstorm_icon_rsc = isandsto.bgf
   sandstorm_desc_rsc = \
      "All the dust in the room is swept up into a swirling storm, "
	   "making it harder and more strenuous to move and fight.  "
	   "Also has a small chance to knock arrows out of the air.  "
      "Requires orc teeth to cast."
   
   sandstorm_already_enchanted = "A sandstorm already rages here."

   sandstorm_on = \
      "From out of nowhere, swirling dust begins to needle into your skin, "
	   "making it very difficult to breathe.  "
	   "You feel yourself tiring."

   sandstorm_off = "The air clears."
   sandstorm_new_entrant = \
      "The air is full of dust, making it very difficult to breathe. "
	   "You feel yourself tiring."

   sandstorm_hurts_you = "You are becoming exhausted from the buffeting winds of sand."
   sandstorm_miss_rsc = "The powerful sandstorm snatches your shot away."
   
   Sandstorm_cast = "You call upon the rage of Faren to bear utter tempest across the area!"
   Sandstorm_starts = "%s calls upon the rage of Faren to bear utter tempest across the area!"
   Sandstorm_ends = "The air clears."
   Sandstorm_caster_ends = "The air clears."
   Sandstorm_caster_enter = "Swirling dust begins to needle into your skin, making it very difficult to breathe."
   Sandstorm_enter = "Swirling dust begins to needle into your skin, making it very difficult to breathe."
   Sandstorm_leave = "The air around you clears."
   
   sandstorm_sound = fheat.wav

classvars:

   radius_ench_cast = Sandstorm_cast
   radius_ench_starts = Sandstorm_starts
   radius_ench_ends = Sandstorm_ends
   radius_ench_caster_ends = Sandstorm_caster_ends
   radius_ench_caster_enter = Sandstorm_caster_enter
   radius_ench_enter = Sandstorm_enter
   radius_ench_leave = Sandstorm_leave
   
   viSpell_num = SID_SANDSTORM

   vrName = sandstorm_name_rsc
   vrIcon = sandstorm_icon_rsc
   vrDesc = sandstorm_desc_rsc

   viSchool = SS_FAREN
   viSpell_level = 5
   viMana = 14
   viChance_To_Increase = 35
   viMeditate_ratio = 50
   viBaseRange = 10
   viPeriodicEffect = TRUE

   vrSucceed_wav = sandstorm_sound

   viAffectsGuildmates = TRUE
   viAffectsEnemies = TRUE

   viCasterPersist = TRUE
   
properties:

   piOldAreaEnchStyle = TRUE

messages:

   ResetReagents()
   {
      plReagents = $;
      plReagents = Cons([&OrcTooth,2],plReagents);

      return;
   }

   GetDuration(iSpellPower=0)
   {
      return Bound(80000+(iSpellPower*1000),80000,180000);
   }

   StartSpecialEffect(what=$,iPower=0,source=$)
   {
      If IsClass(what,&User)
         AND iPower <> 0
      {
         Send(what,@EffectSendUser,#what=self,#effect=EFFECT_SAND);
         Send(what,@AddAim,#points=-(iPower/4));
      }

      return;
   }
   
   EndSpecialEffect(what=$,iPower=0,source=$)
   {
      If IsClass(what,&User)
         AND iPower <> 0
      {
         Send(what,@EffectSendUser,#what=self,#effect=EFFECT_CLEARSAND);
         Send(what,@AddAim,#points=(iPower/4));
      }
      return;
   }

   CalculatePeriodicEffectTime()
   {
      return 3000;
   }
   
   PeriodicEffect(lEnchanted=$,iPower=0,iRange=0,source=$)
   {
      local oBattler, bExhaustedMultiple;
      
      % Sandstorm applies 1-4 slashing damage from the source of the spell.
      % It also drains 1-4 vigor. Both values are based on spellpower, and are not random.
      % Special behavior: Sandstorm ceases to drain vigor below 10 vigor.
      % Instead, it deals triple damage, because the victim is exhausted.

      bExhaustedMultiple = 1;
      
      foreach oBattler in lEnchanted
      {
         if IsClass(Send(oBattler,@GetOwner),&GuildHall)
            AND NOT Send(Send(oBattler,@GetOwner),@InFoyer,#who=oBattler)
         {
            continue;
         }
         
         if IsClass(oBattler,&User)
         {
            if Send(oBattler,@GetVigor) > 9
            {
               Send(oBattler,@AddExertion,#amount=((iPower+1)/25)*10000);
            }
            else
            {
               bExhaustedMultiple = 3;
            }

            if Send(oBattler,@AssessDamage,#what=source,
               #report=FALSE,#damage=(((iPower+1)*4)*bExhaustedMultiple),
               #atype=ATCK_WEAP_SLASH,#precision=TRUE) = $
            {
               If IsClass(source,&Battler)
               {
                  Send(source,@KilledSomething,#what=oBattler);
               }
               Send(oBattler,@Killed,#what=source);
            }
         }
         
         bExhaustedMultiple = 1;
      }
      
      return;
   }

   TryRuinShot(who=$,iSpellPower=0)
   "Returns TRUE if it manages to knock down a shot."
   {
      local iMissChance;

      % This is a bit less than Winds which is (iSpellpower/2 + 10), bound 25 to 60.
      iMissChance = iSpellpower/3 + 10; 
      iMissChance = bound(iMissChance,15,40);
      if Random(1,100) < iMissChance
      {
         send(who,@MsgSendUser,#message_rsc=sandstorm_miss_rsc);
         return TRUE;
      }

      return FALSE;
   }
   

end

%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
