% Meridian 59, Copyright 1994-2012 Andrew Kirmse and Chris Kirmse.
% All rights reserved.
%
% This software is distributed under a license that is described in
% the LICENSE file that accompanies it.
%
% Meridian is a registered trademark.


%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
Umbrella is RadiusEnchantment
% Termination conditions
%  caster breaks trance 
%  enchantee moves out of range
%  total spell time elapses

constants:

   include blakston.khd

resources:

   include umbrella.lkod

   Umbrella_name_rsc = "umbrella"
   Umbrella_icon_rsc = iumbrela.bgf
   Umbrella_desc_rsc = \
      "Creates a dome of magical energy that protects those "
      "near the caster for several minutes.  Requires emeralds to cast."

   Umbrella_cast = "You extend a mystic, shimmering dome of light out from your fingertips."
   Umbrella_starts = "A mystic, shimmering dome of light emerges from %s's fingertips."
   Umbrella_ends = "The dome of magical energy maintained by %s dissipates."
   Umbrella_caster_ends = "Your dome of magical energy dissipates."
   Umbrella_caster_enter = "You feel your umbrella's magic protecting you."
   Umbrella_enter = "You are encompassed by a dome of magical energy emanating from %s."
   Umbrella_leave = "You have passed outside the dome of magical energy emanating from %s."

   Umbrella_deflected = "%s's umbrella dampens the force of your attack."

classvars:

   radius_ench_cast = Umbrella_cast
   radius_ench_starts = Umbrella_starts
   radius_ench_ends = Umbrella_ends
   radius_ench_caster_ends = Umbrella_caster_ends
   radius_ench_caster_enter = Umbrella_caster_enter
   radius_ench_enter = Umbrella_enter
   radius_ench_leave = Umbrella_leave
   
   vrName = Umbrella_name_rsc
   vrIcon = Umbrella_icon_rsc
   vrDesc = Umbrella_desc_rsc

   viSpell_num = SID_UMBRELLA
   viMana = 15
   viManaDrain = 7
   viDrainTime = 5000
   viSchool = SS_SHALILLE
   viSpell_level = 5

   viIndefinite = ARTICLE_AN
   viChance_To_Increase = 35
   viMeditate_ratio = 30
   viBaseRange = 3
   viCast_Time = 2000

   viAffectsGuildmates = TRUE
   viAffectsEnemies = TRUE

properties:
   
messages:

   ResetReagents()
   {
      plReagents = $;
      plReagents = Cons([&Emerald,2],plReagents);

      return;
   }

   ModifyDefensePower(attacker=$,iPower=0,caster=$,defense_power=0)
   {
      if attacker <> $
      {
         if Send(attacker,@IsAffectedByRadiusEnchantment,#what=self,#caster=caster)
         {
            % Umbrella can't help if the attacker is within its limits
            return defense_power;
         }
      }

      % Adds up to 198 defense against attacks from outside the umbrella
      return defense_power + iPower*2;
   }

   ModifyResistance(attacker=$,atype=-ATCK_SPELL_ALL,iPower=0,caster=$,resistance=$)
   {
      if attacker <> $
      {
         if Send(attacker,@IsAffectedByRadiusEnchantment,#what=self,#caster=caster)
         {
            % Umbrella can't help if the attacker is within its limits
            return resistance;
         }
         else
         {
            if caster <> $
            {
               Post(attacker,@MsgSendUser,#message_rsc=Umbrella_deflected,#parm1=Send(caster,@GetName));
            }
         }
      }

      % Resists up to 20% of all attacks from outside the umbrella.
      if resistance > 20
      {
         return resistance;
      }
      else
      {
         return bound(resistance + iPower/5,$,20);
      }
      return resistance;
   }

end
%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
