% Meridian 59, Copyright 1994-2012 Andrew Kirmse and Chris Kirmse.
% All rights reserved.
%
% This software is distributed under a license that is described in
% the LICENSE file that accompanies it.
%
% Meridian is a registered trademark.


%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
MartyrsBattleground is RoomEnchantment

constants:

   include blakston.khd

resources:

   martyrsbg_name_rsc = "martyr's battleground"
   martyrsbg_icon_rsc = imartyrs.bgf

   martyrsbg_desc_rsc = \
      "Calls the attention of the gods to the area, bolstering the defenses of "
		"all warrior as well as increasing "
      "the likelihood they will give those who die here a chance "
      "at vengeance with a revenant.  "
      "Requires elderberry and mushroom to cast."
   
   martyrsbg_unnecessary = "You already feel the presence of the gods here."

   martyrsbg_on = \
      "You suddenly feel as though you are being watched, and judged by intangible beings."

   martyrsbg_off = \
      "The area feels less ordained.  It seems the gods have lost interest in the frays "
      "in the area."

   martyrsbg_new_entrant = \
      "You imagine you can feel the presence of the gods watching you and bolstering your defenses."
	  
   martyrsbg_leave = "The strange feeling of divine guidance subsides."

   martrysbg_sound = smatbat.wav

classvars:

   vrName = martyrsbg_name_rsc
   vrIcon = martyrsbg_icon_rsc
   vrDesc = martyrsbg_desc_rsc

   viSpell_num = SID_MARTYRS_BATTLEGROUND
   viSchool = SS_KRAANAN
   viMana = 10
   viSpell_level = 3

	% Chance to increase is one in viChance_To_Increase.
   viChance_To_Increase = 6
	
   vrSucceed_wav = martrysbg_sound

   vbCastable_in_HappyLand = FALSE

properties:

   piDamage_percent_reduce = 5
   piDefense_bonus = 5

messages:

   ResetReagents()
   {
      plReagents = $;
      plReagents = Cons([&ElderBerry,1],plReagents);
      plReagents = Cons([&Mushroom,1],plReagents);

      return;
   }

   GetNumSpellTargets()
   {
      return 0;
   }

   CanPayCosts(who = $, lTargets = $)
   {
      local oRoom;

      oRoom = Send(who,@GetOwner);
	  
      % check if room already has martyrsbg
      if Send(oRoom,@IsEnchanted,#what=self)
      {
         Send(who,@MsgSendUser,#message_rsc=martyrsbg_unnecessary);

         return FALSE;
      }

      propagate;
   }

   CastSpell(who = $, iSpellPower = 0)
   {
      local oRoom;

      oRoom = Send(who,@GetOwner);

      % global effects of the enchantment
      Send(oRoom,@SomeoneSaid,#type=SAY_MESSAGE,#string=martyrsbg_on,#what=self);
      Send(oRoom,@RoomStartEnchantment,#what=self,#state=iSpellPower*2,#time=send(self,@GetDuration,#iSpellPower=iSpellPower));
      Send(oRoom,@EnchantAllOccupants,#what=self);
      
      propagate;
   }

   StartEnchantmentNewOccupant(who = $)
   "Called on new occupants of the enchanted room."
   {
      Send(self,@StartEnchantment,#who=who);

      return;
   }
   
   StartEnchantment(who = $)
   "Starts enchantment effect on one player"
   {
      Send(who,@AddDefenseModifier,#what=self);
      Send(who,@MsgSendUser,#message_rsc=martyrsbg_new_entrant);
      
      return;
   }

   EndSpell( where = $, state = $ )
   "Called when spell expires."
   {
      Send(where,@SomeoneSaid,#type=SAY_MESSAGE,#string=martyrsbg_off,#what=self);
      Send(where,@UnenchantAllOccupants,#what=self,#state=state);

      return;
   }
   
   EndRoomEnchantment(who = $, state = $)
   "Remove enchantment effects on this occupant"
   {
      Send(who,@RemoveDefenseModifier,#what=self);
      Send(who,@MsgSendUser,#message_rsc=martyrsbg_leave);
      
      return;
   }
   
   GetDuration(iSpellPower=0)
   {
      local iDuration;

      iDuration = 45 + (iSpellPower*3);  %% 30 - 228 seconds
      iDuration = iDuration * 1000;      %% convert to seconds            

      return random(iDuration/2,iDuration);
   }

end
%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
