% Meridian 59, Copyright 1994-2012 Andrew Kirmse and Chris Kirmse.
% All rights reserved.
%
% This software is distributed under a license that is described in
% the LICENSE file that accompanies it.
%
% Meridian is a registered trademark.


%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
Truce is RoomEnchantment

constants:

   include blakston.khd

resources:

   planarcalm_name_rsc = "planar calm"
   planarcalm_icon_rsc = itruce.bgf
   planarcalm_desc_rsc = \
      "The spirit of Shal'ille descends upon the area, "
      "calming disturbances and aggressions. This spell will close spacial rifts and "
      "momentarily relax monsters. "
      "Requires emerald and yrxl sap to cast."

   planarcalm_cast = \
      "A numbing panacea washes across the area."


   planarcalm_sound = shalille.wav

classvars:

   vrIcon = planarcalm_icon_rsc
   vrDesc = planarcalm_desc_rsc
   vrName = planarcalm_name_rsc
   vrSucceed_wav = planarcalm_sound

   viSpell_num = SID_TRUCE
   viSchool = SS_SHALILLE
   viMana = 20
   viSpellExertion = 10
   viSpell_level = 4
   viChance_To_Increase = 15

   % Default animation speed for icon in ms
   viAnimationSpeed = 200
   viAnimation_start = 1
   viAnimation_end = 3
   viIcon_animation_start = 4
   viIcon_animation_end = 6

properties:


messages:

   ResetReagents()
   {
      plReagents = $;
      plReagents = Cons([&Emerald,1],plReagents);
      plReagents = Cons([&Yrxlsap,1],plReagents);

      return;
   }

   GetNumSpellTargets()
   {
      return 0;
   }

   CastSpell( who = $, iSpellPower = 0 )
   "Initiation point for the spell."
   {
      local oRoom, i, oObject, iChance, iRoll, oHold, iCalmDuration;

      oRoom = Send(who,@GetOwner);
      Send(who,@MsgSendUser,#message_rsc=planarcalm_cast);
            
      % spell's chance to close a portal                                               
      iChance = bound(iSpellPower,5,95);
      
      % will calm monsters up to 4 seconds
      iCalmDuration = iSpellPower/25;
      
      % Find the hold spell to temporarily calm monsters
      oHold = send(SYS,@FindSpellByNum,#num=SID_HOLD);
      
      for i in send(oRoom,@GetHolderActive) 
      {
         oObject = Send(oRoom,@HolderExtractObject,#data=i);
      
         if isClass(oObject,&NecropolisPortal)
         {
            iRoll = random(1,100);
            if iRoll < iChance 
            {
               % close the portal
               send(oObject,@ClosePortal);
            }
         }
         else
         {
            if isClass(oObject,&Monster)
            {
               send(oHold,@DoHold,#what=who,#otarget=oObject,#iDurationSecs=iCalmDuration,#bAllowFreeAction=FALSE);
            }
         }
      }

      propagate;
   }

end
%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
