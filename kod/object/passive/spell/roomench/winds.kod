% Meridian 59, Copyright 1994-2012 Andrew Kirmse and Chris Kirmse.
% All rights reserved.
%
% This software is distributed under a license that is described in
% the LICENSE file that accompanies it.
%
% Meridian is a registered trademark.


%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
Winds is RoomEnchantment

%% April 20, 1997 - DSchubert
%% Reduces aim by Spellpower/2
%% lasts 60 to 60+spellpower minutes

constants:

   include blakston.khd

resources:

   winds_name_rsc = "winds"
   winds_icon_rsc = iwinds.bgf
   winds_desc_rsc = \
      "Summons powerful winds which render ranged attacks more difficult "
      "for several minutes.  "
      "Requires elderberries and purple mushrooms to cast."
   
   winds_already_enchanted = "Magical winds are already present."

   winds_on = "Violent winds suddenly sweep the area and buffet your limbs."
   winds_off = "The winds slow and then cease."
   winds_new_entrant = "Violent winds sweep the area and buffet your limbs."
   winds_miss_rsc = "The powerful winds snatch your shot away."

   winds_spell_intro = "Faren Level 3: Summons violent winds, which make ranged combat less effective."

   winds_sound = fwinds.wav

classvars:

   vrName = winds_name_rsc
   vrIcon = winds_icon_rsc
   vrDesc = winds_desc_rsc

   vrSpell_intro = winds_spell_intro
   
   viSpell_num = SID_WINDS
   viMana = 10
   viSpell_level = 3
   viSchool = SS_FAREN

	% Chance to increase is one in viChance_To_Increase.
   viChance_To_Increase = 3

   viShow_enchantment_icon = ENCHANTMENT_SHOW_ROOM

   vrSucceed_wav = winds_sound

properties:

messages:

   ResetReagents()
   {
      plReagents = $;
      plReagents = Cons([&Elderberry,3],plReagents);
      plReagents = Cons([&PurpleMushroom,1],plReagents);

      return;
   }

   GetNumSpellTargets()
   {
      return 0;
   }

   CanPayCosts(who = $, lTargets = $)
   {
      local oRoom;

      oRoom = Send(who,@GetOwner);

      % check for enchantment already applied
      if Send(oRoom,@IsEnchanted,#what=self)
      {
         Send(who,@MsgSendUser,#message_rsc=winds_already_enchanted);
	      return FALSE;
      }

      propagate;
   }

   CastSpell( who = $, iSpellPower = 0 )
   "Initiation point for the spell."
   {
      local oRoom;

      oRoom = Send(who,@GetOwner);

      % global effects of the enchantment
      Send(oRoom,@SomeoneSaid,#type=SAY_MESSAGE,#string=winds_on,#what=self);
      Send(oRoom,@RoomStartEnchantment,#what=self,#time=send(self,@getduration),#state=iSpellPower);
      Send(oRoom,@EnchantAllOccupants,#what=self,#iSpellPower=iSpellPower);
      
      propagate;
   }

   StartEnchantmentNewOccupant( who = $, state=0 )
   "Called on new occupants of the enchanted room."
   {
      Send(who,@MsgSendUser,#message_rsc=winds_new_entrant);
      Send(self,@StartEnchantment,#who=who, #iSpellPower=state);
      return;
   }

   StartEnchantment( who = $, iSpellPower = 0)
   "Starts enchantment effect on one player"
   {    
      Send(who,@AddAim,#points=-(iSpellpower/4));
      Send(who,@StartEnchantment,#what=self,#time=send(self,@GetDuration),
           #state=iSpellpower);
      return;
   }

   GetDuration(iSpellPower=0)
   {
      return random(60000,60000+(iSpellPower*1000));
   }

   EndRoomEnchantment( who = $, state = $ )
   "Ends enchantment effect on one player"
   {
      Send(who,@RemoveEnchantment,#what=self);
      return;
   }

   EndEnchantment( who = $, state = 0 )
   "Ends enchantment effect on one player"
   {
      Send(who,@AddAim,#points=state/4);
      return;
   }

   EndSpell( where = $, state = $ )
   "Called when spell expires."
   {
      Send(where,@SomeoneSaid,#type=SAY_MESSAGE,#string=winds_off,#what=self);
      Send(where,@UnenchantAllOccupants,#what=self,#state=state);
      return;
   }

   RemoveEnchantment( who = $ )
   "Remove enchantment effects on this occupant"
   {
      Send(who,@RemoveEnchantment,#what=self);
      return;
   }

   WindsRuinShot(who=$)
   {
      local oAmmo,iMissChance,iSpellPower,oFire;
      
      iSpellpower = send(send(who,@GetOwner),@GetEnchantmentState,#what=self);  
      
      iMissChance = iSpellpower/2 + 10; 
      iMissChance = bound(iMissChance,25,60);
      if Random(1,100) < iMissChance
      {
         send(who,@MsgSendUser,#message_rsc=winds_miss_rsc);
         
         return TRUE;
      }

      return FALSE;
   }

end

%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
