% Meridian 59, Copyright 1994-2012 Andrew Kirmse and Chris Kirmse.
% All rights reserved.
%
% This software is distributed under a license that is described in
% the LICENSE file that accompanies it.
%
% Meridian is a registered trademark.


%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
shroud is Spell

constants:
   include blakston.khd

resources:

   shroud_cast_rsc = "A dark cloak settles over your %s."
   
   shroud_fails = "%s%s is strangely unaffected by the spell."
   
   shroud_name_rsc = "shroud"
   shroud_icon_rsc = ishroud.bgf
   shroud_desc_rsc = \
	"Used to protect a warrior's tools against polluting magics.  Requires a large amount of inexpensive reagents."
   shroud_lable_rsc = "Shrouder."
   

classvars:

   vrName = shroud_name_rsc
   vrIcon = shroud_icon_rsc
   vrDesc = shroud_desc_rsc

   vrEnchantment_type = shroud_lable_rsc

   viSpell_num = SID_SHROUD
   viSchool = SS_KRAANAN

   viSpell_level = 3
   viMana = 10
   viSpellExertion = 10
   viCast_time = 30000


	% Chance to increase is one in viChance_To_Increase.
   viChance_To_Increase = 4

properties:

messages:

   ResetReagents()
   {
      plReagents = $;
      plReagents = Cons([&Mushroom,3],plReagents);
      plReagents = Cons([&Elderberry,3],plReagents);
      plReagents = Cons([&Herbs,2],plReagents);

      return;
   }

   GetNumSpellTargets()
   {
      return 1;
   }

   CanPayCosts(who = $, lTargets = $)
   {
      local oWeapon, oTarget;
   
      oTarget = First(lTargets);

      % Check that target is an item
      if (not isClass(oTarget,&item) or (not send(oTarget,@CanEnchant,#oSpell=self)))
      {
         Send(who, @MsgSendUser,#message_rsc=shroud_fails,
            #parm1=send(oTarget,@getcapdef),
			#parm2=send(oTarget,@getname));
	      return FALSE;
      }

      propagate;   % Check other things higher up
   }
   
   IsTargetInRange(who = $,target = $)
   {
      return who = Send(target, @GetOwner) 
      OR send(who,@getOwner) = send(target,@getowner);  
   }

   CastSpell(who = $, lTargets = $, iSpellPower = 0)
   {
      local oItem, oItemAtt;

      oItem = first(lTargets);
      if not Send(oItem,@CanEnchant,#oSpell = self)
      {
         Send(who, @MsgSendUser, #message_rsc=spell_resists,
             #parm1=send(oItem,@getCapdef),#parm2=send(oItem,@getname));
	      return;
      }
      
      oItemAtt = send(SYS,@finditemattbynum,#num=IA_SHROUD);

      send(oItemAtt,@AddToItem,#oItem=oItem,
	      #timer_duration = send(self,@GetDuration,#iSpellPower=iSpellPower),
	      #iPower=random(iSpellPower/20,iSpellPower/10));
      Send(who, @MsgSendUser, #message_rsc=shroud_cast_rsc,
	      #parm1=Send(oItem,@GetName), 
         #state3=send(self,@GetMakerInfo,#who=who));

      propagate;
   }

   GetDuration(iSpellPower=0)
   {
      local iDuration;

      iDuration = random(1,4) + (iSpellPower/3);
      %% ~( 1 - 37 )
      %%Convert to hours:
      iDuration = iDuration * 1000 * 60 * 60;
      return iDuration;
   }

end

%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
