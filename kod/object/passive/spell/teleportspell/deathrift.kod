% Meridian 59, Copyright 1994-2012 Andrew Kirmse and Chris Kirmse.
% All rights reserved.
%
% This software is distributed under a license that is described in
% the LICENSE file that accompanies it.
%
% Meridian is a registered trademark.


%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
DeathRift is TeleportationSpell

constants:

   include blakston.khd

resources:

   include deathrift.lkod

   DeathRift_name_rsc = "death rift"
   DeathRift_icon_rsc = ideathrift.bgf
   DeathRift_desc_rsc = \
      "By calling upon a pact with the most ancient of dark forces, "
      "the truest disciples of Qor may traverse the fiery pits of the "
      "Underworld while still retaining life. The interdimensional "
      "pathways can lead Qor's favored to many locations, including "
      "one dedicated destination of their choosing."
      "Requires four dark angel feathers to cast."

   DeathRift_sound = qdthdoor.wav

   DeathRift_no_angeled = \
      "You cannot call upon such an evil pact while your guardian "
      "angel watches over you."
   DeathRift_bind = \
      "You leave a smattering of blood at the ground beneath your "
      "feet, binding a small part of your soul to this location."
   DeathRift_travel = \
      "A vast passage of flames opens, and you find yourself "
      "pulled to the Underworld!"
   DeathRift_travel_notify = \
      "A fiery portal flares open briefly, dragging %s to the Underworld!"
   DeathRift_travel_notify_dest = \
      "A fiery portal flares open briefly, dragging %s through "
      "the Underworld and toward %s!"
   DeathRift_generic_fail = \
      "The evil pact's magic fails. Something seems amiss."
   DeathRift_warning = \
      "~BYou sense a nearby connection forming between dimensions!"
   
classvars:

   vrName = DeathRift_name_rsc
   vrIcon = DeathRift_icon_rsc
   vrDesc = DeathRift_desc_rsc
   
   viSpell_num = SID_DEATH_RIFT
   viSchool = SS_QOR
   viSpell_level = 6
   viMana = 20
   viSpellExertion = 15
   viCast_time = 60000

   viChance_To_Increase = 10
   viMeditate_ratio = 100

   vrSucceed_wav = DeathRift_sound

   vbAngeledCast = TRUE

properties:

messages:

   ResetReagents()
   {
      plReagents = $;
      plReagents = Cons([&DarkAngelFeather,4],plReagents);

      return;
   }

   CanPayCosts(who = $, lTargets = $, bItemCast = FALSE)
   {
      local oRoom, iRegion;

      if who = $
      {
         return FALSE;
      }

      if (NOT vbAngeledCast
         AND NOT Send(who,@CheckPlayerFlag,#flag=PFLAG_PKILL_ENABLE))
      {
         Send(who,@MsgSendUser,#message_rsc=DeathRift_no_angeled);

         return FALSE;
      }

      oRoom = Send(who,@GetOwner);

      if (oRoom = $)
      {
         Send(who,@MsgSendUser,#message_rsc=DeathRift_generic_fail);

         return FALSE;
      }

      iRegion = Send(oRoom,@GetRegion);

      if (NOT Send(oRoom,@CanHavePlayerPortal)
         OR iRegion = RID_NEWB_BASE
         OR iRegion = RID_GUEST_BASE)
      {
         Send(who,@MsgSendUser,#message_rsc=DeathRift_generic_fail);

         return FALSE;
      }

      propagate;
   }

   GetTranceTime(iSpellpower=0,who=$)
   {
      return Bound(viCast_time - (iSpellpower * 500),10000,60000);
   }

   GetNumSpellTargets()
   {
      return 0;
   }

   CastSpell(who = $, lTargets = $)
   {
      local oOwner, oTargetRoom, i, each_obj;

      oOwner = Send(who,@GetOwner);

      if oOwner = $
      {
         return;
      }

      if Send(oOwner,@CheckRoomFlag,#flag=ROOM_NO_COMBAT)
         AND Send(oOwner,@CheckRoomFlag,#flag=ROOM_SAFELOGOFF)
      {
         Send(who,@MsgSendUser,#message_rsc=DeathRift_travel);

         oTargetRoom = Send(SYS,@FindRoomByNum,
                           #num=Send(who,@GetBoundLocationRoomNum));
         if oTargetRoom <> $
         {
            foreach i in Send(oTargetRoom,@GetHolderActive)
            {
               each_obj = Send(oTargetRoom,@HolderExtractObject,#data=i);
               if IsClass(each_obj,&User)
               {
                  Send(each_obj,@MsgSendUser,#message_rsc=DeathRift_warning);
                  Send(each_obj,@WaveSendUser,#wave_rsc=DeathRift_sound,
                        #row=Send(who,@GetBoundLocationRow),
                        #col=Send(who,@GetBoundLocationCol));
               }
            }
         }

         foreach i in Send(oOwner,@GetHolderActive)
         {
            each_obj = Send(oOwner,@HolderExtractObject,#data=i);
            if IsClass(each_obj,&User)
               AND each_obj <> who
            {
               if oTargetRoom <> $
               {
                  Send(each_obj,@MsgSendUser,#message_rsc=DeathRift_travel_notify_dest,
                        #parm1=Send(who,@GetName),#parm2=Send(oTargetRoom,@GetName));
               }
               else
               {
                  Send(each_obj,@MsgSendUser,#message_rsc=DeathRift_travel_notify,
                        #parm1=Send(who,@GetName));
               }
            }
         }

         Post(who,@UserGotoDeadRoom);
         Post(who,@SetDeathRiftProtection,#value=TRUE);
         Post(who,@StartDeathRiftTimer);
      }
      else
      {
         Send(who,@MsgSendUser,#message_rsc=DeathRift_bind);
         Send(who,@BindPlayerToCurrentLocation);
      }

      propagate;
   }

   SuccessChance(who=$)
   {
      return TRUE;
   }

end
%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
